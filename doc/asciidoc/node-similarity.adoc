[[algorithms-node-similarity]]
= The Node Similarity algorithm

[abstract]
--
This section describes the Node Similarity algorithm in the Neo4j Graph Algorithms library.
--

This topic includes:

* <<algorithms-node-similarity-intro, Introduction>>
* <<algorithms-node-similarity-syntax, Syntax>>
* <<algorithms-node-similarity-examples, Examples>>
** <<algorithms-node-similarity-examples-stream, Streaming results>>
** <<algorithms-node-similarity-examples-write, Writing results>>
** <<algorithms-node-similarity-examples-limits, Limiting results>>
*** <<algorithms-node-similarity-examples-limits-topk-bottomk, topK and bottomK>>
*** <<algorithms-node-similarity-examples-limits-topn-bottomn, topN and bottomN>>
** <<algorithms-node-similarity-examples-degree-similarity-cutoff, Degree cutoff and similarity cutoff>>


[[algorithms-node-similarity-intro]]
== Introduction

The Node Similarity algorithm compares a set of nodes based on the nodes they are connected to.
Two nodes are considered similar if they share many of the same neighbors.
Node Similarity computes pair-wise similarities based on the Jaccard metric.

The input of this algorithm is a bipartite, connected graph containing two disjoint node sets.
Each relationship starts from a node in the first node set and ends at a node in the second node set.
The Node Similarity algorithm compares all nodes from the first node set with each other based on their relationships to nodes in the second set.
The complexity of this comparison grows quadratically with the number of nodes to compare.
The algorithm reduces the complexity by ignoring disconnected nodes.

In addition to computational complexity, the memory requirement for producing results also scales roughly quadratically.
In order to bound memory usage, the algorithm requires an explicit limit on the number of results to compute per node.
This is the 'topK' parameter.
It can be set to any value, except 0.

The output of the algorithm are new relationships between pairs of the first node set.
Similarity scores are expressed via relationship properties.

For more information on this algorithm, see:

* https://en.wikipedia.org/wiki/Similarity_(network_science)#Structural_equivalence[Structural equivalence (Wikipedia)]
* https://en.wikipedia.org/wiki/Jaccard_index[The Jaccard index (Wikipedia)].
* https://en.wikipedia.org/wiki/Bipartite_graph[Bipartite graphs (Wikipedia)]

[NOTE]
====
Running this algorithm requires sufficient available memory.
Before running this algorithm, we recommend that you read <<memory-requirements>>.
====


[[algorithms-node-similarity-syntax]]
== Syntax

.The following describes the API for running the algorithm and writing results back to Neo4j:
[source, cypher]
----
CALL gds.nodeSimilarity.write(graphName: STRING, {
  writeRelationshipType: STRING,
  writeProperty: STRING
  // additional configuration
})
YIELD nodesCompared, relationships, writeRelationshipType, writeProperty
----

.Parameters
[opts="header",cols="1,1,1m,1,4"]
|===
| Name      | Type    | Default | Optional | Description
| graphName | string  | null    | yes      | The name of an existing graph on which to run the algorithm. If no graph name is provided, the config map must contain configuration for creating a graph.
| config    | map     | {}      | yes      | Additional configuration, see below.
|===

.Configuration
[opts="header",cols="1,1,1,1,4"]
|===
| Name                   | Type    | Default                | Optional | Description
| nodeProjection         | string  | null                   | yes      | The projection of nodes to use when creating the implicit graph.
| relationshipProjection | string  | null                   | yes      | The projection of relationships to use when creating the implicit graph.
| concurrency            | int     | available CPUs         | yes      | The number of concurrent threads used for running the algorithm. Also provides the default value for 'readConcurrency' and 'writeConcurrency'. This is dependent on the Neo4j edition; for more information, see <<system-requirements-cpu>>.
| readConcurrency        | int     | value of 'concurrency' | yes      | The number of concurrent threads used for reading the graph.
| writeConcurrency       | int     | value of 'concurrency' | yes      | The number of concurrent threads used for writing the result.
| similarityCutoff       | float   | 1E-42                  | yes      | Lower limit for the similarity score to be present in the result.
| degreeCutoff           | int     | 1                      | yes      | Lower limit on the node degree for a node to be considered in the comparisons. This value can not be lower than 1.
| topK                   | int     | 10                     | yes      | Limit on the number of scores per node. The K largest results are returned.
| bottomK                | int     | 10                     | yes      | Limit on the number of scores per node. The K smallest results are returned.
| topN                   | int     | 0                      | yes      | Global limit on the number of scores computed. The N largest total results are returned.
| bottomN                | int     | 0                      | yes      | Global limit on the number of scores computed. The N smallest total results are returned.
| writeRelationshipType  | string  | SIMILAR                | yes      | The relationship type used to represent a similarity score.
| writeProperty          | string  | 'score'                | yes      | The relationship property that stores the similarity score.
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                   | Type   | Description
| nodesCompared          | int    | The number of nodes compared.
| relationships          | int    | The number of relationships created.
| writeRelationshipType  | string | The relationship type used to represent a similarity score.
| writeProperty          | string | The relationship property that stores the similarity score.
| loadMillis             | int    | Milliseconds for loading data.
| computeMillis          | int    | Milliseconds for running the algorithm.
| writeMillis            | int    | Milliseconds for writing result data back to Neo4j.
| postProcessingMillis   | int    | Milliseconds for computing percentiles.
| similarityDistribution | map    | Min, max, mean, stdDev and percentile distribution of the computed similarity results.
|===

[[algorithms-node-similarity-syntax-stream]]
.The following describes the API for running the algorithm and streaming results:
[source, cypher]
----
CALL gds.nodeSimilarity.stream(graphName: STRING, {
  // configuration
})
YIELD node1, node2, similarity
----

.Parameters
[opts="header",cols="1,1,1,1,4"]
|===
| Name      | Type    | Default | Optional | Description
| graphName | string  | null    | yes      | The name of an existing graph on which to run the algorithm. If no graph name is provided, the config map must contain configuration for creating a graph.
| config    | map     | {}      | yes      | Additional configuration, see below.
|===

.Configuration
[opts="header",cols="1m,1,1,1,4"]
|===
| Name              | Type    | Default                | Optional | Description
| concurrency       | int     | available CPUs         | yes      | The number of concurrent threads used for running the algorithm. Also provides the default value for 'readConcurrency' and 'writeConcurrency'. This is dependent on the Neo4j edition; for more information, see <<system-requirements-cpu>>.
| readConcurrency   | int     | value of 'concurrency' | yes      | The number of concurrent threads used for reading the graph.
| similarityCutoff  | float   | 1E-42                  | yes      | Lower limit for the similarity score to be present in the result.
| degreeCutoff      | int     | 1                      | yes      | Lower limit on the node degree for a node to be considered in the comparisons. This value can not be lower than 1.
| topK              | int     | 10                     | yes      | Limit on the number of scores per node. The K largest results are returned.
| bottomK           | int     | 10                     | yes      | Limit on the number of scores per node. The K smallest results are returned.
| topN              | int     | 0                      | yes      | Global limit on the number of scores computed. The N largest total results are returned.
| bottomN           | int     | 0                      | yes      | Global limit on the number of scores computed. The N smallest total results are returned.
|===

.Results
[opts="header",cols="1m,1,6"]
|===
| Name          | Type     | Description
| node1         | int      | The Neo4j ID of the first node.
| node2         | int      | The Neo4j ID of the second node.
| similarity    | double   | The similarity score for the two nodes.
|===


[[algorithms-node-similarity-examples]]
== Examples

Consider the graph created by the following Cypher statement:

[source, cypher]
----
CREATE (alice:Person {name: 'Alice'})
CREATE (bob:Person {name: 'Bob'})
CREATE (carol:Person {name: 'Carol'})
CREATE (dave:Person {name: 'Dave'})
CREATE (eve:Person {name: 'Eve'})
CREATE (guitar:Instrument {name: 'Guitar'})
CREATE (synth:Instrument {name: 'Synthesizer'})
CREATE (bongos:Instrument {name: 'Bongos'})
CREATE (trumpet:Instrument {name: 'Trumpet'})

CREATE (alice)-[:LIKES]->(guitar)
CREATE (alice)-[:LIKES]->(synth)
CREATE (alice)-[:LIKES]->(bongos)
CREATE (bob)-[:LIKES]->(guitar)
CREATE (bob)-[:LIKES]->(synth)
CREATE (carol)-[:LIKES]->(bongos)
CREATE (dave)-[:LIKES]->(guitar)
CREATE (dave)-[:LIKES]->(synth)
CREATE (dave)-[:LIKES]->(bongos);
----

This bipartite graph has two node sets, Person nodes and Instrument nodes.
The two node sets are connected via LIKES relationships.
Each relationship starts at a Person node and ends at an Instrument node.

In the example, we want to use the Node Similarity algorithm to compare persons based on the instruments they like.

The Node Similarity algorithm will only compute similarity for nodes that have a degree of at least 1.
In the example graph, the Eve node will not be compared to other Person nodes.


[[algorithms-node-similarity-examples-stream]]
=== Streaming results

.The following will load the graph, run the algorithm, and stream results:
[source, cypher]
----
CALL gds.nodeSimilarity.stream({
  nodeProjection: 'Person | Instrument',
  relationshipProjection: 'LIKES',
  direction: 'OUTGOING'
})
YIELD node1, node2, similarity
RETURN gds.util.asNode(node1).name AS Person1, gds.util.asNode(node2).name AS Person2, similarity
ORDER BY similarity DESCENDING, Person1, Person2
----

.Results
[opts="header"]
|===
| Person1 | Person2 | similarity
| "Alice" | "Dave"  | 1.0
| "Dave"  | "Alice" | 1.0
| "Alice" | "Bob"   | 0.6666666666666666
| "Bob"   | "Alice" | 0.6666666666666666
| "Bob"   | "Dave"  | 0.6666666666666666
| "Dave"  | "Bob"   | 0.6666666666666666
| "Alice" | "Carol" | 0.3333333333333333
| "Carol" | "Alice" | 0.3333333333333333
| "Carol" | "Dave"  | 0.3333333333333333
| "Dave"  | "Carol" | 0.3333333333333333
3+|10 rows
|===

We use default values for the procedure configuration parameter.
TopK is set to 10, topN is set to 0.
Because of that the result set contains the top 10 similarity scores for each node.


[[algorithms-node-similarity-examples-write]]
=== Writing results

To instead write the similarity results back to the graph in Neo4j, use the following query.
Each result is written as a new relationship between the compared nodes.
The similarity score is written as a property on the relationship.

.The following will load the graph, run the algorithm, and write back results:
[source, cypher]
----
CALL gds.nodeSimilarity.write({
  nodeProjection: 'Person | Instrument',
  relationshipProjection: 'LIKES',
  direction: 'OUTGOING',
  writeRelationshipType: 'SIMILAR',
  writeProperty: 'score'
})
YIELD nodesCompared, relationships, writeProperty, writeRelationshipType;
----

.Results
[opts="header"]
|===
| nodesCompared | relationships | writeProperty | writeRelationshipType
| 4             | 10            | "score"       | "SIMILAR"
|===

As we can see from the results, the number of created relationships is equal to the number of rows in the streaming example.


[[algorithms-node-similarity-examples-limits]]
=== Limiting results

There are four limits that can be applied to the similarity results.
Top limits the result to the highest similarity scores.
Bottom limits the result to the lowest similarity scores.
Both top and bottom limits can apply to the result as a whole ("N"), or to the result per node ("K").

[NOTE]
====
There must always be a "K" limit, either bottomK or topK, which is a positive number.
The default value for topK and bottomK is 10.
====


.Result limits
[opts="header", cols="1h,1,1"]
|===
|               | total results | results per node
| highest score | topN          | topK
| lowest score  | bottomN       | bottomK
|===


[[algorithms-node-similarity-examples-limits-topk-bottomk]]
==== topK and bottomK

TopK and bottomK are limits on the number of scores computed per node.
For topK, the K largest similarity scores per node are returned.
For bottomK, the K smallest similarity scores per node are returned.
TopK and bottomK cannot be 0, used in conjuntion, and the default value is 10.
If neither is specified, topK is used.

.The following will load the graph, run the algorithm, and stream the top 1 result per node:
[source, cypher]
----
CALL gds.nodeSimilarity.stream({
  nodeProjection: 'Person | Instrument',
  relationshipProjection: 'LIKES',
  direction: 'OUTGOING',
  topK: 1
})
YIELD node1, node2, similarity
RETURN gds.util.asNode(node1).name AS Person1, gds.util.asNode(node2).name AS Person2, similarity
ORDER BY Person1
----

.Results
[opts="header"]
|===
| Person1 | Person2 | similarity
| "Alice" | "Dave"  | 1.0
| "Bob"   | "Alice" | 0.6666666666666666
| "Carol" | "Alice" | 0.3333333333333333
| "Dave"  | "Alice" | 1.0
3+|4 rows
|===

.The following will load the graph, run the algorithm, and stream the bottom 1 result per node:
[source, cypher]
----
CALL gds.nodeSimilarity.stream({
  nodeProjection: 'Person | Instrument',
  relationshipProjection: 'LIKES',
  direction: 'OUTGOING',
  bottomK: 1
})
YIELD node1, node2, similarity
RETURN gds.util.asNode(node1).name AS Person1, gds.util.asNode(node2).name AS Person2, similarity
ORDER BY Person1
----

.Results
[opts="header"]
|===
| Person1 | Person2 | similarity
| Alice   | Carol   | 0.3333333333333333
| Bob     | Alice   | 0.6666666666666666
| Carol   | Alice   | 0.3333333333333333
| Dave    | Carol   | 0.3333333333333333
3+|4 rows
|===


[[algorithms-node-similarity-examples-limits-topn-bottomn]]
==== topN and bottomN

TopN and bottomN limit the number of similarity scores across all nodes.
This is a limit on the total result set, in addition to the topK or bottomK limit on the results per node.
For topN, the N largest similarity scores are returned.
For bottomN, the N smallest similarity scores are returned.
A value of 0 means no global limit is imposed and all results from topK or bottomK are returned.

.The following will load the graph, run the algorithm, and stream the 3 highest out of the top 1 results per node:
[source, cypher]
----
CALL gds.nodeSimilarity.stream({
  nodeProjection: 'Person | Instrument',
  relationshipProjection: 'LIKES',
  direction: 'OUTGOING',
  topK: 1,
  topN: 3
})
YIELD node1, node2, similarity
RETURN gds.util.asNode(node1).name AS Person1, gds.util.asNode(node2).name AS Person2, similarity
ORDER BY similarity DESC, Person1, Person2
----

.Results
[opts="header"]
|===
| Person1 | Person2 | similarity
| "Alice" | "Dave"  | 1.0
| "Dave"  | "Alice" | 1.0
| "Bob"   | "Alice" | 0.6666666666666666
3+|3 rows
|===


[[algorithms-node-similarity-examples-degree-similarity-cutoff]]
=== Degree cutoff and similarity cutoff

Degree cutoff is a lower limit on the node degree for a node to be considered in the comparisons.
This value can not be lower than 1.

.The following will ignore nodes with less than 3 LIKES relationships:
[source, cypher]
----
CALL gds.nodeSimilarity.stream({
  nodeProjection: 'Person | Instrument',
  relationshipProjection: 'LIKES',
  direction: 'OUTGOING',
  degreeCutoff: 3
})
YIELD node1, node2, similarity
RETURN gds.util.asNode(node1).name AS Person1, gds.util.asNode(node2).name AS Person2, similarity
ORDER BY Person1
----

.Results
[opts="header"]
|===
| Person1 | Person2 | similarity
| "Alice" | "Dave"  | 1.0
| "Dave"  | "Alice" | 1.0
3+|2 rows
|===

Similarity cutoff is a lower limit for the similarity score to be present in the result.
The default value is very small (`1E-42`) to exclude results with a similarity score of 0.

[NOTE]
====
Setting similarity cutoff to 0 may yield a very large result set, increased runtime and memory consumption.
====

.The following will ignore node pairs with a similarty score less than 0.5:
[source, cypher]
----
CALL gds.nodeSimilarity.stream({
  nodeProjection: 'Person | Instrument',
  relationshipProjection: 'LIKES',
  direction: 'OUTGOING',
  similarityCutoff: 0.5
})
YIELD node1, node2, similarity
RETURN gds.util.asNode(node1).name AS Person1, gds.util.asNode(node2).name AS Person2, similarity
ORDER BY Person1
----

.Results
[opts="header"]
|===
| Person1 | Person2 | similarity
| "Alice" | "Dave"  | 1.0
| "Alice" | "Bob"   | 0.6666666666666666
| "Bob"   | "Dave"  | 0.6666666666666666
| "Bob"   | "Alice" | 0.6666666666666666
| "Dave"  | "Alice" | 1.0
| "Dave"  | "Bob"   | 0.6666666666666666
3+|6 rows
|===

