[[migration-node-sim]]
= Node Similarity

.Changes in Configuration
[opts=header,cols="1a,1a"]
|===
|Graph Algorithms v3.5 |Graph Data Science v1.0
| `direction`                         | -
| `concurrency`                       | `concurrency`
| `readConcurrency`                   | `readConcurrency` footnote:node-sim-read[Only when using anonymous graph]
| `writeConcurrency`                  | `writeConcurrency` footnote:node-sim-write[Only for `write` mode]
| `topK`                              | `topK`
| `bottomK`                           | `bottomK`
| `topN`                              | `topN`
| `bottomN`                           | `bottomN`
| `similarityCutoff`                  | `similarityCutoff`
| `degreeCutoff`                      | `degreeCutoff`
| `writeProperty`                     | `writeProperty` footnote:node-sim-write[]
| `writeRelationshipType`             | `writeRelationshipType` footnote:node-sim-write[]
| `write`                             | -
| `graph`                             | -
|===

.Changes in YIELD fields
[opts=header,cols="1a,1a"]
|===
|Graph Algorithms v3.5 |Graph Data Science v1.0
| `loadMillis`             | `createMillis`
| `computeMillis`          | `computeMillis`
| `writeMillis`            | `writeMillis`
| `postProcessingMillis`   | `postProcessingMillis`
| `node1`                  | `node1` footnote:node-sim-stream-yield[Only for `stream` mode]
| `node2`                  | `node2` footnote:node-sim-stream-yield[]
| `similarity`             | `similarity` footnote:node-sim-stream-yield[]
| `nodesCompared`          | `nodesCompared` footnote:node-sim-write-yield[Only for `write` mode]
| `relationships`          | `relationshipsWritten` footnote:node-sim-write-yield[]
| `write`                  | -
| -                      | `similarityDistribution`
| -                      | `configuration` footnote:node-sim-gds-config[The configuration used to run the algorithm]
| `writeProperty` footnote:node-sim-config[Inlined into `configuration`]         | -
| `writeRelationshipType` footnote:node-sim-config[]         | -
| `min, max, mean, p50, p75, p90, p95, p99, p999` footnote:node-sim-distribution[Inlined into `similarityDistribution`] | -
|===

.Node Similarity Stream Mode
[opts=header,cols="1a,1a"]
|===
|Graph Algorithms v3.5 |Graph Data Science v1.0
2+| Minimalistic streaming over named graph:
|
[source, cypher]
----
CALL algo.nodeSimilarity.stream(null, null, {graph: 'myGraph'})
YIELD node1, node2, similarity
----
|
[source, cypher]
----
CALL gds.nodeSimilarity.stream('myGraph')
YIELD node1, node2, similarity
----
2+| Streaming over named graph using `topK` and `similarityCutoff` configuration properties:
|
[source, cypher]
----
CALL algo.nodeSimilarity.stream(
  null,
  null,
  {
    graph: 'myGraph',
    topK: 1,
    similarityCutoff: 0.5
  }
)
----
|
[source, cypher]
----
CALL gds.nodeSimilarity.stream(
  'myGraph',
  {
    topK: 1,
    similarityCutoff: 0.5
  }
)
----
2+| Streaming over named graph using `bottomK` configuration property:
|
[source, cypher]
----
CALL algo.nodeSimilarity.stream(
  null,
  null,
  {
    graph: 'myGraph',
    bottomK: 15
  }
)
----
|
[source, cypher]
----
CALL gds.nodeSimilarity.stream(
  'myGraph',
  {
    bottomK: 15
  }
)
----
2+| Minimalistic streaming over anonymous graph:
|
[source, cypher]
----
CALL algo.nodeSimilarity.stream(
  'MyLabel',
  'MY_RELATIONSHIP_TYPE'
)
----
|
[source, cypher]
----
CALL gds.nodeSimilarity.stream({
  nodeProjection: 'MyLabel',
  relationshipProjection: 'MY_RELATIONSHIP_TYPE'
})
----
2+| Streaming over anonymous graph using `REVERSE` relationship projection:
|
[source, cypher]
----
CALL algo.nodeSimilarity.stream(
  'MyLabel',
  'MY_RELATIONSHIP_TYPE',
  { direction: 'INCOMING' }
)
----
|
[source, cypher]
----
CALL gds.nodeSimilarity.stream({
  nodeProjection: 'MyLabel',
  relationshipProjection: {
    MY_RELATIONSHIP_TYPE: {
      orientation: 'REVERSE'
    }
  }
})
----
2+| Streaming over anonymous graph using two way relationships footnote:[Not algorithm specific. This is also possible with named graphs. The graph has to be created with two relationship projections - one for each direction]:
|
[source, cypher]
----
CALL algo.nodeSimilarity.stream(
  'MyLabel',
  'MY_RELATIONSHIP_TYPE',
  { direction: 'BOTH' }
)
----
|
[source, cypher]
----
CALL gds.nodeSimilarity.stream({
  nodeProjection: 'MyLabel',
  relationshipProjection: {
    MY_RELATIONSHIP_TYPE_NATURAL: {
      type: 'MY_RELATIONSHIP_TYPE',
      orientation: 'NATURAL'
    },
    MY_RELATIONSHIP_TYPE_REVERSE: {
      type: 'MY_RELATIONSHIP_TYPE',
      orientation: 'REVERSE'
    }
  }
})
----
|===

.Node Similarity Write Mode
[opts=header,cols="1a,1a"]
|===
|Graph Algorithms v3.5 |Graph Data Science v1.0
2+|Minimalistic `write` with `writeRelationshipType` and `writeProperty`:
|
[source, cypher]
----
CALL algo.nodeSimilarity(
  null,
  null,
  {
    graph: 'myGraph',
    writeRelationshipType: 'MY_WRITE_REL_TYPE',
    writeProperty: 'myWriteProperty',
    write: true
  }
)
YIELD
  nodesCompared,
  relationships,
  writeMillis,
  iterations,
  p1,
  writeProperty
----
|
[source, cypher]
----
CALL gds.nodeSimilarity.write(
  'myGraph',
  {
    writeRelationshipType: 'MY_WRITE_REL_TYPE',
    writeProperty: 'myWriteProperty'
  }
)
YIELD
  nodesCompared,
  relationships,
  writeMillis,
  ranIterations,
  similarityDistribution AS sd,
  configuration AS conf
RETURN
  nodesCompared,
  relationships,
  writeMillis,
  ranIterations,
  sd.p1 AS p1,
  conf.writeProperty AS writeProperty
----
2+| Memory estimation of the algorithm:
|
[source, cypher]
----
CALL algo.memrec(
  'MyLabel',
  'MY_RELATIONSHIP_TYPE',
  'nodeSimilarity',
  {
    writeRelationshipType: 'MY_WRITE_REL_TYPE',
    writeProperty: 'myWriteProperty',
    write: true
  }
)
----
|
[source, cypher]
----
CALL gds.nodeSimilarity.write.estimate(
  {
    nodeProjection: 'MyLabel',
    relationshipProjection: 'MY_RELATIONSHIP_TYPE',
    writeRelationshipType: 'MY_WRITE_REL_TYPE',
    writeProperty: 'myWriteProperty'
  }
)
----
|===