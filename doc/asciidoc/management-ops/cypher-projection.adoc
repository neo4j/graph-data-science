// tag::overview[]
[[cypher-projection]]
= Cypher projection

[abstract]
--
This chapter explains how to create a graph using a Cypher projection.
--

If the <<native-projection, Native projection>> is not expressive enough to describe the in-memory graph, we can instead use Cypher queries to select nodes and relationships.
One benefit of using Cypher queries is the possibility to form the graph from data that exists only at query time.
A common use case is the reduction of paths into single relationships between the start and end node of the path.

.The following query reduces a 2-hop path to a single relationship effectively representing co-authors:
[source,cypher]
----
MATCH (p1:Author)-[:WROTE]->(a:Article)<-[:WROTE]-(p2:Author)
RETURN id(p1) AS source, id(p2) AS target, count(a) AS weight
----

Cypher projections are especially useful during the development phase.
Their flexibility is convenient when exploring data and algorithms, and designing a workflow.
However, creating a graph from a Cypher projection can be significantly slower than creating it directly from the Neo4j store files.
For production, it is recommended to adapt the domain model in a way that it can take advantage of the loading speed of <<native-projection, native projections>>.

This section includes:

* <<cypher-projection-syntax, Syntax>>
* <<cypher-projection-query-constraints, Cypher query constraints>>
* <<cypher-projection-relationship-types, Relationship types>>
* <<cypher-projection-relationship-aggregation, Relationship aggregation>>


[[cypher-projection-syntax]]
== Syntax

A Cypher projection takes three mandatory arguments: `graphName`, `nodeQuery` and `relationshipQuery`.
In addition, the optional `configuration` parameter allows us to further configure graph creation.

[source,cypher]
----
CALL gds.graph.create.cypher(
    graphName: String,
    nodeQuery: String,
    relationshipQuery: String,
    configuration: Map
)
----

.Parameters
[opts="header",cols="1,1,1"]
|===
| Name              | Optional | Description
| graphName         | no       | The name under which the graph is stored in the catalog.
| nodeQuery         | no       | Cypher query to project nodes.
| relationshipQuery | no       | Cypher query to project relationships.
| configuration     | yes      | Additional parameters to configure the Cypher projection.
|===

.Configuration
[opts="header",cols="1,1,1,4"]
|===
| Name                   | Type    | Default        | Description
| readConcurrency        | Integer | 4              | The number of concurrent threads used for creating the graph.
| nodeProperties         | Map     | empty map      | Mappings between the RETURN items and node properties in the graph projection.
| relationshipProperties | Map     | empty map      | Mappings between the RETURN items and relationship properties in the graph projection.
| parameters             | Map     | empty map      | A map of user-defined query parameters that are passed into the node and relationship query.
|===


[[cypher-projection-query-constraints]]
== Query constraints

The node query projects nodes and optionally their properties to an in-memory graph.
Each row in the query result represents a node in the projected graph.

The query result must contain a column called `id`.
The value in that column is used to uniquely identify the node.

.Simple example of a node query used for Cypher projection.
[source,cypher]
----
MATCH (n) RETURN id(n) AS id
----

The relationship query projects relationships and optionally their type and properties to an in-memory graph.
Each row in the query result represents a relationship in the projected graph.

The query result must contain a column called `source` and a column called `target`.
The values in those columns represent the source node id and the target node id of the relationship.
The values are used to connect the relationships to the nodes selected by the node query.
If either the source or the target value can not be mapped to a node, the relationship is not projected.

.Simple example of a relationship query used for Cypher projection.
[source,cypher]
----
MATCH (n)-->(m) RETURN id(n) AS source, id(m) AS target
----

Using both example queries in a Cypher projection, we can project the whole Neo4j graph into an in-memory graph and store it in the catalog:

[source,cypher]
----
CALL gds.graph.create.cypher(
    'my-cypher-graph',
    'MATCH (n) RETURN id(n) AS id',
    'MATCH (n)-->(m) RETURN id(n) AS source, id(m) AS target'
)
----


[NOTE]
====
Cypher projections allow creating graphs from arbitrary query results, regardless of whether these map to actual identifiers in the Neo4j graph.
*Executing an algorithm on such a graph in `write` mode may lead to unexpected changes in the Neo4j database.*
====


[[cypher-projection-properties]]
== Node and relationship properties

Similar to the default native projection, we can load node and relationship properties using a Cypher projection.

Both node and relationship queries must return their respective mandatory columns, i.e., `id`, `source` and `target`.
If a query returns additional columns, those columns are used as node and relationship properties, respectively.

The values stored in property columns need to be numeric.
If a value is `null` a default value (`Double.NaN`) is loaded instead.
If we want to use a different default value, the `coalesce` function can be used.

The following Cypher projection loads multiple node and relationship properties:

.Projecting query columns into node and relationship properties.
[source,cypher]
----
CALL gds.graph.create.cypher(
    'my-cypher-graph',
    'MATCH (n:City) RETURN id(n) AS id, n.stateId AS community, n.population AS population',
    'MATCH (n:City)-[r:ROAD]->(m:City) RETURN id(n) AS source, id(m) AS target, r.distance AS distance, coalesce(r.condition, 1.0) AS quality'
)
----

The projected properties can be referred to by any algorithm that uses properties as input, for example, <<algorithms-label-propagation, Label Propagation>>.

[source,cypher]
----
CALL gds.labelPropagation.stream(
    'my-cypher-graph', {
        seedProperty: 'community',
        relationshipWeightProperty: 'quality'
    }
)
----


[[cypher-projection-relationship-types]]
== Relationship types

The native projection supports loading multiple relationship types which can be filtered in an individual algorithm execution.
The Cypher projection can achieve the same feature by returning the relationship type in the query.
If the `type` column is present in the query result, we use the values in that column to distinguish relationship types.

For the following example, let's assume `City` nodes to be connected by either `ROAD` or `RAIL` relationships.

.Using the `type` column to distinguish between multiple relationship types.
[source,cypher]
----
CALL gds.graph.create.cypher(
    'my-cypher-graph',
    'MATCH (n:City) RETURN id(n) AS id',
    'MATCH (n:City)-[r:ROAD|RAIL]->(m:City) RETURN id(n) AS source, id(m) AS target, type(r) AS type'
)
----

The loaded graph will be composed of the two relationship types.
This allows us to apply a relationship filter during algorithm execution:

.Using a relationship filter to run the algorithm on a subgraph.
[source,cypher]
----
CALL gds.labelPropagation.stream(
    'my-cypher-graph', {
        relationshipTypes: ['ROAD']
    }
)
----


[[cypher-projection-relationship-aggregation]]
== Relationship aggregation

The property graph model supports parallel relationships, which means two nodes can be connected by multiple relationships of the same relationship type.
For some algorithms, we want the projected graph to contain at most one relationship between two nodes.

The simplest way to achieve this is to use the `DISTINCT` operator in the relationship query:

[source,cypher]
----
MATCH (n:City)-[r:ROAD]->(m:City)
RETURN DISINCT id(n) AS source, id(m) AS target
----

If we also want to load relationship properties, aggregating the values of parallel edges can also be achieved using Cypher.

[source,cypher]
----
MATCH (n:City)-[r:ROAD]->(m:City)
RETURN
    id(n) AS source,
    id(m) AS target,
    min(r.distance) AS minDistance,
    max(coalesce(r.condition, 1.0), 1.0) AS maxQuality
----

One drawback of that approach is that we put more pressure on the Cypher execution engine and the query result consumes additional memory.
An alternative approach is to use `relationshipProperties` as part of the optional configuration map.
The syntax is identical to the property mappings used in the native projection.

[source,cypher]
----
CALL gds.graph.create.cypher(
    'my-cypher-graph',
    'MATCH (n:City) RETURN id(n) AS id, n.stateId AS community, n.population AS population',
    'MATCH (n:City)-[r:ROAD]->(m:City) RETURN id(n) AS source, id(m) AS target, r.distance AS distance, r.condition AS quality',
    {
        relationshipProperties: {
            minDistance: {
                property: 'distance',
                aggregation: 'MIN',
                defaultValue: 42.0
            },
            maxQuality: {
                property: 'quality',
                aggregation: 'MAX',
                defaultValue: 1.0
            }
        }
    }
)
----

* The key of each mapping is the name under which the resulting property is stored in the graph.
* The `property` entry refers to the column name in the query result.
* The `aggregation` entry sets the aggregation function for values of parallel relationships (e.g. `MIN` or `MAX`).
* The `defaultValue` entry is used if the cell contains `null` (instead of `Double.NAN`).


[[cypher-projection-parameters]]
== Using query parameters

Similar to https://neo4j.com/docs/cypher-manual/current/syntax/parameters/[Cypher], it is also possible to set query parameters.
In the following example we supply a list of strings to limit the cities we want to project.

[source,cypher]
----
CALL gds.graph.create.cypher(
    'my-cypher-graph',
    'MATCH (n:City) WHERE n.name IN $cities RETURN id(n) AS id',
    'MATCH (n:City)-[r:ROAD]->(m:City) RETURN id(n) AS source, id(m) AS target',
    {
       parameters: { cities: ["Leipzig", "Malm√∂"] }
    }
)
----
// end::overview[]

// tag::explanation[]
If node label and relationship type are not selective enough to create the graph projection to run the algorithm on, you can use Cypher queries to project your graph.
This can also be used to run algorithms on a virtual graph.
You can learn more in the <<cypher-projection>> section of the manual.
// end::explanation[]

// tag::similarity-explanation[]
If the similarity lists are very large they can take up a lot of memory.
For cases where those lists contain lots of values that should be skipped, you can use the less memory-intensive approach of using Cypher statements to project the graph instead.

The Cypher loader expects to receive 3 fields:

* `item` - should contain node ids, which we can return using the `id` function.
* `category` - should contain node ids, which we can return using the `id` function.
* `weight` - should contain a double value.
// end::similarity-explanation[]