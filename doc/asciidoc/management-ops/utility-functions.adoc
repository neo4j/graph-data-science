[[utility-functions]]
= Utility functions

[abstract]
--
This section provides explanations and examples for each of the utility functions in the Neo4j Graph Data Science library.
--

[[utility-functions-system]]
== System Functions

[[system-functions-table]]
[opts=header,cols="1m,1"]
|===
| Name        | Description
| gds.version | Return the version of the installed Neo4j Graph Data Science library.
|===

[role=query-example]
--
.Usage:
[source, cypher, role=noplay]
----
RETURN gds.version() AS version
----

.Results
[opts="header"]
|===
| version
| "1.5.3"
|===
--


[[utility-functions-numeric]]
== Numeric Functions

.Numeric Functions
[[Numeric-functions-table]]
[opts=header,cols="1m,1"]
|===
| Name                | Description
| gds.util.NaN        | Returns NaN as a Cypher value.
| gds.util.infinity   | Return infinity as a Cypher value.
| gds.util.isFinite   | Return true iff the given argument is a finite value (not ±Infinity, NaN, or null).
| gds.util.isInfinite | Return true iff the given argument is not a finite value (not ±Infinity, NaN, or null).
|===


=== Syntax

[[Numeric-functions-syntax-table]]
[opts=header,cols="1m,1"]
|===
| Name                                  | Parameter
| gds.util.NaN()                        | -
| gds.util.infinity()                   | -
| gds.util.isFinite(value: NUMBER)      | value to be checked if it is finite
| gds.util.isInfinite(value: NUMBER)    | value to be checked if it is infinite.
|===


=== Examples

[role=query-example]
--
.Example for gds.util.IsFinite:
[source, cypher, role=noplay]
----
UNWIND [1.0, gds.util.NaN(), gds.util.infinity()] AS value
RETURN gds.util.isFinite(value) AS isFinite
----

.Results
[opts="header"]
|===
| isFinite
| true
| false
| false
|===
--

[role=query-example]
--
.Example for gds.util.isInfinite():
[source, cypher, role=noplay]
----
UNWIND [1.0, gds.util.NaN(), gds.util.infinity()] AS value
RETURN gds.util.isInfinite(value) AS isInfinite
----

.Results
[opts="header"]
|===
| isInfinite
| false
| true
| true
|===
--

The utility function `gds.util.NaN` can be used as an default value for input parameters, as shown in the examples of <<algorithms-similarity-cosine-procedure-sample, cosine similarity>>.
A common usage of `gds.util.IsFinite` and `gds.util.IsInfinite` is for filtering streamed results, as for instance seen in the examples of <<algorithm-all-pairs-shortest-path-sample, `gds.alpha.allShortestPaths`>>.


[[utility-functions-node-path]]
== Node and Path Functions

.Node and Path Functions
[[Node-and-Path-functions-table]]
[opts=header,cols="1m,1m"]
|===
| Name             | Description
| gds.util.asNode  | Return the node object for the given node id or null if none exists.
| gds.util.asNodes | Return the node objects for the given node ids or an empty list if none exists.
|===


=== Syntax

[[Node-and-Path-functions-syntax-table]]
[opts=header,cols="1m,1"]
|===
| Name                                | Parameters
| gds.util.asNode(nodeId: NUMBER)     | nodeId of a node in the neo4j-graph
| gds.util.asNodes(nodeIds: NUMBER[]) | list of nodeIds of nodes in the neo4j-graph
|===


=== Examples

Consider the graph created by the following Cypher statement:

.Example graph:
[source, cypher, role=noplay setup-query]
----
CREATE  (nAlice:User {name: 'Alice'})
CREATE  (nBridget:User {name: 'Bridget'})
CREATE  (nCharles:User {name: 'Charles'})
CREATE  (nAlice)-[:LINK]->(nBridget)
CREATE  (nBridget)-[:LINK]->(nCharles)
----

[role=query-example]
--
.Example for gds.util.asNode:
[source, cypher, role=noplay]
----
MATCH (u:User{name: 'Alice'})
WITH id(u) AS nodeId
RETURN gds.util.asNode(nodeId).name AS node
----

.Results
[opts="header"]
|===
| node
| "Alice"
|===
--

[role=query-example]
--
.Example for gds.util.asNodes:
[source, cypher, role=noplay]
----
MATCH (u:User)
WHERE NOT u.name = 'Charles'
WITH collect(id(u)) AS nodeIds
RETURN [x in gds.util.asNodes(nodeIds)| x.name] AS nodes
----

.Results
[opts="header"]
|===
| nodes
| [Alice, Bridget]
|===
--

As many algorithms streaming mode only return the node id, `gds.util.asNode` and `gds.util.asNodes` can be used to retrieve the whole node from the neo4j database.


[[utility-functions-catalog]]
== Catalog Functions

Catalog functions allow accessing in-memory graphs directly from a Cypher query.

.Catalog Functions
[opts=header,cols="1m,1"]
|===
| Name                  | Description
| gds.util.nodeProperty | Allows accessing a node property stored in a named graph.
|===


=== Syntax

[opts=header,cols="1m,1"]
|===
| Name                                                                                               | Description
| gds.util.nodeProperty(graphName: STRING, nodeId: INTEGER, propertyKey: STRING, nodeLabel: STRING?) | Named graph in the catalog, Neo4j node id, node property key and optional node label present in the named-graph.
|===

If a node label is given, the property value for the corresponding projection and the given node is returned.
If no label or `'*'` is given, the property value is retrieved and returned from an arbitrary projection that contains the given propertyKey.
If the property value is missing for the given node, `null` is returned.


=== Examples

.Create a graph in the catalog:
[source, cypher, role=noplay graph-create-query]
----
CALL gds.graph.create('my-graph', 'User', 'LINK');
----

.Run an algorithm that updates the named-graph:
[source, cypher, role=noplay graph-create-query]
----
CALL gds.pageRank.mutate('my-graph', { mutateProperty: 'score' })
----

We can now access the property `score` without writing the data to Neo4j.

[role=query-example]
--
.Access a property node property for Alice:
[source, cypher, role=noplay]
----
MATCH (alice:User)
WHERE alice.name = 'Alice'
RETURN
  alice.name AS name,
  gds.util.nodeProperty('my-graph', id(alice), 'score') AS score
----

.Results
[opts="header",cols="2"]
|===
| name    | score
| "Alice" | 0.15000000000000002
|===
--

We can also specifically return the `score` property from the `User` projection in case other projections also have a `score` property as follows.

[role=query-example]
--
.Access a property node property from User for Alice:
[source, cypher, role=noplay]
----
MATCH (alice:User)
WHERE alice.name = 'Alice'
RETURN
  alice.name AS name,
  gds.util.nodeProperty('my-graph', id(alice), 'score', 'User') AS score
----

.Results
[opts="header",cols="2"]
|===
| name    | score
| "Alice" | 0.15000000000000002
|===
--
