[[utility-functions]]
= Utility functions

This section describes the utility functions and shows their usage in concrete examples.

[[utility-functions-table]]
[opts=header,cols="1m,1m"]
|===
| Name                | Description
| gds.version         | Return the version of the installed Neo4j Graph Data Science library.
|===

[role=query-example]
--
.Usage:
[source, cypher]
----
RETURN gds.version() AS version
----

.Results
[opts="header",cols="1"]
|===
| version
| "1.1.0-alpha03"
|===
--

== Numeric Functions

.Numeric Functions
[[Numeric-functions-table]]
[opts=header,cols="1m,1m"]
|===
| Name                | Description
| gds.util.NaN        | Returns NaN as a Cypher value.
| gds.util.infinity   | Return infinity as a Cypher value.
| gds.util.isFinite   | Return true iff the given argument is a finite value (not ±Infinity, NaN, or null).
| gds.util.isInfinite | Return true iff the given argument is not a finite value (not ±Infinity, NaN, or null).
|===

=== Syntax

[[Numeric-functions-syntax-table]]
[opts=header,cols="1m,1m"]
|===
| Name                                  | Parameter
| gds.util.NaN()                        | -
| gds.util.infinity()                   | -
| gds.util.isFinite(value: NUMBER)      | value to be checked if it is finite
| gds.util.isInfinite(value: NUMBER)    | value to be checked if it is infinite.
|===

=== Examples

[role=query-example]
--
.Example for gds.util.IsFinite:
[source, cypher]
----
UNWIND [1.0, gds.util.NaN(), gds.util.infinity()] AS value
RETURN gds.util.isFinite(value) AS isFinite
----

.Results
[opts="header",cols="1"]
|===
| isFinite
| true
| false
| false
|===
--

[role=query-example]
--
.Example for gds.util.isInfinite():
[source, cypher]
----
UNWIND [1.0, gds.util.NaN(), gds.util.infinity()] AS value
RETURN gds.util.isInfinite(value) AS isInfinite
----

.Results
[opts="header",cols="1"]
|===
| isInfinite
| false
| true
| true
|===
--

The utility function `gds.util.NaN` can be used as an default value for input parameters, as shown in the examples of <<algorithms-similarity-cosine-procedure-sample, cosine similarity>>.
A common usage of `gds.util.IsFinite` and `gds.util.IsInfinite` is for filtering streamed results, as for instance seen in the examples of <<algorithm-all-pairs-shortest-path-sample, `gds.alpha.allShortestPaths`>>.

== Node and Path Functions

.Node and Path Functions
[[Node-and-Path-functions-table]]
[opts=header,cols="1m,1m"]
|===
| Name                | Description
| gds.util.asNode     | Return the node object for the given node id or null if none exists.
| gds.util.asNodes    | Return the node objects for the given node ids or an empty list if none exists.
|===

=== Syntax

[[Node-and-Path-functions-syntax-table]]
[opts=header,cols="1m,1m"]
|===
| Name                                                                                      | Parameters
| gds.util.asNode(nodeId: NUMBER)                                                           | nodeId of a node in the neo4j-graph
| gds.util.asNodes(nodeIds: NUMBER[])                                                       | list of nodeIds of nodes in the neo4j-graph
|===

=== Examples

Consider the graph created by the following Cypher statement:

.Example graph:
[source, cypher, role=setup-query]
----
CREATE  (nAlice:User {name: 'Alice'})
CREATE  (nBridget:User {name: 'Bridget'})
CREATE  (nCharles:User {name: 'Charles'})
CREATE  (nAlice)-[:LINK]->(nBridget)
CREATE  (nBridget)-[:LINK]->(nCharles)
----

[role=query-example]
--
.Example for gds.util.asNode:
[source, cypher]
----
MATCH (u:User{name: 'Alice'})
WITH id(u) AS nodeId
RETURN gds.util.asNode(nodeId).name AS node
----

.Results
[opts="header",cols="1"]
|===
| node
| "Alice"
|===
--

[role=query-example]
--
.Example for gds.util.asNodes:
[source, cypher]
----
MATCH (u:User)
WHERE NOT u.name = 'Charles'
WITH collect(id(u)) AS nodeIds
RETURN [x in gds.util.asNodes(nodeIds)| x.name] AS nodes
----

.Results
[opts="header",cols="1"]
|===
| nodes
| [Alice, Bridget]
|===
--

As many algorithms streaming mode only return the node id, `gds.util.asNode` and `gds.util.asNodes` can be used to retrieve the whole node from the neo4j database.
