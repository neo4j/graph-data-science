[[catalog-graph-create-subgraph]]
[.beta]
= Creating a subgraph

In GDS, algorithms can be executed on a named graph that has been filtered based on its <<common-configuration-node-labels,node labels>> and <<common-configuration-relationship-types, relationship types>>.
However, that filtered graph only exists during the execution of the algorithm and it is not possible to filter on property values.
If a filtered graph needs to be used multiple times, one can use the subgraph catalog procedure to create a new graph in the graph catalog.

The filter predicates in the subgraph procedure can take labels, relationship types as well as node and relationship properties into account.
The new graph can be used in the same way as any other in-memory graph in the catalog.
Creating subgraphs of subgraphs is also possible.

.A new graph can be created by using the `gds.beta.graph.create.subgraph()` procedure:
[source, cypher, role=noplay]
----
CALL gds.beta.graph.subgraph(
  graphName: String,
  fromGraphName: String,
  nodeFilter: String,
  relationshipFilter: String,
  configuration: Map
) YIELD
  graphName,
  fromGraphName,
  nodeFilter,
  relationshipFilter,
  nodeCount,
  relationshipCount,
  createMillis;
----

.Subgraph specific configuration
[opts="header",cols="1,1,4"]
|===
| Name               | Type    | Description
| graphName          | String  | The name of the new graph that is stored in the graph catalog.
| fromGraphName      | String  | The name of the original graph in the graph catalog.
| nodeFilter         | String  | A Cypher predicate for filtering nodes in the input graph. `*` can be used to allow all nodes.
| relationshipFilter | String  | A Cypher predicate for filtering relationships in the input graph.  `*` can be used to allow all relationships.
|===

.Subgraph specific configuration
[opts="header",cols="1,1,1m,1,5"]
|===
| Name               | Type    | Default | Optional | Description
| concurrency        | Integer | 4       | yes      | The number of concurrent threads used for filtering the graph.
|===

.Results
[opts="header",cols="1,1,4"]
|===
| Name                   | Type     | Description
| graphName              | String   | The name of the new graph that is stored in the graph catalog.
| fromGraphName          | String   | The name of the original graph in the graph catalog.
| nodeCount              | Integer  | Number of nodes in the subgraph.
| relationshipCount      | Integer  | Number of relationships in the subgraph.
| createMillis           | Integer  | Milliseconds for creating the subgraph.
| configuration          | Map      | The configuration used for running the procedure.
|===

The `nodeFilter` and `relationshipFilter` configuration keys can be used to express filter predicates.
Filter predicates are https://neo4j.com/docs/cypher-manual/current/clauses/where/#query-where[Cypher] predicates bound to a single entity.
An entity is either a node or a relationship.
The filter predicate always needs to evaluate to `true` or `false`.
A node is contained in the subgraph if the node filter evaluates to `true`.
A relationship is contained in the subgraph if the relationship filter evaluates to `true` and its source and target nodes are contained in the subgraph.

A predicate is a combination of expressions.
The simplest form of expression is a literal.
GDS currently supports the following literals:

* float literals, e.g., `13.37`
* integer literals, e.g., `42`
* boolean literals, i.e., `TRUE` and `FALSE`

Property, label and relationship type expressions are bound to an entity.
The node entity is always identified by the variable `n`, the relationship entity is identified by `r`.
Using the variable, we can refer to:

* node label expression, e.g., `n:Person`
* relationship type expression, e.g., `r:KNOWS`
* node property expression, e.g., `n.age`
* relationship property expression, e.g., `r.since`

Boolean predicates combine two expressions and return either `true` or `false`.
GDS supports the following boolean predicates:

* greater/lower than, such as `n.age > 42` or `r.since < 1984`
* greater/lower than or equal, such as `n.age > 42` or `r.since < 1984`
* equality, such as `n.age = 23` or `r.since = 2020`
* logical operators, such as
  * `n.age > 23 AND n.age < 42`
  * `n.age = 23 OR n.age = 42`
  * `n.age = 23 XOR n.age = 42`
  * `n.age IS NOT 23`

Variable names that can be used within predicates are not arbitrary.
A node predicate must refer to variable `n`.
A relationship predicate must refer to variable `r`.


== Examples

.Example graph with Person nodes connected by KNOWS relationships.
[source, cypher, role=noplay setup-query]
----
CREATE
  (p0:Person { age: 16 }),
  (p1:Person { age: 18 }),
  (p2:Person { age: 20 }),
  (b0:Book   { isbn: 1234 }),
  (b1:Book   { isbn: 4242 }),
  (p0)-[:KNOWS { since: 2010 }]->(p1),
  (p0)-[:KNOWS { since: 2018 }]->(p2),
  (p0)-[:READS]->(b0),
  (p1)-[:READS]->(b0),
  (p2)-[:READS]->(b1)
----

.Create a graph using a Cypher projection:
[source, cypher, role=noplay graph-create-query]
----
CALL gds.graph.create.cypher(
  'social-graph',
  'MATCH (n) RETURN id(n) AS id, labels(n) AS labels, n.age AS age',
  'MATCH (n)-[r]->(m) RETURN id(n) AS source, id(m) AS target, r.since AS since, type(r) AS type'
)
YIELD graphName, nodeCount, relationshipCount, createMillis;
----

[role=query-example]
--
.Create a new graph containing only users of a certain age group:
[source, cypher, role=noplay]
----
CALL gds.beta.graph.create.subgraph('teenagers', 'social-graph', 'n.age > 13 AND n.age <= 18', '*')
YIELD graphName, fromGraphName, nodeCount, relationshipCount;
----

.Results
[opts="header"]
|===
| graphName    | fromGraphName    | nodeCount | relationshipCount
| "teenagers"  | "social-graph"   | 2         | 1
|===
--

[role=query-example]
--
.Create a new graph containing only users of a certain age group that know each other since a given point a time:
[source, cypher, role=noplay]
----
CALL gds.beta.graph.create.subgraph('teenagers', 'social-graph', 'n.age > 13 AND n.age <= 18', 'r.since >= 2012')
YIELD graphName, fromGraphName, nodeCount, relationshipCount;
----
.Results
[opts="header"]
|===
| graphName    | fromGraphName    | nodeCount | relationshipCount
| "teenagers"  | "social-graph"   | 2         | 0
|===
--

[role=query-example]
--
.Create a new bipartite graph between books and users connected by the `READS` relationship type:
[source, cypher, role=noplay]
----
CALL gds.beta.graph.create.subgraph('teenagers-books', 'social-graph', 'n:Book OR n:Person', 'r:READS')
YIELD graphName, fromGraphName, nodeCount, relationshipCount;
----

.Results
[opts="header"]
|===
| graphName          | fromGraphName    | nodeCount | relationshipCount
| "teenagers-books"  | "social-graph"   | 5         | 3
|===
--

[role=query-example]
--
.The previous example can be extended with an additional filter applied only to persons:
[source, cypher, role=noplay]
----
CALL gds.beta.graph.create.subgraph('teenagers-books', 'social-graph', 'n:Book OR (n:Person AND n.age > 18)', 'r:READS')
YIELD graphName, fromGraphName, nodeCount, relationshipCount;
----

.Results
[opts="header"]
|===
| graphName    | fromGraphName    | nodeCount | relationshipCount
| "teenagers-books"  | "social-graph"   | 3         | 1
|===
--
