[[algorithms-k1coloring]]
= The K-1 Coloring algorithm

[abstract]
--
This section describes the K-1 Coloring algorithm in the Neo4j Graph Algorithms library.
--

This topic includes:

* <<algorithms-k1coloring-intro, Introduction>>
* <<algorithms-k1coloring-syntax, Syntax>>
* <<algorithms-k1coloring-examples, Examples>>


[[algorithms-k1coloring-intro]]
== Introduction

The K-1 Coloring algorithm assigns a color to every node in the graph, trying to optimize for two objectives:

1. To make sure that every neighbor of a given node has a different color than the node itself.
2. To use as few colors as possible.

Note that the graph coloring problem is proven to be NP-complete, which makes it intractable on anything but trivial graph sizes.
For that reason the implemented algorithm is a greedy algorithm.
Thus it is neither guaranteed that the result is an optimal solution, using as few colors as theoretically possible, nor does it always produce a correct result where no two neighboring nodes have different colors.
However the precision of the latter can be controlled by the number of iterations this algorithm runs.

For more information on this algorithm, see:

* https://arxiv.org/pdf/1205.3809.pdf[Çatalyürek, Ümit V., et al. "Graph coloring algorithms for multi-core and massively multithreaded architectures."^]
* https://en.wikipedia.org/wiki/Graph_coloring#Vertex_coloring

[NOTE]
====
Running this algorithm requires sufficient memory availability.
Before running this algorithm, we recommend that you read <<memory-requirements>>.
====


[[algorithms-k1coloring-syntax]]
== Syntax

.The following describes the API for running the algorithm and writing results back to Neo4j:
[source, cypher]
----
CALL gds.beta.k1coloring.write(graphName: STRING, {
  writeProperty: STRING
  // additional configuration
})
YIELD nodes, colorCount, ranIterations, didConverge, writeProperty, loadMillis, computeMillis, writeMillis
----

.Parameters
[opts="header",cols="1,1,1m,1,4"]
|===
| Name      | Type    | Default | Optional | Description
| graphName | string  | null    | yes      | The name of an existing graph on which to run the algorithm. If no graph name is provided, the config map must contain configuration for creating a graph.
| config    | map     | {}      | yes      | Additional configuration, see below.
|===

.Configuration
[opts="header",cols="1,1,1,1,4"]
|===
| Name                   | Type    | Default                | Optional | Description
| nodeProjection         | string  | null                   | yes      | The projection of nodes to use when creating the implicit graph.
| relationshipProjection | string  | null                   | yes      | The projection of relationships to use when creating the implicit graph.
| concurrency            | int     | 4                      | yes      | The number of concurrent threads used for running the algorithm. Also provides the default value for 'readConcurrency' and 'writeConcurrency'. This is dependent on the Neo4j edition; for more information, see <<system-requirements-cpu>>.
| readConcurrency        | int     | value of 'concurrency' | yes      | The number of concurrent threads used for reading the graph.
| writeConcurrency       | int     | value of 'concurrency' | yes      | The number of concurrent threads used for writing the result.
| maxIterations          | int     | 20                     | yes      | The maximum number of iterations of K1 Coloring to run.
| writeProperty          | string  | n/a                    | no       | The node property that stores the color.
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name          | Type    | Description
| nodes         | int     | The number of nodes considered.
| ranIterations | int     | The actual number of iterations the algorithm ran.
| didConverge   | boolean | An indicator of whether the algorithm found a correct coloring.
| colorCount    | int     | The number of colors used.
| writeProperty | string  | The node property that stores the color.
| write         | boolean | Specifies if the result was written back as a node property.
| loadMillis    | int     | Milliseconds for loading data.
| computeMillis | int     | Milliseconds for running the algorithm.
| writeMillis   | int     | Milliseconds for writing result data back to Neo4j.
|===

[[algorithms-k1coloring-syntax-stream]]
.The following describes the API for running the algorithm and stream results:
[source, cypher]
----
CALL gds.beta.k1coloring.stream(graphName: STRING, {
  // additional configuration
})
YIELD nodeId, color
----

.Parameters
[opts="header",cols="1,1,1m,1,4"]
|===
| Name      | Type    | Default | Optional | Description
| graphName | string  | null    | yes      | The name of an existing graph on which to run the algorithm. If no graph name is provided, the config map must contain configuration for creating a graph.
| config    | map     | {}      | yes      | Additional configuration, see below.
|===

.Configuration
[opts="header",cols="1,1,1,1,4"]
|===
| Name                   | Type    | Default                | Optional | Description
| nodeProjection         | string  | null                   | yes      | The projection of nodes to use when creating the implicit graph.
| relationshipProjection | string  | null                   | yes      | The projection of relationships to use when creating the implicit graph.
| concurrency            | int     | 4                      | yes      | The number of concurrent threads used for running the algorithm. Also provides the default value for 'readConcurrency' and 'writeConcurrency'. This is dependent on the Neo4j edition; for more information, see <<system-requirements-cpu>>.
| readConcurrency        | int     | value of 'concurrency' | yes      | The number of concurrent threads used for reading the graph.
| maxIterations          | int     | 20                     | yes      | The maximum number of iterations of K1 Coloring to run.
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name          | Type    | Description
| nodeId        | int     | The ID of the Node
| color         | int     | The color of the Node
|===


[[algorithms-k1coloring-examples]]
== Examples

Consider the graph created by the following Cypher statement:

[source, cypher]
----
CREATE (alice:User {name: 'Alice'}),
       (bridget:User {name: 'Bridget'}),
       (charles:User {name: 'Charles'}),
       (doug:User {name: 'Doug'}),

       (alice)-[:LINK]->(bridget),
       (alice)-[:LINK]->(charles),
       (alice)-[:LINK]->(doug),
       (bridget)-[:LINK]->(charles)
----

This graph has a super node with name "Alice" that connects to all other nodes.
It should therefore not be possible for any other node to be assigned to the color as the Alice node.
In the following examples we will demonstrate using the K-1 Coloring algorithm on this graph.

[[algorithms-k1coloring-examples-projection]]
=== Named graphs and Cypher projections

In the examples below, we will rely on the _implicit_ loading of graphs for the algorithm computation.
However, like other algorithms K-1 Coloring also accepts _named graphs_ and _Cypher projections_ as inputs.
See <<projected-graph-model, Projected Graph Model>> for more details.

.Using a named graph:
[source, cypher]
----
CALL gds.graph.create('myGraph', 'User', 'LINK');

CALL gds.beta.k1coloring.stream('myGraph')
YIELD nodeId, color
RETURN gds.util.asNode(nodeId).name AS name, color
ORDER BY name
----

.Results
[opts="header",cols="1m,1m"]
|===
| name      | color
| "Alice"   | 2
| "Bridget" | 1
| "Charles" | 0
| "Doug"    | 0
|===

.Using a Cypher projection:
[source, cypher]
----
CALL gds.beta.k1coloring.stream({
  nodeQuery: 'MATCH (n:User) RETURN id(n) AS id',
  relationshipQuery: 'MATCH (s:User)-[:LINK]->(t:User) RETURN id(s) AS source, id(t) AS target'
})
YIELD nodeId, color
RETURN gds.util.asNode(nodeId).name AS name, color
ORDER BY name
----

.Results
[opts="header",cols="1m,1m"]
|===
| name      | color
| "Alice"   | 2
| "Bridget" | 1
| "Charles" | 0
| "Doug"    | 0
|===

These results are identical to those of the named graph, as the Cypher projection we use mimics the behaviour of the default loading configuration.
Of course, the Cypher projection feature enables more advanced control over which exact parts of the graph to compute over; please see <<cypher-projection>> for more details.
