[[algorithms-label-propagation]]
= The Label Propagation algorithm

[abstract]
--
This section describes the Label Propagation algorithm in the Neo4j Graph Algorithms library.
--

The Label Propagation algorithm (LPA) is a fast algorithm for finding communities in a graph.
It detects these communities using network structure alone as its guide, and doesn't require a pre-defined objective function or prior information about the communities.

One interesting feature of LPA is that nodes can be assigned preliminary labels to narrow down the range of solutions generated.
This means that it can be used as semi-supervised way of finding communities where we hand-pick some initial communities.

This section includes:

* <<algorithms-label-propagation-intro, Introduction>>
* <<algorithms-label-propagation-syntax, Syntax>>
* <<algorithms-label-propagation-examples, Examples>>
** <<algorithms-label-propagation-examples-unweighted, Unweighted>>
** <<algorithms-label-propagation-examples-weighted, Weighted>>
** <<algorithms-label-propagation-examples-seeded, Seeded>>
** <<algorithms-label-propagation-examples-projection, Named graphs and Cypher projections>>
* <<algorithms-label-propagation-usage, Usage>>

[[algorithms-label-propagation-intro]]
== Introduction

LPA works by propagating labels throughout the network and forming communities based on this process of label propagation.

The intuition behind the algorithm is that a single label can quickly become dominant in a densely connected group of nodes, but will have trouble crossing a sparsely connected region.
Labels will get trapped inside a densely connected group of nodes, and those nodes that end up with the same label when the algorithms finish can be considered part of the same community.

The algorithm works as follows:

* Every node is initialized with a unique community label (an identifier).
* These labels propagate through the network.
* At every iteration of propagation, each node updates its label to the one that the maximum numbers of its neighbours belongs to.
  Ties are broken uniformly and randomly.
* LPA reaches convergence when each node has the majority label of its neighbours.
* LPA stops if either convergence or the user-defined maximum number of iterations is achieved.

As labels propagate, densely connected groups of nodes quickly reach a consensus on a unique label.
At the end of the propagation only a few labels will remain - most will have disappeared.
Nodes that have the same community label at convergence are said to belong to the same community.

For more information on this algorithm, see:

* https://arxiv.org/pdf/0709.2938.pdf["Near linear time algorithm to detect community structures in large-scale networks"^]
* Use cases:
** https://dl.acm.org/citation.cfm?id=2140465[Twitter polarity classification with label propagation over lexical links and the follower graph]
** https://www.nature.com/articles/srep12339[Label Propagation Prediction of Drug-Drug Interactions Based on Clinical Side Effects]
** https://www.uni-ulm.de/fileadmin/website_uni_ulm/iui.iwsds2017/papers/IWSDS2017_paper_12.pdf["Feature Inference Based on Label Propagation on Wikidata Graph for DST"]

[NOTE]
====
Running this algorithm requires sufficient memory availability.
Before running this algorithm, we recommend that you read <<memory-requirements>>.
====

[[algorithms-label-propagation-syntax]]
== Syntax

.The following will run the algorithm and write back results:
[source, cypher]
----
CALL gds.labelPropagation.write(graphNameOrConfig: STRING|MAP, configuration: MAP})
YIELD nodes, ranIterations, didConverge, createMillis, computeMillis, writeMillis
----


.Parameters
[opts="header",cols="1,1,1,1,4"]
|===
| Name              | Type    | Default        | Optional | Description
| graphNameOrConfig | string  | null           | no      | Either then name of a loaded graph or directly the config.
| configuration     | map     | n/a            | yes      | Additional configuration, optional if the first parameter was a map.
|===

.Configuration
[opts="header",cols="1,1,1,1,4"]
|===
| Name                          | Type    | Default                | Optional | Description
| maxIterations                 | int     | 10                     | yes      | The maximum number of iterations to run.
| concurrency                   | int     | available CPUs         | yes      | The number of concurrent threads used for running the algorithm. Also provides the default value for 'readConcurrency' and 'writeConcurrency'.
| readConcurrency               | int     | value of 'concurrency' | yes      | The number of concurrent threads used for reading the graph.
| writeConcurrency              | int     | value of 'concurrency' | yes      | The number of concurrent threads used for writing the result.
| nodeWeightProperty            | string  | null                   | yes      | The name of the node property that represents weight.
| relationshipWeightProperty    | string  | null                   | yes      | The name of the relationship property that represents weight.
| seedProperty                  | string  | n/a                    | yes      | Used to define initial set of labels (must be a number).
| writeProperty                 | string  | n/a                    | yes      | The property name written back to the partition of the graph in which the node resides.
| graph                         | string  | 'huge'                 | yes      | Use 'huge' when describing the subset of the graph with label and relationship-type parameter. Use 'cypher' for describing the subset with cypher node statement and relationship statement.
|===



// TODO: return nodes, relationships for estimate mode
.Results
[opts="header",cols="1,1,6"]
|===
| Name | Type | Description
| createMillis | int | Milliseconds for loading data.
| computeMillis | int | Milliseconds for running the algorithm.
| writeMillis | int | Milliseconds for writing result data back.
| postProcessingMillis    | int  | Milliseconds for computing percentiles and community count.

| nodes | int | The number of nodes considered.
| communityCount | int  | The number of communities found.
| ranIterations | int | The number of iterations that were executed.
| didConverge | boolean | True if the algorithm did converge to a stable labelling within the provided number of maximum iterations.

| communityDistribution    | map  | The containing min, max, mean as well as 50, 75, 90, 95, 99 and 999 percentile of community size.
| configuration | map     | The configuration used for running the algorithm.

|===

.The following will run the algorithm and stream back results:
[source,cypher]
----
CALL gds.labelPropagation.stream(graphNameOrConfig: STRING|MAP, configuration: MAP})
YIELD nodeId, communityId
----

.Parameters
[opts="header",cols="1,1,1,1,4"]
|===
| Name              | Type    | Default        | Optional | Description
| graphNameOrConfig | string  | null           | no      | Either then name of a loaded graph or directly the config.
| configuration     | map     | n/a            | yes      | Additional configuration, optional if the first parameter was a map.
|===

.Configuration
[opts="header",cols="1,1,1,1,4"]
|===
| Name                          | Type   | Default                | Optional | Description
| maxIterations                 | int    | 10                     | yes      | The maximum number of iterations to run.
| concurrency                   | int    | available CPUs         | yes      | The number of concurrent threads used for running the algorithm. Also provides the default value for 'readConcurrency'.
| readConcurrency               | int    | value of 'concurrency' | yes      | The number of concurrent threads used for reading the graph.
| nodeWeightProperty            | string | null                   | yes      | The property name of node that contain weight. Must be numeric.
| relationshipWeightProperty    | string | null                   | yes      | The property name of relationship that contain weight. Must be numeric.
| seedProperty                  | string | n/a                    | yes      | Used to define initial set of labels (must be a number).
| graph                         | string | 'huge'                 | yes      | Use 'huge' when describing the subset of the graph with label and relationship-type parameter. Use 'cypher' for describing the subset with cypher node statement and relationship statement.
|===

.Results
[opts="header"]
|===
| Name          | Type | Description
| nodeId        | int  | Node ID
| communityId   | int  | Community ID
|===

.The following will run the algorithm and returns the result in form of statistical and measurement values:
[source, cypher]
----
CALL gds.labelPropagation.stats(graphNameOrConfig: STRING|MAP, configuration: MAP})
YIELD nodes, ranIterations, didConverge, createMillis, computeMillis, writeMillis
----

.Parameters
[opts="header",cols="1,1,1,1,4"]
|===
| Name              | Type    | Default        | Optional   | Description
| graphNameOrConfig | string  | null           | no         | Either then name of a loaded graph or directly the config.
| configuration     | map     | n/a            | yes        | Additional configuration, optional if the first parameter was a map.
|===

The configuration is the same as for the `write` mode.

.Results
[opts="header",cols="1,1,6"]
|===
| Name                          | Type      | Description
| createMillis                  | int       | Milliseconds for loading data.
| computeMillis                 | int       | Milliseconds for running the algorithm.
// TODO: stats mode should not have writeMillis in the result
| writeMillis                   | int       | Milliseconds for writing result data back.
| postProcessingMillis          | int       | Milliseconds for computing percentiles and community count.
| nodes                         | int       | The number of nodes considered.
| communityCount                | int       | The number of communities found.
| ranIterations                 | int       | The number of iterations that were executed.
| didConverge                   | boolean   | True if the algorithm did converge to a stable labelling within the provided number of maximum iterations.
| communityDistribution         | map       | The containing min, max, mean as well as 50, 75, 90, 95, 99 and 999 percentile of community size.
| configuration                 | map       | The configuration used for running the algorithm.
|===

.The following will estimate the memory requirements for running the algorithm:
[source,cypher]
----
CALL gds.labelPropagation.<mode>.estimate(graphNameOrConfig: STRING|MAP, configuration: MAP})
YIELD nodes, relationships, bytesMin, bytesMax, requiredMemory, mapView
----

The `mode` can be substituted with the available modes (`stream`, `write` and `stats`).

.Parameters
[opts="header",cols="1,1,1,1,4"]
|===
| Name              | Type    | Default        | Optional | Description
| graphNameOrConfig | string  | null           | no      | Either then name of a loaded graph or directly the config.
| configuration     | map     | n/a            | yes      | Additional configuration, optional if the first parameter was a map.
|===

.Configuration
[opts="header",cols="1,1,1,1,4"]
|===
| Name              | Type   | Default                | Optional | Description
| nodeCount         | int       | 0                 | yes       | The number of nodes in a fictive graph.
| relationshipCount | int       | 0                 | yes       | The number of relationships in a fictive graph.
|===

Setting the `nodeCount` and `relationshipCount` parameters allows a memory estimation without loading the graph.
For explicitly loaded graphs, the config-map needs to contain the graph name in the `graph` key.
Additionally algorithm specific parameters can also be provided as config.

.Results
[opts="header",cols="1,1,6"]
|===
| Name          | Type    | Description
| requiredMemory         | String     | The required memory in human readable format.
| bytesMin    | int     | The minimum number of bytes required.
| bytesMax | int   | The maximum number of bytes required.
| mapView | Map  | A breakdown of the memory used by different components involved in the execution of a graph algorithm.

| nodes         | int     | The number of nodes in the graph.
| relationships         | int     | The number of relationships in the graph.
|===

[[algorithms-label-propagation-examples]]
== Examples

Consider the graph created by the following Cypher statement:

[source, cypher]
----
CREATE (alice:User {name: 'Alice', seed_label: 52})
CREATE (bridget:User {name: 'Bridget', seed_label: 21})
CREATE (charles:User {name: 'Charles', seed_label: 43})
CREATE (doug:User {name: 'Doug', seed_label: 21})
CREATE (mark:User {name: 'Mark', seed_label: 19})
CREATE (michael:User {name: 'Michael', seed_label: 52})

CREATE (alice)-[:FOLLOW {weight: 1}]->(bridget)
CREATE (alice)-[:FOLLOW {weight: 10}]->(charles)
CREATE (mark)-[:FOLLOW {weight: 1}]->(doug)
CREATE (bridget)-[:FOLLOW {weight: 1}]->(michael)
CREATE (doug)-[:FOLLOW {weight: 1}]->(mark)
CREATE (michael)-[:FOLLOW {weight: 1}]->(alice)
CREATE (alice)-[:FOLLOW {weight: 1}]->(michael)
CREATE (bridget)-[:FOLLOW {weight: 1}]->(alice)
CREATE (michael)-[:FOLLOW {weight: 1}]->(bridget)
CREATE (charles)-[:FOLLOW {weight: 1}]->(doug)
----

This graph represents six users, some of whom follow each other.
Besides a `name` property, each user also has a `seed_label`  property.
The `seed_label` property represents a value in the graph used to seed the node with a label.
For example, this can be a result from a previous run of the Label Propagation algorithm.
In addition, each relationship has a weight property.

[[algorithms-label-propagation-examples-unweighted]]
=== Unweighted

.The following will run the algorithm and stream results:
[source, cypher]
----
CALL gds.labelPropagation.stream({
  nodeProjection: 'User',
  relationshipProjection: 'FOLLOW'
})
YIELD nodeId, communityId AS Community
RETURN gds.util.asNode(nodeId).name AS Name, Community
ORDER BY Community, Name
----

.Results
[opts="header",cols="1,1"]
|===
| Name    | Community
| "Alice"   | 1
| "Bridget" | 1
| "Michael" | 1
| "Charles" | 4
| "Doug"    | 4
| "Mark"    | 4
|===


.The following will load the graph, run the algorithm, and write back results:
[source, cypher]
----
CALL gds.labelPropagation.write({
  nodeProjection: 'User',
  relationshipProjection: 'FOLLOW',
  writeProperty: 'community'
})
YIELD ranIterations, communityCount
----

.Results
[opts="header",cols="1m,1m"]
|===
| ranIterations | communityCount
| 3             | 2
|===


Our algorithm found two communities, with 3 members each.

It appears that Michael, Bridget, and Alice belong together, as do Doug and Mark.
Only Charles doesn't strongly fit into either side, but ends up with Doug and Mark.

[[algorithms-label-propagation-examples-weighted]]
=== Weighted

The Label-Propagation algorithm can also run on weighted graphs,
taking the given relationship or node weights into concern when forming the communities.

.The following will run the algorithm on a graph with weighted relationships and stream results:
[source, cypher]
----
CALL gds.labelPropagation.stream({
  nodeProjection: 'User',
  relationshipProjection: 'FOLLOW',
  relationshipWeightProperty: 'weight'
})
YIELD nodeId, communityId AS Community
RETURN gds.util.asNode(nodeId).name AS Name, Community
ORDER BY Community, Name
----

.Results
[opts="header",cols="1,1"]
|===
| Name    | Community
| "Bridget" | 2
| "Michael" | 2
| "Alice"   | 4
| "Charles" | 4
| "Doug"    | 4
| "Mark"    | 4
|===

Using the weighted relationships, `Alice` and `Charles` are now in the same community as there is a strong link between them.


.The following will load the graph, run the algorithm on a weighted graph and write back results:
[source, cypher]
----
CALL gds.labelPropagation.write({
  nodeProjection: 'User',
  relationshipProjection: 'FOLLOW',
  relationshipProperties: 'weight',
  writeProperty: 'community',
  relationshipWeightProperty: 'weight'
})
YIELD ranIterations, communityCount
----

.Results
[opts="header",cols="1m,1m"]
|===
| ranIterations | communityCount
| 4             | 2
|===

As we can see, the weighted example takes 4 iterations to converge, instead of 3 for the unweighted case.


[[algorithms-label-propagation-examples-seeded]]
=== Seeded

At the beginning of the algorithm, every node is initialized with a unique label and the labels propagate through the network.

It is possible to define preliminary labels of nodes using the `seedProperty` parameter.
We need to store a preliminary set of labels that we would like to run the Label Propagation algorithm with as node properties.
That property needs to be a number.
In our example graph we saved them as the property `seed_label`.

The algorithm first checks if there is a seed label assigned to the node, and loads it if there is one.
If there isn't one, it assigns a new unique label to the node.
Using this preliminary set of labels, it then sequentially updates each node's label to a new one, which is the most frequent label among its neighbors at every iteration of label propagation.

.The following will run the algorithm with pre-defined labels:
[source, cypher]
----
CALL gds.labelPropagation.stream({
  nodeProjection: 'User',
  relationshipProjection: 'FOLLOW',
  nodeProperties: 'seed_label',
  seedProperty: 'seed_label'
})
YIELD nodeId, communityId AS Community
RETURN gds.util.asNode(nodeId).name AS Name, Community
ORDER BY Community, Name
----

.Results
[opts="header",cols="1,1"]
|===
| Name    | Community
| "Alice"   | 19
| "Bridget" | 19
| "Charles" | 19
| "Doug"    | 21
| "Mark"    | 21
| "Michael" | 21
|===

As we can see, the communities are based on the `seed_label` property, concretely `19` is from the user `Mark` and `21` from `Doug`.

.The following will load the graph, run the algorithm, and write back results:
[source, cypher]
----
CALL gds.labelPropagation.write({
  nodeProjection: 'User',
  relationshipProjection: 'FOLLOW',
  nodeProperties: 'seed_label',
  writeProperty: 'community',
  seedProperty: 'seed_label'
  })
YIELD ranIterations, communityCount
----

.Results
[opts="header",cols="1m,1m"]
|===
| ranIterations | communityCount
| 3             | 2
|===


[[algorithms-label-propagation-examples-projection]]
=== Cypher projection

In the examples above, we have relied on the _implicit_ loading of graphs for the algorithm computation.
However, like other algorithms WCC also accepts _named graphs_ and _Cypher projections_ as inputs.
See <<projected-graph-model, Projected Graph Model>> for more details.

.Using a named graph:
[source,cypher]
----
CALL gds.graph.create('myGraph', ['User'], ['FOLLOW']);

CALL gds.labelPropagation.stream('myGraph', {})
YIELD nodeId, communityId AS Community
RETURN gds.util.asNode(nodeId).name AS Name, Community
ORDER BY Community, Name
----

.Results
[opts="header",cols="1,1"]
|===
| Name    | Community
| "Alice   | 1
| "Bridget" | 1
| "Michael" | 1
| "Charles" | 4
| "Doug"    | 4
| "Mark"    | 4
|===

As we can see, the results are identical to the results in the <<algorithms-label-propagation-examples-unweighted>> example.

.Set `graph:'cypher'` in the config:
[source, cypher]
----
CALL gds.labelPropagation.stream({
  nodeQuery: 'MATCH (p:User) RETURN id(p) AS id',
  relationshipQuery: 'MATCH (p1:User)-[f:FOLLOW]->(p2:User)
    RETURN id(p1) AS source, id(p2) AS target',
  graph: 'cypher'
})
YIELD nodeId, communityId AS Community
RETURN gds.util.asNode(nodeId).name AS Name, Community
ORDER BY Community, Name
----

.Results
[opts="header",cols="1,1"]
|===
| Name    | Community
| "Alice"   | 1
| "Bridget" | 1
| "Michael" | 1
| "Charles" | 4
| "Doug"    | 4
| "Mark"    | 4
|===

Again, results are identical, as the Cypher projection we use mimics the behaviour of the default loading configuration.
Of course, the Cypher projection feature enables more advanced control over which exact parts of the graph to compute over; please see <<cypher-projection>> for more details.

[[algorithms-label-propagation-usage]]
=== Usage

When executing LabelPropagation in parallel with direction 'BOTH', it is possible that results are flaky because of the asynchronous execution fashion of the algorithm.
This might also happen in an direction 'OUTGOING' graph, if two nodes have relationships that point to each other.
