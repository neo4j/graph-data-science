[[algorithms-louvain]]
= The Louvain algorithm

[abstract]
--
This section describes the Louvain algorithm in the Neo4j Graph Algorithms library.
--

This topic includes:

* <<algorithms-louvain-intro, Introduction>>
* <<algorithms-louvain-syntax, Syntax>>
* <<algorithms-louvain-examples, Examples>>
** <<algorithms-louvain-examples-stream, Streaming>>
** <<algorithms-louvain-examples-write, Writing>>
** <<algorithms-louvain-examples-stream-weighted, Weights>>
** <<algorithms-louvain-examples-stream-seeded, Seeding>>
** <<algorithms-louvain-examples-stream-intermediate, Multi-Level>>

[[algorithms-louvain-intro]]
== Introduction
The Louvain method for community detection is an algorithm for detecting communities in networks.
It maximizes a modularity score for each community, where the modularity quantifies the quality of an assignment of nodes to communities.
This means evaluating how much more densely connected the nodes within a community are, compared to how connected they would be in a random network.

The Louvain algorithm is a hierarchical clustering algorithm, that recursively merges communities into a single node and executes the modularity clustering on the condensed graphs.

For more information on this algorithm, see:

* https://arxiv.org/pdf/1410.1237.pdf[Lu, Hao, Mahantesh Halappanavar, and Ananth Kalyanaraman "Parallel heuristics for scalable community detection."^]
* https://en.wikipedia.org/wiki/Louvain_modularity

[NOTE]
====
Running this algorithm requires sufficient memory availability.
Before running this algorithm, we recommend that you read <<memory-requirements>>.
====


[[algorithms-louvain-syntax]]
== Syntax

.The following describes the API for running the algorithm and writing results back to Neo4j:
[source, cypher]
----
CALL gds.algo.louvain.write(graphNameOrConfig: STRING|MAP, configuration: MAP})
YIELD nodes, communities, modularity, loadMillis, computeMillis, writeMillis
----

.Parameters
[opts="header",cols="1,1,1m,1,4"]
|===
| Name         | Type    | Default | Optional | Description
| node label   | string  | null    | yes      | The node label to load from the graph. If `null`, load all nodes.
| relationship | string  | null    | yes      | The relationship type to load from the graph. If `null`, load all relationships.
| config       | map     | {}      | yes      | Additional configuration, see below.
|===

.Configuration
[opts="header",cols="1m,1,1,1,4"]
|===
| Name                              | Type      | Default                   | Optional | Description
| concurrency                       | int       | available CPUs            | yes      | The number of concurrent threads used for running the algorithm. Also provides the default value for 'readConcurrency' and 'writeConcurrency'. This is dependent on the Neo4j edition; for more information, see <<system-requirements-cpu>>.
| readConcurrency                   | int       | value of 'concurrency'    | yes      | The number of concurrent threads used for reading the graph.
| writeConcurrency                  | int       | value of 'concurrency'    | yes      | The number of concurrent threads used for writing the result.
| weightProperty                    | string    | `null`                    | yes      | The property name that contains weight. If `null`, treats the graph as unweighted. Must be numeric.
| seedProperty                      | string    | n/a                       | yes      | Used to set the initial community for a node. The property value needs to be a number.
| write                             | boolean   | `true`                    | yes      | Specifies if the result should be written back as a node property.
| writeProperty                     | string    | `'partition'`             | yes      | The property name written back the ID of the partition particular node belongs to.
| maxLevels                         | int       | 10                        | yes      | The maximum number of levels in which the graph is clustered and then condensed.
| maxIterations                     | int       | 10                        | yes      | The maximum number of iterations that the modularity optimization will run for each level.
| tolerance                         | float     | 0.0001                    | yes      | Minimum change in modularity between iterations. If the modularity changes less than the tolerance value, the result is considered stable and the algorithm returns.
| includeIntermediateCommunities    | boolean   | false                     | yes      | Indicates whether to write intermediate communities. If set to false, only the final community is persisted.
| graph                             | string    | `'huge'`                  | yes      | Use `'huge'` when describing the subset of the graph with node label and relationship type parameters. Use `'cypher'` for describing the subset using Cypher queries for nodes and relationships.
|===

.Results
[opts="header",cols="1m,1,6"]
|===
| Name                              | Type          | Description
| loadMillis                        | int           | Milliseconds for loading data.
| computeMillis                     | int           | Milliseconds for running the algorithm.
| writeMillis                       | int           | Milliseconds for writing result data back.
| postProcessingMillis              | int           | Milliseconds for computing percentiles and community count.
| nodes                             | int           | The number of nodes considered.
| communityCount                    | int           | The number of communities found.
| levels                            | int           | The number of supersteps the algorithm actually ran.
| modularity                        | float         | The final modularity score.
| modularities                      | list of int   | The final modularity scores for each level.
| includeIntermediateCommunitie     | boolean       | Indicates whether all intermediate communities where written or only the final one.
| p1                                | int           | The 1 percentile of community size.
| p5                                | int           | The 5 percentile of community size.
| p10                               | int           | The 10 percentile of community size.
| p25                               | int           | The 25 percentile of community size.
| p50                               | int           | The 50 percentile of community size.
| p75                               | int           | The 75 percentile of community size.
| p90                               | int           | The 90 percentile of community size.
| p95                               | int           | The 95 percentile of community size.
| p99                               | int           | The 99 percentile of community size.
| p100                              | int           | The 100 percentile of community size.
| write                             | boolean       | Specifies if the result was written back as a node property.
| writeProperty                     | string        | The property name written back to.
|===


[[algorithms-louvain-syntax-stream]]
.The following describes the API for running the algorithm and stream results:
[source, cypher]
----
CALL gds.algo.louvain.stream(label: STRING, relationship: STRING, {
 // configuration
})
YIELD nodeId, community, communities
----

.Parameters
[opts="header",cols="1,1,1m,1,4"]
|===
| Name         | Type    | Default | Optional | Description
| node label   | string  | null    | yes      | The node label to load from the graph. If `null`, load all nodes.
| relationship | string  | null    | yes      | The relationship type to load from the graph. If `null`, load all relationships.
| config       | map     | {}      | yes      | Additional configuration, see below.
|===

.Configuration
[opts="header",cols="1m,1,1,1,4"]
|===
| Name                              | Type      | Default                   | Optional | Description
| concurrency                       | int       | available CPUs            | yes      | The number of concurrent threads used for running the algorithm. Also provides the default value for 'readConcurrency' and 'writeConcurrency'. This is dependent on the Neo4j edition; for more information, see <<system-requirements-cpu>>.
| readConcurrency                   | int       | value of 'concurrency'    | yes      | The number of concurrent threads used for reading the graph.
| weightProperty                    | string    | `null`                    | yes      | The property name that contains weight. If `null`, treats the graph as unweighted. Must be numeric.
| seedProperty                      | string    | n/a                       | yes      | Used to set the initial community for a node. The property value needs to be a number.
| levels                            | int       | 10                        | yes      | The maximum number of levels in which the graph is clustered and then condensed.
| innerIterations                   | int       | 10                        | yes      | The maximum number of iterations that the modularity optimization will run for each level.
| tolerance                         | float     | 0.0001                    | yes      | Minimum change in modularity between iterations. If the modularity changes less than the tolerance value, the result is considered stable and the algorithm returns.
| includeIntermediateCommunities    | boolean   | false                     | yes      | Indicates whether to write intermediate communities. If set to false, only the final community is persisted.
| graph                             | string    | `'huge'`                  | yes      | Use `'huge'` when describing the subset of the graph with node label and relationship type parameters. Use `'cypher'` for describing the subset using Cypher queries for nodes and relationships.
|===


.Results
[opts="header",cols="1m,1,6"]
|===
| Name          | Type         | Description
| nodeId        | int          | Node ID.
| community     | int          | The community ID of the final level.
| communities   | list of int  | Community IDs for each level. `Null` if `includeIntermediateCommunities` is set to false.
|===

[[algorithms-louvain-examples]]
== Examples

Consider the graph created by the following Cypher statement:

[source, cypher]
----
CREATE (nAlice:User {name: 'Alice', seed: 42})
CREATE (nBridget:User {name: 'Bridget', seed: 42})
CREATE (nCharles:User {name: 'Charles', seed: 42})
CREATE (nDoug:User {name: 'Doug'})
CREATE (nMark:User {name: 'Mark'})
CREATE (nMichael:User {name: 'Michael'})

CREATE (nAlice)-[:LINK {weight: 1}]->(nBridget)
CREATE (nAlice)-[:LINK {weight: 1}]->(nCharles)
CREATE (nCharles)-[:LINK {weight: 1}]->(nBridget)

CREATE (nAlice)-[:LINK {weight: 5}]->(nDoug)

CREATE (nMark)-[:LINK {weight: 1}]->(nDoug)
CREATE (nMark)-[:LINK {weight: 1}]->(nMichael);
CREATE (nMichael)-[:LINK {weight: 1}]->(nMark);
----

This graph has two clusters of _Users_, that are closely connected.
Between those clusters there is one single edge.
The relationships that connect the nodes in each component have a property `weight` which determines the strength of the relationship.
In the following examples we will demonstrate using the Louvain algorithm on this graph.


[[algorithms-louvain-examples-stream]]
=== Streaming results

.The following will load the graph, run the algorithm, and stream results:
[source, cypher]
----
CALL gds.algo.louvain.stream({
    nodeProjection: ['User'],
    relationshipProjection: {
        LINK: {
            type: 'LINK',
            projection: 'undirected'
        }
    }
}) YIELD nodeId, communityId, communityIds
RETURN algo.asNode(nodeId).name as name, communityId, communityIds
ORDER BY name ASC
----

.Results
[opts="header"]
|===
| name      | communityId   | communityIds
| "Alice"   | 2             | <null>
| "Bridget" | 2             | <null>
| "Charles" | 2             | <null>
| "Doug"    | 5             | <null>
| "Mark"    | 5             | <null>
| "Michael" | 5             | <null>
|6 rows
|===

We use default values for the procedure configuration parameter.
Levels and `innerIterations` are set to 10 and the tolerance value is 0.0001.
Because we did not set the value of `includeIntermediateCommunities` to `true`, the column communities is always `null`.


[[algorithms-louvain-examples-write]]
=== Writing results

To instead write the community results back to the graph in Neo4j, use the following query.
For each node a property is written that holds the assigned community.

.The following will load the graph, run the algorithm, and write back results:
[source, cypher]
----
CALL gds.algo.louvain.write({
    nodeProjection: ['User'],
    relationshipProjection: {
        LINK: {
            type: 'LINK',
            projection: 'undirected',
            aggregation: 'NONE'
        }
    },
    writeProperty: 'community'
}) YIELD communityCount, modularity, modularities
----

.Results
[opts="header"]
|===
| communityCount | modularity         | modularities
| 2              | 0.3571428571428571 | [0.3571428571428571]
|1 row
|===

When writing back the results, only a single row is returned by the procedure.
The result contains meta information, like the number of identified communities and the modularity values.


[[algorithms-louvain-examples-stream-weighted]]
=== Running on weighted graphs

The Louvain algorithm can also run on weighted graphs, taking the given relationship weights into concern when calculating the modularity.

.The following will load the graph, run the algorithm on a weighted graph and stream results:
[source, cypher]
----
CALL gds.algo.louvain.stream({
    nodeProjection: ['User'],
    relationshipProjection: {
        LINK: {
            type: 'LINK',
            projection: 'UNDIRECTED',
            aggregation: 'NONE',
            properties: ['weight']
        }
    },
    weightProperty: 'weight'
}) YIELD nodeId, communityId, communityIds
RETURN algo.asNode(nodeId).name as name, communityId, communityIds
ORDER BY name ASC
----

.Results
[opts="header"]
|===
| name      | communityId   | communityIds
| "Alice"   | 3             | <null>
| "Bridget" | 2             | <null>
| "Charles" | 2             | <null>
| "Doug"    | 3             | <null>
| "Mark"    | 5             | <null>
| "Michael" | 5             | <null>
|6 rows
|===

Using the weighted relationships, we see that `Alice` and `Doug` have formed their own community, as their link is much stronger than all the others.


[[algorithms-louvain-examples-stream-seeded]]
=== Running with seed communities

The Louvain algorithm can be run incrementally, by providing a seed property.
With the seed property an initial community mapping can be supplied for a subset of the loaded nodes.
The algorithm will try to keep the seeded community IDs.

.The following will load the seeded graph, run the algorithm and stream results:
[source, cypher]
----
CALL gds.algo.louvain.stream({
    nodeProjection: {
        User: {
            labels: 'User',
            properties: ['seed']
        }
    },
    relationshipProjection: {
        LINK: {
            type: 'LINK',
            projection: 'UNDIRECTED',
            aggregation: 'NONE'
        }
    },
    seedProperty: 'seed'
}) YIELD nodeId, communityId, communityIds
RETURN algo.asNode(nodeId).name as name, communityId, communityIds
ORDER BY name ASC
----

.Results
[opts="header"]
|===
| name      | community | communities
| "Alice"   | 42        | <null>
| "Bridget" | 42        | <null>
| "Charles" | 42        | <null>
| "Doug"    | 47        | <null>
| "Mark"    | 47        | <null>
| "Michael" | 47        | <null>
|6 rows
|===

Using the seeded graph, we see that the community around `Alice` keeps its initial community ID of `42`.
The other community is assigned a new community ID, which is guaranteed to be larger than the largest community ID.


[[algorithms-louvain-examples-stream-intermediate]]
=== Streaming intermediate communities

As described before, Louvain is a hierarchical clustering algorithm.
That means that after every clustering step all nodes that belong to the same cluster are reduced to a single node.
Relationships between nodes of the same cluster become self-relationships, relationships to nodes of other clusters connect to the clusters representative.
This condensed graph is then used to run the next level of clustering.
The process is repeated until the clusters are stable.

In order to demonstrate this iterative behavior, we need to construct a more complex graph.

image::louvain-multilevel-graph.svg[align="center"]

.The following will load the example graph, run the algorithm and stream results including the intermediate communities:
[source, cypher]
----
CALL gds.algo.louvain.stream({
    nodeProjection: ['Node'],
    relationshipProjection: {
        TYPE: {
            type: 'TYPE',
            projection: 'undirected',
            aggregation: 'NONE'
        }
    },
    includeIntermediateCommunities: true
}) YIELD nodeId, communityId, communityIds
RETURN algo.asNode(nodeId).name as name, communityId, communityIds
ORDER BY name ASC
----

.Results
[opts="header"]
|===
| name  | community | communities
| a     | 14        | [3,14]
| b     | 14        | [3,14]
| c     | 14        | [14,14]
| d     | 14        | [3,14]
| e     | 14        | [14,14]
| f     | 14        | [14,14]
| g     | 7         | [7,7]
| h     | 7         | [7,7]
| i     | 7         | [7,7]
| j     | 12        | [12,12]
| k     | 12        | [12,12]
| l     | 12        | [12,12]
| m     | 12        | [12,12]
| n     | 12        | [12,12]
| x     | 14        | [14,14]
|15 rows
|===

In this example graph, after the first iteration we see 4 clusters, which in the second iteration are reduced to three.
