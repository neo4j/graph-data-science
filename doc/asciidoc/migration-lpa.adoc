[[migration-lpa]]
= Label Propagation

.Changes in Configuration
[opts=header,cols="1a,1a"]
|===
|Graph Algorithms v3.5 |Graph Data Science v1.0
| `direction`         | -
| `iterations`        | `maxIterations`
| `concurrency`       | `concurrency`
| `readConcurrency`   | `readConcurrency` footnote:lpa-read[Only when using anonymous graph]
| `writeConcurrency`  | `writeConcurrency` footnote:lpa-write[Only for `write` mode]
| `weightProperty` footnote:weight-prop[Can be configured separately by using `nodeWeightProperty` and `relationshipWeightProperty`]    | -
| -                 | `nodeWeightProperty`
| -                 | `relationshipWeightProperty`
| `seedProperty`      | `seedProperty`
| `partitionProperty` | -
| `writeProperty`     | `writeProperty` footnote:lpa-write[]
| `write`             | -
| `graph`             | -
|===

.Changes in YIELD fields
[opts=header,cols="1a,1a"]
|===
|Graph Algorithms v3.5 |Graph Data Science v1.0
|`loadMillis`             |`createMillis`
|`computeMillis`          |`computeMillis`
|`writeMillis`            |`writeMillis`
|`postProcessingMillis`   |`postProcessingMillis`
|`nodes`                  |`nodePropertiesWritten`
|`communityCount`         |`communityCount`
|`didConverge`            |`didConverge`
|-                      |`ranIterations`
|`write`                  |-
|-                      |`communityDistribution`
|-                      |`configuration` footnote:lpa-gds-config[The configuration used to run the algorithm]
|`writeProperty` footnote:lpa-write-config[Inlined into `configuration`]         |-
|`weightProperty` footnote:lpa-weight-config[Inlined into `configuration` as `nodeWeightProperty` and/or `relationshipWeightProperty`]        |-
| `min, max, mean, p50, p75, p90, p95, p99, p999` footnote:lpa-distribution[Inlined into `communityDistribution`] | -
|===

.Label Propagation Stream Mode
[opts=header,cols="1a,1a"]
|===
|Graph Algorithms v3.5 |Graph Data Science v1.0
2+| Streaming over a named graph:
|
[source, cypher]
----
CALL algo.labelPropagation.stream(null, null, {graph: 'myGraph'})
YIELD nodeId, label
----
|
[source, cypher]
----
CALL gds.labelPropagation.stream('myGraph')
YIELD nodeId, communityId
----
2+| Streaming over a named graph using configuration for iterations and relationship weight property:
|
[source, cypher]
----
CALL algo.labelPropagation.stream(
  null,
  null,
  {
    graph: 'myGraph',
    iterations: 15,
    weightProperty: 'myWeightProperty'
  }
)
----
|
[source, cypher]
----
CALL gds.labelPropagation.stream(
  'myGraph',
  {
    maxIterations: 15,
    relationshipWeightProperty: 'myWeightProperty'
  }
)
----
2+| Streaming over anonymous graph:
|
[source, cypher]
----
CALL algo.labelPropagation.stream(
  'MyLabel',
  'MY_RELATIONSHIP_TYPE'
)
----
|
[source, cypher]
----
CALL gds.labelPropagation.stream({
  nodeProjection: 'MyLabel',
  relationshipProjection: 'MY_RELATIONSHIP_TYPE'
})
----
2+| Streaming over anonymous graph using relationship with `REVERSE` projection:
|
[source, cypher]
----
CALL algo.labelPropagation.stream(
  'MyLabel',
  'MY_RELATIONSHIP_TYPE',
  { direction: 'INCOMING' }
)
----
|
[source, cypher]
----
CALL gds.labelPropagation.stream({
  nodeProjection: 'MyLabel',
  relationshipProjection: {
    MY_RELATIONSHIP_TYPE: {
      orientation: 'REVERSE'
    }
  }
})
----
2+| Streaming over anonymous graph using two way relationships footnote:[Not algorithm specific. This is also possible with named graphs. The graph has to be created with two relationship projections - one for each direction]:
|
[source, cypher]
----
CALL algo.labelPropagation.stream(
  'MyLabel',
  'MY_RELATIONSHIP_TYPE',
  { direction: 'BOTH' }
)
----
|
[source, cypher]
----
CALL gds.labelPropagation.stream({
  nodeProjection: 'MyLabel',
  relationshipProjection: {
    MY_RELATIONSHIP_TYPE_NATURAL: {
      type: 'MY_RELATIONSHIP_TYPE',
      orientation: 'NATURAL'
    },
    MY_RELATIONSHIP_TYPE_REVERSE: {
      type: 'MY_RELATIONSHIP_TYPE',
      orientation: 'REVERSE'
    }
  }
})
----
|===

.Label Propagation Write Mode
[opts=header,cols="1a,1a"]
|===
|Graph Algorithms v3.5 |Graph Data Science v1.0
2+|Minimalistic write:
|
[source, cypher]
----
CALL algo.labelPropagation(
  null,
  null,
  {
    graph: 'myGraph',
    writeProperty: 'myWriteProperty',
    write: true
  }
)
YIELD
  writeMillis,
  iterations,
  p1,
  writeProperty
----
|
[source, cypher]
----
CALL gds.labelPropagation.write(
  'myGraph',
  { writeProperty: 'myWriteProperty' }
)
YIELD
  writeMillis,
  ranIterations,
  communityDistribution AS cd,
  configuration AS conf
RETURN
  writeMillis,
  ranIterations,
  cd.p1 AS p1,
  conf.writeProperty AS writeProperty
----
2+|Write using weight properties footnote:lpa-write-weighted[In GA the weightProperty is for Nodes and Relationships while in GDS there are separate properties]:
|
[source, cypher]
----
CALL algo.labelPropagation(
  null,
  null,
  {
    graph: 'myGraph',
    writeProperty: 'myWriteProperty',
    weightProperty: 'myRelationshipWeightProperty',
    write: true
  }
)
----
|
[source, cypher]
----
CALL gds.labelPropagation.write(
  'myGraph',
  {
    writeProperty: 'myWriteProperty',
    relationshipWeightProperty: 'myRelationshipWeightProperty',
    nodeWeightProperty: 'myNodeWeightProperty'
  }
)
----
2+|Memory estimation of the algorithm:
|
[source, cypher]
----
CALL algo.memrec(
  'MyLabel',
  'MY_RELATIONSHIP_TYPE',
  'labelPropagation',
  {
    writeProperty: 'myWriteProperty',
    weightProperty: 'myRelationshipWeightProperty',
    write: true
  }
)
----
|
[source, cypher]
----
CALL gds.labelPropagation.write.estimate(
  {
    nodeProjection: 'MyLabel',
    relationshipProjection: 'MY_RELATIONSHIP_TYPE'
  },
  {
    writeProperty: 'myWriteProperty',
    relationshipWeightProperty: 'myRelationshipWeightProperty',
    nodeWeightProperty: 'myNodeWeightProperty'
  }
)
----
|===