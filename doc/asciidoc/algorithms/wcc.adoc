[[algorithms-wcc]]
= The Weakly Connected Components algorithm

[abstract]
--
This section describes the Weakly Connected Components (WCC) algorithm in the Neo4j Graph Data Science library.
--

This topic includes:

* <<algorithms-wcc-intro, Introduction>>
* <<algorithms-wcc-syntax, Syntax>>
* <<algorithms-wcc-examples, Examples>>
** <<algorithms-wcc-examples-unweighted, Unweighted>>
** <<algorithms-wcc-examples-weighted, Weighted>>
** <<algorithms-wcc-examples-seeding, Seeded components>>
** <<algorithms-wcc-examples-projection, Named graphs and Cypher projections>>
//* <<algorithms-wcc-usage-details, Usage Details>>


[[algorithms-wcc-intro]]
== Introduction

The WCC algorithm finds sets of connected nodes in an undirected graph, where all nodes in the same set form a connected component.
WCC is often used early in an analysis to understand the structure of a graph.

WCC has previously been known as Union Find or Connected Components in this User Guide.

// TODO: something about use cases

For more information on this algorithm, see:

* http://citeseerx.ist.psu.edu/viewdoc/summary?doi=10.1.1.28.8405["An efficient domain-independent algorithm for detecting approximately duplicate database records"^].
* One study uses WCC to work out how well connected the network is, and then to see whether the connectivity remains if 'hub' or 'authority' nodes are moved from the graph: https://link.springer.com/article/10.1007%2Fs10115-003-0128-3["Characterizing and Mining Citation Graph of Computer Science Literature"^]

[NOTE]
====
Running this algorithm requires sufficient memory availability.
Before running this algorithm, we recommend that you read <<memory-estimation>>.
====


[[algorithms-wcc-syntax]]
== Syntax

.The following describes the API for running the algorithm and writing results back to Neo4j:
[source, cypher]
----
CALL gds.wcc.write(
  graphName: string,
  configuration: map
)
YIELD
  // general write return columns
  componentCount: integer,
  componentDistribution: map
----

.Parameters
[opts="header",cols="1,1,1m,1,4"]
|===
| Name          | Type      | Default | Optional | Description
| graphName     | string    | null    | yes      | Name of an explicitly created graph to run the algorithm on.
| configuration | map       | {}      | yes      | Configuration for implicit graph creation, algorithm-specifics and/or graph filtering.
|===

.Configuration
[opts="header",cols="1m,1,1,1,4"]
|===
| Name                          | Type          | Default                   | Optional  | Description
// general configuration
| nodeProjection                | map or list   | null                      | yes       | The node projection used for implicit graph loading or filtering nodes of an explicitly loaded graph.
| relationshipProjection        | map or list   | null                      | yes       | The relationship projection used for implicit graph loading or filtering relationship of an explicitly loaded graph.
| nodeQuery                     | string        | null                      | yes       | The Cypher query used to select the nodes for implicit graph loading via a Cypher projection.
| relationshipQuery             | string        | null                      | yes       | The Cypher query used to select the relationships for implicit graph loading via a Cypher projection.
| nodeProperties                | map or list   | null                      | yes       | The node properties to load during implicit graph loading.
| relationshipProperties        | map or list   | null                      | yes       | The relationship properties to load during implicit graph loading.
| concurrency                   | int           | available CPUs            | yes       | The number of concurrent threads used for running the algorithm. Also provides the default value for 'readConcurrency' and 'writeConcurrency'. This is dependent on the Neo4j edition; for more information, see <<system-requirements-cpu>>.
| readConcurrency               | int           | value of 'concurrency'    | yes       | The number of concurrent threads used for implicit graph loading.
| writeConcurrency              | int           | value of 'concurrency'    | yes       | The number of concurrent threads used for writing the result to Neo4j.
// algorithm specific
| writeProperty                 | string        | n/a                       | no        | The node property to which the component ID is written to.
| relationshipWeightProperty    | string        | `null`                    | yes       | The relationship property that contains the weight. If `null`, the graph is treated as unweighted. Must be numeric.
| defaultValue                  | float         | `null`                    | yes       | The default value of the relationship weight in case it is missing or invalid.
| seedProperty                  | string        | n/a                       | yes       | Used to set the initial community for a node. The property value needs to be a number.
| threshold                     | float         | `null`                    | yes       | The value of the weight above which the relationship is considered in the computation.
| consecutiveIds                | boolean       | `false`                   | yes       | Flag to decide whether community identifiers are mapped into a consecutive id space (requires additional memory).
|===

.Results
[opts="header",cols="1m,1,6"]
|===
| Name                          | Type    | Description
| createMillis                  | int     | Milliseconds for loading data.
| computeMillis                 | int     | Milliseconds for running the algorithm.
| writeMillis                   | int     | Milliseconds for writing result data back.
| postProcessingMillis          | int     | Milliseconds for computing component count and distribution statistics.
| nodePropertiesWritten         | int     | The number of node properties written.
| relationshipPropertiesWritten | int     | The number of relationship properties written.
| componentCount                | int     | The number of computed components.
| componentDistribution         | map     | Statistical information about the component sizes.
| configuration                 | map     | The configuration used for running the algorithm.
|===

[[algorithms-wcc-syntax-stream]]
.The following describes the API for running the algorithm and stream results:
[source, cypher]
----
CALL gds.wcc.stream(
  graphName: string,
  configuration: map
)
YIELD
  // general stream return columns
  nodeId: int,
  communityId: int
----

.Parameters
[opts="header",cols="1,1,1m,1,4"]
|===
| Name          | Type      | Default | Optional | Description
| graphName     | string    | null    | yes      | Name of an explicitly loaded graph to run the algorithm on.
| configuration | map       | {}      | yes      | Configuration for implicit graph creation, algorithm-specifics and/or graph filtering.
|===

.Configuration
[opts="header",cols="1m,1,1,1,4"]
|===
| Name                          | Type          | Default                   | Optional  | Description
// general configuration
| nodeProjection                | map or list   | null                      | yes       | The node projection used for implicit graph loading or filtering nodes of an explicitly loaded graph.
| relationshipProjection        | map or list   | null                      | yes       | The relationship projection used for implicit graph loading or filtering relationship of an explicitly loaded graph.
| nodeQuery                     | string        | null                      | yes       | The Cypher query used to select the nodes for implicit graph loading via a Cypher projection.
| relationshipQuery             | string        | null                      | yes       | The Cypher query used to select the relationships for implicit graph loading via a Cypher projection.
| nodeProperties                | map or list   | null                      | yes       | The node properties to load during implicit graph loading.
| relationshipProperties        | map or list   | null                      | yes       | The relationship properties to load during implicit graph loading.
| concurrency                   | int           | available CPUs            | yes       | The number of concurrent threads used for running the algorithm. Also provides the default value for 'readConcurrency' and 'writeConcurrency'. This is dependent on the Neo4j edition; for more information, see <<system-requirements-cpu>>.
| readConcurrency               | int           | value of 'concurrency'    | yes       | The number of concurrent threads used for implicit graph loading.
| writeConcurrency              | int           | value of 'concurrency'    | yes       | The number of concurrent threads used for writing the result to Neo4j.
// algorithm specific
| relationshipWeightProperty    | string        | `null`                    | yes       | The relationship property that contains the weight. If `null`, the graph is treated as unweighted. Must be numeric.
| defaultValue                  | float         | `null`                    | yes       | The default value of the relationship weight in case it is missing or invalid.
| seedProperty                  | string        | n/a                       | yes       | Used to set the initial community for a node. The property value needs to be a number.
| threshold                     | float         | `null`                    | yes       | The value of the weight above which the relationship is considered in the computation.
| consecutiveIds                | boolean       | `false`                   | yes       | Flag to decide whether community identifiers are mapped into a consecutive id space (requires additional memory).
|===

.Results
[opts="header",cols="1m,1,6"]
|===
| Name          | Type | Description
| nodeId        | int  | The Neo4j node ID.
| componentId   | int  | The component ID.
|===


[[algorithms-wcc-examples]]
== Examples

Consider the graph created by the following Cypher statement:

[source, cypher]
----
CREATE (nAlice:User {name: 'Alice'})
CREATE (nBridget:User {name: 'Bridget'})
CREATE (nCharles:User {name: 'Charles'})
CREATE (nDoug:User {name: 'Doug'})
CREATE (nMark:User {name: 'Mark'})
CREATE (nMichael:User {name: 'Michael'})

CREATE (nAlice)-[:LINK {weight: 0.5}]->(nBridget)
CREATE (nAlice)-[:LINK {weight: 4}]->(nCharles)
CREATE (nMark)-[:LINK {weight: 1.1}]->(nDoug)
CREATE (nMark)-[:LINK {weight: 2}]->(nMichael);
----

This graph has two connected components, each with three nodes.
The relationships that connect the nodes in each component have a property `weight` which determines the strength of the relationship.
In the following examples we will demonstrate using the Weakly Connected Components algorithm on this graph.


[[algorithms-wcc-examples-unweighted]]
=== Unweighted

.The following will load the graph, run the algorithm, and stream results:
[source, cypher]
----
CALL gds.wcc.stream({
  nodeProjection: 'User',
  relationshipProjection: 'LINK'
})
YIELD nodeId, componentId
RETURN gds.util.asNode(nodeId).name AS name, componentId ORDER BY componentId, name
----

.Results
[opts="header",cols="1m,1m"]
|===
| name      | componentId
| "Alice"   | 0
| "Bridget" | 0
| "Charles" | 0
| "Doug"    | 3
| "Mark"    | 3
| "Michael" | 3
|===

To instead write the component ID to a node property in the Neo4j graph, use this query:

.The following will load the graph with weights, run the algorithm, and write back results:
[source, cypher]
----
CALL gds.wcc.write({
  nodeProjection: 'User',
  relationshipProjection: 'LINK',
  writeProperty: "componentId"
})
YIELD nodePropertiesWritten, componentCount;
----

.Results
[opts="header",cols="1m,1m"]
|===
| nodePropertiesWritten | componentCount
| 6                     | 2
|===

As we can see from the results, the nodes connected to one another are calculated by the algorithm as belonging to the same connected component.


[[algorithms-wcc-examples-weighted]]
=== Weighted

By configuring the algorithm to use a weight we can increase granularity in the way the algorithm calculates component assignment.
We do this by specifying the property key with the `relationshipWeightProperty` configuration parameter.
Additionally, we can specify a threshold for the weight value in such a way that only weighs greater than the threshold value will be considered by the algorithm.
We do this by specifying the threshold value with the `threshold` configuration parameter.

If a relationship does not have a weight property, a default weight is used.
The default is zero, and can be configured to another value using the `defaultValue` configuration parameter.

.The following will load the graph with weights, run the algorithm, and stream results:
[source, cypher]
----
CALL gds.wcc.stream({
  nodeProjection: 'User',
  relationshipProjection: 'LINK',
  relationshipProperties: 'weight',
  relationshipWeightProperty: "weight",
  threshold: 1.0
})
YIELD nodeId, componentId
RETURN gds.util.asNode(nodeId).name AS name, componentId ORDER BY componentId, name
----

.Results
[opts="header",cols="1m,1m"]
|===
| Name      | ComponentId
| "Alice"   | 0
| "Charles" | 0
| "Bridget" | 1
| "Doug"    | 3
| "Mark"    | 3
| "Michael" | 3
|===


.The following will load the graph with weights, run the algorithm, and write back results:
[source, cypher]
----
CALL gds.wcc.write({
  nodeProjection: 'User',
  relationshipProjection: 'LINK',
  writeProperty: "componentId",
  relationshipWeightProperty: "weight",
  threshold: 1.0
})
YIELD nodePropertiesWritten, componentCount;
----

.Results
[opts="header",cols="1m,1m"]
|===
| nodePropertiesWritten | componentCount
| 6                     | 3
|===

As we can see from the results, the node named 'Bridget' is now in its own component, due to its relationship weight being less than the configured threshold and thus ignored.


[[algorithms-wcc-examples-seeding]]
=== Seeded components

It is possible to define preliminary component IDs for nodes using the `seedProperty` configuration parameter.
This is helpful if we want to retain components from a previous run and it is known that no components have been split by removing relationships.
The property value needs to be a number.

The algorithm first checks if there is a seeded component ID assigned to the node.
If there is one, that component ID is used.
Otherwise, a new unique component ID is assigned to the node.

Once every node belongs to a component, the algorithm merges components of connected nodes.
When components are merged, the resulting component is always the one with the lower component ID.

[NOTE]
====
The algorithm assumes that nodes with the same seed value do in fact belong to the same component.
If any two nodes in different components have the same seed, behavior is undefined.
It is then recommended to run WCC without seeds.
====

To show this in practice, we will run the algorithm, then add another node to our graph, then run the algorithm again with the `seedProperty` configuration parameter.
We will use the weighted variant of WCC.

.The following will load the graph, run the algorithm, and write back results:
[source, cypher]
----
CALL gds.wcc.write({
  nodeProjection: 'User',
  relationshipProjection: 'LINK',
  writeProperty: "componentId",
  relationshipWeightProperty: "weight",
  threshold: 1.0
})
YIELD nodePropertiesWritten, componentCount;
----

.Results
[opts="header",cols="1m,1m"]
|===
| nodePropertiesWritten | componentCount
| 6                     | 3
|===

.The following will create a new node in the Neo4j graph, with no component ID:
[source, cypher]
----
MATCH (b:User {name: 'Bridget'})
CREATE (b)-[:LINK {weight: 2.0}]->(new:User {name: 'Mats'})
----

.Results
[cols="1"]
|===
|No data returned.
|===

.The following will load the graph, run the algorithm, and stream results:
[source, cypher]
----
CALL gds.wcc.stream({
  nodeProjection: 'User',
  nodeProperties: 'componentId',
  relationshipProjection: 'LINK',
  relationshipProperties: 'weight',
  seedProperty: "componentId",
  relationshipWeightProperty: "weight",
  threshold: 1.0
})
YIELD nodeId, componentId
RETURN gds.util.asNode(nodeId).name AS name, componentId ORDER BY componentId, name
----

.Results
[opts="header",cols="1m,1m"]
|===
| name      | componentId
| "Alice"   | 0
| "Charles" | 0
| "Bridget" | 1
| "Mats"    | 1
| "Doug"    | 3
| "Mark"    | 3
| "Michael" | 3
|===

.The following will load the graph, run the algorithm, and write back results:
[source, cypher]
----
CALL gds.wcc.write({
  nodeProjection: 'User',
  relationshipProjection: 'LINK',
  seedProperty: "componentId",
  writeProperty: "componentId",
  relationshipWeightProperty: "weight",
  threshold: 1.0
})
YIELD nodePropertiesWritten, componentCount;
----

.Results
[opts="header",cols="1m,1m"]
|===
| nodePropertiesWritten | componentCount
| 7                     | 3
|===

[NOTE]
====
If the `seedProperty` configuration parameter has the same value as `writeProperty`, the algorithm only writes properties for nodes where the component ID has changed.
If they differ, the algorithm writes properties for all nodes.
====


[[algorithms-wcc-examples-projection]]
=== Named graphs and Cypher projections

In the examples above, we have relied on the _implicit_ loading of graphs for the algorithm computation.
However, like other algorithms WCC also accepts _named graphs_ and _Cypher projections_ as inputs.
See <<graph-catalog-ops>> for more details.

.Using a named graph:
[source, cypher]
----
CALL gds.graph.create('myGraph', ['User'], ['LINK']) YIELD graphName;

CALL gds.wcc.stream('myGraph')
YIELD nodeId, componentId
RETURN gds.util.asNode(nodeId).name AS name, componentId
ORDER BY componentId, Name;
----

.Results
[opts="header",cols="1m,1m"]
|===
| name      | componentId
| "Alice"   | 0
| "Bridget" | 0
| "Charles" | 0
| "Doug"    | 3
| "Mark"    | 3
| "Michael" | 3
|===

As we can see, the results are identical to the results in the <<algorithms-wcc-examples-unweighted>> example.

.Using a Cypher projection:
[source, cypher]
----
CALL gds.wcc.stream({
    nodeQuery: 'MATCH (u:User) RETURN id(u) AS id',
    relationshipQuery: 'MATCH (u1:User)-[:LINK]->(u2:User) RETURN id(u1) AS source, id(u2) AS target'
})
YIELD nodeId, componentId
RETURN gds.util.asNode(nodeId).name AS name, componentId
ORDER BY componentId, name
----

.Results
[opts="header",cols="1m,1m"]
|===
| Name      | ComponentId
| "Alice"   | 0
| "Bridget" | 0
| "Charles" | 0
| "Doug"    | 3
| "Mark"    | 3
| "Michael" | 3
|===

Again, results are identical, as the Cypher projection we use mimics the behaviour of the default loading configuration.
Of course, the Cypher projection feature enables more advanced control over which exact parts of the graph to compute over; please see <<cypher-projection>> for more details.
