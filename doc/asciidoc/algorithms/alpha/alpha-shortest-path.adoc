[[labs-algorithms-shortest-path]]
= The Shortest Path algorithm

[abstract]
--
This section describes the Shortest Path algorithm in the Neo4j Graph Data Science library.
--

The Shortest Path algorithm calculates the shortest (weighted) path between a pair of nodes.
In this category, Dijkstra's algorithm is the most well known.
It is a real time graph algorithm, and can be used as part of the normal user flow in a web or mobile application.

[WARNING]
--
The Shortest Path algorithm was developed by the Neo4j Labs team and is not officially supported.
--

This section includes:

* <<algorithms-shortest-path-context, History and explanation>>
* <<algorithms-shortest-path-usecase, Use-cases - when to use the Shortest Path algorithm>>
* <<algorithms-shortest-path-limitations, Constraints - when not to use the Shortest Path algorithm>>
* <<algorithms-shortest-path-syntax, Syntax>>
* <<algorithms-shortest-path-sample, Shortest Path algorithm sample>>
** <<algorithms-shortest-path-dijkstra, The Dijkstra Shortest Path algorithm>>
*** <<algorithms-shortest-path-cp, Cypher projection>>


[[algorithms-shortest-path-context]]
== History and explanation

Path finding has a long history, and is considered to be one of the classical graph problems; it has been researched as far back as the 19th century.
It gained prominence in the early 1950s in the context of ‘alternate routing’, i.e. finding a second shortest route if the shortest route is blocked.

Dijkstra came up with his algorithm in 1956 while trying to come up with something to show off the new ARMAC computers.
He needed to find a problem and solution that people not familiar with computing would be able to understand, and designed what is now known as Dijkstra's algorithm.
He later implemented it for a slightly simplified transportation map of 64 cities in the Netherlands.


[[algorithms-shortest-path-usecase]]
== Use-cases - when to use the Shortest Path algorithm

* Finding directions between physical locations.
  This is the most common usage, and web mapping tools such as Google Maps use the shortest path algorithm, or a variant of it, to provide driving directions.

* Social networks can use the algorithm to find the degrees of separation between people.
  For example, when you view someone's profile on LinkedIn, it will indicate how many people separate you in the connections graph, as well as listing your mutual connections.


[[algorithms-shortest-path-limitations]]
== Constraints - when not to use the Shortest Path algorithm

Dijkstra does not support negative weights.
The algorithm assumes that adding a relationship to a path can never make a path shorter - an invariant that would be violated with negative weights.


[[algorithms-shortest-path-syntax]]
== Syntax

.The following will run the algorithm and write back results:
[source, cypher]
----
CALL gds.alpha.shortestPath.write(configuration: Map)
YIELD nodeCount, totalCost, loadMillis, evalMillis, writeMillis

----

.Configuration
[opts="header",cols="1,1,1,1,4"]
|===
| Name                       | Type    | Default  | Optional | Description
| startNode                  | node    | null     | no       | The start node
| endNode                    | node    | null     | no       | The end node
| relationshipWeightProperty | string  | null     | yes      | The property name that contains weight. If null, treats the graph as unweighted. Must be numeric.
| writeProperty              | string  | 'sssp'   | yes      | The property name written back to the node sequence of the node in the path
| direction                  | string  | outgoing | yes      | The relationship direction to load from the graph. If 'both', treats the relationships as undirected
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name        | Type  | Description
| nodeCount   | int   | The number of nodes considered
| totalCost   | float | The sum of all weights along the path
| loadMillis  | int   | Milliseconds for loading data
| evalMillis  | int   | Milliseconds for running the algorithm
| writeMillis | int   | Milliseconds for writing result data back
|===

.The following will run the algorithm and stream results:
[source, cypher]
----
CALL algo.shortestPath.stream(configuration: Map)
YIELD nodeId, cost
----

.Configuration
[opts="header",cols="1,1,1,1,4"]
|===
| Name                       | Type   | Default  | Optional | Description
| startNode                  | node   | null     | no       | The start node
| endNode                    | node   | null     | no       | The end node
| relationshipWeightProperty | string | null     | yes      | The property name that contains weight. If null, treats the graph as unweighted. Must be numeric.
| direction                  | string | outgoing | yes      | The relationship direction to load from the graph. If 'both', treats the relationships as undirected
|===

.Results
[opts="header"]
|===
| Name   | Type | Description
| nodeId | int  | Node ID
| cost   | int  | The cost it takes to get from start node to specific node
|===


[[algorithms-shortest-path-sample]]
== Shortest Path algorithm sample

image::sssp.png[]

.The following will create a sample graph:
[source, cypher]
----
CREATE (a:Loc {name: 'A'}),
       (b:Loc {name: 'B'}),
       (c:Loc {name: 'C'}),
       (d:Loc {name: 'D'}),
       (e:Loc {name: 'E'}),
       (f:Loc {name: 'F'}),
       (a)-[:ROAD {cost: 50}]->(b),
       (a)-[:ROAD {cost: 50}]->(c),
       (a)-[:ROAD {cost: 100}]->(d),
       (b)-[:ROAD {cost: 40}]->(d),
       (c)-[:ROAD {cost: 40}]->(d),
       (c)-[:ROAD {cost: 80}]->(e),
       (d)-[:ROAD {cost: 30}]->(e),
       (d)-[:ROAD {cost: 80}]->(f),
       (e)-[:ROAD {cost: 40}]->(f);
----


[[algorithms-shortest-path-dijkstra]]
=== The Dijkstra Shortest Path algorithm

.The following will run the algorithm and stream results:
[source, cypher]
----
MATCH (start:Loc {name: 'A'}), (end:Loc {name: 'F'})
CALL gds.alpha.shortestPath.stream({
  nodeProjection: 'Loc',
  relationshipProjection: {
    ROAD: {
      type: 'ROAD',
      properties: 'cost',
      projection: 'UNDIRECTED'
    }
  },
  startNode: start,
  endNode: end,
  weightProperty: 'cost'
})
YIELD nodeId, cost
RETURN gds.util.asNode(nodeId).name AS name, cost
----

.Results
[opts="header",cols="1,1"]
|===
| Name | Cost
| A    | 0
| C    | 50
| D    | 90
| E    | 120
| F    | 160
|===

The quickest route takes us from A to F, via C, D, and E, at a total cost of 160:

* First, we go from A to C, at a cost of 50.
* Then, we go from C to D, for an additional 40.
* Then, from D to E, for an additional 30.
* Finally, from E to F, for a further 40.

.The following will run the algorithm and write back results:
[source, cypher]
----
MATCH (start:Loc {name: 'A'}), (end:Loc {name: 'F'})
CALL gds.alpha.shortestPath.write({
  nodeProjection: 'Loc',
  relationshipProjection: {
    ROAD: {
      type: 'ROAD',
      properties: 'cost',
      projection: 'UNDIRECTED'
    }
  },
  startNode: start,
  endNode: end,
  weightProperty: 'cost',
  writeProperty: 'sssp'
})
YIELD nodeCount, totalCost
RETURN nodeCount,totalCost
----

.Results
[opts="header",cols="1,1"]
|===
| nodeCount | totalCost
| 5         | 160
|===

[[algorithms-shortest-path-cp]]
=== Cypher projection

include::../../management-ops/cypher-projection.adoc[tag=explanation]

.Set `graph:'cypher'` in the config:

[source, cypher]
----
MATCH (start:Loc {name: 'A'}), (end:Loc {name: 'F'})
CALL gds.alpha.shortestPath.write({
  nodeQuery:'MATCH(n:Loc) WHERE NOT n.name = "c" RETURN id(n) AS id',
  relationshipQuery:'MATCH(n:Loc)-[r:ROAD]->(m:Loc) RETURN id(n) AS source, id(m) AS target, r.cost AS weight',
  startNode: start,
  endNode: end,
  weightProperty: 'weight',
  writeProperty: 'sssp'
})
YIELD nodeCount, totalCost
RETURN nodeCount,totalCost
----
