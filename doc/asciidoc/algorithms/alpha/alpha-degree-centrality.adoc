[[algorithms-degree-centrality]]
[.alpha]
= Degree Centrality

[abstract]
--
This section describes the Degree Centrality algorithm in the Neo4j Graph Data Science library.
--

Degree centrality measures the number of incoming and outgoing relationships from a node.

The Degree Centrality algorithm can help us find popular nodes in a graph.

include::alpha-note.adoc[tag=alpha-note]

This section includes:

* <<algorithms-degree-context, History and explanation>>
* <<algorithms-degree-usecase, Use-cases - when to use the Degree Centrality algorithm>>
* <<algorithms-degree-sample, Degree Centrality algorithm sample>>
* <<algorithms-degree-weighted-sample, Weighted Degree Centrality algorithm sample>>
* <<algorithms-degree-cp, Cypher projection>>
* <<algorithms-degree-syntax, Syntax>>


[[algorithms-degree-context]]
== History and explanation

Degree Centrality was proposed by Linton C. Freeman in his 1979 paper http://leonidzhukov.net/hse/2014/socialnetworks/papers/freeman79-centrality.pdf[Centrality in Social Networks Conceptual Clarification^].
While the Degree Centrality algorithm can be used to find the popularity of individual nodes, it is often used as part of a global analysis where we calculate the minimum degree, maximum degree, mean degree, and standard deviation across the whole graph.


[[algorithms-degree-usecase]]
== Use-cases - when to use the Degree Centrality algorithm

* Degree centrality is an important component of any attempt to determine the most important people on a social network.
For example, in BrandWatch's https://www.brandwatch.com/blog/react-influential-men-and-women-2017/[most influential men and women on Twitter 2017^] the top 5 people in each category have over 40m followers each.

* Weighted degree centrality has been used to help separate fraudsters from legitimate users of an online auction.
The weighted centrality for fraudsters is significantly higher because they tend to collude with each other to artificially increase the price of items.
Read more in https://link.springer.com/chapter/10.1007/978-3-319-23461-8_11[Two Step graph-based semi-supervised Learning for Online Auction Fraud Detection^]


[[algorithms-degree-syntax]]
== Syntax

.The following will run the algorithm and write back results:
[source, cypher]
----
CALL gds.alpha.degree.write(configuration: Map)
YIELD nodes, createMillis, computeMillis, writeMillis, write, writeProperty
----

.Configuration
[opts="header",cols="1,1,1,1,4"]
|===
| Name                          | Type    | Default                | Optional | Description
| concurrency                   | int     | 4                      | yes      | The number of concurrent threads used for running the algorithm. Also provides the default value for 'readConcurrency' and 'writeConcurrency'.
| readConcurrency               | int     | value of 'concurrency' | yes      | The number of concurrent threads used for reading the graph.
| writeConcurrency              | int     | value of 'concurrency' | yes      | The number of concurrent threads used for writing the result.
| relationshipWeightProperty    | string  | null                   | yes      | The property name that contains weight. If null, treats the graph as unweighted. Must be numeric.
| defaultValue                  | float   | 0.0                    | yes      | The default value of the weight in case it is missing or invalid.
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name          | Type    | Description
| nodes         | int     | The number of nodes considered.
| writeProperty | string  | The property name written back to.
| createMillis  | int     | Milliseconds for loading data.
| computeMillis | int     | Milliseconds for running the algorithm.
| writeMillis   | int     | Milliseconds for writing result data back.
|===

.The following will run the algorithm and stream results:
[source, cypher]
----
CALL gds.alpha.degree.stream(configuration: Map)
YIELD node, score
----

.Configuration
[opts="header",cols="1,1,1,1,4"]
|===
| Name             | Type   | Default                | Optional | Description
| concurrency      | int    | 4                      | yes      | The number of concurrent threads used for running the algorithm. Also provides the default value for 'readConcurrency'.
| readConcurrency  | int    | value of 'concurrency' | yes      | The number of concurrent threads used for reading the graph.
| weightProperty   | string | null                   | yes      | The property name that contains weight. If null, treats the graph as unweighted. Must be numeric.
| defaultValue     | float  | 0.0                    | yes      | The default value of the weight in case it is missing or invalid.
|===

.Results
[opts="header"]
|===
| Name   | Type  | Description
| nodeId | long  | Node ID
| score  | float | Degree Centrality score
|===


[[algorithms-degree-sample]]
== Degree Centrality algorithm sample

This sample will explain the Degree Centrality algorithm, using a simple graph:

.Create sample graph
[source,cypher]
----
CREATE (alice:User {name: 'Alice'}),
       (bridget:User {name: 'Bridget'}),
       (charles:User {name: 'Charles'}),
       (doug:User {name: 'Doug'}),
       (mark:User {name: 'Mark'}),
       (michael:User {name: 'Michael'}),
       (alice)-[:FOLLOWS]->(doug),
       (alice)-[:FOLLOWS]->(bridget),
       (alice)-[:FOLLOWS]->(charles),
       (mark)-[:FOLLOWS]->(doug),
       (mark)-[:FOLLOWS]->(michael),
       (bridget)-[:FOLLOWS]->(doug),
       (charles)-[:FOLLOWS]->(doug),
       (michael)-[:FOLLOWS]->(doug)
----

.The following will run the algorithm and stream results, showing which users have the most followers:
[source,cypher]
----
CALL gds.alpha.degree.stream({
  nodeProjection: 'User',
  relationshipProjection: {
    FOLLOWS: {
      type: 'FOLLOWS',
      projection: 'REVERSE'
    }
  }
})
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name AS name, score AS followers
ORDER BY followers DESC
----

.The following will run the algorithm and store results, showing which users have the most followers:
[source,cypher]
----
CALL gds.alpha.degree.write({
  nodeProjection: 'User',
  relationshipProjection: {
    FOLLOWS: {
      type: 'FOLLOWS',
      projection: 'REVERSE'
    }
  },
  writeProperty: 'followers'
})
----

.Results
[opts="header",cols="1,1"]
|===
| Name | Followers
| Doug    | 5.0
| Bridget | 1.0
| Charles | 1.0
| Michael | 1.0
| Mark    | 0.0
| Alice   | 0.0
|===

.The following will run the algorithm and stream results, showing which users follow the most other users:
[source,cypher]
----
CALL gds.alpha.degree.stream({
  nodeProjection: 'User',
  relationshipProjection: 'FOLLOWS'
})
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name AS name, score AS followers
ORDER BY followers DESC
----

.The following will run the algorithm and store results, showing which users follow the most other users:
[source,cypher]
----
CALL gds.alpha.degree.write({
  nodeProjection: 'User',
  relationshipProjection: 'FOLLOWS',
  writeProperty: 'followers'
})
----

.Results
[opts="header",cols="1,1"]
|===
| Name | Following
| Alice   | 3.0
| Mark    | 2.0
| Bridget | 1.0
| Charles | 1.0
| Michael | 1.0
| Doug    | 0.0
|===

We can see that Doug is the most popular user in our imaginary Twitter graph, with 5 followers - all other users follow him, but he doesn't follow anybody back.
In the real Twitter network celebrities have very high follower counts but tend to follow very few back people.
We could therefore consider Doug a celebrity!


[[algorithms-degree-weighted-sample]]
== Weighted Degree Centrality algorithm sample

This sample will explain the weighted Degree Centrality algorithm, using a simple graph:


.The following will create a sample graph:
[source, cypher]
----
CREATE (alice:User {name:'Alice'}),
       (bridget:User {name:'Bridget'}),
       (charles:User {name:'Charles'}),
       (doug:User {name:'Doug'}),
       (mark:User {name:'Mark'}),
       (michael:User {name:'Michael'}),
       (alice)-[:FOLLOWS {score: 1}]->(doug),
       (alice)-[:FOLLOWS {score: 2}]->(bridget),
       (alice)-[:FOLLOWS {score: 5}]->(charles),
       (mark)-[:FOLLOWS {score: 1.5}]->(doug),
       (mark)-[:FOLLOWS {score: 4.5}]->(michael),
       (bridget)-[:FOLLOWS {score: 1.5}]->(doug),
       (charles)-[:FOLLOWS {score: 2}]->(doug),
       (michael)-[:FOLLOWS {score: 1.5}]->(doug)
----

This algorithm is a variant of the Degree Centrality algorithm, that measures the sum of the weights of incoming and outgoing relationships.

.The following will run the algorithm and stream results, showing which users have the most weighted followers:
[source,cypher]
----
CALL gds.alpha.degree.stream({
   nodeProjection: 'User',
   relationshipProjection: {
       FOLLOWS: {
           type: 'FOLLOWS',
           orientation: 'REVERSE',
           properties: 'score'
       }
   },
   relationshipWeightProperty: 'score'
})
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name AS name, score AS weightedFollowers
ORDER BY weightedFollowers DESC
----

.The following will run the algorithm and store results, showing which users have the most weighted followers:
[source,cypher]
----
CALL gds.alpha.degree.write({
   nodeProjection: 'User',
   relationshipProjection: {
       FOLLOWS: {
           type: 'FOLLOWS',
           orientation: 'REVERSE',
           properties: 'score'
       }
   },
   relationshipWeightProperty: 'score',
   writeProperty: 'weightedFollowers'
})
YIELD nodes, writeProperty
----

.Results
[opts="header",cols="1,1"]
|===
| Name | `weightedFollowers`
| Doug    | 7.5
| Charles | 5.0
| Michael | 4.5
| Bridget | 2.0
| Alice   | 0.0
| Mark    | 0.0
|===

Doug still remains our most popular user, but there isn't such a big gap to the next person.
Charles and Michael both only have one follower, but those relationships have a high relationship weight.


[[algorithms-degree-cp]]
== Cypher projection

include::../../management-ops/cypher-projection.adoc[tag=explanation]

[source, cypher]
----
CALL gds.alpha.degree.write({
  nodeQuery: 'MATCH (u:User) RETURN id(u) as id',
  relationshipQuery: 'MATCH (u1:User)<-[:FOLLOWS]-(u2:User) RETURN id(u1) as source, id(u2) as target',
  writeProperty: 'followers'
})
----

Note, that if we want to find the number of users that a user is following rather than their number of followers, we need to handle that in our Cypher query.

.The following will run the algorithm and store the results, calculating the number of users that a user follows:
[source, cypher]
----
CALL gds.alpha.degree.write({
  nodeQuery: 'MATCH (u:User) RETURN id(u) as id',
  relationshipQuery: 'MATCH (u1:User)-[:FOLLOWS]->(u2:User) RETURN id(u1) as source, id(u2) as target',
  writeProperty: 'followers'
})
----
