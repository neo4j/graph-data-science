[[algorithms-greedy]]
[.alpha]
= Greedy
:entity: influenceMaximization
:result: spread
:algorithm: Greedy

[abstract]
--
This section describes the Greedy influence maximization algorithm in the Neo4j Graph Data Science library.
--

include::../alpha-note.adoc[]

This topic includes:

* <<alpha-algorithms-greedy-intro, Introduction>>
* <<alpha-algorithms-greedy-syntax, Syntax>>
* <<alpha-algorithms-greedy-examples, Examples>>
** <<alpha-algorithms-greedy-examples-stream, Stream>>

[[alpha-algorithms-greedy-intro]]
== Introduction
The Greedy algorithm for influence maximization aims to find `k` nodes that maximize the expected spread of influence in a network.
It simulates the influence spread using the Independent Cascade model, which calculates the expected spread by taking the average spread over the `mc` Monte-Carlo simulations.
In the propagation process, a node is influenced in case that a uniform random draw is less than the probability `p`.

Kempe et al. 2003 introduced the Greedy algorithm in their study https://www.cs.cornell.edu/home/kleinber/kdd03-inf.pdf[Maximizing the Spread of Influence through a Social Network] to deal with the *NP*-hard problem of influence maximization.
The Greedy algorithm successively selecting the node within the maximum marginal gain approximation in polynomial time.
For large networks <<algorithms-celf, CELF>> algorithm should be used.

[[alpha-algorithms-greedy-syntax]]
== Syntax

include::../alpha-note.adoc[]

.Greedy syntax per mode
[.tabbed-example]
====

[.include-with-stream]
======

.Run Greedy in stream mode on a named graph.
[source, cypher, role=no-play]
----
CALL gds.alpha.influenceMaximization.greedy.stream(
  graphName: String,
  configuration: Map
)
YIELD
  nodeId: Integer,
  spread : Float
----

include::../../common-configuration/common-parameters-named-graph.adoc[]

include::../../common-configuration/common-stream-stats-configuration-named-graph.adoc[]

include::specific-configuration.adoc[]

.Results
[opts="header"]
|===
| Name         | Type       | Description
| nodeId       | Integer    | Node ID.
| spread       | Float      | The spread gained by selecting the node.
|===
======
[.include-with-stats]
======
.Run Greedy in stats mode on a named graph.
[source, cypher, role=no-play]
----
CALL gds.alpha.influenceMaximization.greedy.stats(
  graphName: String,
  configuration: Map
)
YIELD
  nodes: Integer,
  computeMillis: Integer,
----

include::../../common-configuration/common-parameters-named-graph.adoc[]

include::../../common-configuration/common-stream-stats-configuration-named-graph.adoc[]

include::specific-configuration.adoc[]

.Results
[opts="header",cols="1,1,6"]
|===
| Name                   | Type      | Description
| nodes                  | Integer   | The number of nodes in the graph.
| computeMillis          | Integer   | Milliseconds for running the algorithm.
|===
======
====


[[alpha-algorithms-greedy-examples]]
== Examples

:algorithm-name: {algorithm}
:graph-description: social network
:image-file: influence-maximization.svg
include::../../shared/examples-intro.adoc[]

.The following Cypher statement will create the example graph in the Neo4j database:
[source, cypher, role=setup-query no-play]
----
CREATE
  (a:Person {name: 'Jimmy'}),
  (b:Person {name: 'Jack'}),
  (c:Person {name: 'Alice'}),
  (d:Person {name: 'Ceri'}),
  (e:Person {name: 'Mohammed'}),
  (f:Person {name: 'Michael'}),
  (g:Person {name: 'Ethan'}),
  (h:Person {name: 'Lara'}),
  (i:Person {name: 'Amir'}),
  (j:Person {name: 'Willie'}),

  (b)-[:FRIEND_OF]->(c),
  (c)-[:FRIEND_OF]->(a),
  (c)-[:FRIEND_OF]->(g),
  (c)-[:FRIEND_OF]->(h),
  (c)-[:FRIEND_OF]->(i),
  (c)-[:FRIEND_OF]->(j),
  (d)-[:FRIEND_OF]->(g),
  (f)-[:FRIEND_OF]->(e),
  (f)-[:FRIEND_OF]->(g),
  (g)-[:FRIEND_OF]->(a),
  (g)-[:FRIEND_OF]->(b),
  (g)-[:FRIEND_OF]->(h),
  (g)-[:FRIEND_OF]->(e),
  (h)-[:FRIEND_OF]->(i);
----

In the example, we will use the Greedy algorithm to find `k` nodes subset.

.The following statement will create the graph and store it in the graph catalog.
[source, cypher, role=graph-create-query no-play]
----
CALL gds.graph.create(
  'myGraph',
  'Person',
  'FRIEND_OF'
);
----

In the following examples we will demonstrate using the Greedy algorithm on this graph.


[[alpha-algorithms-greedy-examples-stream]]
=== Stream

In the `stream` execution mode, the algorithm returns the {result} for each node.
This allows us to inspect the results directly or post-process them in Cypher without any side effects.

For more details on the `stream` mode in general, see <<running-algos-stream>>.

[role=query-example]
--
.The following will run the algorithm, and stream results:
[source, cypher, role=no-play]
----
CALL gds.alpha.influenceMaximization.greedy.stream('myGraph', {seedSetSize: 3, concurrency: 4})
YIELD nodeId, spread
RETURN gds.util.asNode(nodeId).name AS Name, spread
ORDER BY spread ASC
----

.Results
[opts="header"]
|===
| Name      | spread
| "Alice"   | 1.519
| "Ethan"   | 2.701
| "Michael" | 3.8
|===
--
