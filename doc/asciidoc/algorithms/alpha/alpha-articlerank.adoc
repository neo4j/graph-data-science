
[[algorithms-articlerank]]
[.alpha]
= ArticleRank

[abstract]
--
This section describes the ArticleRank algorithm in the Neo4j Graph Data Science library.
--

ArticleRank is a variant of the <<algorithms-page-rank, Page Rank algorithm>>, which measures the *transitive* influence or connectivity of nodes.

include::alpha-note.adoc[]


[[algorithms-articlerank-context]]
== History and explanation

// tag::explanation[]

Where ArticleRank differs to Page Rank is that Page Rank assumes that relationships from nodes that have a low out-degree are more important than relationships from nodes with a higher out-degree.
ArticleRank weakens this assumption.
// end::explanation[]

// tag::formula[]
ArticleRank is defined in https://www.emeraldinsight.com/doi/abs/10.1108/00012530911005544[ArticleRank: a PageRankâ€based alternative to numbers of citations for analysing citation networks^] as follows:

----
AR(A) = (1-d) + d * C(AVG) * (AR(T1)/(C(T1) + C(AVG)) + ... + AR(Tn)/(C(Tn) + C(AVG))
----

where,

* we assume that a page `A` has pages `T1` to `Tn` which point to it (i.e., are citations).
* `d` is a damping factor which can be set between 0 and 1.
  It is usually set to 0.85.
* `C(A)` is defined as the number of links going out of page `A`.
* `C(AVG)` is defined as the average number of links going out of all pages.
// end::formula[]


[[algorithms-articlerank-sample]]
== ArticleRank algorithm sample

This sample will explain the ArticleRank algorithm, using a simple graph:

image::articlerank.svg[]

.The following will create a sample graph:
[source, cypher, role=noplay setup-query]
----
CREATE
  (paper0:Paper {name:'Paper 0'}),
  (paper1:Paper {name:'Paper 1'}),
  (paper2:Paper {name:'Paper 2'}),
  (paper3:Paper {name:'Paper 3'}),
  (paper4:Paper {name:'Paper 4'}),
  (paper5:Paper {name:'Paper 5'}),
  (paper6:Paper {name:'Paper 6'}),

  (paper1)-[:CITES]->(paper0),

  (paper2)-[:CITES]->(paper0),
  (paper2)-[:CITES]->(paper1),

  (paper3)-[:CITES]->(paper0),
  (paper3)-[:CITES]->(paper1),
  (paper3)-[:CITES]->(paper2),

  (paper4)-[:CITES]->(paper0),
  (paper4)-[:CITES]->(paper1),
  (paper4)-[:CITES]->(paper2),
  (paper4)-[:CITES]->(paper3),

  (paper5)-[:CITES]->(paper1),
  (paper5)-[:CITES]->(paper4),

  (paper6)-[:CITES]->(paper1),
  (paper6)-[:CITES]->(paper4);
----

[role=query-example]
--
.The following will run the algorithm and stream results:
[source, cypher, role=noplay]
----
CALL gds.alpha.articleRank.stream({
  nodeProjection: 'Paper',
  relationshipProjection: 'CITES',
  maxIterations: 20,
  dampingFactor: 0.85
})
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name AS Name, score as ArticleRank
ORDER BY score DESC
----

.Results
[opts="header",cols="1,1"]
|===
| Name      | ArticleRank
| "Paper 0" | 0.7561931365286
| "Paper 1" | 0.5640594441909343
| "Paper 2" | 0.306357501167804
| "Paper 4" | 0.27750000506639483
| "Paper 3" | 0.22862500101327898
| "Paper 5" | 0.15000000000000002
| "Paper 6" | 0.15000000000000002
|===
--

[role=query-example, no-result=true]
--
.The following will run the algorithm and write back results:
[source, cypher, role=noplay]
----
CALL gds.alpha.articleRank.write({
  nodeProjection: 'Paper',
  relationshipProjection: 'CITES',
  maxIterations:20, dampingFactor:0.85,
  writeProperty: "pagerank"
}) YIELD nodes
----
--

Paper 0 is the most important paper, but it's only the 2nd most cited paper - Paper 1 has more citations.
However, Paper 1 cites Paper 0, which lets us see that it's not only the number of incoming links that is important, but also the importance of the papers behind those links.
Papers 5 and 6 are not cited by any other papers, so their score doesn't increase above the initial score of `1 - dampingFactor`.


[[algorithms-articlerank-syntax]]
== Syntax

.The following will run the algorithm and write back results:
[source, cypher, role=noplay]
----
CALL gds.alpha.articleRank.write(graphNameOrConfig: String|Map, configuration: Map)
YIELD nodes, maxIterations, createMillis, computeMillis, writeMillis, dampingFactor, writeProperty, centralityDistribution
----

.Parameters
[opts="header",cols="1,1,1,1,4"]
|===
| Name                          | Type    | Default                | Optional | Description
| label                         | string  | null                   | yes      | The label to load from the graph. If null, load all nodes.
| relationship                  | string  | null                   | yes      | The relationship type to load from the graph. If null, load all relationships.
| maxIterations                 | int     | 20                     | yes      | How many iterations of Page Rank to run.
| concurrency                   | int     | 4                      | yes      | The number of concurrent threads used for running the algorithm. Also provides the default value for 'readConcurrency' and 'writeConcurrency'.
| readConcurrency               | int     | value of 'concurrency' | yes      | The number of concurrent threads used for reading the graph.
| writeConcurrency              | int     | value of 'concurrency' | yes      | The number of concurrent threads used for writing the result.
| dampingFactor                 | float   | 0.85                   | yes      | The damping factor of the Page Rank calculation.
| graph                         | string  | 'huge'                 | yes      | Use 'huge' when describing the subset of the graph with label and relationship-type parameter. Use 'cypher' for describing the subset with cypher node statement and relationship statement.
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                   | Type      | Description
| nodes                  | int       | The number of nodes considered.
| maxIterations          | int       | The number of iterations run.
| dampingFactor          | float     | The damping factor used.
| writeProperty          | string    | The property name written back to.
| createMillis           | int       | Milliseconds for loading data.
| computeMillis          | int       | Milliseconds for running the algorithm.
| writeMillis            | int       | Milliseconds for writing result data back.
| centralityDistribution | Map       | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of centrality values.
|===

.The following will run the algorithm and stream results:
[source, cypher, role=noplay]
----
CALL gds.alpha.articleRank.stream(graphNameOrConfig: String|Map, configuration: Map)
YIELD node, score
----

.Parameters
[opts="header",cols="1,1,1,1,4"]
|===
| Name                          | Type   | Default                | Optional | Description
| label                         | string | null                   | yes      | The label to load from the graph. If null, load all nodes.
| relationship                  | string | null                   | yes      | The relationship type to load from the graph. If null, load all nodes.
| maxIterations                 | int    | 20                     | yes      | Specify how many iterations of Page Rank to run.
| concurrency                   | int    | 4                      | yes      | The number of concurrent threads used for running the algorithm. Also provides the default value for 'readConcurrency'.
| readConcurrency               | int    | value of 'concurrency' | yes      | The number of concurrent threads used for reading the graph.
| writeConcurrency              | int    | value of 'concurrency' | yes      | The number of concurrent threads used for writing the result.
| dampingFactor                 | float  | 0.85                   | yes      | The damping factor of the Page Rank calculation.
| graph                         | string | 'huge'                 | yes      | Use 'huge' when describing the subset of the graph with label and relationship-type parameter. Use 'cypher' for describing the subset with cypher node statement and relationship statement.
|===

.Results
[opts="header"]
|===
| Name  | Type  | Description
| node  | long  | Node ID
| score | float | Page Rank weight
|===


[[algorithms-articlerank-support]]
== Graph type support

The ArticleRank algorithm supports the following graph types:

* [x] directed, unweighted

* [x] undirected, unweighted
