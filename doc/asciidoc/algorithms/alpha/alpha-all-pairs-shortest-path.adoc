[[labs-algorithm-all-pairs-shortest-path]]
= The All Pairs Shortest Path algorithm

[abstract]
--
This section describes the All Pairs Shortest Path algorithm in the Neo4j Graph Data Science library.
--

The All Pairs Shortest Path (APSP) calculates the shortest (weighted) path between all pairs of nodes.
This algorithm has optimisations that make it quicker than calling the Single Source Shortest Path algorithm for every pair of nodes in the graph.

[WARNING]
--
The All Pairs Shortest Path algorithm was developed by the Neo4j Labs team and is not officially supported.
--

This section includes:

* <<algorithm-all-pairs-shortest-path-context, History and explanation>>
* <<algorithm-all-pairs-shortest-path-usecase, Use-cases - when to use the All Pairs Shortest Path algorithm>>
* <<algorithm-all-pairs-shortest-path-syntax, Syntax>>
* <<algorithm-all-pairs-shortest-path-sample, All Pairs Shortest Path algorithm sample>>


[[algorithm-all-pairs-shortest-path-context]]
== History and explanation

// tag::explanation[]
Some pairs of nodes might not be reachable between each other, so no shortest path exists between these pairs.
In this scenario, the algorithm will return `Infinity` value as a result between these pairs of nodes.
// end::explanation[]

Plain cypher does not support filtering `Infinity` values, so `gds.util.isFinite` function was added to help filter `Infinity` values from results.


[[algorithm-all-pairs-shortest-path-usecase]]
== Use-cases - when to use the All Pairs Shortest Path algorithm

// tag::use-case[]

* The All Pairs Shortest Path algorithm is used in urban service system problems, such as the location of urban facilities or the distribution or delivery of goods.
  One example of this is determining the traffic load expected on different segments of a transportation grid.
  For more information, see http://web.mit.edu/urban_or_book/www/book/[Urban Operations Research^].
* All pairs shortest path is used as part of the REWIRE data center design algorithm that finds a network with maximum bandwidth and minimal latency.
  There are more details about this approach in https://cs.uwaterloo.ca/research/tr/2011/CS-2011-21.pdf["REWIRE: An Optimization-based Framework for Data Center Network Design"^]

// end::use-case[]


[[algorithm-all-pairs-shortest-path-syntax]]
== Syntax
.The following will run the algorithm and stream results:
[source, cypher]
----
CALL gds.alpha.allShortestPaths.stream(configuration: MAP)
YIELD startNodeId, targetNodeId, distance
----

.Parameters
[opts="header",cols="1,1,1,1,4"]
|===
| Name                          | Type    | Default                | Optional | Description
| relationshipWeightProperty    | string  | null                   | yes      | The name of the relationship property that represents weight. If null, treats the graph as unweighted. Must be numeric.
| direction                     | string  | both                   | yes      | The relationship direction to load from the graph. If 'both', treats the relationships as undirected
| concurrency                   | int     | 4                      | yes      | The number of concurrent threads used for running the algorithm. Also provides the default value for 'readConcurrency' and 'writeConcurrency'. This is dependent on the Neo4j edition; for more information, see <<system-requirements-cpu>>.
| readConcurrency               | int     | value of 'concurrency' | yes      | The number of concurrent threads used for reading the graph.
|===


[[algorithm-all-pairs-shortest-path-sample]]
== All Pairs Shortest Path algorithm sample

image::sssp.png[]

.The following will create a sample graph:
[source, cypher]
----
CREATE (a:Loc {name:'A'}),
      (b:Loc {name:'B'}),
      (c:Loc {name:'C'}),
      (d:Loc {name:'D'}),
      (e:Loc {name:'E'}),
      (f:Loc {name:'F'}),
      (a)-[:ROAD {cost:50}]->(b),
      (a)-[:ROAD {cost:50}]->(c),
      (a)-[:ROAD {cost:100}]->(d),
      (b)-[:ROAD {cost:40}]->(d),
      (c)-[:ROAD {cost:40}]->(d),
      (c)-[:ROAD {cost:80}]->(e),
      (d)-[:ROAD {cost:30}]->(e),
      (d)-[:ROAD {cost:80}]->(f),
      (e)-[:ROAD {cost:40}]->(f);

----

.The following will run the algorithm and stream results:
[source, cypher]
----
CALL gds.alpha.allShortestPaths.stream({
  nodeProjection: 'Loc',
  relationshipProjection: {
    ROAD: {
      type: 'ROAD',
      properties: 'cost',
      defaultValue: 1.0
    }
  },
  relationshipWeightProperty: 'cost'
})
YIELD sourceNodeId, targetNodeId, distance
WITH sourceNodeId, targetNodeId, distance
WHERE algo.isFinite(distance) = true

MATCH (source:Loc) WHERE id(source) = sourceNodeId
MATCH (target:Loc) WHERE id(target) = targetNodeId
WITH source, target, distance WHERE source <> target

RETURN source.name AS source, target.name AS target, distance
ORDER BY distance DESC, source ASC, target ASC
LIMIT 10
----

.Results
[opts="header",cols="1,1,1"]
|===
| Source | Target | Cost
| A      | F      | 160
| A      | E      | 120
| B      | F      | 110
| C      | F      | 110
| A      | D      | 90
| B      | E      | 70
| C      | E      | 70
| D      | F      | 70
| A      | B      | 50
| A      | C      | 50
|===

This query returned the top 10 pairs of nodes that are the furthest away from each other.
F and E appear to be quite distant from the others.

For now, only single-source shortest path support loading the relationship as undirected, but we can use Cypher loading to help us solve this.
Undirected graph can be represented as https://en.wikipedia.org/wiki/Bidirected_graph[Bidirected graph], which is a directed graph in which the reverse of every relationship is also a relationship.

We do not have to save this reversed relationship, we can project it using *Cypher loading*.
Note that relationship query does not specify direction of the relationship.
This is applicable to all other algorithms that use Cypher loading.

.The following will run the algorithm, treating the graph as undirected:
[source, cypher]
----
CALL gds.alpha.allShortestPaths.stream({
  nodeQuery:'MATCH (n:Loc) RETURN id(n) as id',
  relationshipQuery:'MATCH (n:Loc)-[r:ROAD]-(p:Loc) RETURN id(n) as source, id(p) as target, r.cost as weight',
  relationshipWeightProperty: 'cost'
})
YIELD sourceNodeId, targetNodeId, distance
WITH sourceNodeId, targetNodeId, distance
WHERE algo.isFinite(distance) = true

MATCH (source:Loc) WHERE id(source) = sourceNodeId
MATCH (target:Loc) WHERE id(target) = targetNodeId
WITH source, target, distance WHERE source <> target

RETURN source.name AS source, target.name AS target, distance
ORDER BY distance DESC
LIMIT 10
----

.Results
[opts="header",cols="1,1,1"]
|===
| Source | Target | Cost
| A      | F      | 160
| F      | A      | 160
| A      | E      | 120
| E      | A      | 120
| B      | F      | 110
| C      | F      | 110
| F      | B      | 110
| F      | C      | 110
| A      | D      | 90
| D      | A      | 90
|===