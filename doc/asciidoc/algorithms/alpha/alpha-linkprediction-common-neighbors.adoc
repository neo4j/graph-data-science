[[alpha-algorithms-linkprediction-common-neighbors]]
[.alpha]
= Common Neighbors

[abstract]
--
This section describes the Common Neighbors algorithm in the Neo4j Graph Data Science library.
--

Common neighbors captures the idea that two strangers who have a friend in common are more likely to be introduced than those who don't have any friends in common.

include::alpha-note.adoc[tag=alpha-note]

This section includes:

* <<algorithms-linkprediction-common-neighbors-context, History and explanation>>
* <<algorithms-linkprediction-common-neighbors-syntax, Syntax>>
* <<algorithms-linkprediction-common-neighbors-sample, Common Neighbors algorithm sample>>


[[algorithms-linkprediction-common-neighbors-context]]
== History and explanation


It is computed using the following formula:

image::common-neighbors.svg[role="middle"]

where `N(x)` is the set of nodes adjacent to node `x`, and `N(y)` is the set of nodes adjacent to node `y`.

A value of 0 indicates that two nodes are not close, while higher values indicate nodes are closer.

The library contains a function to calculate closeness between two nodes.

[[algorithms-linkprediction-common-neighbors-syntax]]
== Syntax

.The following will run the algorithm and return the result:
[source, cypher]
----
RETURN gds.alpha.linkprediction.commonNeighbors(node1:Node, node2:Node, {
    relationshipQuery:String,
    direction:String
})
----


.Parameters
[opts="header",cols="1,1,1,1,4"]
|===
| Name                   | Type    | Default        | Optional | Description
| `node1`                | Node    | null           | no       | A node
| `node2`                | Node    | null           | no       | Another node
| `relationshipQuery`    | String  | null           | yes      | The relationship type used to compute similarity between `node1` and `node2`
| `direction`            | String  | BOTH           | yes      | The direction of relationship type used to compute similarity between `node1` and `node2`
|===


[[algorithms-linkprediction-common-neighbors-sample]]
== Common Neighbors algorithm sample

.The following will create a sample graph:
[source, cypher]
----
CREATE
 (zhen:Person {name: 'Zhen'}),
 (praveena:Person {name: 'Praveena'}),
 (michael:Person {name: 'Michael'}),
 (arya:Person {name: 'Arya'}),
 (karin:Person {name: 'Karin'}),

 (zhen)-[:FRIENDS]->(arya),
 (zhen)-[:FRIENDS]->(praveena),
 (praveena)-[:WORKS_WITH]->(karin),
 (praveena)-[:FRIENDS]->(michael),
 (michael)-[:WORKS_WITH]->(karin),
 (arya)-[:FRIENDS]->(karin)
----

.The following will return the number of common neighbors for Michael and Karin:
[source, cypher]
----
 MATCH (p1:Person {name: 'Michael'})
 MATCH (p2:Person {name: 'Karin'})
 RETURN gds.alpha.linkprediction.commonNeighbors(p1, p2) AS score
----

.Results
[opts="header",cols="1"]
|===
| `score`
| 1.0
|===


We can also compute the score of a pair of nodes based on a specific relationship type.

.The following will return the number of common neighbors for Michael and Karin based only on the `FRIENDS` relationships:
[source, cypher]
----
 MATCH (p1:Person {name: 'Michael'})
 MATCH (p2:Person {name: 'Karin'})
 RETURN gds.alpha.linkprediction.commonNeighbors(p1, p2, {relationshipQuery: "FRIENDS"}) AS score
----

.Results
[opts="header",cols="1"]
|===
| `score`
| 0.0
|===
