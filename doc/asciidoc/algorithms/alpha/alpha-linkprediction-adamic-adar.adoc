[[labs-algorithms-linkprediction-adamic-adar]]
= Adamic Adar

[abstract]
--
This section describes the Adamic Adar algorithm in the Neo4j Graph Data Science library.
--

link:https://en.wikipedia.org/wiki/Adamic/Adar_index[Adamic Adar] is a measure used to compute the closeness of nodes based on their shared neighbors.

include::alpha-note.adoc[tag=alpha-note]

This section includes:

* <<algorithms-linkprediction-adamic-adar-context, History and explanation>>
* <<algorithms-linkprediction-adamic-adar-syntax, Syntax>>
* <<algorithms-linkprediction-adamic-adar-sample, Adamic Adar algorithm sample>>


[[algorithms-linkprediction-adamic-adar-context]]
== History and explanation

The Adamic Adar algorithm was introduced in 2003 by Lada Adamic and Eytan Adar to https://www.semanticscholar.org/paper/Friends-and-neighbors-on-the-Web-Adamic-Adar/39348c10c90be968357e2a6b65d5e0e479307735[predict links in a social network^].
It is computed using the following formula:

image::adamic-adar.svg[role="middle"]

where `N(u)` is the set of nodes adjacent to `u`.

A value of 0 indicates that two nodes are not close, while higher values indicate nodes are closer.

The library contains a function to calculate closeness between two nodes.


[[algorithms-linkprediction-adamic-adar-syntax]]
== Syntax

.The following will run the algorithm and return the result:
[source, cypher]
----
RETURN gds.alpha.linkprediction.adamicAdar(node1:Node, node2:Node, {
    relationshipQuery:STRING,
    direction:STRING
})
----


.Parameters
[opts="header",cols="1,1,1,1,4"]
|===
| Name                   | Type    | Default        | Optional | Description
| `node1`                | Node    | null           | no       | A node
| `node2`                | Node    | null           | no       | Another node
| `relationshipQuery`    | String  | null           | yes      | The relationship type used to compute similarity between `node1` and `node2`
| `direction`            | String  | BOTH           | yes      | The direction of relationship type used to compute similarity between `node1` and `node2`
|===


[[algorithms-linkprediction-adamic-adar-sample]]
== Adamic Adar algorithm sample

.The following will create a sample graph:
[source, cypher]
----
CREATE
 (zhen:Person {name: 'Zhen'}),
 (praveena:Person {name: 'Praveena'}),
 (michael:Person {name: 'Michael'}),
 (arya:Person {name: 'Arya'}),
 (karin:Person {name: 'Karin'}),

 (zhen)-[:FRIENDS]->(arya),
 (zhen)-[:FRIENDS]->(praveena),
 (praveena)-[:WORKS_WITH]->(karin),
 (praveena)-[:FRIENDS]->(michael),
 (michael)-[:WORKS_WITH]->(karin),
 (arya)-[:FRIENDS]->(karin)
----

.The following will return the Adamic Adar score for Michael and Karin:
[source, cypher]
----
 MATCH (p1:Person {name: 'Michael'})
 MATCH (p2:Person {name: 'Karin'})
 RETURN gds.alpha.linkprediction.adamicAdar(p1, p2) AS score
----

.Results
[opts="header",cols="1"]
|===
| `score`
| 0.9102392266268373
|===


We can also compute the score of a pair of nodes based on a specific relationship type.

.The following will return the Adamic Adar score for Michael and Karin based only on the `FRIENDS` relationships:
[source, cypher]
----
 MATCH (p1:Person {name: 'Michael'})
 MATCH (p2:Person {name: 'Karin'})
 RETURN gds.alpha.linkprediction.adamicAdar(p1, p2, {relationshipQuery: 'FRIENDS'}) AS score
----


.Results
[opts="header",cols="1"]
|===
| `score`
| 0.0
|===



