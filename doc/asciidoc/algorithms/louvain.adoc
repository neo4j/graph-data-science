[[algorithms-louvain]]
= Louvain

[abstract]
--
This section describes the Louvain algorithm in the Neo4j Graph Data Science library.
--

This topic includes:

* <<algorithms-louvain-intro, Introduction>>
* <<algorithms-louvain-syntax, Syntax>>
* <<algorithms-louvain-examples, Examples>>
** <<algorithms-louvain-examples-stream, Streaming>>
** <<algorithms-louvain-examples-write, Writing>>
** <<algorithms-louvain-examples-stream-weighted, Weights>>
** <<algorithms-louvain-examples-stream-seeded, Seeding>>
** <<algorithms-louvain-examples-stream-intermediate, Multi-Level>>

[[algorithms-louvain-intro]]
== Introduction
The Louvain method is an algorithm to detect communities in large networks.
It maximizes a modularity score for each community, where the modularity quantifies the quality of an assignment of nodes to communities.
This means evaluating how much more densely connected the nodes within a community are, compared to how connected they would be in a random network.

The Louvain algorithm is a hierarchical clustering algorithm, that recursively merges communities into a single node and executes the modularity clustering on the condensed graphs.

For more information on this algorithm, see:

* https://arxiv.org/pdf/1410.1237.pdf[Lu, Hao, Mahantesh Halappanavar, and Ananth Kalyanaraman "Parallel heuristics for scalable community detection."^]
* https://en.wikipedia.org/wiki/Louvain_modularity

[NOTE]
====
Running this algorithm requires sufficient memory availability.
Before running this algorithm, we recommend that you read <<memory-estimation>>.
====


[[algorithms-louvain-syntax]]
== Syntax


=== Write mode

.Run Louvain in write mode on a graph stored in the catalog.
[source, cypher]
----
CALL gds.louvain.write(
  graphName: String,
  configuration: Map
)
YIELD
  // general write return columns
  nodePropertiesWritten: Integer,
  communityCount: Integer,
  modularity: Float
----

include::common-parameters-named-graph.adoc[]

include::common-configuration-named-graph.adoc[]

.Run Louvain in write mode on an anonymous graph.
[source, cypher]
----
CALL gds.louvain.write(configuration: Map)
YIELD
  // general write return columns
  nodePropertiesWritten: Integer,
  communityCount: Integer,
  modularity: Float
----

:entity: node
:result: community ID
include::common-parameters-anonymous-graph.adoc[]

include::common-configuration-anonymous-graph.adoc[]

.Algorithm specific configuration
[opts="header",cols="1,1,1m,1,4"]
|===
| Name                           | Type    | Default     | Optional | Description
| relationshipWeightProperty     | String  | null        | yes      | The property name that contains weight. If `null`, treats the graph as unweighted. Must be numeric.
| seedProperty                   | String  | n/a         | yes      | Used to set the initial community for a node. The property value needs to be a number.
| maxLevels                      | Integer | 10          | yes      | The maximum number of levels in which the graph is clustered and then condensed.
| maxIterations                  | Integer | 10          | yes      | The maximum number of iterations that the modularity optimization will run for each level.
| tolerance                      | Float   | 0.0001      | yes      | Minimum change in modularity between iterations. If the modularity changes less than the tolerance value, the result is considered stable and the algorithm returns.
| includeIntermediateCommunities | Boolean | false       | no       | Indicates whether to write intermediate communities. If set to false, only the final community is persisted.
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                  | Type      | Description
| createMillis          | Integer   | Milliseconds for loading data.
| computeMillis         | Integer   | Milliseconds for running the algorithm.
| writeMillis           | Integer   | Milliseconds for writing result data back.
| postProcessingMillis  | Integer   | Milliseconds for computing percentiles and community count.
| nodePropertiesWritten | Integer   | The number of node properties written.
| communityCount        | Integer   | The number of communities found.
| ranLevels             | Integer   | The number of supersteps the algorithm actually ran.
| modularity            | Float     | The final modularity score.
| modularities          | Integer[] | The modularity scores for each level.
| communityDistribution | Map       | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of community size for the last level.
| configuration         | Map       | The configuration used for running the algorithm.
|===


[[algorithms-louvain-syntax-stream]]
=== Stream mode

.Run Louvain in stream mode on a graph stored in the catalog.
[source, cypher]
----
CALL gds.louvain.stream(
  graphName: String,
  configuration: Map
)
YIELD
  nodeId: Integer,
  communityId: Integer,
  intermediateCommunityIds: Integer[]
----

include::common-parameters-named-graph.adoc[]

include::common-configuration-named-graph.adoc[]

.Run Louvain in stream mode on an anonymous graph.
[source, cypher]
----
CALL gds.louvain.stream(configuration: Map)
YIELD
  nodeId: Integer,
  communityId: Integer,
  intermediateCommunityIds: Integer[]
----

include::common-parameters-anonymous-graph.adoc[]

include::common-configuration-anonymous-graph.adoc[]

.Algorithm specific configuration
[opts="header",cols="1,1,1m,1,4"]
|===
| Name                           | Type     | Default | Optional | Description
| relationshipWeightProperty     | String   | null    | yes      | The property name that contains weight. If `null`, treats the graph as unweighted. Must be numeric.
| seedProperty                   | String   | n/a     | yes      | Used to set the initial community for a node. The property value needs to be a number.
| maxLevels                      | Integer  | 10      | yes      | The maximum number of levels in which the graph is clustered and then condensed.
| maxIterations                  | Integer  | 10      | yes      | The maximum number of iterations that the modularity optimization will run for each level.
| tolerance                      | Float    | 0.0001  | yes      | Minimum change in modularity between iterations. If the modularity changes less than the tolerance value, the result is considered stable and the algorithm returns.
| includeIntermediateCommunities | Boolean  | false   | yes      | Indicates whether to write intermediate communities. If set to false, only the final community is persisted.
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                      | Type      | Description
| nodeId                    | Integer   | Node ID.
| communityId               | Integer   | The community ID of the final level.
| intermediateCommunityIds  | Integer[] | Community IDs for each level. `Null` if `includeIntermediateCommunities` is set to false.
|===

[[algorithms-louvain-examples]]
== Examples

Consider the graph created by the following Cypher statement:

[source, cypher, role=setup-query]
----
CREATE (nAlice:User {name: 'Alice', seed: 42})
CREATE (nBridget:User {name: 'Bridget', seed: 42})
CREATE (nCharles:User {name: 'Charles', seed: 42})
CREATE (nDoug:User {name: 'Doug'})
CREATE (nMark:User {name: 'Mark'})
CREATE (nMichael:User {name: 'Michael'})

CREATE (nAlice)-[:LINK {weight: 1}]->(nBridget)
CREATE (nAlice)-[:LINK {weight: 1}]->(nCharles)
CREATE (nCharles)-[:LINK {weight: 1}]->(nBridget)

CREATE (nAlice)-[:LINK {weight: 5}]->(nDoug)

CREATE (nMark)-[:LINK {weight: 1}]->(nDoug)
CREATE (nMark)-[:LINK {weight: 1}]->(nMichael)
CREATE (nMichael)-[:LINK {weight: 1}]->(nMark);
----

This graph has two clusters of _Users_, that are closely connected.
Between those clusters there is one single edge.
The relationships that connect the nodes in each component have a property `weight` which determines the strength of the relationship.

We can now create the graph and store it in the graph catalog.
We load the `LINK` relationships with orientation set to `UNDIRECTED` as this works best with the Louvain algorithm.

[NOTE]
====
In the examples below we will use named graphs and standard projections as the norm.
However, <<cypher-projection, Cypher projection>> and anonymous graphs could also be used.
====

.The following statement will create the graph and store it in the graph catalog.
[source, cypher, role=setup-query]
----
CALL gds.graph.create(
    'myGraph',
    'User',
    {
        LINK: {
            orientation: 'UNDIRECTED'
        }
    },
    {
        nodeProperties: 'seed',
        relationshipProperties: 'weight'
    }
)
----

In the following examples we will demonstrate using the Louvain algorithm on this graph.


[[algorithms-louvain-examples-stream]]
=== Streaming results

[role=query-example]
--
.The following will run the algorithm and stream results:
[source, cypher]
----
CALL gds.louvain.stream('myGraph')
YIELD nodeId, communityId, intermediateCommunityIds
RETURN gds.util.asNode(nodeId).name AS name, communityId, intermediateCommunityIds
ORDER BY name ASC
----

.Results
[opts="header"]
|===
| name      | communityId   | intermediateCommunityIds
| "Alice"   | 2             | null
| "Bridget" | 2             | null
| "Charles" | 2             | null
| "Doug"    | 5             | null
| "Mark"    | 5             | null
| "Michael" | 5             | null
|6 rows
|===
--

We use default values for the procedure configuration parameter.
Levels and `innerIterations` are set to 10 and the tolerance value is 0.0001.
Because we did not set the value of `includeIntermediateCommunities` to `true`, the column communities is always `null`.


[[algorithms-louvain-examples-write]]
=== Writing results

To instead write the community results back to the graph in Neo4j, use the following query.
For each node a property is written that holds the assigned community.

[role=query-example]
--
.The following run the algorithm, and write back results:
[source, cypher]
----
CALL gds.louvain.write('myGraph', { writeProperty: 'community' })
YIELD communityCount, modularity, modularities
----

.Results
[opts="header"]
|===
| communityCount | modularity         | modularities
| 2              | 0.3571428571428571 | [0.3571428571428571]
|1 row
|===
--

When writing back the results, only a single row is returned by the procedure.
The result contains meta information, like the number of identified communities and the modularity values.


[[algorithms-louvain-examples-stream-weighted]]
=== Running on weighted graphs

The Louvain algorithm can also run on weighted graphs, taking the given relationship weights into concern when calculating the modularity.

[role=query-example]
--
.The following will run the algorithm on a weighted graph and stream results:
[source, cypher]
----
CALL gds.louvain.stream('myGraph', { relationshipWeightProperty: 'weight' })
YIELD nodeId, communityId, intermediateCommunityIds
RETURN gds.util.asNode(nodeId).name AS name, communityId, intermediateCommunityIds
ORDER BY name ASC
----

.Results
[opts="header"]
|===
| name      | communityId   | intermediateCommunityIds
| "Alice"   | 3             | null
| "Bridget" | 2             | null
| "Charles" | 2             | null
| "Doug"    | 3             | null
| "Mark"    | 5             | null
| "Michael" | 5             | null
|6 rows
|===
--

Using the weighted relationships, we see that `Alice` and `Doug` have formed their own community, as their link is much stronger than all the others.


[[algorithms-louvain-examples-stream-seeded]]
=== Running with seed communities

The Louvain algorithm can be run incrementally, by providing a seed property.
With the seed property an initial community mapping can be supplied for a subset of the loaded nodes.
The algorithm will try to keep the seeded community IDs.

[role=query-example]
--
.The following will run the algorithm and stream results:
[source, cypher]
----
CALL gds.louvain.stream('myGraph', { seedProperty: 'seed' })
YIELD nodeId, communityId, intermediateCommunityIds
RETURN gds.util.asNode(nodeId).name AS name, communityId, intermediateCommunityIds
ORDER BY name ASC
----

.Results
[opts="header"]
|===
| name      | communityId | intermediateCommunityIds
| "Alice"   | 42          | null
| "Bridget" | 42          | null
| "Charles" | 42          | null
| "Doug"    | 47          | null
| "Mark"    | 47          | null
| "Michael" | 47          | null
|6 rows
|===
--

Using the seeded graph, we see that the community around `Alice` keeps its initial community ID of `42`.
The other community is assigned a new community ID, which is guaranteed to be larger than the largest seeded community ID.


[[algorithms-louvain-examples-stream-intermediate]]
=== Streaming intermediate communities

As described before, Louvain is a hierarchical clustering algorithm.
That means that after every clustering step all nodes that belong to the same cluster are reduced to a single node.
Relationships between nodes of the same cluster become self-relationships, relationships to nodes of other clusters connect to the clusters representative.
This condensed graph is then used to run the next level of clustering.
The process is repeated until the clusters are stable.

In order to demonstrate this iterative behavior, we need to construct a more complex graph.

image::louvain-multilevel-graph.svg[align="center"]

[source, cypher, role=setup-query]
----
CREATE (a:Node {name: 'a'})
CREATE (b:Node {name: 'b'})
CREATE (c:Node {name: 'c'})
CREATE (d:Node {name: 'd'})
CREATE (e:Node {name: 'e'})
CREATE (f:Node {name: 'f'})
CREATE (g:Node {name: 'g'})
CREATE (h:Node {name: 'h'})
CREATE (i:Node {name: 'i'})
CREATE (j:Node {name: 'j'})
CREATE (k:Node {name: 'k'})
CREATE (l:Node {name: 'l'})
CREATE (m:Node {name: 'm'})
CREATE (n:Node {name: 'n'})
CREATE (x:Node {name: 'x'})

CREATE (a)-[:TYPE]->(b)
CREATE (a)-[:TYPE]->(d)
CREATE (a)-[:TYPE]->(f)
CREATE (b)-[:TYPE]->(d)
CREATE (b)-[:TYPE]->(x)
CREATE (b)-[:TYPE]->(g)
CREATE (b)-[:TYPE]->(e)
CREATE (c)-[:TYPE]->(x)
CREATE (c)-[:TYPE]->(f)
CREATE (d)-[:TYPE]->(k)
CREATE (e)-[:TYPE]->(x)
CREATE (e)-[:TYPE]->(f)
CREATE (e)-[:TYPE]->(h)
CREATE (f)-[:TYPE]->(g)
CREATE (g)-[:TYPE]->(h)
CREATE (h)-[:TYPE]->(i)
CREATE (h)-[:TYPE]->(j)
CREATE (i)-[:TYPE]->(k)
CREATE (j)-[:TYPE]->(k)
CREATE (j)-[:TYPE]->(m)
CREATE (j)-[:TYPE]->(n)
CREATE (k)-[:TYPE]->(m)
CREATE (k)-[:TYPE]->(l)
CREATE (l)-[:TYPE]->(n)
CREATE (m)-[:TYPE]->(n);
----

[role=query-example]
--
.The following will load the example graph, run the algorithm and stream results including the intermediate communities:
[source, cypher]
----
CALL gds.louvain.stream({
    nodeProjection: 'Node',
    relationshipProjection: {
        TYPE: {
            type: 'TYPE',
            orientation: 'undirected',
            aggregation: 'NONE'
        }
    },
    includeIntermediateCommunities: true
}) YIELD nodeId, communityId, intermediateCommunityIds
RETURN gds.util.asNode(nodeId).name AS name, communityId, intermediateCommunityIds
ORDER BY name ASC
----

.Results
[opts="header"]
|===
| name  | communityId | intermediateCommunityIds
| "a"   | 14          | [3, 14]
| "b"   | 14          | [3, 14]
| "c"   | 14          | [14, 14]
| "d"   | 14          | [3, 14]
| "e"   | 14          | [14, 14]
| "f"   | 14          | [14, 14]
| "g"   | 7           | [7, 7]
| "h"   | 7           | [7, 7]
| "i"   | 7           | [7, 7]
| "j"   | 12          | [12, 12]
| "k"   | 12          | [12, 12]
| "l"   | 12          | [12, 12]
| "m"   | 12          | [12, 12]
| "n"   | 12          | [12, 12]
| "x"   | 14          | [14, 14]
|15 rows
|===
--

In this example graph, after the first iteration we see 4 clusters, which in the second iteration are reduced to three.
