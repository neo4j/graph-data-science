[[algorithms-degree-centrality]]
= Degree Centrality
:entity: node
:result: centrality
:algorithm: Degree Centrality

[abstract]
--
This section describes the Degree Centrality algorithm in the Neo4j Graph Data Science library.
--


[[algorithms-degree-centrality-intro]]
== Introduction

The Degree Centrality algorithm can be used to find popular nodes within a graph.
Degree centrality measures the number of incoming or outgoing relationships from a node.
It can be applied to both, weighted and unweighted graphs.
In the weighted case the algorithm computes the sum of all positive weights of adjacent relationships of a node, for each node in the graph.

For more information on this algorithm, see:

* http://leonidzhukov.net/hse/2014/socialnetworks/papers/freeman79-centrality.pdf[Linton C. Freeman: Centrality in Social Networks Conceptual Clarification, 1979.^]


[[algorithms-degree-centrality-usecase]]
== Use-cases - when to use the Degree Centrality algorithm

* Degree centrality is an important component of any attempt to determine the most important people on a social network.
For example, in BrandWatch's https://www.brandwatch.com/blog/react-influential-men-and-women-2017/[most influential men and women on Twitter 2017^] the top 5 people in each category have over 40m followers each.

* Weighted degree centrality has been used to help separate fraudsters from legitimate users of an online auction.
The weighted centrality for fraudsters is significantly higher because they tend to collude with each other to artificially increase the price of items.
Read more in https://link.springer.com/chapter/10.1007/978-3-319-23461-8_11[Two Step graph-based semi-supervised Learning for Online Auction Fraud Detection^]


[[algorithms-degree-centrality-syntax]]
== Syntax

include::../shared/syntax-intro-named-graph.adoc[]

.Degree Centrality syntax per mode
[.tabbed-example]
====

[.include-with-stream]
======
.Run Degree Centrality in stream mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.degree.stream(
  graphName: String,
  configuration: Map
)
YIELD
  nodeId: Integer,
  score: Float
----

include::../common-configuration/common-parameters-named-graph.adoc[]

include::../common-configuration/common-stream-stats-configuration-named-graph.adoc[]

include::specific-configuration.adoc[]

.Results
[opts="header"]
|===
| Name   | Type     | Description
| nodeId | Integer  | Node ID.
| score  | Float    | Degree Centrality score.
|===
======


[.include-with-stats]
======
.Run Degree Centrality in stats mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.degree.stats(
  graphName: String,
  configuration: Map
)
YIELD
  centralityDistribution: Map,
  createMillis: Integer,
  computeMillis: Integer,
  postProcessingMillis: Integer,
  configuration: Map
----

include::../common-configuration/common-parameters-named-graph.adoc[]

include::../common-configuration/common-stream-stats-configuration-named-graph.adoc[]

include::specific-configuration.adoc[]

.Results
[opts="header",cols="1,1,6"]
|===
| Name                   | Type      | Description
| centralityDistribution | Map       | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of centrality values.
| createMillis           | Integer   | Milliseconds for creating the graph.
| computeMillis          | Integer   | Milliseconds for running the algorithm.
| postProcessingMillis   | Integer   | Milliseconds for computing the statistics.
| configuration          | Map       | Configuration used for running the algorithm.
|===
======


[.include-with-mutate]
======
.Run Degree Centrality in mutate mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.degree.mutate(
  graphName: String,
  configuration: Map
)
YIELD
  centralityDistribution: Map,
  createMillis: Integer,
  computeMillis: Integer,
  postProcessingMillis: Integer,
  mutateMillis: Integer,
  nodePropertiesWritten: Integer,
  configuration: Map
----

include::../common-configuration/common-parameters-named-graph.adoc[]

include::../common-configuration/common-mutate-configuration-named-graph.adoc[]

include::specific-configuration.adoc[]

.Results
[opts="header",cols="1,1,6"]
|===
| Name                   | Type      | Description
| centralityDistribution | Map       | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of centrality values.
| createMillis           | Integer   | Milliseconds for creating the graph.
| computeMillis          | Integer   | Milliseconds for running the algorithm.
| postProcessingMillis   | Integer   | Milliseconds for computing the statistics.
| mutateMillis           | Integer   | Milliseconds for adding properties to the in-memory graph.
| nodePropertiesWritten  | Integer   | Number of properties added to the in-memory graph.
| configuration          | Map       | Configuration used for running the algorithm.
|===
======


[.include-with-write]
======
.Run Degree Centrality in write mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.degree.write(
  graphName: String,
  configuration: Map
)
YIELD
  centralityDistribution: Map,
  createMillis: Integer,
  computeMillis: Integer,
  postProcessingMillis: Integer,
  writeMillis: Integer,
  nodePropertiesWritten: Integer,
  configuration: Map
----

include::../common-configuration/common-parameters-named-graph.adoc[]

include::../common-configuration/common-write-configuration-named-graph.adoc[]

include::specific-configuration.adoc[]

.Results
[opts="header",cols="1,1,6"]
|===
| Name                   | Type      | Description
| centralityDistribution | Map       | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of centrality values.
| createMillis           | Integer   | Milliseconds for creating the graph.
| computeMillis          | Integer   | Milliseconds for running the algorithm.
| postProcessingMillis   | Integer   | Milliseconds for computing the statistics.
| writeMillis            | Integer   | Milliseconds for writing result data back.
| nodePropertiesWritten  | Integer   | Number of properties written to Neo4j.
| configuration          | Map       | The configuration used for running the algorithm.
|===
======

====


[[algorithms-degree-centrality-syntax-anonymous]]
=== Anonymous graphs

include::../shared/syntax-anonymous-graphs.adoc[]

.Run Degree Centrality in write mode on an anonymous graph:
[source, cypher, role=noplay]
----
CALL gds.degree.write(
  configuration: Map
)
YIELD
  centralityDistribution: Map,
  createMillis: Integer,
  computeMillis: Integer,
  writeMillis: Integer,
  nodePropertiesWritten: Integer,
  configuration: Map
----

include::../common-configuration/common-write-configuration-anonymous-graph.adoc[]

include::specific-configuration.adoc[]


The results are the same as for running write mode with a named graph, see the <<algorithms-degree-centrality-syntax, write mode syntax above>>.


[[algorithms-degree-centrality-exsamples]]
== Examples

:algorithm-name: {algorithm}
:graph-description: social network
:image-file: degree-centralit-graph.svg
include::../shared/examples-intro.adoc[]

.The following Cypher statement will create the example graph in the Neo4j database:
[source, cypher, role=noplay setup-query]
----
CREATE
  (alice:User {name: 'Alice'}),
  (bridget:User {name: 'Bridget'}),
  (charles:User {name: 'Charles'}),
  (doug:User {name: 'Doug'}),
  (mark:User {name: 'Mark'}),
  (michael:User {name: 'Michael'}),

  (alice)-[:FOLLOWS {score: 1}]->(doug),
  (alice)-[:FOLLOWS {score: -2}]->(bridget),
  (alice)-[:FOLLOWS {score: 5}]->(charles),
  (mark)-[:FOLLOWS {score: 1.5}]->(doug),
  (mark)-[:FOLLOWS {score: 4.5}]->(michael),
  (bridget)-[:FOLLOWS {score: 1.5}]->(doug),
  (charles)-[:FOLLOWS {score: 2}]->(doug),
  (michael)-[:FOLLOWS {score: 1.5}]->(doug)
----

With the graph in Neo4j we can now project it into the graph catalog to prepare it for algorithm execution.
We do this using a native projection targeting the `User` nodes and the `FOLLOWS` relationships.

include::../shared/examples-named-native-note.adoc[]

.The following statement will create a graph using a reverse projection and store it in the graph catalog under the name 'myGraph'.
[source, cypher, role=noplay graph-create-query]
----
CALL gds.graph.create(
  'myGraph',
  'User',
  {
    FOLLOWS: {
      orientation: 'REVERSE',
      properties: ['score']
    }
  }
)
----

The graph is projected in a `REVERSE` orientation in order to retrieve people with the most followers in the following examples.
This will be demonstrated using the Degree Centrality algorithm on this graph.


[[algorithms-degree-centrality-examples-memory-estimation]]
=== Memory Estimation

:mode: write
include::../shared/examples-estimate-intro.adoc[]

[role=query-example]
--
.The following will estimate the memory requirements for running the algorithm:
[source, cypher, role=noplay]
----
CALL gds.degree.write.estimate('myGraph', { writeProperty: 'degree' })
YIELD nodeCount, relationshipCount, bytesMin, bytesMax, requiredMemory
----

.Results
[opts="header"]
|===
| nodeCount | relationshipCount | bytesMin | bytesMax | requiredMemory
| 6         | 8                 | 40     | 40     | "40 Bytes"
|===
--


[[algorithms-degree-centrality-examples-stream]]
=== Stream

:stream-details: For example, we can order the results to find the nodes with the highest degree centrality.
include::../shared/examples-stream-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `stream` mode:
[source, cypher, role=noplay]
----
CALL gds.degree.stream('myGraph')
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name AS name, score AS followers
ORDER BY followers DESC
----

.Results
[opts="header",cols="1,1"]
|===
| name    | followers
| Doug    | 5.0
| Bridget | 1.0
| Charles | 1.0
| Michael | 1.0
| Mark    | 0.0
| Alice   | 0.0
|===
--

We can see that Doug is the most popular user in our imaginary Twitter graph, with 5 followers - all other users follow him, but he doesn't follow anybody back.
In the real Twitter network celebrities have very high follower counts but tend to follow very few back people.
We could therefore consider Doug a celebrity!


[[algorithms-degree-centrality-examples-stats]]
=== Stats

:stats-syntax: algorithms-degree-centrality-syntax
include::../shared/examples-stats-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `stats` mode:
[source, cypher, role=noplay]
----
CALL gds.degree.stats('myGraph')
YIELD centralityDistribution
RETURN centralityDistribution.min AS minimumScore, centralityDistribution.mean AS meanScore
----

.Results
[opts="header"]
|===
| minimumScore | meanScore
| 0.0          | 1.3333358764648438
|===
--

Comparing this to the results we saw in the <<algorithms-degree-centrality-examples-stream, stream example>>, we can find our minimum and maximum values from the table.


[[algorithms-degree-centrality-examples-mutate]]
=== Mutate

include::../shared/examples-mutate-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `mutate` mode:
[source, cypher, role=noplay]
----
CALL gds.degree.mutate('myGraph', { mutateProperty: 'degree' })
YIELD centralityDistribution, nodePropertiesWritten
RETURN centralityDistribution.min AS minimumScore, centralityDistribution.mean AS meanScore, nodePropertiesWritten
----

.Results
[opts="header"]
|===
| minimumScore | meanScore                  | nodePropertiesWritten
| 0.0          | 1.3333358764648438         | 6
|===
--

The returned result is the same as in the `stats` example.
Additionally, the graph 'myGraph' now has a node property `degree` which stores the degree centrality score for each node.
To find out how to inspect the new schema of the in-memory graph, see <<catalog-graph-list>>.


[[algorithms-degree-centrality-examples-write]]
=== Write

include::../shared/examples-write-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `write` mode:
[source, cypher, role=noplay]
----
CALL gds.degree.write('myGraph', { writeProperty: 'degree' })
YIELD centralityDistribution, nodePropertiesWritten
RETURN centralityDistribution.min AS minimumScore, centralityDistribution.mean AS meanScore, nodePropertiesWritten
----

.Results
[opts="header"]
|===
| minimumScore | meanScore                  | nodePropertiesWritten
| 0.0          | 1.3333358764648438         | 6
|===
--

The returned result is the same as in the `stats` example.
Additionally, each of the seven nodes now has a new property `degree` in the Neo4j database, containing the degree centrality score for that node.


[[algorithms-degree-centrality-weighted-example]]
== Weighted Degree Centrality example

This sample will explain the weighted Degree Centrality algorithm, using a simple graph:

This algorithm is a variant of the Degree Centrality algorithm, that measures the sum of positive weights of incoming and outgoing relationships.

[role=query-example]
--
.The following will run the algorithm in `stream` mode, showing which users have the most weighted followers:
[source, cypher, role=noplay]
----
CALL gds.degree.stream(
   'myGraph',
   { relationshipWeightProperty: 'score' }
)
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name AS name, score AS weightedFollowers
ORDER BY weightedFollowers DESC
----

.Results
[opts="header",cols="1,1"]
|===
| name    | weightedFollowers
| Doug    | 7.5
| Charles | 5.0
| Michael | 4.5
| Bridget | 0.0
| Alice   | 0.0
| Mark    | 0.0
|===
--

Doug still remains our most popular user, but there isn't such a big gap to the next person.
Charles and Michael both only have one follower, but those relationships have a high relationship weight.
Note that Bridget also has a weighted score of 0.0, despite having a connection from Alice.
That is because the `score` property value between Bridget and Alice is negative and will be ignored by the algorithm.
