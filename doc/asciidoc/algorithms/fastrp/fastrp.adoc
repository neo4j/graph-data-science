[[algorithms-embeddings-fastrp]]
= Fast Random Projection
:entity: node
:result: embedding
:algorithm: FastRP

[abstract]
--
This section describes the Fast Random Projection (FastRP) node embedding algorithm in the Neo4j Graph Data Science library.
--


[[algorithms-embeddings-fastrp-introduction]]
== Introduction

Fast Random Projection, or FastRP for short, is a node embedding algorithm in the family of random projection algorithms.
These algorithms are theoretically backed by the Johnsson-Lindenstrauss lemma according to which one can project _n_ vectors of _arbitrary_ dimension into _O(log(n))_ dimensions and still approximately preserve pairwise distances among the points.
In fact, a linear projection chosen in a random way satisfies this property.

Such techniques therefore allow for aggressive dimensionality reduction while preserving most of the distance information.
The FastRP algorithm operates on graphs, in which case we care about preserving similarity between nodes and their neighbors.
This means that two nodes that have similar neighborhoods should be assigned similar embedding vectors.
Conversely, two nodes that are not similar should be not be assigned similar embedding vectors.

The FastRP algorithm initially assigns random vectors to all nodes using a technique called _very sparse random projection_, see (Achlioptas, 2003) below.
The algorithm then iteratively constructs _intermediate_ embeddings by averaging either neighboring intermediate embeddings from the previous iteration, or the generated random vectors during the first iteration.
In each iteration, the intermediate embedding is normalised using a https://en.wikipedia.org/wiki/Norm_(mathematics)#Euclidean_norm[standard euclidean norm].
That is, each element in the embedding is divided by the square root of the sum of squares of the emdedding elements.

In the end, the resulting embedding for each node is a weighted sum of the intermediate embeddings, where the weights are a configuration parameter called `iterationWeights`.

Therefore, each node's embedding depends on a neighborhood of radius equal to the number of iterations.
This way FastRP exploits higher-order relationships in the graph while still being highly scalable.

The present implementation extends the original algorithm to support weighted graphs, which computes weighted averages of neighboring embeddings using the relationship weights.
In order to make use of this, the `relationshipWeightProperty` parameter should be set to an existing relationship property.

The original algorithm is intended only for undirected graphs.
We support running on both on directed graphs and undirected graph.
For directed graphs we consider only the outgoing neighbors when computing the intermediate embeddings for a node.
Therefore, using the orientations `NATURAL`, `REVERSE` or `UNDIRECTED` will all give different embeddings.
In general, it is recommended to first use `UNDIRECTED` as this is what the original algorithm was evaluated on.

For more information on this algorithm see:

* https://arxiv.org/pdf/1908.11512.pdf[H. Chen, S.F. Sultan, Y. Tian, M. Chen, S. Skiena: Fast and Accurate Network Embeddings via Very Sparse Random Projection, 2019.^]
* https://core.ac.uk/download/pdf/82724427.pdf[Dimitris Achlioptas. Database-friendly random projections: Johnson-Lindenstrauss with binary coins. Journal of Computer and System Sciences, 66(4):671â€“687, 2003.]


[[algorithms-embeddings-fastrp-parameter-tuning]]
== Tuning algorithm parameters

In order to improve the embedding quality using FastRP on one of your graphs, it is possible to tune the algorithm parameters.
This process of finding the best parameters for your specific use case and graph is typically referred to as https://en.wikipedia.org/wiki/Hyperparameter_optimization[hyperparameter tuning].
We will go through each of the configuration parameters and explain how they behave.

For statistically sound results, it is a good idea to reserve a test set excluded from parameter tuning.
After selecting a set of parameter values, the embedding quality can be evaluated using a downstream machine learning task on the test set.
By varying the parameter values and studying the precision of the machine learning task, it is possible to deduce the parameter values that best fit the concrete dataset and use case.
To construct such a set you may want to use a dedicated node label in the graph to denote a subgraph without the test data.


=== Embedding dimension

The embedding dimension is the length of the produced vectors.
A greater dimension offers a greater precision, but is more costly to operate over.

The optimal embedding dimension depends on the number of nodes in the graph.
Since the amount of information the embedding can encode is limited by its dimension, a larger graph will tend to require a greater embedding dimension.
A typical value is a power of two in the range 128 - 1024.
A value of at least 256 gives good results on graphs in the order of 10^5^ nodes, but in general increasing the dimension improves results.
Increasing embedding dimension will however increase memory requirements and runtime linearly.


=== Normalization strength

The normalization strength is used to control how node degrees influence the embedding.
Using a negative value will downplay the importance of high degree neighbors, while a positive value will instead increase their importance.
The optimal normalization strength depends on the graph and on the task that the embeddings will be used for.
In the original paper, hyperparameter tuning was done in the range of `[-1,0]` (no positive values), but we have found cases where a positive normalization strengths gives better results.


=== Iteration weights

The iteration weights parameter control two aspects: the number of iterations, and their relative impact on the final node embedding.
The parameter is a list of numbers, indicating one iteration per number where the number is the weight applied to that iteration.

In each iteration, the algorithm will expand across all relationships in the graph.
This has some implications:

* With a single iteration, only direct neighbors will be considered for each node embedding.
* With two iterations, direct neighbors and second-degree neighbors will be considered for each node embedding.
* With three iterations, direct neighbors, second-degree neighbors, and third-degree neighbors will be considered for each node embedding.
Direct neighbors may be reached twice, in different iterations.
* In general, the embedding corresponding to the `i`:th iteration contains features depending on nodes reachable with paths of length `i`.
If the graph is undirected, then a node reachable with a path of length `L` can also be reached with length `L+2k`, for any integer `k`.
* In particular, a node may reach back to itself on each even iteration (depending on the direction in the graph).

It is good to have at least one non-zero weight in an even and in an odd position.
Typically, using at least a few iterations, for example three, is recommended.
However, a too high value will consider nodes far away and may not be informative or even be detrimental.
The intuition here is that as the projections reach further away from the node, the less specific the neighborhood becomes.
Of course, a greater number of iterations will also take more time to complete.


=== Orientation

Choosing the right orientation when creating the graph may have the single greatest impact.
The FastRP algorithm is designed to work with undirected graphs, and we expect this to be the best in most cases.
If you expect only outgoing or incoming relationships to be informative for a prediction task, then you may want to try using the orientations `NATURAL` or `REVERSE` respectively.


[[algorithms-embeddings-fastrp-syntax]]
== Syntax

include::../shared/syntax-intro-named-graph.adoc[]

.FastRP syntax per mode
[.tabbed-example]
====

[.include-with-stream]
======

.Run FastRP in stream mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.fastRP.stream(
  graphName: String,
  configuration: Map
) YIELD
  nodeId: Integer,
  embedding: Float[]
----

include::../common-configuration/common-parameters-named-graph.adoc[]

include::../common-configuration/common-stream-stats-configuration-named-graph.adoc[]

include::specific-configuration.adoc[]

.Results
[opts="header"]
|===
| Name      | Type         | Description
| nodeId    | Integer      | Node ID.
| embedding | List of Float  | FastRP node embedding.
|===
======

[.include-with-stats]
======

.Run FastRP in stats mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.fastRP.stats(
  graphName: String,
  configuration: Map
) YIELD
  nodeCount: Integer,
  createMillis: Integer,
  computeMillis: Integer,
  configuration: Map
----

include::../common-configuration/common-parameters-named-graph.adoc[]

include::../common-configuration/common-stream-stats-configuration-named-graph.adoc[]

include::specific-configuration.adoc[]

.Results
[opts="header",cols="1,1,6"]
|===
| Name          | Type    | Description
| nodeCount     | Integer | Number of nodes processed.
| createMillis  | Integer | Milliseconds for creating the graph.
| computeMillis | Integer | Milliseconds for running the algorithm.
| configuration | Map     | Configuration used for running the algorithm.
|===

======

[.include-with-mutate]
======

.Run FastRP in mutate mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.fastRP.mutate(
  graphName: String,
  configuration: Map
) YIELD
  nodeCount: Integer,
  nodePropertiesWritten: Integer,
  createMillis: Integer,
  computeMillis: Integer,
  mutateMillis: Integer,
  configuration: Map
----

include::../common-configuration/common-parameters-named-graph.adoc[]

include::../common-configuration/common-mutate-configuration-named-graph.adoc[]

include::specific-configuration.adoc[]

.Results
[opts="header"]
|===
| Name                  | Type    | Description
| nodeCount             | Integer | Number of nodes processed.
| nodePropertiesWritten | Integer | Number of node properties written.
| createMillis          | Integer | Milliseconds for creating the graph.
| computeMillis         | Integer | Milliseconds for running the algorithm.
| mutateMillis          | Integer | Milliseconds for adding properties to the in-memory graph.
| configuration         | Map     | Configuration used for running the algorithm.
|===
======

[.include-with-write]
======

.Run FastRP in write mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.fastRP.write(
  graphName: String,
  configuration: Map
) YIELD
  nodeCount: Integer,
  nodePropertiesWritten: Integer,
  createMillis: Integer,
  computeMillis: Integer,
  writeMillis: Integer,
  configuration: Map
----

include::../common-configuration/common-parameters-named-graph.adoc[]

include::../common-configuration/common-write-configuration-named-graph.adoc[]

include::specific-configuration.adoc[]

.Results
[opts="header"]
|===
| Name                  | Type    | Description
| nodeCount             | Integer | Number of nodes processed.
| nodePropertiesWritten | Integer | Number of node properties written.
| createMillis          | Integer | Milliseconds for creating the graph.
| computeMillis         | Integer | Milliseconds for running the algorithm.
| writeMillis           | Integer | Milliseconds for writing result data back to Neo4j.
| configuration         | Map     | Configuration used for running the algorithm.
|===

======

====


[[algorithms-embeddings-fastrp-syntax-anonymous]]
=== Anonymous graphs

include::../shared/syntax-anonymous-graphs.adoc[]

.Run FastRP in write mode on an anonymous graph.
[source, cypher, role=noplay]
----
CALL gds.fastRP.write(
  configuration: Map
)
YIELD
  nodeCount: Integer,
  nodePropertiesWritten: Integer,
  createMillis: Integer,
  computeMillis: Integer,
  writeMillis: Integer,
  configuration: Map
----

include::../common-configuration/common-configuration-anonymous-graph.adoc[]

include::specific-configuration.adoc[]

The results are the same as for running write mode with a named graph, see the <<algorithms-embeddings-fastrp-syntax, write mode syntax above>>.


[[algorithms-embeddings-fastrp-examples]]
== Examples

:algorithm-name: FastRP node embedding
:graph-description: social network
:image-file: fastrp.svg
include::../shared/examples-intro.adoc[]

.The following Cypher statement will create the example graph in the Neo4j database:
[source, cypher, role=noplay setup-query]
----
CREATE
  (dan:Person {name: 'Dan'}),
  (annie:Person {name: 'Annie'}),
  (matt:Person {name: 'Matt'}),
  (jeff:Person {name: 'Jeff'}),
  (brie:Person {name: 'Brie'}),
  (elsa:Person {name: 'Elsa'}),
  (john:Person {name: 'John'}),

  (dan)-[:KNOWS {weight: 1.0}]->(annie),
  (dan)-[:KNOWS {weight: 1.0}]->(matt),
  (annie)-[:KNOWS {weight: 1.0}]->(matt),
  (annie)-[:KNOWS {weight: 1.0}]->(jeff),
  (annie)-[:KNOWS {weight: 1.0}]->(brie),
  (matt)-[:KNOWS {weight: 3.5}]->(brie),
  (brie)-[:KNOWS {weight: 1.0}]->(elsa),
  (brie)-[:KNOWS {weight: 2.0}]->(jeff),
  (john)-[:KNOWS {weight: 1.0}]->(jeff);
----

This graph represents seven people who know one another.
A relationship property `weight` denotes the strength of the knowledge between two persons.

With the graph in Neo4j we can now project it into the graph catalog to prepare it for algorithm execution.
We do this using a native projection targeting the `Person` nodes and the `KNOWS` relationships.
For the relationships we will use the `UNDIRECTED` orientation.
This is because the FastRP algorithm has been measured to compute more predictive node embeddings in undirected graphs.
We will also add the `weight` relationship property which we will make use of when running the weighted version of FastRP.

include::../shared/examples-named-native-note.adoc[]

.The following statement will create a graph using a native projection and store it in the graph catalog under the name 'persons'.
[source, cypher, role=noplay graph-create-query]
----
CALL gds.graph.create(
  'persons',
  'Person',
  {
    KNOWS: {
      orientation: 'UNDIRECTED',
      properties: 'weight'
    }
})
----


[[algorithms-embeddings-fastrp-examples-memory-estimation]]
=== Memory Estimation

:mode: stream
include::../shared/examples-estimate-intro.adoc[]

[role=query-example]
--
.The following will estimate the memory requirements for running the algorithm:
[source, cypher, role=noplay]
----
CALL gds.fastRP.stream.estimate('persons', {embeddingDimension: 128})
YIELD nodeCount, relationshipCount, bytesMin, bytesMax, requiredMemory
----

.Results
[opts="header", cols="1,1,1,1,1"]
|===
| nodeCount | relationshipCount | bytesMin | bytesMax | requiredMemory
| 7         | 18                | 11416    | 11416    | "11416 Bytes"
|===
--


[[algorithms-embeddings-fastrp-examples-stream]]
=== Stream

:stream-details: For example, we can collect the results and pass them into a similarity algorithm.
include::../shared/examples-stream-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm, and stream results:
[source, cypher, role=noplay]
----
CALL gds.fastRP.stream('persons',
  {
    embeddingDimension: 4,
    randomSeed: 42
  }
)
YIELD nodeId, embedding
----

[opts=header]
.Results
|===
| nodeId | embedding
| 0      | [0.4774002134799957, -0.6602408289909363, -0.36686956882476807, -1.7089111804962158]
| 1      | [0.7989360094070435, -0.4918718934059143, -0.41281944513320923, -1.6314401626586914]
| 2      | [0.47275322675704956, -0.49587157368659973, -0.3340468406677246, -1.7141895294189453]
| 3      | [0.8290714025497437, -0.3260476291179657, -0.3317275643348694, -1.4370529651641846]
| 4      | [0.7749264240264893, -0.4773247539997101, 0.0675133764743805, -1.5248265266418457]
| 5      | [0.8408374190330505, -0.37151476740837097, 0.12121132016181946, -1.530960202217102]
| 6      | [1.0, -0.11054422706365585, -0.3697933852672577, -0.9225144982337952]
|===
--

The results of the algorithm are not very intuitively interpretable, as the node embedding format is a mathematical abstraction of the node within its neighborhood, designed for machine learning programs.
What we can see is that the embeddings have four elements (as configured using `embeddingDimension`) and that the numbers are relatively small (they all fit in the range of `[-2, 2]`).
The magnitude of the numbers is controlled by the `embeddingDimension`, the number of nodes in the graph, and by the fact that FastRP performs euclidean normalization on the intermediate embedding vectors.

[NOTE]
====
Due to the random nature of the algorithm the results will vary between the runs.
However, this does not necessarily mean that the pairwise distances of two node embeddings vary as much.
====


[[algorithms-embeddings-fastrp-examples-stats]]
=== Stats

:stats-syntax: algorithms-embeddings-fastrp-syntax
include::../shared/examples-stats-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm and returns the result in form of statistical and measurement values
[source, cypher, role=noplay]
----
CALL gds.fastRP.stats('persons', { embeddingDimension: 8 })
YIELD nodeCount
----

[opts=header]
.Results
|===
| nodeCount
| 7
|===
--

The `stats` mode does not currently offer any statistical results for the embeddings themselves.
We can however see that the algorithm has successfully processed all seven nodes in our example graph.


[[algorithms-embeddings-fastrp-examples-mutate]]
=== Mutate

include::../shared/examples-mutate-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `mutate` mode:
[source, cypher, role=noplay]
----
CALL gds.fastRP.mutate(
  'persons',
  {
    embeddingDimension: 8,
    mutateProperty: 'fastrp-embedding'
  }
)
YIELD nodePropertiesWritten
----

[opts=header]
.Results
|===
| nodePropertiesWritten
| 7
|===
--

The returned result is similar to the `stats` example.
Additionally, the graph 'persons' now has a node property `fastrp-embedding` which stores the node embedding for each node.
To find out how to inspect the new schema of the in-memory graph, see <<catalog-graph-list>>.


[[algorithms-embeddings-fastrp-examples-write]]
=== Write

include::../shared/examples-write-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `write` mode:
[source, cypher, role=noplay]
----
CALL gds.fastRP.write(
  'persons',
  {
    embeddingDimension: 8,
    writeProperty: 'fastrp-embedding'
  }
)
YIELD nodePropertiesWritten
----

[opts=header]
.Results
|===
| nodePropertiesWritten
| 7
|===
--

The returned result is similar to the `stats` example.
Additionally, each of the seven nodes now has a new property `fastrp-embedding` in the Neo4j database, containing the node embedding for that node.


[[algorithms-embeddings-fastrp-examples-weighted]]
=== Weighted

By default, the algorithm is considering the relationships of the graph to be unweighted.
To change this behaviour we can use configuration parameter called `relationshipWeightProperty`.
Below is an example of running the weighted variant of algorithm.

[role=query-example]
--
.The following will run the algorithm, and stream results:
[source, cypher, role=noplay]
----
CALL gds.fastRP.stream(
  'persons',
  {
    embeddingDimension: 4,
    randomSeed: 42,
    relationshipWeightProperty: 'weight'
  }
)
YIELD nodeId, embedding
----

[opts=header]
.Results
|===
| nodeId | embedding
| 0      | [0.10945529490709305, -0.5032674074172974, 0.464673787355423, -1.7539862394332886]
| 1      | [0.3639600872993469, -0.39210301637649536, 0.46271592378616333, -1.829423427581787]
| 2      | [0.12314096093177795, -0.3213110864162445, 0.40100979804992676, -1.471055269241333]
| 3      | [0.30704641342163086, -0.24944794178009033, 0.3947891891002655, -1.3463698625564575]
| 4      | [0.23112300038337708, -0.30148714780807495, 0.584831714630127, -1.2822188138961792]
| 5      | [0.14497177302837372, -0.2312137484550476, 0.5552002191543579, -1.2605633735656738]
| 6      | [0.5139184594154358, -0.07954332232475281, 0.3690345287322998, -0.9176374077796936]
|===
--

Since the initial state of the algorithm is randomised, it isn't possible to intuitively analyse the effect of the relationship weights.

[[algorithms-embeddings-fastrp-extended]]
== Extended Algorithm: Using Node Properties

include::../beta-aspect-note.adoc[]

Most real-world graphs contain node properties which store information about the nodes and what they represent.
An embedding algorithm which can process the node properties as features and incorporate them in the embeddings can therefore represent the graph more accurately.

The extended FastRP algorithm (FastRPExtended) has the additional configuration parameters `featureProperties` and `propertyDimension`.
Each node property specified in the former is associated with a randomly generated vector of dimension `propertyDimension`.
Each node is then initialized with a vector of size `embeddingDimension` formed by concatenation of two parts:
The first part is formed like in the FastRP algorithm, and the second one is a linear combination of the property vectors, using the property values of the node as weights.
The algorithm then proceeds with the same logic as the FastRP algorithm.
Therefore, the algorithm will output arrays of size `embeddingDimension`.
The last `propertyDimension` coordinates in the embedding captures information about property values of nearby nodes, and the remaining coordinates (`embeddingDimension` - `propertyDimension` of them) captures information about nearby presence of nodes.

As other configuration parameters, `propertyDimension` needs to be tuned for optimal performance.
We suggest keeping the previously selected value for `embeddingDimension` and setting `propertyDimension` to half that value as a starting point for using FastRPExtended.
Other factors that may influence the choice of `propertyDimension` are vaguely the amount of valuable information contained in the node properties, i.e. number of properties and how independent they are, as well as how relevant the properties are to the problem at hand.

.FastRPExtended syntax per mode
[.tabbed-example]
====

[.include-with-stream]
======
.Run FastRPExtended in stream mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.beta.fastRPExtended.stream(
  graphName: String,
  configuration: Map
) YIELD
  nodeId: Integer,
  embedding: List of Float
----

include::../common-configuration/common-parameters-named-graph.adoc[]

include::../common-configuration/common-stream-stats-configuration-named-graph.adoc[]

include::specific-configuration-ext.adoc[]

.Results
[opts="header",cols="1m,1,6"]
|===
| Name      | Type         | Description
| nodeId    | Integer      | The Neo4j node ID.
| embedding | List of Float  | The computed node embedding.
|===
======

[.include-with-stats]
======
.Run FastRPExtended in stats mode on a graph stored in the catalog.
[source, cypher, role=noplay]
----
CALL gds.beta.fastRPExtended.stats(
  graphName: String,
  configuration: Map
)
YIELD
  nodeCount: Integer,
  createMillis: Integer,
  computeMillis: Integer,
  configuration: Map
----

include::../common-configuration/common-parameters-named-graph.adoc[]

include::../common-configuration/common-stream-stats-configuration-named-graph.adoc[]

include::specific-configuration-ext.adoc[]

.Results
[opts="header",cols="1,1,6"]
|===
| Name               | Type                 | Description
| nodeCount          | Integer              | The number of nodes processed.
| createMillis       | Integer              | Milliseconds for loading data.
| computeMillis      | Integer              | Milliseconds for running the algorithm.
| configuration      | Map                  | The configuration used for running the algorithm.
|===
======

[.include-with-mutate]
======
.Run FastRPExtended in mutate mode on a graph stored in the catalog.
[source, cypher, role=noplay]
----
CALL gds.beta.fastRPExtended.mutate(
  graphName: String,
  configuration: Map
)
YIELD
  nodeCount: Integer,
  nodePropertiesWritten: Integer,
  createMillis: Integer,
  computeMillis: Integer,
  mutateMillis: Integer,
  configuration: Map
----

include::../common-configuration/common-parameters-named-graph.adoc[]

include::../common-configuration/common-mutate-configuration-named-graph.adoc[]

include::specific-configuration-ext.adoc[]

.Results
[opts="header",cols="1,1,6"]
|===
| Name                   | Type                 | Description
| nodeCount              | Integer              | The number of nodes processed.
| nodePropertiesWritten  | Integer              | The number of node properties written.
| createMillis           | Integer              | Milliseconds for loading data.
| computeMillis          | Integer              | Milliseconds for running the algorithm.
| mutateMillis           | Integer              | Milliseconds for adding properties to the in-memory graph.
| configuration          | Map                  | The configuration used for running the algorithm.
|===
======

[.include-with-write]
======
.Run FastRPExtended in write mode on a graph stored in the catalog.
[source, cypher, role=noplay]
----
CALL gds.beta.fastRPExtended.write(
  graphName: String,
  configuration: Map
)
YIELD
  nodeCount: Integer,
  nodePropertiesWritten: Integer,
  createMillis: Integer,
  computeMillis: Integer,
  writeMillis: Integer,
  configuration: Map
----

include::../common-configuration/common-parameters-named-graph.adoc[]

include::../common-configuration/common-write-configuration-named-graph.adoc[]

include::specific-configuration-ext.adoc[]

.Results
[opts="header",cols="1,1,6"]
|===
| Name                   | Type                 | Description
| nodeCount              | Integer              | The number of nodes processed.
| nodePropertiesWritten  | Integer              | The number of node properties written.
| createMillis           | Integer              | Milliseconds for loading data.
| computeMillis          | Integer              | Milliseconds for running the algorithm.
| writeMillis            | Integer              | Milliseconds for writing result data back to Neo4j.
| configuration          | Map                  | The configuration used for running the algorithm.
|===
======
====

Memory estimation for FastRPExtended works similarly to FastRP, which we can see in the following example for the stream mode:

[role=query-example]
--
.The following will estimate the memory requirements for FastRPExtended:
[source, cypher, role=noplay]
----
CALL gds.beta.fastRPExtended.stream.estimate('persons', {embeddingDimension: 128, propertyDimension: 64, featureProperties: ['p1', 'p2']})
YIELD nodeCount, relationshipCount, bytesMin, bytesMax, requiredMemory
----

.Results
[opts="header", cols="1,1,1,1,1"]
|===
| nodeCount | relationshipCount | bytesMin | bytesMax | requiredMemory
| 7         | 18                | 11928    | 11928    | "11928 Bytes"
|===
--
