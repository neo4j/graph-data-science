[[algorithms-embeddings-fastrp]]
= Fast Random Projection
:stem: latexmath
:entity: node
:result: embedding
:algorithm: FastRP

[abstract]
--
This section describes the Fast Random Projection (FastRP) node embedding algorithm in the Neo4j Graph Data Science library.
--

:directed:
:undirected:
:homogeneous:
:weighted:
include::../shared/algorithm-traits.adoc[]


[[algorithms-embeddings-fastrp-introduction]]
== Introduction

Fast Random Projection, or FastRP for short, is a node embedding algorithm in the family of random projection algorithms.
These algorithms are theoretically backed by the Johnsson-Lindenstrauss lemma according to which one can project _n_ vectors of _arbitrary_ dimension into _O(log(n))_ dimensions and still approximately preserve pairwise distances among the points.
In fact, a linear projection chosen in a random way satisfies this property.

Such techniques therefore allow for aggressive dimensionality reduction while preserving most of the distance information.
The FastRP algorithm operates on graphs, in which case we care about preserving similarity between nodes and their neighbors.
This means that two nodes that have similar neighborhoods should be assigned similar embedding vectors.
Conversely, two nodes that are not similar should be not be assigned similar embedding vectors.

The FastRP algorithm initially assigns random vectors to all nodes using a technique called _very sparse random projection_, see (Achlioptas, 2003) below.
Moreover, in GDS it is possible to use node properties for the creation of these _initial random vectors_ in a way described <<algorithms-embeddings-fastrp-extended,below>>.
We will also use _projection of a node_ synonymously with the initial random vector of a node.

Starting with these random vectors and iteratively averaging over node neighborhoods, the algorithm constructs a sequence of _intermediate embeddings_ stem:[e^i_n] for each node stem:[n].
More precisely,
[stem]
++++
e^i_n = \text{avg}({e^{i-1}_m}),
++++
where stem:[m] ranges over neighbors of stem:[n] and stem:[e^0_n] is the node's initial random vector .

The embedding stem:[e_n] of node stem:[n], which is the output of the algorithm, is a combination of the vectors and embeddings defined above:
[stem]
++++
e_n = w_0 \cdot \text{normalize}(r_n) + \sum_{i=1}^{i=k}w_i \cdot \text{normalize}(e^i_n),
++++
where stem:[\text{normalize}] is the function which divides a vector with its https://en.wikipedia.org/wiki/Norm_(mathematics)#Euclidean_norm[L2 norm], the value of `nodeSelfInfluence` is stem:[w_0], and the values of `iterationWeights` are stem:[[w_1, w_2, \ldots, w_k]].
We will return to <<algorithms-embeddings-fastrp-node-self-influence,Node Self Influence>> later on.

Therefore, each node's embedding depends on a neighborhood of radius equal to the number of iterations.
This way FastRP exploits higher-order relationships in the graph while still being highly scalable.

The present implementation extends the original algorithm to support weighted graphs, which computes weighted averages of neighboring embeddings using the relationship weights.
In order to make use of this, the `relationshipWeightProperty` parameter should be set to an existing relationship property.

The original algorithm is intended only for undirected graphs.
We support running on both on directed graphs and undirected graph.
For directed graphs we consider only the outgoing neighbors when computing the intermediate embeddings for a node.
Therefore, using the orientations `NATURAL`, `REVERSE` or `UNDIRECTED` will all give different embeddings.
In general, it is recommended to first use `UNDIRECTED` as this is what the original algorithm was evaluated on.

For more information on this algorithm see:

* https://arxiv.org/pdf/1908.11512.pdf[H. Chen, S.F. Sultan, Y. Tian, M. Chen, S. Skiena: Fast and Accurate Network Embeddings via Very Sparse Random Projection, 2019.^]
* https://core.ac.uk/download/pdf/82724427.pdf[Dimitris Achlioptas. Database-friendly random projections: Johnson-Lindenstrauss with binary coins. Journal of Computer and System Sciences, 66(4):671â€“687, 2003.]


[[algorithms-embeddings-fastrp-parameter-tuning]]
== Tuning algorithm parameters

In order to improve the embedding quality using FastRP on one of your graphs, it is possible to tune the algorithm parameters.
This process of finding the best parameters for your specific use case and graph is typically referred to as https://en.wikipedia.org/wiki/Hyperparameter_optimization[hyperparameter tuning].
We will go through each of the configuration parameters and explain how they behave.

For statistically sound results, it is a good idea to reserve a test set excluded from parameter tuning.
After selecting a set of parameter values, the embedding quality can be evaluated using a downstream machine learning task on the test set.
By varying the parameter values and studying the precision of the machine learning task, it is possible to deduce the parameter values that best fit the concrete dataset and use case.
To construct such a set you may want to use a dedicated node label in the graph to denote a subgraph without the test data.


=== Embedding dimension

The embedding dimension is the length of the produced vectors.
A greater dimension offers a greater precision, but is more costly to operate over.

The optimal embedding dimension depends on the number of nodes in the graph.
Since the amount of information the embedding can encode is limited by its dimension, a larger graph will tend to require a greater embedding dimension.
A typical value is a power of two in the range 128 - 1024.
A value of at least 256 gives good results on graphs in the order of 10^5^ nodes, but in general increasing the dimension improves results.
Increasing embedding dimension will however increase memory requirements and runtime linearly.


=== Normalization strength

The normalization strength is used to control how node degrees influence the embedding.
Using a negative value will downplay the importance of high degree neighbors, while a positive value will instead increase their importance.
The optimal normalization strength depends on the graph and on the task that the embeddings will be used for.
In the original paper, hyperparameter tuning was done in the range of `[-1,0]` (no positive values), but we have found cases where a positive normalization strengths gives better results.


=== Iteration weights

The iteration weights parameter control two aspects: the number of iterations, and their relative impact on the final node embedding.
The parameter is a list of numbers, indicating one iteration per number where the number is the weight applied to that iteration.

In each iteration, the algorithm will expand across all relationships in the graph.
This has some implications:

* With a single iteration, only direct neighbors will be considered for each node embedding.
* With two iterations, direct neighbors and second-degree neighbors will be considered for each node embedding.
* With three iterations, direct neighbors, second-degree neighbors, and third-degree neighbors will be considered for each node embedding.
Direct neighbors may be reached twice, in different iterations.
* In general, the embedding corresponding to the `i`:th iteration contains features depending on nodes reachable with paths of length `i`.
If the graph is undirected, then a node reachable with a path of length `L` can also be reached with length `L+2k`, for any integer `k`.
* In particular, a node may reach back to itself on each even iteration (depending on the direction in the graph).

It is good to have at least one non-zero weight in an even and in an odd position.
Typically, using at least a few iterations, for example three, is recommended.
However, a too high value will consider nodes far away and may not be informative or even be detrimental.
The intuition here is that as the projections reach further away from the node, the less specific the neighborhood becomes.
Of course, a greater number of iterations will also take more time to complete.

[[algorithms-embeddings-fastrp-node-self-influence]]
=== Node Self Influence

Node Self Influence is a variation of the original FastRP algorithm.

How much a node's embedding is affected by the intermediate embedding at iteration stem:[i] is controlled by the stem:[i]'th element of `iterationWeights`.
This can also be seen as how much the initial random vectors, or projections, of nodes that can be reached in stem:[i] hops from a node affect the embedding of the node.
Similarly, `nodeSelfInfluence` behaves like an iteration weight for a zeroth iteration, or the amount of influence the projection of a node has on the embedding of the same node.

A reason for setting this parameter to a non-zero value is if your graph has low connectivity or a significant amount of isolated nodes.
Isolated nodes combined with using `propertyRatio = 0.0` leads to embeddings that contain all zeros.
However using node properties along with node self influence can thus produce more meaningful embeddings for such nodes.
This can be seen as producing fallback features when graph structure is (locally) missing.
Moreover, sometimes a node's own properties are simply informative features and are good to include even if connectivity is high.
Finally, node self influence can be used for pure dimensionality reduction to compress node properties used for node classification.

If node properties are not used, the usefulness of `nodeSelfInfluence` is less clear.
The intermediate embedding from the first iteration encodes information about the neighborhood of nodes.
Therefore the similarity of first intermediate embeddings measures how much the neighborhoods of two nodes overlap but not whether the two nodes are neighbors or not.
Using positive iteration weights for the first iteration together with positive node self influence gives embeddings that reflect neighborhood overlap and whose similarity increases further if the nodes are connected.
However, because of the averaging and L2 normalization, if two nodes have high degree, their embedding similarity will have a low sensitivity to whether the nodes are neighbors or not.


=== Orientation

Choosing the right orientation when creating the graph may have the single greatest impact.
The FastRP algorithm is designed to work with undirected graphs, and we expect this to be the best in most cases.
If you expect only outgoing or incoming relationships to be informative for a prediction task, then you may want to try using the orientations `NATURAL` or `REVERSE` respectively.


[[algorithms-embeddings-fastrp-syntax]]
== Syntax

include::../shared/syntax-intro-named-graph.adoc[]

.FastRP syntax per mode
[.tabbed-example, caption = ]
====

[.include-with-stream]
======

.Run FastRP in stream mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.fastRP.stream(
  graphName: String,
  configuration: Map
) YIELD
  nodeId: Integer,
  embedding: List of Float
----

include::../common-configuration/common-parameters-named-graph.adoc[]

include::../common-configuration/common-stream-stats-configuration-named-graph.adoc[]

include::specific-configuration.adoc[]

.Results
[opts="header"]
|===
| Name      | Type         | Description
| nodeId    | Integer      | Node ID.
| embedding | List of Float  | FastRP node embedding.
|===
======

[.include-with-stats]
======

.Run FastRP in stats mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.fastRP.stats(
  graphName: String,
  configuration: Map
) YIELD
  nodeCount: Integer,
  createMillis: Integer,
  computeMillis: Integer,
  configuration: Map
----

include::../common-configuration/common-parameters-named-graph.adoc[]

include::../common-configuration/common-stream-stats-configuration-named-graph.adoc[]

include::specific-configuration.adoc[]

.Results
[opts="header",cols="1,1,6"]
|===
| Name          | Type    | Description
| nodeCount     | Integer | Number of nodes processed.
| createMillis  | Integer | Milliseconds for creating the graph.
| computeMillis | Integer | Milliseconds for running the algorithm.
| configuration | Map     | Configuration used for running the algorithm.
|===

======

[.include-with-mutate]
======

.Run FastRP in mutate mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.fastRP.mutate(
  graphName: String,
  configuration: Map
) YIELD
  nodeCount: Integer,
  nodePropertiesWritten: Integer,
  createMillis: Integer,
  computeMillis: Integer,
  mutateMillis: Integer,
  configuration: Map
----

include::../common-configuration/common-parameters-named-graph.adoc[]

include::../common-configuration/common-mutate-configuration-named-graph.adoc[]

include::specific-configuration.adoc[]

.Results
[opts="header"]
|===
| Name                  | Type    | Description
| nodeCount             | Integer | Number of nodes processed.
| nodePropertiesWritten | Integer | Number of node properties written.
| createMillis          | Integer | Milliseconds for creating the graph.
| computeMillis         | Integer | Milliseconds for running the algorithm.
| mutateMillis          | Integer | Milliseconds for adding properties to the in-memory graph.
| configuration         | Map     | Configuration used for running the algorithm.
|===
======

[.include-with-write]
======

.Run FastRP in write mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.fastRP.write(
  graphName: String,
  configuration: Map
) YIELD
  nodeCount: Integer,
  nodePropertiesWritten: Integer,
  createMillis: Integer,
  computeMillis: Integer,
  writeMillis: Integer,
  configuration: Map
----

include::../common-configuration/common-parameters-named-graph.adoc[]

include::../common-configuration/common-write-configuration-named-graph.adoc[]

include::specific-configuration.adoc[]

.Results
[opts="header"]
|===
| Name                  | Type    | Description
| nodeCount             | Integer | Number of nodes processed.
| nodePropertiesWritten | Integer | Number of node properties written.
| createMillis          | Integer | Milliseconds for creating the graph.
| computeMillis         | Integer | Milliseconds for running the algorithm.
| writeMillis           | Integer | Milliseconds for writing result data back to Neo4j.
| configuration         | Map     | Configuration used for running the algorithm.
|===

======

====


[[algorithms-embeddings-fastrp-syntax-anonymous]]
=== Anonymous graphs

include::../shared/syntax-anonymous-graphs.adoc[]

.Run FastRP in write mode on an anonymous graph.
[source, cypher, role=noplay]
----
CALL gds.fastRP.write(
  configuration: Map
)
YIELD
  nodeCount: Integer,
  nodePropertiesWritten: Integer,
  createMillis: Integer,
  computeMillis: Integer,
  writeMillis: Integer,
  configuration: Map
----

include::../common-configuration/common-configuration-anonymous-graph.adoc[]

include::specific-configuration.adoc[]

The results are the same as for running write mode with a named graph, see the <<algorithms-embeddings-fastrp-syntax, write mode syntax above>>.


[[algorithms-embeddings-fastrp-examples]]
== Examples

:algorithm-name: FastRP node embedding
:graph-description: social network
:image-file: fastrp.svg
include::../shared/examples-intro.adoc[]

.The following Cypher statement will create the example graph in the Neo4j database:
[source, cypher, role=noplay setup-query]
----
CREATE
  (dan:Person {name: 'Dan', age: 18}),
  (annie:Person {name: 'Annie', age: 12}),
  (matt:Person {name: 'Matt', age: 22}),
  (jeff:Person {name: 'Jeff', age: 51}),
  (brie:Person {name: 'Brie', age: 45}),
  (elsa:Person {name: 'Elsa', age: 65}),
  (john:Person {name: 'John', age: 64}),

  (dan)-[:KNOWS {weight: 1.0}]->(annie),
  (dan)-[:KNOWS {weight: 1.0}]->(matt),
  (annie)-[:KNOWS {weight: 1.0}]->(matt),
  (annie)-[:KNOWS {weight: 1.0}]->(jeff),
  (annie)-[:KNOWS {weight: 1.0}]->(brie),
  (matt)-[:KNOWS {weight: 3.5}]->(brie),
  (brie)-[:KNOWS {weight: 1.0}]->(elsa),
  (brie)-[:KNOWS {weight: 2.0}]->(jeff),
  (john)-[:KNOWS {weight: 1.0}]->(jeff);
----

This graph represents seven people who know one another.
A relationship property `weight` denotes the strength of the knowledge between two persons.

With the graph in Neo4j we can now project it into the graph catalog to prepare it for algorithm execution.
We do this using a native projection targeting the `Person` nodes and the `KNOWS` relationships.
For the relationships we will use the `UNDIRECTED` orientation.
This is because the FastRP algorithm has been measured to compute more predictive node embeddings in undirected graphs.
We will also add the `weight` relationship property which we will make use of when running the weighted version of FastRP.

include::../shared/examples-named-native-note.adoc[]

.The following statement will create a graph using a native projection and store it in the graph catalog under the name 'persons'.
[source, cypher, role=noplay graph-create-query]
----
CALL gds.graph.create(
  'persons',
  'Person',
  {
    KNOWS: {
      orientation: 'UNDIRECTED',
      properties: 'weight'
    }
  },
  { nodeProperties: ['age'] }
)
----


[[algorithms-embeddings-fastrp-examples-memory-estimation]]
=== Memory Estimation

:mode: stream
include::../shared/examples-estimate-intro.adoc[]

[role=query-example]
--
.The following will estimate the memory requirements for running the algorithm:
[source, cypher, role=noplay]
----
CALL gds.fastRP.stream.estimate('persons', {embeddingDimension: 128})
YIELD nodeCount, relationshipCount, bytesMin, bytesMax, requiredMemory
----

.Results
[opts="header", cols="1,1,1,1,1"]
|===
| nodeCount | relationshipCount | bytesMin | bytesMax | requiredMemory
| 7         | 18                | 11424    | 11424    | "11424 Bytes"
|===
--


[[algorithms-embeddings-fastrp-examples-stream]]
=== Stream

:stream-details: For example, we can collect the results and pass them into a similarity algorithm.
include::../shared/examples-stream-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm, and stream results:
[source, cypher, role=noplay]
----
CALL gds.fastRP.stream('persons',
  {
    embeddingDimension: 4,
    randomSeed: 42
  }
)
YIELD nodeId, embedding
----

[opts=header]
.Results
|===
| nodeId | embedding
| 0      | [0.4774002134799957, -0.6602408289909363, -0.36686956882476807, -1.7089111804962158]
| 1      | [0.7989360094070435, -0.4918718934059143, -0.41281944513320923, -1.6314401626586914]
| 2      | [0.47275322675704956, -0.49587157368659973, -0.3340468406677246, -1.7141895294189453]
| 3      | [0.8290714025497437, -0.3260476291179657, -0.3317275643348694, -1.4370529651641846]
| 4      | [0.7749264240264893, -0.4773247539997101, 0.0675133764743805, -1.5248265266418457]
| 5      | [0.8408374190330505, -0.37151476740837097, 0.12121132016181946, -1.530960202217102]
| 6      | [1.0, -0.11054422706365585, -0.3697933852672577, -0.9225144982337952]
|===
--

The results of the algorithm are not very intuitively interpretable, as the node embedding format is a mathematical abstraction of the node within its neighborhood, designed for machine learning programs.
What we can see is that the embeddings have four elements (as configured using `embeddingDimension`) and that the numbers are relatively small (they all fit in the range of `[-2, 2]`).
The magnitude of the numbers is controlled by the `embeddingDimension`, the number of nodes in the graph, and by the fact that FastRP performs euclidean normalization on the intermediate embedding vectors.

[NOTE]
====
Due to the random nature of the algorithm the results will vary between the runs.
However, this does not necessarily mean that the pairwise distances of two node embeddings vary as much.
====


[[algorithms-embeddings-fastrp-examples-stats]]
=== Stats

:stats-syntax: algorithms-embeddings-fastrp-syntax
include::../shared/examples-stats-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm and returns the result in form of statistical and measurement values
[source, cypher, role=noplay]
----
CALL gds.fastRP.stats('persons', { embeddingDimension: 8 })
YIELD nodeCount
----

[opts=header]
.Results
|===
| nodeCount
| 7
|===
--

The `stats` mode does not currently offer any statistical results for the embeddings themselves.
We can however see that the algorithm has successfully processed all seven nodes in our example graph.


[[algorithms-embeddings-fastrp-examples-mutate]]
=== Mutate

include::../shared/examples-mutate-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `mutate` mode:
[source, cypher, role=noplay]
----
CALL gds.fastRP.mutate(
  'persons',
  {
    embeddingDimension: 8,
    mutateProperty: 'fastrp-embedding'
  }
)
YIELD nodePropertiesWritten
----

[opts=header]
.Results
|===
| nodePropertiesWritten
| 7
|===
--

The returned result is similar to the `stats` example.
Additionally, the graph 'persons' now has a node property `fastrp-embedding` which stores the node embedding for each node.
To find out how to inspect the new schema of the in-memory graph, see <<catalog-graph-list>>.


[[algorithms-embeddings-fastrp-examples-write]]
=== Write

include::../shared/examples-write-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `write` mode:
[source, cypher, role=noplay]
----
CALL gds.fastRP.write(
  'persons',
  {
    embeddingDimension: 8,
    writeProperty: 'fastrp-embedding'
  }
)
YIELD nodePropertiesWritten
----

[opts=header]
.Results
|===
| nodePropertiesWritten
| 7
|===
--

The returned result is similar to the `stats` example.
Additionally, each of the seven nodes now has a new property `fastrp-embedding` in the Neo4j database, containing the node embedding for that node.


[[algorithms-embeddings-fastrp-examples-weighted]]
=== Weighted

By default, the algorithm is considering the relationships of the graph to be unweighted.
To change this behaviour we can use configuration parameter called `relationshipWeightProperty`.
Below is an example of running the weighted variant of algorithm.

[role=query-example]
--
.The following will run the algorithm, and stream results:
[source, cypher, role=noplay]
----
CALL gds.fastRP.stream(
  'persons',
  {
    embeddingDimension: 4,
    randomSeed: 42,
    relationshipWeightProperty: 'weight'
  }
)
YIELD nodeId, embedding
----

[opts=header]
.Results
|===
| nodeId | embedding
| 0      | [0.10945529490709305, -0.5032674074172974, 0.464673787355423, -1.7539862394332886]
| 1      | [0.3639600872993469, -0.39210301637649536, 0.46271592378616333, -1.829423427581787]
| 2      | [0.12314096093177795, -0.3213110864162445, 0.40100979804992676, -1.471055269241333]
| 3      | [0.30704641342163086, -0.24944794178009033, 0.3947891891002655, -1.3463698625564575]
| 4      | [0.23112300038337708, -0.30148714780807495, 0.584831714630127, -1.2822188138961792]
| 5      | [0.14497177302837372, -0.2312137484550476, 0.5552002191543579, -1.2605633735656738]
| 6      | [0.5139184594154358, -0.07954332232475281, 0.3690345287322998, -0.9176374077796936]
|===
--

Since the initial state of the algorithm is randomised, it isn't possible to intuitively analyse the effect of the relationship weights.

[[algorithms-embeddings-fastrp-extended]]
== Extended Algorithm: Using Node Properties

Most real-world graphs contain node properties which store information about the nodes and what they represent.
The FastRP algorithm in the GDS library extends the original FastRP algorithm with a capability to take node properties into account.
The resulting embeddings can therefore represent the graph more accurately.

The node property aware aspect of the algorithm is configured via the parameters `featureProperties` and `propertyRatio`.
Each node property in `featureProperties` is associated with a randomly generated vector of dimension `propertyDimension`, where `propertyDimension = embeddingDimension * propertyRatio`.
Each node is then initialized with a vector of size `embeddingDimension` formed by concatenation of two parts:
The first part is formed like in the FastRP algorithm, and the second one is a linear combination of the property vectors, using the property values of the node as weights.
The algorithm then proceeds with the same logic as the FastRP algorithm.
Therefore, the algorithm will output arrays of size `embeddingDimension`.
The last `propertyDimension` coordinates in the embedding captures information about property values of nearby nodes, and the remaining coordinates (`embeddingDimension` - `propertyDimension` of them) captures information about nearby presence of nodes.

[role=query-example]
--
.The following will run FastRP with feature properties:
[source, cypher, role=noplay]
----
CALL gds.fastRP.stream('persons', {
    randomSeed: 42,
    embeddingDimension: 2,
    propertyRatio: 1.0,
    featureProperties: ['age'],
    iterationWeights: [1.0]
}) YIELD nodeId, embedding
----

.Results
[opts="header", cols="1,1"]
|===
| nodeId | embedding
| 0 | [0.0, -1.0]
| 1 | [0.0, -1.0]
| 2 | [0.0, -0.9999999403953552]
| 3 | [0.0, -1.0]
| 4 | [0.0, -0.9999999403953552]
| 5 | [0.0, -1.0]
| 6 | [0.0, -1.0]
|===
--

In this example, the embeddings are based on the `age` property.
Because of L2 normalization which is applied to each iteration (here only one iteration), all nodes have the same embedding despite having different age values (apart from rounding errors).
