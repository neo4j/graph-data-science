[[algorithms-embeddings-graph-sage]]
[.beta]
= GraphSAGE
:entity: node
:result: embedding
:modelType: GraphSAGE
:algorithm: GraphSAGE

[abstract]
--
This section describes the GraphSAGE node embedding algorithm in the Neo4j Graph Data Science library.
--

GraphSAGE is an _inductive_ algorithm for computing node embeddings.
GraphSAGE is using node feature information to generate node embeddings on unseen nodes or graphs.
Instead of training individual embeddings for each node, the algorithm learns a function that generates embeddings by sampling and aggregating features from a node's local neighborhood.

NOTE: The algorithm is defined for UNDIRECTED graphs.

For more information on this algorithm see:

* https://arxiv.org/pdf/1706.02216.pdf[William L. Hamilton, Rex Ying, and Jure Leskovec. "Inductive Representation Learning on Large Graphs." 2018.^]
* https://arxiv.org/pdf/1911.10232.pdf[Amit Pande, Kai Ni and Venkataramani Kini. "SWAG: Item Recommendations using Convolutions on Weighted Graphs." 2019.^]

[[algorithms-embeddings-graph-sage-syntax]]
== Syntax

.GraphSAGE syntax per mode
[.tabbed-example]
====

[.include-with-train]
======
.Run GraphSAGE in train mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.beta.graphSage.train(
  graphName: String,
  configuration: Map
) YIELD
  graphName: String,
  modelInfo: Map,
  configuration: Map,
  trainMillis: Integer
----

include::../../common-configuration/common-parameters-named-graph.adoc[]

include::../../common-configuration/common-stream-stats-configuration-named-graph.adoc[]

include::specific-train-configuration.adoc[]

.Results
[opts="header",cols="1m,1,6"]
|===
| Name          | Type    | Description
| graphName     | String  | The name of the in-memory graph used during training.
| modelInfo     | Map     | Details of the trained model.
| configuration | Map     | The configuration used to run the procedure.
| trainMillis   | Integer | Milliseconds to train the model.
|===

.Details on `modelInfo`
[opts="header",cols="1m,1,6"]
|===
| Name    | Type   | Description
| name    | String | The name of the trained model.
| type    | String | The type of the trained model. Always `graphSage`.
| metrics | Map    | Metrics related to running the training, details in the table below.
|===

.Metrics collected during training
[opts="header",cols="1m,1,6"]
|===
| Name        | Type    | Description
| ranEpochs   | Integer | The number of ran epochs during training.
| epochLosses | List    | Ordered list of the losses after each epoch.
| didConverge | Boolean | Indicates if the training has converged.
|===

======

[.include-with-stream]
======
.Run GraphSAGE in stream mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.beta.graphSage.stream(
  graphName: String,
  configuration: Map
) YIELD
  nodeId: Integer,
  embedding: List<Float>
----

include::../../common-configuration/common-parameters-named-graph.adoc[]

include::../../common-configuration/common-stream-stats-configuration-named-graph.adoc[]

include::specific-configuration.adoc[]

.Results
[opts="header",cols="1m,1,6"]
|===
| Name      | Type         | Description
| nodeId    | Integer      | The Neo4j node ID.
| embedding | List<Float>  | The computed node embedding.
|===
======

[.include-with-mutate]
======
.Run GraphSAGE in mutate mode on a graph stored in the catalog.
[source, cypher, role=noplay]
----
CALL gds.beta.graphSage.mutate(
  graphName: String,
  configuration: Map
)
YIELD
  nodeCount: Integer,
  nodePropertiesWritten: Integer,
  createMillis: Integer,
  computeMillis: Integer,
  mutateMillis: Integer,
  configuration: Map
----

include::../../common-configuration/common-parameters-named-graph.adoc[]

include::../../common-configuration/common-mutate-configuration-named-graph.adoc[]

include::specific-configuration.adoc[]

.Results
[opts="header",cols="1,1,6"]
|===
| Name                  | Type                 | Description
| nodesCount            | Integer              | The number of nodes processed.
| nodePropertiesWritten | Integer              | The number of node properties written.
| createMillis          | Integer              | Milliseconds for loading data.
| computeMillis         | Integer              | Milliseconds for running the algorithm.
| mutateMillis          | Integer              | Milliseconds for writing result data back to the in-memory graph.
| configuration         | Map                  | The configuration used for running the algorithm.
|===
======

[.include-with-write]
======
.Run GraphSAGE in write mode on a graph stored in the catalog.
[source, cypher, role=noplay]
----
CALL gds.beta.graphSage.write(
  graphName: String,
  configuration: Map
)
YIELD
  nodeCount: Integer,
  nodePropertiesWritten: Integer,
  createMillis: Integer,
  computeMillis: Integer,
  writeMillis: Integer,
  configuration: Map
----

include::../../common-configuration/common-parameters-named-graph.adoc[]

include::../../common-configuration/common-write-configuration-named-graph.adoc[]

include::specific-configuration.adoc[]

.Results
[opts="header",cols="1,1,6"]
|===
| Name                  | Type                 | Description
| nodesCount            | Integer              | The number of nodes processed.
| nodePropertiesWritten | Integer              | The number of node properties written.
| createMillis          | Integer              | Milliseconds for loading data.
| computeMillis         | Integer              | Milliseconds for running the algorithm.
| writeMillis           | Integer              | Milliseconds for writing result data back to Neo4j.
| configuration         | Map                  | The configuration used for running the algorithm.
|===
======
====


[[algorithms-embeddings-graph-sage-syntax-anonymous]]
=== Anonymous graphs

include::../../shared/syntax-anonymous-graphs.adoc[]

.Run GraphSAGE in write mode on an anonymous graph.
[source, cypher, role=noplay]
----
CALL gds.beta.graphSage.write(
  configuration: Map
)
YIELD
  createMillis: Integer,
  computeMillis: Integer,
  writeMillis: Integer,
  nodeCount: Integer,
  nodePropertiesWritten: Integer,
  configuration: Map
----

include::../../common-configuration/common-configuration-anonymous-graph.adoc[]

include::specific-configuration.adoc[]

The results are the same as for running write mode with a named graph, see the <<algorithms-embeddings-graph-sage-syntax, write mode syntax above>>.

[[algorithms-embeddings-graph-sage-examples]]
== Examples

:algorithm-name: {algorithm}
:graph-description: friends network
:image-file: graph-sage-graph.svg
include::../../shared/examples-intro.adoc[]

.The following Cypher statement will create the example graph in the Neo4j database:
[source, cypher, role=noplay setup-query]
----
CREATE
  // Persons
  (  dan:Person {name: 'Dan',   age: 20, heightAndWeight: [185, 75]}),
  (annie:Person {name: 'Annie', age: 12, heightAndWeight: [124, 42]}),
  ( matt:Person {name: 'Matt',  age: 67, heightAndWeight: [170, 80]}),
  ( jeff:Person {name: 'Jeff',  age: 45, heightAndWeight: [192, 85]}),
  ( brie:Person {name: 'Brie',  age: 27, heightAndWeight: [176, 57]}),
  ( elsa:Person {name: 'Elsa',  age: 32, heightAndWeight: [158, 55]}),
  ( john:Person {name: 'John',  age: 35, heightAndWeight: [172, 76]}),

  (dan)-[:KNOWS {relWeight: 1.0}]->(annie),
  (dan)-[:KNOWS {relWeight: 1.6}]->(matt),
  (annie)-[:KNOWS {relWeight: 0.1}]->(matt),
  (annie)-[:KNOWS {relWeight: 3.0}]->(jeff),
  (annie)-[:KNOWS {relWeight: 1.2}]->(brie),
  (matt)-[:KNOWS {relWeight: 10.0}]->(brie),
  (brie)-[:KNOWS {relWeight: 1.0}]->(elsa),
  (brie)-[:KNOWS {relWeight: 2.2}]->(jeff),
  (john)-[:KNOWS {relWeight: 5.0}]->(jeff)
----

[source, cypher, role=noplay graph-create-query]
----
CALL gds.graph.create(
  'persons',
  {
    Person: {
      label: 'Person',
      properties: ['age', 'heightAndWeight']
    }
  }, {
    KNOWS: {
      type: 'KNOWS',
      orientation: 'UNDIRECTED',
      properties: ['relWeight']
    }
})
----

NOTE: The algorithm is defined for UNDIRECTED graphs.


=== Train

Before we are able to generate node embeddings we need to train a model and store it in the model catalog.
Below is an example of how to do that.

NOTE: The names specified in the `featureProperties` configuration parameter must exist in the in-memory graph.

[source, cypher, role=noplay query-example, no-result=true]
----
CALL gds.beta.graphSage.train(
  'persons',
  {
    modelName: 'exampleTrainModel',
    featureProperties: ['age', 'heightAndWeight'],
    aggregator: 'mean',
    activationFunction: 'sigmoid',
    sampleSizes: [25, 10]
  }
) YIELD modelInfo as info
RETURN
  info.name as modelName,
  info.metrics.didConverge as didConverge,
  info.metrics.ranEpochs as ranEpochs,
  info.metrics.epochLosses as epochLosses
----

[opts=header]
.Results
|===
| modelName         | didConverge | ranEpochs | epochLosses
| exampleTrainModel | true        | 1         | [186.0494816886275, 186.04946806237382]
|===

NOTE: Due to the random initialisation of the weight variables the results may vary between different runs.

Looking at the results we can draw the following conclusions, the training converged after a single epoch, the losses are almost identical.
Tuning the algorithm parameters, such as trying out different `sampleSizes`, `searchDepth`, `embeddingDimension` or `batchSize` can improve the losses.
For different datasets, GraphSAGE may require different train parameters for producing good models.

The trained model is automatically registered in the <<model-catalog-ops, model catalog>>.


=== Train with multiple node labels

In this section we describe how to train on a graph with multiple labels.
The different labels may have different sets of properties.
To run on such a graph, GraphSAGE is run in _multi-label mode_, in which the feature properties are projected into a common feature space.
Therefore, all nodes have feature vectors of the same dimension after the projection.

The projection for a label is linear and given by a matrix of weights.
The weights for each label are learned jointly with the other weights of the GraphSAGE model.

In the multi-label mode, the following is applied prior to the usual aggregation layers:

. A property representing the label is added to the feature properties for that label
. The feature properties for each label are projected into a feature vector of a shared dimension

The projected feature dimension is configured with `projectedFeatureDimension`, and specifying it enables the multi-label mode.

The feature properties used for a label are those present in the `featureProperties` configuration parameter which exist in the graph for that label.
In the multi-label mode, it is no longer required that all labels have all the specified properties.


==== Assumptions

- A requirement for multi-label mode is that each node belongs to exactly one label.
- A GraphSAGE model trained in this mode must be applied on graphs with the same schema with regards to node labels and properties.


==== Examples

In order to demonstrate GraphSAGE with multiple labels, we add instruments and relationships of type `LIKE` between person and instrument to the example graph.

image::example-graphs/graph-sage-multi-label-graph.svg[Visualization of the multi-label example graph,align="center"]

.The following Cypher statement will extend the example graph in the Neo4j database:
[source, cypher, role=noplay setup-query]
----
MATCH
  (dan:Person {name: "Dan"}),
  (annie:Person {name: "Annie"}),
  (matt:Person {name: "Matt"}),
  (brie:Person {name: "Brie"}),
  (john:Person {name: "John"})
CREATE
  (guitar:Instrument {name: 'Guitar', cost: 1337.0}),
  (synth:Instrument {name: 'Synthesizer', cost: 1337.0}),
  (bongos:Instrument {name: 'Bongos', cost: 42.0}),
  (trumpet:Instrument {name: 'Trumpet', cost: 1337.0}),
  (dan)-[:LIKES]->(guitar),
  (dan)-[:LIKES]->(synth),
  (dan)-[:LIKES]->(bongos),
  (annie)-[:LIKES]->(guitar),
  (annie)-[:LIKES]->(synth),
  (matt)-[:LIKES]->(bongos),
  (brie)-[:LIKES]->(guitar),
  (brie)-[:LIKES]->(synth),
  (brie)-[:LIKES]->(bongos),
  (john)-[:LIKES]->(trumpet)
----

[source, cypher, role=noplay graph-create-query]
----
CALL gds.graph.create(
  'persons_with_instruments',
  {
    Person: {
      label: 'Person',
      properties: ['age', 'heightAndWeight']
    },
    Instrument: {
      label: 'Instrument',
      properties: ['cost']
    }
  }, {
    KNOWS: {
      type: 'KNOWS',
      orientation: 'UNDIRECTED'
    },
    LIKES: {
      type: 'LIKES',
      orientation: 'UNDIRECTED'
    }
})
----

We can now run GraphSAGE in multi-label mode on that graph by specifying the `projectedFeatureDimension` parameter.
Multi-label GraphSAGE removes the requirement, that each node in the in-memory graph must have all `featureProperties`.
However, the algorithm collects the `featureProperties` per label into a `feature` list with its length defined by the `projectedFeatureDimension`.
The `projectedFeatureDimension` equals the maximum length of the feature-array, i.e., `age` and `cost` both are scalar features plus the list feature `heightAndWeight` which has a length of two.
Note that the `cost` feature is only defined for the instrument nodes, while `age` and `heightAndWeight` are only defined for persons.

[source, cypher, role=noplay query-example, no-result=true]
----
CALL gds.beta.graphSage.train(
  'persons_with_instruments',
  {
    modelName: 'multiLabelModel',
    featureProperties: ['age', 'heightAndWeight', 'cost'],
    projectedFeatureDimension: 4
  }
)
----


=== Train with relationship weights

The GraphSAGE implementation supports training using relationship weights.
Greater relationship weight between nodes signifies that the nodes should have more similar embedding values.

.The following Cypher query trains a GraphSAGE model using relationship weights
[source, cypher, role=noplay query-example, no-result=true]
----
CALL gds.beta.graphSage.train(
  'persons',
  {
    modelName: 'weightedTrainedModel',
    featureProperties: ['age', 'heightAndWeight'],
    relationshipWeightProperty: 'relWeight',
    nodeLabels: ['Person'],
    relationshipTypes: ['KNOWS']
  }
)
----


=== Train when there are no node properties present in the graph

In the case when you have a graph that does not have node properties we recommend to use existing algorithm in `mutate` mode to create node properties.
Good candidates are <<algorithms-centrality, Centrality algorithms>> or <<algorithms-community, Community algorithms>>.

The following example illustrates calling Degree Centrality in `mutate` mode and then using the mutated property as feature of GraphSAGE training.
For the purpose of this example we are going to use the `Persons` graph, but we will not load any properties to the in-memory graph.

.Create the in-memory graph without loading any node properties
[source, cypher, role=noplay graph-create-query]
----
CALL gds.graph.create(
  'noPropertiesGraph',
  'Person', {
    KNOWS: {
      type: 'KNOWS',
      orientation: 'UNDIRECTED'
    }
})
----

.Run DegreeCentrality mutate to create a new property for each node
[source, cypher, role=noplay query-example, no-result=true, group=noProps]
----
CALL gds.degree.mutate(
  'noPropertiesGraph',
  {
    mutateProperty: 'degree'
  }
) YIELD nodePropertiesWritten
----

.Run GraphSAGE train using the property produced by DegreeCentrality as feature property
[source, cypher, role=noplay query-example, no-result=true, group=noProps]
----
CALL gds.beta.graphSage.train(
  'noPropertiesGraph',
  {
    modelName: 'myModel',
    featureProperties: ['degree']
  }
)
YIELD trainMillis
RETURN trainMillis
----

`gds.degree.mutate` will create a new node property `degree` for each of the nodes in the in-memory graph, which then can be used as `featureProperty` in the `GraphSAGE.train` mode.

NOTE: Using separate algorithms to produce featureProperties can also be very useful to capture graph topology properties.

=== Stream

To generate embeddings and stream them back to the client we can use the stream mode.
We must first train a model, which we do using the `gds.beta.graphSage.train` procedure.

[[graph-sage-trained-model-example]]
[source, cypher, role=noplay query-example, no-result=true, group=graphSage]
----
CALL gds.beta.graphSage.train(
  'persons',
  {
    modelName: 'graphSage',
    featureProperties: ['age', 'heightAndWeight'],
    embeddingDimension: 3,
    randomSeed: 19
  }
)
----

Once we have trained a model (named `'graphSage'`) we can use it to generate and stream the embeddings.

[role=query-example, group=graphSage]
--
[source, cypher, role=noplay]
----
CALL gds.beta.graphSage.stream(
  'persons',
  {
    modelName: 'graphSage'
  }
)
YIELD nodeId, embedding
----

[opts=header]
.Results
|===
| nodeId | embedding
| 0      | [0.5285002502143177, 0.4682181762801141, 0.7081378570737874]
| 1      | [0.5285002502147674, 0.46821817628034773, 0.7081378570732975]
| 2      | [0.5285002502143014, 0.46821817628010554, 0.7081378570738053]
| 3      | [0.5285002502129178, 0.46821817627938667, 0.7081378570753134]
| 4      | [0.5285002502572376, 0.46821817630241636, 0.7081378570270093]
| 5      | [0.5285002503196665, 0.46821817633485613, 0.7081378569589678]
| 6      | [0.528500250213112, 0.46821817627948753, 0.7081378570751017]
|===
--

NOTE: Due to the random initialisation of the weight variables the results may vary slightly between the runs.

=== Mutate

The <<graph-sage-trained-model-example, model trained as part of the stream example>> can be reused to write the results to the in-memory graph using the `mutate` mode of the procedure.
Below is an example of how to achieve this.

--
[source, cypher, role=noplay query-example, no-result=true, group=graphSage]
----
CALL gds.beta.graphSage.mutate(
  'persons',
  {
    mutateProperty: 'inMemoryEmbedding',
    modelName: 'graphSage'
  }
) YIELD
  nodeCount,
  nodePropertiesWritten
----

[opts=header]
.Results
|===
| nodeCount | nodePropertiesWritten
| 7         | 7
|===
--

=== Write

The <<graph-sage-trained-model-example, model trained as part of the stream example>> can be reused to write the results to Neo4j.
Below is an example of how to achieve this.

--
[source, cypher, role=noplay query-example, no-result=true, group=graphSage]
----
CALL gds.beta.graphSage.write(
  'persons',
  {
    writeProperty: 'embedding',
    modelName: 'graphSage'
  }
) YIELD
  nodeCount,
  nodePropertiesWritten
----

[opts=header]
.Results
|===
| nodeCount | nodePropertiesWritten
| 7         | 7
|===
--

[[algorithms-embeddings-graph-sage-caveats]]
== Caveats

If you are embedding a graph that has an isolated node, the aggregation step in GraphSAGE can only draw information from the node itself.
When all the properties of that node are `0.0`, and the activation function is relu, this leads to an all-zero vector for that node.
However, since GraphSAGE normalizes node embeddings using the L2-norm, and a zero vector cannot be normalized, we assign all-zero embeddings to such nodes under these special circumstances.
In scenarios where you generate all-zero embeddings for orphan nodes, that may have impacts on downstream tasks such as nearest neighbor or other similarity algorithms. It may be more appropriate to filter out these disconnected nodes prior to running GraphSAGE.

When running `gds.beta.graphSage.train.estimate`, the feature dimension is computed as if each feature property is scalar.
