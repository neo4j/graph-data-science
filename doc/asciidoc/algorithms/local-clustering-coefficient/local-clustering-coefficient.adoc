[[algorithms-local-clustering-coefficient]]
= Local Clustering Coefficient
:entity: node
:result: local clustering coefficient
:algorithm: Local Clustering Coefficient

[abstract]
--
This section describes the Local Clustering Coefficient algorithm in the Neo4j Graph Data Science library.
--

:undirected:
:monopartite:
include::../shared/algorithm-traits.adoc[]


[[algorithms-local-clustering-coefficient-intro]]
== Introduction

The Local Clustering Coefficient algorithm computes the local clustering coefficient for each node in the graph.
The local clustering coefficient _C~n~_ of a node _n_ describes the likelihood that the neighbours of _n_ are also connected.
To compute _C~n~_ we use the number of triangles a node is a part of _T~n~_, and the degree of the node _d~n~_.
The formula to compute the local clustering coefficient is as follows:

image::lcc_formula.svg[align="center"]

As we can see the triangle count is required to compute the local clustering coefficient.
To do this the <<algorithms-triangle-count, Triangle Count>> algorithm is utilised.

Additionally, the algorithm can compute the _average clustering coefficient_ for the whole graph.
This is the normalised sum over all the local clustering coefficients.

For more information, see https://en.wikipedia.org/wiki/Clustering_coefficient[Clustering Coefficient].


[[algorithms-local-clustering-coefficient-syntax]]
== Syntax

include::../shared/syntax-intro-named-graph.adoc[]


.Local Clustering Coefficient syntax per mode
[.tabbed-example, caption = ]
====

[.include-with-stream]
======
.Run Local Clustering Coefficient in stream mode on a named graph:
[source, cypher, role=noplay]
----
CALL gds.localClusteringCoefficient.stream(
  graphName: String,
  configuration: Map
)
YIELD
  nodeId: Integer,
  localClusteringCoefficient: Double
----

include::../common-configuration/common-parameters-named-graph.adoc[]

include::../common-configuration/common-stream-stats-configuration-named-graph.adoc[]

include::specific-configuration.adoc[]

.Results
[opts="header"]
|===
| Name                       | Type      | Description
| nodeId                     | Integer   | Node ID.
| localClusteringCoefficient | Double    | Local clustering coefficient.
|===
======

[.include-with-stats]
======
.Run Local Clustering Coefficient in stats mode on a named graph:
[source, cypher, role=noplay]
----
CALL gds.localClusteringCoefficient.stats(
  graphName: String,
  configuration: Map
)
YIELD
  averageClusteringCoefficient: Double,
  nodeCount: Integer,
  createMillis: Integer,
  computeMillis: Integer,
  postProcessingMillis: Integer,
  configuration: Map
----

include::../common-configuration/common-parameters-named-graph.adoc[]

include::../common-configuration/common-stream-stats-configuration-named-graph.adoc[]

include::specific-configuration.adoc[]

.Results
[opts="header"]
|===
| Name                         | Type    | Description
| averageClusteringCoefficient | Double  | The average clustering coefficient.
| nodeCount                    | Integer | Number of nodes in the graph.
| createMillis                 | Integer | Milliseconds for creating the graph.
| computeMillis                | Integer | Milliseconds for running the algorithm.
| postProcessingMillis         | Integer | Milliseconds for computing the global metrics.
| configuration                | Map     | The configuration used for running the algorithm.
|===
======

[.include-with-mutate]
======
.Run Local Clustering Coefficient in mutate mode on a named graph:
[source, cypher, role=noplay]
----
CALL gds.localClusteringCoefficient.mutate(
  graphName: String,
  configuration: Map
)
YIELD
  averageClusteringCoefficient: Double,
  nodeCount: Integer,
  nodePropertiesWritten: Integer,
  createMillis: Integer,
  computeMillis: Integer,
  postProcessingMillis: Integer,
  mutateMillis: Integer,
  configuration: Map
----

include::../common-configuration/common-parameters-named-graph.adoc[]

include::../common-configuration/common-mutate-configuration-named-graph.adoc[]

include::specific-configuration.adoc[]

.Results
[opts="header"]
|===
| Name                          | Type    | Description
| averageClusteringCoefficient  | Double  | The average clustering coefficient.
| nodeCount                     | Integer | Number of nodes in the graph.
| nodePropertiesWritten         | Integer | Number of properties added to the in-memory graph.
| createMillis                  | Integer | Milliseconds for creating the graph.
| computeMillis                 | Integer | Milliseconds for running the algorithm.
| postProcessingMillis          | Integer | Milliseconds for computing the global metrics.
| mutateMillis                  | Integer | Milliseconds for adding properties to the in-memory graph.
| configuration                 | Map     | The configuration used for running the algorithm.
|===
======

[.include-with-write]
======
.Run Local Clustering Coefficient in write mode on a named graph:
[source, cypher, role=noplay]
----
CALL gds.localClusteringCoefficient.write(
  graphName: String,
  configuration: Map
)
YIELD
  averageClusteringCoefficient: Double,
  nodeCount: Integer,
  nodePropertiesWritten: Integer,
  createMillis: Integer,
  computeMillis: Integer,
  postProcessingMillis: Integer,
  writeMillis: Integer,
  configuration: Map
----

include::../common-configuration/common-parameters-named-graph.adoc[]

include::../common-configuration/common-write-configuration-named-graph.adoc[]

include::specific-configuration.adoc[]

.Results
[opts="header"]
|===
| Name                          | Type    | Description
| averageClusteringCoefficient  | Double  | The average clustering coefficient.
| nodeCount                     | Integer | Number of nodes in the graph.
| nodePropertiesWritten         | Integer | Number of properties written to Neo4j.
| createMillis                  | Integer | Milliseconds for creating the graph.
| computeMillis                 | Integer | Milliseconds for running the algorithm.
| postProcessingMillis          | Integer | Milliseconds for computing the global metrics.
| writeMillis                   | Integer | Milliseconds for writing results back to Neo4j.
| configuration                 | Map     | The configuration used for running the algorithm.
|===
======

====


[[algorithms-local-clustering-coefficient-syntax-anonymous]]
=== Anonymous graphs

include::../shared/syntax-anonymous-graphs.adoc[]

.Run Local Clustering Coefficient in write mode on an anonymous graph:
[source, cypher, role=noplay]
----
CALL gds.localClusteringCoefficient.write(
  configuration: Map
)
YIELD
  averageClusteringCoefficient: Double,
  nodeCount: Integer,
  nodePropertiesWritten: Integer,
  createMillis: Integer,
  computeMillis: Integer,
  writeMillis: Integer,
  configuration: Map
----

include::../common-configuration/common-write-configuration-anonymous-graph.adoc[]

include::specific-configuration.adoc[]

The results are the same as for running write mode with a named graph, see the <<algorithms-local-clustering-coefficient-syntax, write mode syntax above>>.


[[algorithms-local-clustering-coefficient-examples]]
== Examples

:algorithm-name: {algorithm}
:graph-description: social network
:image-file: triangle_count.png
include::../shared/examples-intro.adoc[]

.The following Cypher statement will create the example graph in the Neo4j database:
[source, cypher, role=noplay setup-query]
----
CREATE
  (alice:Person {name: 'Alice'}),
  (michael:Person {name: 'Michael'}),
  (karin:Person {name: 'Karin'}),
  (chris:Person {name: 'Chris'}),
  (will:Person {name: 'Will'}),
  (mark:Person {name: 'Mark'}),

  (michael)-[:KNOWS]->(karin),
  (michael)-[:KNOWS]->(chris),
  (will)-[:KNOWS]->(michael),
  (mark)-[:KNOWS]->(michael),
  (mark)-[:KNOWS]->(will),
  (alice)-[:KNOWS]->(michael),
  (will)-[:KNOWS]->(chris),
  (chris)-[:KNOWS]->(karin)
----

With the graph in Neo4j we can now project it into the graph catalog to prepare it for algorithm execution.
We do this using a native projection targeting the `Person` nodes and the `KNOWS` relationships.
For the relationships we must use the `UNDIRECTED` orientation.
This is because the Local Clustering Coefficient algorithm is defined only for undirected graphs.

include::../shared/examples-named-native-note.adoc[]

.The following statement will create a graph using a native projection and store it in the graph catalog under the name 'myGraph'.
[source, cypher, role=noplay graph-create-query]
----
CALL gds.graph.create(
  'myGraph',
  'Person',
  {
    KNOWS: {
      orientation: 'UNDIRECTED'
    }
  }
)
----

WARNING: The Local Clustering Coefficient algorithm requires the graph to be created using the `UNDIRECTED` orientation for relationships.

In the following examples we will demonstrate using the Local Clustering Coefficient algorithm on 'myGraph'.


[[algorithms-local-clustering-coefficient-examples-memory-estimation]]
=== Memory Estimation

:mode: write
include::../shared/examples-estimate-intro.adoc[]

[role=query-example]
--
.The following will estimate the memory requirements for running the algorithm:
[source, cypher, role=noplay]
----
CALL gds.localClusteringCoefficient.write.estimate('myGraph', {
  writeProperty: 'localClusteringCoefficient'
})
YIELD nodeCount, relationshipCount, bytesMin, bytesMax, requiredMemory
----

.Results
[opts="header"]
|===
| nodeCount | relationshipCount | bytesMin | bytesMax | requiredMemory
| 6         | 16                | 296      | 296      | "296 Bytes"
|===
--

Note that the relationship count is 16 although we only created 8 relationships in the original Cypher statement.
This is because we used the `UNDIRECTED` orientation, which will project each relationship in each direction, effectively doubling the number of relationships.


[[algorithms-local-clustering-coefficient-examples-stream]]
=== Stream

:stream-details: For example, we can order the results to find the nodes with the highest local clustering coefficient.
include::../shared/examples-stream-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `stream` mode:
[source, cypher, role=noplay]
----
CALL gds.localClusteringCoefficient.stream('myGraph')
YIELD nodeId, localClusteringCoefficient
RETURN gds.util.asNode(nodeId).name AS name, localClusteringCoefficient
ORDER BY localClusteringCoefficient DESC
----

.Results
[opts="header"]
|===
| name      | localClusteringCoefficient
| "Karin"   | 1.0
| "Mark"    | 1.0
| "Chris"   | 0.6666666666666666
| "Will"    | 0.6666666666666666
| "Michael" | 0.3
| "Alice"   | 0.0
|===
--

From the results we can see that the nodes 'Karin' and 'Mark' have the highest local clustering coefficients.
This shows that they are the best at introducing their friends - all the people who know them, know each other!
This can be verified in the <<algorithms-local-clustering-coefficient-examples, example graph>>.


[[algorithms-local-clustering-coefficient-examples-stats]]
=== Stats

:stats-details: The summary result contains the avearage clustering coefficient of the graph, which is the normalised sum over all local clustering coefficients.
:stats-syntax: algorithms-local-clustering-coefficient-syntax
include::../shared/examples-stats-intro.adoc[]

.The following will run the algorithm in `stats` mode:
[role=query-example]
--
[source, cypher, role=noplay]
----
CALL gds.localClusteringCoefficient.stats('myGraph')
YIELD averageClusteringCoefficient, nodeCount
----

.Results
[opts="header"]
|===
| averageClusteringCoefficient | nodeCount
| 0.6055555555555555           | 6
|===
--

The result shows that on average each node of our example graph has approximately 60% of its neighbours connected.


[[algorithms-local-clustering-coefficient-examples-mutate]]
=== Mutate

include::../shared/examples-mutate-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `mutate` mode:
[source, cypher, role=noplay]
----
CALL gds.localClusteringCoefficient.mutate('myGraph', {
  mutateProperty: 'localClusteringCoefficient'
})
YIELD averageClusteringCoefficient, nodeCount
----

.Results
[opts="header"]
|===
| averageClusteringCoefficient | nodeCount
| 0.6055555555555555           | 6
|===
--

The returned result is the same as in the `stats` example.
Additionally, the graph 'myGraph' now has a node property `localClusteringCoefficient` which stores the local clustering coefficient for each node.
To find out how to inspect the new schema of the in-memory graph, see <<catalog-graph-list>>.


[[algorithms-local-clustering-coefficient-examples-write]]
=== Write

include::../shared/examples-write-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `write` mode:
[source, cypher, role=noplay]
----
CALL gds.localClusteringCoefficient.write('myGraph', {
  writeProperty: 'localClusteringCoefficient'
})
YIELD averageClusteringCoefficient, nodeCount
----

.Results
[opts="header",cols="1,1"]
|===
| averageClusteringCoefficient | nodeCount
| 0.6055555555555555           | 6
|===
--

The returned result is the same as in the `stats` example.
Additionally, each of the six nodes now has a new property `localClusteringCoefficient` in the Neo4j database, containing the local clustering coefficient for that node.


[[algorithms-local-clustering-coefficient-examples-pre-computed]]
=== Pre-computed Counts

By default, the Local Clustering Coefficient algorithm executes <<algorithms-triangle-count, Triangle Count>> as part of its computation.
It is also possible to avoid the triangle count computation by configuring the Local Clustering Coefficient algorithm to read the triangle count from a node property.
In order to do that we specify the `triangleCountProperty` configuration parameter.
Please note that the Local Clustering Coefficient algorithm depends on the property holding actual triangle counts and not another number for the results to be actual local clustering coefficients.

To illustrate this we make use of the <<algorithms-triangle-count, Triangle Count algorithm>>
in `mutate` mode.
The Triangle Count algorithm is going to store its result back into 'myGraph'.
It is also possible to obtain the property value from the Neo4j database using a graph projection with a node property when creating the in-memory graph.

[role=query-example, no-result=true, group=triangleCountProperty]
--
.The following computes the triangle counts and stores the result into the in-memory graph:
[source, cypher, role=noplay]
----
CALL gds.triangleCount.mutate('myGraph', {
  mutateProperty: 'triangles'
})
----
--

[role=query-example, group=triangleCountProperty]
--
.The following will run the algorithm in `stream` mode using pre-computed triangle counts:
[source, cypher, role=noplay]
----
CALL gds.localClusteringCoefficient.stream('myGraph', {
  triangleCountProperty: 'triangles'
})
YIELD nodeId, localClusteringCoefficient
RETURN gds.util.asNode(nodeId).name AS name, localClusteringCoefficient
ORDER BY localClusteringCoefficient DESC
----

.Results
[opts="header"]
|===
| name      | localClusteringCoefficient
| "Karin"   | 1.0
| "Mark"    | 1.0
| "Chris"   | 0.6666666666666666
| "Will"    | 0.6666666666666666
| "Michael" | 0.3
| "Alice"   | 0.0
|===
--

As we can see the results are the same as in <<algorithms-local-clustering-coefficient-examples-stream, the `stream` example>> where we did not specify a `triangleCountProperty`.
