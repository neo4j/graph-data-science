[[algorithms-pagerank]]
= Page Rank
:entity: node
:result: score

[abstract]
--
This section describes the Page Rank algorithm in the Neo4j Graph Data Science library.
--

Page Rank is an algorithm that measures the influence or importance of nodes in a directed graph.

It is computed by either iteratively distributing each node's score (originally based on degree) over its neighbours.
Theoretically this is equivalent traversing the graph in a random fashion and counting the frequency of hitting each node during these walks.

This section includes:

* <<algorithms-pagerank-intro, Introduction>>
* <<algorithms-pagerank-syntax, Syntax>>
* <<algorithms-pagerank-examples, Examples>>
** <<algorithms-pagerank-examples-unweighted, Unweighted>>
** <<algorithms-pagerank-examples-weighted, Weighted>>
** <<algorithms-pagerank-examples-personalized, Personalized>>
** <<algorithms-pagerank-examples-memory-estimation, Memory Estimation>>
** <<algorithms-pagerank-examples-stats, Stats>>
* <<algorithms-pagerank-usage, Usage>>

[[algorithms-pagerank-intro]]
== Introduction

The Page Rank algorithm measures the importance of each node within the graph, based on the number incoming relationships and the importance of the corresponding source nodes.
The underlying assumption roughly speaking is that a page is only as important as the pages that link to it.

Page Rank is defined in the original Google paper as a function that solves the following equation:

----
PR(A) = (1-d) + d (PR(T1)/C(T1) + ... + PR(Tn)/C(Tn))
----

where,

* we assume that a page `A` has pages `T1` to `Tn` which point to it (i.e., are citations).
* `d` is a damping factor which can be set between 0 and 1.
  It is usually set to 0.85.
* `C(A)` is defined as the number of links going out of page `A`.

This equation is used to iteratively update a candidate solution and arrive at an approximate solution to the same equation.


For more information on this algorithm, see:

* http://infolab.stanford.edu/~backrub/google.html[The original google paper^]
// according to java doc implantation based on
* http://delab.csd.auth.gr/~dimitris/courses/ir_spring06/page_rank_computing/01531136.pdf[An Efficient Partition-Based Parallel PageRank Algorithm^]
* https://arxiv.org/pdf/1407.5107.pdf[PageRank beyond the web^] for use cases


[NOTE]
====
Running this algorithm requires sufficient memory availability.
Before running this algorithm, we recommend that you read <<memory-estimation>>.
====

[[algorithms-pagerank-syntax]]
== Syntax


=== Write mode

.Run PageRank in write mode on a graph stored in the catalog.
[source, cypher]
----
CALL gds.pageRank.write(
  graphName: String,
  configuration: Map
)
YIELD
  // general write return columns
  ranIterations: Integer,
  didConverge: Boolean
----

include::common-parameters-named-graph.adoc[]

include::common-configuration-named-graph.adoc[]

.Run PageRank in write mode on an anonymous graph.
[source, cypher]
----
CALL gds.pageRank.write(configuration: Map)
YIELD
  // general write return columns
  ranIterations: Integer,
  didConverge: Boolean
----

include::common-parameters-anonymous-graph.adoc[]

include::common-configuration-anonymous-graph.adoc[]

.Algorithm specific configuration
[opts="header",cols="1,1,1m,1,4"]
|===
| Name                       | Type    | Default   | Optional | Description
| dampingFactor              | Float   | 0.85      | yes      | The damping factor of the Page Rank calculation.
| maxIterations              | Integer | 20        | yes      | The maximum number of iterations of Page Rank to run.
| tolerance                  | Float   | 0.0000001 | yes      | Minimum change in scores between iterations. If all scores change less than the tolerance value the result is considered stable and the algorithm returns.
| relationshipWeightProperty | String  | null      | yes      | The property name that contains weight. If null, treats the graph as unweighted. Must be numeric.
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                   | Type    | Description
| ranIterations          | Integer | The number of iterations run.
| didConverge            | Boolean | Indicates if the algorithm converged.
| createMillis           | Integer | Milliseconds for creating the graph.
| computeMillis          | Integer | Milliseconds for running the algorithm.
| writeMillis            | Integer | Milliseconds for writing result data back.
| nodePropertiesWritten  | Integer | The number of properties that were written to Neo4j.
| configuration          | Map     | The configuration used for running the algorithm.
|===


=== Mutate mode

.Run PageRank in mutate mode on a graph stored in the catalog.
[source, cypher]
----
CALL gds.pageRank.mutate(
  graphName: String,
  configuration: Map
)
YIELD
  // general mutate return columns
  ranIterations: Integer,
  didConverge: Boolean
----

The configuration for the `mutate` mode is similar to the `write` mode.
Instead of specifying a `writeProperty`, we need to specify a `mutateProperty`.
Also, specifying `writeConcurrency` is not possible in `mutate` mode.

.The following will run the algorithm and store the results in `myGraph`:
[source, cypher]
----
CALL gds.pageRank.mutate('myGraph', { mutateProperty: 'pagerank' })
----


=== Stream mode

.Run PageRank in stream mode on a graph stored in the catalog.
[source, cypher]
----
CALL gds.pageRank.stream(
  graphName: String,
  configuration: Map
)
YIELD
  nodeId: Integer,
  score: Float
----

include::common-parameters-named-graph.adoc[]

include::common-configuration-named-graph.adoc[]

.Run PageRank in stream mode on an anonymous graph.
[source, cypher]
----
CALL gds.pageRank.stream(configuration: Map)
YIELD
  nodeId: Integer,
  score: Float
----

include::common-parameters-anonymous-graph.adoc[]

include::common-configuration-anonymous-graph.adoc[]

.Algorithm specific configuration
[opts="header",cols="1,1,1m,1,4"]
|===
| Name                       | Type    | Default   | Optional | Description
| dampingFactor              | Float   | 0.85      | yes      | The damping factor of the Page Rank calculation.
| maxIterations              | Integer | 20        | yes      | The maximum number of iterations of Page Rank to run.
| tolerance                  | Float   | 0.0000001 | yes      | Minimum change in scores between iterations. If all scores change less than the tolerance value the result is considered stable and the algorithm returns.
| relationshipWeightProperty | String  | null      | yes      | The property name that contains weight. If null, treats the graph as unweighted. Must be numeric.
|===

.Results
[opts="header"]
|===
| Name    | Type    | Description
| nodeId  | Integer | Node ID
| score   | Float   | Page Rank
|===


=== Stats mode

.Run PageRank in stats mode on an named graph.
[source, cypher]
----
CALL gds.pageRank.stats(
  graphName: String,
  configuration: Map
)
YIELD
  ranIterations: Integer,
  didConverge: Boolean,
  createMillis: Integer,
  computeMillis: Integer
----

include::common-parameters-named-graph.adoc[]

.Run PageRank in stats mode on an anonymous graph.
[source, cypher]
----
CALL gds.pageRank.stats(configuration: Map)
YIELD
  ranIterations: Integer,
  didConverge: Boolean,
  createMillis: Integer,
  computeMillis: Integer
----

include::common-parameters-anonymous-graph.adoc[]

The configuration is the same as for the `write` mode.

The `mode` can be substituted with the available modes (`stream`, `write` and `stats`).


=== Estimate mode

The following will estimate the memory requirements for running the algorithm.
The `mode` can be substituted with the available modes (`stream`, `write` and `stats`).

.Run PageRank in estimate mode on an named graph.
[source, cypher]
----
CALL gds.pageRank.<mode>.estimate(
  graphName: String,
  configuration: Map
)
----

include::common-parameters-named-graph.adoc[]

.Run PageRank in estimate mode on an anonymous graph.
[source, cypher]
----
CALL gds.pageRank.<mode>.estimate(configuration: Map)
----

include::common-parameters-anonymous-graph.adoc[]

include::common-estimate-mode.adoc[]

[[algorithms-pagerank-examples]]
== Examples

Consider the graph created by the following Cypher statement:

[source, cypher, role=setup-query]
----
CREATE (home:Page {name:'Home'})
CREATE (about:Page {name:'About'})
CREATE (product:Page {name:'Product'})
CREATE (links:Page {name:'Links'})
CREATE (a:Page {name:'Site A'})
CREATE (b:Page {name:'Site B'})
CREATE (c:Page {name:'Site C'})
CREATE (d:Page {name:'Site D'})

CREATE (home)-[:LINKS {weight: 0.2}]->(about)
CREATE (home)-[:LINKS {weight: 0.2}]->(links)
CREATE (home)-[:LINKS {weight: 0.6}]->(product)
CREATE (about)-[:LINKS {weight: 1.0}]->(home)
CREATE (product)-[:LINKS {weight: 1.0}]->(home)
CREATE (a)-[:LINKS {weight: 1.0}]->(home)
CREATE (b)-[:LINKS {weight: 1.0}]->(home)
CREATE (c)-[:LINKS {weight: 1.0}]->(home)
CREATE (d)-[:LINKS {weight: 1.0}]->(home)
CREATE (links)-[:LINKS {weight: 0.8}]->(home)
CREATE (links)-[:LINKS {weight: 0.05}]->(a)
CREATE (links)-[:LINKS {weight: 0.05}]->(b)
CREATE (links)-[:LINKS {weight: 0.05}]->(c)
CREATE (links)-[:LINKS {weight: 0.05}]->(d)
----

This graph represents seven pages, linking to another.
Each relationship has a property called `weight`, which describes the importance of the relationship.

[NOTE]
====
In the examples below we will use named graphs and standard projections as the norm.
However, <<cypher-projection, Cypher projection>> and anonymous graphs could also be used.
====

.The following statement will create the graph and store it in the graph catalog.
[source, cypher, role=setup-query]
----
CALL gds.graph.create(
    'myGraph',
    'Page',
    'LINKS',
    {
        relationshipProperties: 'weight'
    }
)
----

In the following examples we will demonstrate using the Page Rank algorithm on this graph.


[[algorithms-pagerank-examples-unweighted]]
=== Unweighted

[role=query-example]
--
.The following will run the algorithm and stream results:
[source, cypher]
----
CALL gds.pageRank.stream('myGraph', { maxIterations: 20, dampingFactor: 0.85 })
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name AS name, score
ORDER BY score DESC, name ASC
----

.Results
[opts="header",cols="1,1"]
|===
| name      | score
| "Home"    | 3.2362017153762284
| "About"   | 1.0611098567023873
| "Links"   | 1.0611098567023873
| "Product" | 1.0611098567023873
| "Site A"  | 0.3292259009438567
| "Site B"  | 0.3292259009438567
| "Site C"  | 0.3292259009438567
| "Site D"  | 0.3292259009438567
|===
--

To instead write the page-rank score to a node property in the Neo4j graph, use this query:

[role=query-example]
--
.The following will run the algorithm and write back results:
[source, cypher]
----
CALL gds.pageRank.write('myGraph', {
  maxIterations: 20,
  dampingFactor: 0.85,
  writeProperty: 'pagerank'
})
YIELD nodePropertiesWritten AS writtenProperties, ranIterations
----

.Results
[opts="header",cols="1m,1m"]
|===
| writtenProperties | ranIterations
| 8                 | 20
|===
--

[[algorithms-pagerank-examples-weighted]]
=== Weighted

[role=query-example]
--
.The following will run the algorithm and stream results:
[source, cypher]
----
CALL gds.pageRank.stream('myGraph', {
  maxIterations: 20,
  dampingFactor: 0.85,
  relationshipWeightProperty: 'weight'
})
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name AS name, score
ORDER BY score DESC, name ASC
----

.Results
[opts="header",cols="1,1"]
|===
| name      | score
| "Home"    | 3.5528567278757683
| "Product" | 1.9541301048360766
| "About"   | 0.7513767024036497
| "Links"   | 0.7513767024036497
| "Site A"  | 0.18167360233856014
| "Site B"  | 0.18167360233856014
| "Site C"  | 0.18167360233856014
| "Site D"  | 0.18167360233856014
|===
--

To instead write the page-rank score to a node property in the Neo4j graph, use this query:

[role=query-example]
--
.The following will run the algorithm and write back results:
[source, cypher]
----
CALL gds.pageRank.write('myGraph', {
  maxIterations: 20,
  dampingFactor: 0.85,
  writeProperty: 'pagerank',
  relationshipWeightProperty: 'weight'
})
YIELD nodePropertiesWritten AS writtenProperties, ranIterations
----

.Results
[opts="header",cols="1m,1m"]
|===
| writtenProperties | ranIterations
| 8                 | 20
|===
--


[[algorithms-pagerank-examples-personalized]]
=== Personalized

Personalized Page Rank is a variation of Page Rank which is biased towards a set of `sourceNodes`.
This variant of Page Rank is often used as part of https://www.r-bloggers.com/from-random-walks-to-personalized-pagerank/[recommender systems^].

The following examples show how to run Page Rank centered around 'Site A'.

[role=query-example]
--
.The following will run the algorithm and stream results:
[source, cypher]
----
MATCH (siteA:Page {name: 'Site A'})
CALL gds.pageRank.stream('myGraph', {
  maxIterations: 20,
  dampingFactor: 0.85,
  sourceNodes: [siteA]
})
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name AS name, score
ORDER BY score DESC, name ASC
----

.Results
[opts="header",cols="1,1"]
|===
| name      | score
| "Home"    | 0.4015879109501838
| "Site A"  | 0.1690742586266424
| "About"   | 0.11305649263085797
| "Links"   | 0.11305649263085797
| "Product" | 0.11305649263085797
| "Site B"  | 0.01907425862664241
| "Site C"  | 0.01907425862664241
| "Site D"  | 0.01907425862664241
|===
--


[role=query-example]
--
.The following will run the algorithm and write back results:
[source, cypher]
----
MATCH (siteA:Page {name: 'Site A'})
CALL gds.pageRank.write('myGraph', {
  maxIterations: 20,
  dampingFactor: 0.85,
  writeProperty: 'pagerank',
  sourceNodes: [siteA]
})
YIELD nodePropertiesWritten, ranIterations
RETURN nodePropertiesWritten AS writtenProperties, ranIterations
----

.Results
[opts="header",cols="1m,1m"]
|===
| writtenProperties | ranIterations
| 8                 | 20
|===
--

[[algorithms-pagerank-examples-memory-estimation]]
=== Memory Estimation

[role=query-example]
--
.The following will estimate the memory requirements for running the algorithm:
[source, cypher]
----
CALL gds.pageRank.write.estimate('myGraph', {
  writeProperty: 'pageRank',
  maxIterations: 20,
  dampingFactor: 0.85
})
YIELD nodeCount, relationshipCount, bytesMin, bytesMax, requiredMemory
----

.Results
[opts="header",cols="1,1,1,1,1"]
|===
| nodeCount | relationshipCount | bytesMin | bytesMax | requiredMemory
| 8         | 14                | 1536     | 1536     | "1536 Bytes"
|===
--

[[algorithms-pagerank-examples-stats]]
=== Stats

[role=query-example]
--
.The following will run the algorithm and returns the result in form of statistical and measurement values
[source, cypher]
----
CALL gds.pageRank.stats('myGraph', {
  maxIterations: 20,
  dampingFactor: 0.85
})
YIELD ranIterations
----

.Results
[opts="header",cols="1"]
|===
| ranIterations
| 20
|===
--


[[algorithms-pagerank-usage]]
== Usage

There are some things to be aware of when using the Page Rank algorithm:

* If there are no links from within a group of pages to outside of the group, then the group is considered a spider trap.
* Rank sink can occur when a network of pages form an infinite cycle.
* Dead-ends occur when pages have no out-links.
If a page contains a link to another page which has no out-links, the link would be known as a dangling link.


