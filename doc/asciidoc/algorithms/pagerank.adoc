[[algorithms-pagerank]]
= Page Rank

[abstract]
--
This section describes the Page Rank algorithm in the Neo4j Graph Data Science library.
--

Page Rank is an algorithm that measures the *transitive* influence or connectivity of nodes.

It can be computed by either iteratively distributing one node's rank (originally based on degree) over its neighbours or by randomly traversing the graph and counting the frequency of hitting each node during these walks.

This section includes:

* <<algorithms-pagerank-intro, Introduction>>
* <<algorithms-pagerank-syntax, Syntax>>
* <<algorithms-pagerank-examples, Examples>>
** <<algorithms-pagerank-examples-unweighted, Unweighted>>
** <<algorithms-pagerank-examples-weighted, Weighted>>
** <<algorithms-pagerank-examples-personalized, Personalized>>
** <<algorithms-pagerank-examples-memory-estimation, Memory Estimation>>
** <<algorithms-pagerank-examples-stats, Stats>>
* <<algorithms-pagerank-usage, Usage>>

[[algorithms-pagerank-intro]]
== Introduction

The Page Rank algorithm measures the importance of each node within the graph, based on the number and quality of incoming relationships.
The underlying assumption is that pages of importance are more likely to receive a higher volume of links from other pages.

Page Rank is defined in the original Google paper as follows:

----
PR(A) = (1-d) + d (PR(T1)/C(T1) + ... + PR(Tn)/C(Tn))
----

where,

* we assume that a page `A` has pages `T1` to `Tn` which point to it (i.e., are citations).
* `d` is a damping factor which can be set between 0 and 1.
  It is usually set to 0.85.
* `C(A)` is defined as the number of links going out of page `A`.


For more information on this algorithm, see:

* http://infolab.stanford.edu/~backrub/google.html[The original google paper^]
// according to java doc implantation based on
* http://delab.csd.auth.gr/~dimitris/courses/ir_spring06/page_rank_computing/01531136.pdf[An Efficient Partition-Based Parallel PageRank Algorithm^]
* https://arxiv.org/pdf/1407.5107.pdf[PageRank beyond the web^] for use cases


[NOTE]
====
Running this algorithm requires sufficient memory availability.
Before running this algorithm, we recommend that you read <<memory-estimation>>.
====

[[algorithms-pagerank-syntax]]
== Syntax

.The following will run the algorithm and write back results:
[source, cypher]
----
CALL gds.pageRank.write(graphNameOrConfig: STRING|MAP, configuration: MAP)
YIELD nodes, createMillis, computeMillis, writeMillis, configuration
----

.Parameters
[opts="header",cols="1,1,1,1,4"]
|===
| Name              | Type    | Default        | Optional | Description
| graphNameOrConfig | string  | null           | no      | Either then name of a loaded graph or directly the config.
| configuration     | map     | n/a            | yes      | Additional configuration, if the first parameter was a graph name.
|===

.Configuration
[opts="header",cols="1,1,1,1,4"]
|===
| dampingFactor                 | float   | 0.85                   | yes | The damping factor of the Page Rank calculation.
| maxIterations                 | int     | 20                     | yes | The maximum number of iterations of Page Rank to run.
| tolerance                     | float   | 0.0000001              | yes | Minimum change in scores between iterations. If the score changes less than the tolerance value the result is considered stable and the algorithm returns.
| concurrency                   | int     | 4                      | yes | The number of concurrent threads used for running the algorithm. Also provides the default value for 'readConcurrency' and 'writeConcurrency'.
| readConcurrency               | int     | value of 'concurrency' | yes | The number of concurrent threads used for reading the graph.
| writeConcurrency              | int     | value of 'concurrency' | yes | The number of concurrent threads used for writing the result.
| relationshipWeightProperty    | string  | null                   | yes | The property name that contains weight. If null, treats the graph as unweighted. Must be numeric.
| writeProperty                 | string  | n/a                    | no  | The property name written back to.
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name          | Type    | Description
| ranIterations | int     | The number of iterations run.
| createMillis  | int     | Milliseconds for creating the graph.
| computeMillis | int     | Milliseconds for running the algorithm.
| writeMillis   | int     | Milliseconds for writing result data back.
| configuration | map     | The configuration used for running the algorithm.
|===

.The following will run the algorithm and stream results:
[source, cypher]
----
CALL gds.pageRank.stream(graphNameOrConfig: STRING|MAP, configuration: MAP)
YIELD node, score
----

.Parameters
[opts="header",cols="1,1,1,1,4"]
|===
| Name              | Type    | Default        | Optional | Description
| graphNameOrConfig | string  | null           | no      | Either then name of a loaded graph or directly the config.
| configuration     | map     | n/a            | yes      | Additional configuration, if the first parameter was a graph name.
|===

.Configuration
[opts="header",cols="1,1,1,1,4"]
|===
| dampingFactor                 | float  | 0.85                   | yes | The damping factor of the Page Rank calculation.
| maxIterations                 | int    | 20                     | yes | The maximum number of iterations of Page Rank to run.
| tolerance                     | float  | 0.0000001              | yes | Minimum change in scores between iterations. If the score changes less than the tolerance value the result is considered stable and the algorithm returns.
| concurrency                   | int    | 4                      | yes | The number of concurrent threads used for running the algorithm. Also provides the default value for 'readConcurrency'.
| readConcurrency               | int    | value of 'concurrency' | yes | The number of concurrent threads used for reading the graph.
| writeConcurrency              | int    | value of 'concurrency' | yes | The number of concurrent threads used for writing the result.
| relationshipWeightProperty    | string | null                   | yes | The property name that contains weight. If null, treats the graph as unweighted. Must be numeric.
|===

.Results
[opts="header"]
|===
| Name    | Type  | Description
| nodeId  | long  | Node ID
| score   | float | Page Rank weight
|===

.The following will run the algorithm and returns the result in form of statistical and measurement values:
[source, cypher]
----
CALL gds.pageRank.stats(graphNameOrConfig: STRING|MAP, configuration: MAP)
YIELD nodes, relationships, ranIterations, didConverge, createMillis, computeMillis, writeMillis
----

.Parameters
[opts="header",cols="1,1,1,1,4"]
|===
| Name              | Type    | Default        | Optional | Description
| graphNameOrConfig | string  | null           | no      | Either then name of a loaded graph or directly the config.
| configuration     | map     | n/a            | yes      | Additional configuration, if the first parameter was a graph name.
|===

The configuration is the same as for the `write` mode.

.The following will estimate the memory requirements for running the algorithm:
[source, cypher]
----
CALL gds.pageRank.<mode>.estimate(graphNameOrConfig: STRING|MAP, configuration: MAP})
YIELD nodeCount, relationshipCount, bytesMin, bytesMax, requiredMemory, mapView
----

.Parameters
[opts="header",cols="1,1,1,1,4"]
|===
| Name              | Type    | Default        | Optional | Description
| graphNameOrConfig | string  | null           | no      | Either then name of a loaded graph or directly the config.
| configuration     | map     | n/a            | yes      | Additional configuration, if the first parameter was a graph name.
|===

The `mode` can be substituted with the available modes (`stream`, `write` and `stats`).

.Configuration
[opts="header",cols="1,1,1,1,4"]
|===
| Name              | Type   | Default           | Optional | Description
| nodeCount         | int    | 0                 | yes      | The number of nodes in a fictive graph.
| relationshipCount | int    | 0                 | yes      | The number of relationships in a fictive graph.
|===

Setting the `nodeCount` and `relationshipCount` parameters allows a memory estimation without loading the graph.
For explicitly loaded graphs, the config-map needs to contain the graph name in the `graph` key.
Additionally algorithm specific parameters can also be provided as config.

[[algorithms-pagerank-examples]]
== Examples

Consider the graph created by the following Cypher statement:

[source, cypher, role=setup-query]
----
CREATE (home:Page {name:'Home'})
CREATE (about:Page {name:'About'})
CREATE (product:Page {name:'Product'})
CREATE (links:Page {name:'Links'})
CREATE (a:Page {name:'Site A'})
CREATE (b:Page {name:'Site B'})
CREATE (c:Page {name:'Site C'})
CREATE (d:Page {name:'Site D'})

CREATE (home)-[:LINKS {weight: 0.2}]->(about)
CREATE (home)-[:LINKS {weight: 0.2}]->(links)
CREATE (home)-[:LINKS {weight: 0.6}]->(product)
CREATE (about)-[:LINKS {weight: 1.0}]->(home)
CREATE (product)-[:LINKS {weight: 1.0}]->(home)
CREATE (a)-[:LINKS {weight: 1.0}]->(home)
CREATE (b)-[:LINKS {weight: 1.0}]->(home)
CREATE (c)-[:LINKS {weight: 1.0}]->(home)
CREATE (d)-[:LINKS {weight: 1.0}]->(home)
CREATE (links)-[:LINKS {weight: 0.8}]->(home)
CREATE (links)-[:LINKS {weight: 0.05}]->(a)
CREATE (links)-[:LINKS {weight: 0.05}]->(b)
CREATE (links)-[:LINKS {weight: 0.05}]->(c)
CREATE (links)-[:LINKS {weight: 0.05}]->(d)
----

This graph represents seven pages, linking to another.
Each relationship has a property called `weight`, which describes the importance of the relationship.

[NOTE]
====
In the examples below we will use named graphs and standard projections as the norm.
However, <<cypher-projection, Cypher projection>> and anonymous graphs could also be used.
====

.The following statement will create the graph and store it in the graph catalog.
[source, cypher, role=setup-query]
----
CALL gds.graph.create(
    'myGraph',
    'Page',
    'LINKS',
    {
        relationshipProperties: 'weight'
    }
)
----

In the following examples we will demonstrate using the Page Rank algorithm on this graph.


[[algorithms-pagerank-examples-unweighted]]
=== Unweighted

[role=query-example]
--
.The following will run the algorithm and stream results:
[source, cypher]
----
CALL gds.pageRank.stream('myGraph', { maxIterations: 20, dampingFactor: 0.85 })
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name AS name, score
ORDER BY score DESC, name ASC
----

.Results
[opts="header",cols="1,1"]
|===
| name      | score
| "Home"    | 3.2362017153762284
| "About"   | 1.0611098567023873
| "Links"   | 1.0611098567023873
| "Product" | 1.0611098567023873
| "Site A"  | 0.3292259009438567
| "Site B"  | 0.3292259009438567
| "Site C"  | 0.3292259009438567
| "Site D"  | 0.3292259009438567
|===
--

To instead write the page-rank score to a node property in the Neo4j graph, use this query:

[role=query-example]
--
.The following will run the algorithm and write back results:
[source, cypher]
----
CALL gds.pageRank.write('myGraph', {
  maxIterations: 20,
  dampingFactor: 0.85,
  writeProperty: 'pagerank'
})
YIELD nodePropertiesWritten AS writtenProperties, ranIterations
----

.Results
[opts="header",cols="1m,1m"]
|===
| writtenProperties | ranIterations
| 8                 | 20
|===
--

[[algorithms-pagerank-examples-weighted]]
=== Weighted

[role=query-example]
--
.The following will run the algorithm and stream results:
[source, cypher]
----
CALL gds.pageRank.stream('myGraph', {
  maxIterations: 20,
  dampingFactor: 0.85,
  relationshipWeightProperty: 'weight'
})
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name AS name, score
ORDER BY score DESC, name ASC
----

.Results
[opts="header",cols="1,1"]
|===
| name      | score
| "Home"    | 3.5528567278757683
| "Product" | 1.9541301048360766
| "About"   | 0.7513767024036497
| "Links"   | 0.7513767024036497
| "Site A"  | 0.18167360233856014
| "Site B"  | 0.18167360233856014
| "Site C"  | 0.18167360233856014
| "Site D"  | 0.18167360233856014
|===
--

To instead write the page-rank score to a node property in the Neo4j graph, use this query:

[role=query-example]
--
.The following will run the algorithm and write back results:
[source, cypher]
----
CALL gds.pageRank.write('myGraph', {
  maxIterations: 20,
  dampingFactor: 0.85,
  writeProperty: 'pagerank',
  relationshipWeightProperty: 'weight'
})
YIELD nodePropertiesWritten AS writtenProperties, ranIterations
----

.Results
[opts="header",cols="1m,1m"]
|===
| writtenProperties | ranIterations
| 8                 | 20
|===
--


[[algorithms-pagerank-examples-personalized]]
=== Personalized

Personalized Page Rank is a variation of Page Rank which is biased towards a set of `sourceNodes`.
This variant of Page Rank is often used as part of https://www.r-bloggers.com/from-random-walks-to-personalized-pagerank/[recommender systems^].

The following examples show how to run Page Rank centered around 'Site A'.

[role=query-example]
--
.The following will run the algorithm and stream results:
[source, cypher]
----
MATCH (siteA:Page {name: 'Site A'})
CALL gds.pageRank.stream('myGraph', {
  maxIterations: 20,
  dampingFactor: 0.85,
  sourceNodes: [siteA]
})
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name AS name, score
ORDER BY score DESC, name ASC
----

.Results
[opts="header",cols="1,1"]
|===
| name      | score
| "Home"    | 0.4015879109501838
| "Site A"  | 0.1690742586266424
| "About"   | 0.11305649263085797
| "Links"   | 0.11305649263085797
| "Product" | 0.11305649263085797
| "Site B"  | 0.01907425862664241
| "Site C"  | 0.01907425862664241
| "Site D"  | 0.01907425862664241
|===
--


[role=query-example]
--
.The following will run the algorithm and write back results:
[source, cypher]
----
MATCH (siteA:Page {name: 'Site A'})
CALL gds.pageRank.write('myGraph', {
  maxIterations: 20,
  dampingFactor: 0.85,
  writeProperty: 'pagerank',
  sourceNodes: [siteA]
})
YIELD nodePropertiesWritten, ranIterations
RETURN nodePropertiesWritten AS writtenProperties, ranIterations
----

.Results
[opts="header",cols="1m,1m"]
|===
| writtenProperties | ranIterations
| 8                 | 20
|===
--

[[algorithms-pagerank-examples-memory-estimation]]
=== Memory Estimation

[role=query-example]
--
.The following will estimate the memory requirements for running the algorithm:
[source, cypher]
----
CALL gds.pageRank.write.estimate('myGraph', {
  writeProperty: 'pageRank',
  maxIterations: 20,
  dampingFactor: 0.85
})
YIELD nodeCount, relationshipCount, bytesMin, bytesMax, requiredMemory
----

.Results
[opts="header",cols="1,1,1,1,1"]
|===
| nodeCount | relationshipCount | bytesMin | bytesMax | requiredMemory
| 8         | 14                | 1536     | 1536     | "1536 Bytes"
|===
--

[[algorithms-pagerank-examples-stats]]
=== Stats

[role=query-example]
--
.The following will run the algorithm and returns the result in form of statistical and measurement values
[source, cypher]
----
CALL gds.pageRank.stats('myGraph', {
  maxIterations: 20,
  dampingFactor: 0.85
})
YIELD ranIterations
----

.Results
[opts="header",cols="1"]
|===
| ranIterations
| 20
|===
--


[[algorithms-pagerank-usage]]
== Usage

There are some things to be aware of when using the Page Rank algorithm:

* If there are no links from within a group of pages to outside of the group, then the group is considered a spider trap.
* Rank sink can occur when a network of pages form an infinite cycle.
* Dead-ends occur when pages have no out-links.
If a page contains a link to another page which has no out-links, the link would be known as a dangling link.


