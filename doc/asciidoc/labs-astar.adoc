[[labs-algorithms-a_star]]
= The A* algorithm

[abstract]
--
This section describes the A* algorithm in the Neo4j Labs Graph Algorithms library.
--

The A* (pronounced “A-star”) algorithm improves on the classic Dijkstra algorithm.
It is based upon the observation that some searches are informed, and that by being informed we can make better choices over which paths to take through the graph.

[WARNING]
--
The A* algorithm was developed by the Neo4j Labs team and is not officially supported.
--

This section includes:

* <<algorithms-a_star-context, History and explanation>>
* <<algorithms-a_star-usecase, Use-cases - when to use the A* algorithm>>
* <<algorithms-a_star-syntax, Syntax>>
* <<algorithms-a_star-sample, A* algorithm sample>>
* <<algorithms-a_star-cp, Cypher projection>>


[[algorithms-a_star-context]]
== History and explanation

The A* algorithm was first described in 1968 by Peter Hart, Nils Nilsson, and Bertram Raphael.
For more information, see https://ieeexplore.ieee.org/document/4082128/[A Formal Basis for the Heuristic Determination of Minimum Cost Paths].

In A*, we split the path cost into two parts:

`g(n)`::
 This is the cost of the path from the starting point to some node n.

`h(n)`::
 This represents the estimated cost of the path from the node n to the destination node, as computed by a heuristic (an intelligent guess).

The A* algorithm balances `g(n)` and `h(n)` as it iterates the graph, thereby ensuring that at each iteration it chooses the node with the lowest overall cost `f(n) = g(n) + h(n)`.

In our implementation, geospatial distance is used as heurestic.


[[algorithms-a_star-usecase]]
== Use-cases - when to use the A* algorithm

* The A* algorithm can be used to find shortest paths between single pairs of locations, where GPS coordinates are known.


[[algorithms-a_star-syntax]]
== Syntax

.The following will run the algorithm and stream results:
[source, cypher]
----
CALL gds.alpha.shortestPath.astar.stream(configuration:Map)
YIELD nodeId, cost
----

.Configuration
[opts="header",cols="1,1,1,1,4"]
|===
| Name                          | Type   | Default  | Optional | Description
| startNode                     | node   | null     | no       | The start node
| endNode                       | node   | null     | no       | The end node
| relationshipWeightProperty    | string | null     | yes      | The property name that contains weight
| propertyKeyLat                | string | null     | no       | The property name that contains latitude coordinate
| propertyKeyLon                | string | null     | no       | The property name that contains longitude coordinate
| nodeQuery                     | string | null     | yes      | The label to load from the graph. If null, load all nodes
| relationshipQuery             | string | null     | yes      | The relationship type to load from the graph. If null, load all nodes
| defaultValue                  | float  | null     | yes      | The default value of the weight in case it is missing or invalid
| direction                     | string | outgoing | yes      | The relationship direction to load from the graph. If 'both', treats the relationships as undirected
|===

.Results
[opts="header"]
|===
| Name   | Type | Description
| nodeId | int  | Node ID
| cost   | int  | The cost it takes to get from start node to specific node
|===


[[algorithms-a_star-sample]]
== A* algorithm sample

.The following will create a sample graph:
[source, cypher]
----
CREATE (a:Station {name: 'King\'s Cross St. Pancras', latitude: 51.5308, longitude: -0.1238}),
       (b:Station {name: 'Euston',                    latitude: 51.5282, longitude: -0.1337}),
       (c:Station {name: 'Camden Town',               latitude: 51.5392, longitude: -0.1426}),
       (d:Station {name: 'Mornington Crescent',       latitude: 51.5342, longitude: -0.1387}),
       (e:Station {name: 'Kentish Town',              latitude: 51.5507, longitude: -0.1402}),
       (a)-[:CONNECTION {time: 2}]->(b),
       (b)-[:CONNECTION {time: 3}]->(c),
       (b)-[:CONNECTION {time: 2}]->(d),
       (d)-[:CONNECTION {time: 2}]->(c),
       (c)-[:CONNECTION {time: 2}]->(e)
----

// TODO: runs weighted, even it is not specified
.The following will run the algorithm and stream results:
[source, cypher]
----
MATCH (start:Station {name: 'King\'s Cross St. Pancras'}), (end:Station {name: 'Kentish Town'})
CALL gds.alpha.shortestPath.astar.stream({
  nodeProjection: '*',
  relationshipProjection: {
    CONNECTION: {
      type: 'CONNECTION',
      projection: 'UNDIRECTED',
      properties: 'time'
    }
  },
  startNode: start,
  endNode: end,
  propertyKeyLat: 'latitude',
  propertyKeyLon: 'longitude'
})
YIELD nodeId, cost
RETURN gds.util.asNode(nodeId).name AS station, cost
----

.Results
[opts="header",cols="1,1"]
|===
| station                    | cost
| "King's Cross St. Pancras" | 0
| "Euston"                   | 2
| "Camden Town"              | 5
| "Kentish Town"             | 7
|===


[[algorithms-a_star-cp]]
== Cypher projection

include::projected-graph-model/cypher-projection.adoc[tag=explanation]

[source, cypher]
----
MATCH (start:Station {name: "King's Cross St. Pancras"}), (end:Station {name: "Kentish Town"})
CALL gds.alpha.shortestPath.astar.stream({
  nodeQuery: 'MATCH (p:Station) RETURN id(p) AS id',
  relationshipQuery: 'MATCH (p1:Station)-[r:CONNECTION]->(p2:Station) RETURN id(p1) AS source, id(p2) AS target, r.time AS weight',
  startNode: start,
  endNode: end,
  relationshipWeightProperty: 'time',
  propertyKeyLat: 'latitude',
  propertyKeyLat: 'longitude'
})
YIELD nodeId, cost
RETURN gds.util.asNode(nodeId).name AS station, cost
----

.Results
[opts="header",cols="1,1"]
|===
| station                    | cost
| "King's Cross St. Pancras" | 0
| "Euston"                   | 2
| "Camden Town"              | 5
| "Kentish Town"             | 7
|===


// == References
//
// * [1]https://ieeexplore.ieee.org/document/4082128/[A Formal Basis for the Heuristic Determination of Minimum Cost Paths]
