[[nodeclassification-pipelines-predict]]
[.beta]
= Applying a trained model for prediction
:description: This section describes prediction using Node classification pipelines in the Neo4j Graph Data Science library.
:modelType: NodeClassification

include::partial$/operations-reference/beta-note.adoc[]


In the previous sections we have seen how to build up a Node Classification training pipeline and train it to produce a classification pipeline.
After xref:machine-learning/node-property-prediction/nodeclassification-pipelines/training.adoc[training], the runnable model is of type `{modelType}` and resides in the xref:model-catalog/index.adoc[model catalog].

The classification model can be executed with a graph in the graph catalog to predict the class of previously unseen nodes.
In addition to the predicted class for each node, the predicted probability for each class may also be retained on the nodes.
The order of the probabilities matches the order of the classes registered in the model.

Since the model has been trained on features which are created using the feature pipeline, the same feature pipeline is stored within the model and executed at prediction time.
As during training, intermediate node properties created by the node property steps in the feature pipeline are transient and not visible after execution.

The predict graph must contain the properties that the pipeline requires and the used array properties must have the same dimensions as in the train graph.
If the predict and train graphs are distinct, it is also beneficial that they have similar origins and semantics, so that the model is able to generalize well.

== Syntax

.Node Classification syntax per mode
[.tabbed-example, caption = ]
====
[.include-with-stream]
======
.Run Node Classification in stream mode on a named graph:
[source, cypher, role=noplay]
----
CALL gds.beta.pipeline.nodeClassification.predict.stream(
  graphName: String,
  configuration: Map
)
YIELD
  nodeId: Integer,
  predictedClass: Integer,
  predictedProbabilities: List of Float
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/machine-learning/node-property-prediction/node-property-prediction-stream-config.adoc[]
include::partial$/machine-learning/node-property-prediction/nodeclassification-pipeline/specific-stream-configuration.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                   | Type          | Description
| nodeId                 | Integer       | Node ID.
| predictedClass         | Integer       | Predicted class for this node.
| predictedProbabilities | List of Float | Probabilities for all classes, for this node.
|===
======

[.include-with-mutate]
======
.Run Node Classification in mutate mode on a named graph:
[source, cypher, role=noplay]
----
CALL gds.beta.pipeline.nodeClassification.predict.mutate(
  graphName: String,
  configuration: Map
)
YIELD
  preProcessingMillis: Integer,
  computeMillis: Integer,
  postProcessingMillis: Integer,
  mutateMillis: Integer,
  nodePropertiesWritten: Integer,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]
.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/machine-learning/node-property-prediction/node-property-prediction-mutate-config.adoc[]
include::partial$/machine-learning/node-property-prediction/nodeclassification-pipeline/specific-write-mutate-configuration.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                    | Type    | Description
| preProcessingMillis     | Integer | Milliseconds for preprocessing the graph.
| computeMillis           | Integer | Milliseconds for running the algorithm.
| postProcessingMillis    | Integer | Milliseconds for computing the global metrics.
| mutateMillis            | Integer | Milliseconds for adding properties to the in-memory graph.
| nodePropertiesWritten   | Integer | Number of node properties written.
| configuration           | Map     | Configuration used for running the algorithm.
|===
======

[.include-with-write]
======
.Run Node Classification in write mode on a named graph:
[source, cypher, role=noplay]
----
CALL gds.beta.pipeline.nodeClassification.predict.write(
  graphName: String,
  configuration: Map
)
YIELD
  preProcessingMillis: Integer,
  computeMillis: Integer,
  postProcessingMillis: Integer,
  writeMillis: Integer,
  nodePropertiesWritten: Integer,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/machine-learning/node-property-prediction/node-property-prediction-write-config.adoc[]
include::partial$/machine-learning/node-property-prediction/nodeclassification-pipeline/specific-write-mutate-configuration.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                    | Type    | Description
| preProcessingMillis     | Integer | Milliseconds for preprocessing the graph.
| computeMillis           | Integer | Milliseconds for running the algorithm.
| postProcessingMillis    | Integer | Milliseconds for computing the global metrics.
| writeMillis             | Integer | Milliseconds for writing result back to Neo4j.
| nodePropertiesWritten   | Integer | Number of node properties written.
| configuration           | Map     | Configuration used for running the algorithm.
|===
======

====

[[nodeclassification-pipelines-predict-examples]]
== Example

In the following examples we will show how to use a classification model to predict the class of a node in your in-memory graph.
In addition to the predicted class, we will also produce the probability for each class in another node property.
In order to do this, we must first have an already trained model registered in the Model Catalog.
We will use the model which we trained in the xref:machine-learning/node-property-prediction/nodeclassification-pipelines/training.adoc[train example] which we gave the name `'nc-pipeline-model'`.

[[nodeclassification-pipelines-examples-predict-memory-estimation]]
=== Memory Estimation
:mode: stream

include::partial$/algorithms/shared/examples-estimate-intro.adoc[]

[role=query-example, group=nc]
--
.The following will estimate the memory requirements for running the algorithm in stream mode:
[source, cypher, role=noplay]
----
CALL gds.beta.pipeline.nodeClassification.predict.stream.estimate('myGraph', {
  modelName: 'nc-pipeline-model',
  includePredictedProbabilities: true,
  targetNodeLabels: ['UnknownHouse']
})
YIELD requiredMemory
----

.Results
[opts="header"]
|===
| requiredMemory
| +"3200 Bytes"+
|===
--

NOTE: If a node property step does not have an estimation implemented, the step will be ignored in the estimation.

[[node-classification-pipelines-predict-examples-stream]]
=== Stream
[role=query-example, group=nc]
--
[source, cypher]
----
CALL gds.beta.pipeline.nodeClassification.predict.stream('myGraph', {
  modelName: 'nc-pipeline-model',
  includePredictedProbabilities: true,
  targetNodeLabels: ['UnknownHouse']
})
 YIELD nodeId, predictedClass, predictedProbabilities
WITH gds.util.asNode(nodeId) AS houseNode, predictedClass, predictedProbabilities
RETURN
  houseNode.color AS classifiedHouse,
  predictedClass,
  floor(predictedProbabilities[predictedClass] * 100) AS confidence
  ORDER BY classifiedHouse
----

.Results
[opts="header",cols="m,m,m"]
|===
| classifiedHouse | predictedClass | confidence
| "Pink"          |              0 | 96.0
| "Tan"           |              1 | 97.0
| "Yellow"        |              2 | 75.0
|===

As we can see, the model was able to predict the pink house into class 0, tan house into class 1, and yellow house into class 2.
This makes sense, as all houses in class 0 had three stories, class 1 two stories and class 2 one story, and the same is true of the pink, tan and yellow houses, respectively.
Additionally, we see that the model is confident in these predictions, as the confidence is >=79% in all cases.
--

NOTE: The indices in the `predictedProbabilities` correspond to the order of the classes in the classification model. To inspect the order of the classes, we can look at its `modelInfo` (see xref:model-catalog/list.adoc[listing models]).

[[nodeclassification-pipelines-examples-mutate]]
=== Mutate

The `mutate` execution mode updates the named graph with a new node property containing the predicted class for that node.
The name of the new property is specified using the mandatory configuration parameter `mutateProperty`.
The result is a single summary row including information about timings and how many properties were written.
The `mutate` mode is especially useful when multiple algorithms are used in conjunction.

For more details on the `mutate` mode in general, see xref:common-usage/running-algos.adoc#running-algos-mutate[Mutate].

[role=query-example, group=nc]
--
[source, cypher, role=noplay]
----
CALL gds.beta.pipeline.nodeClassification.predict.mutate('myGraph', {
  targetNodeLabels: ['UnknownHouse'],
  modelName: 'nc-pipeline-model',
  mutateProperty: 'predictedClass',
  predictedProbabilityProperty: 'predictedProbabilities'
}) YIELD nodePropertiesWritten
----

.Results
[opts="header"]
|===
| nodePropertiesWritten
| 6
|===
--

Since we specified also the `predictedProbabilityProperty` we are writing two properties for each of the 3 `UnknownHouse` nodes.


[[nodeclassification-pipelines-examples-write]]
=== Write

The `write` execution mode writes the predicted property for each node as a property to the Neo4j database.
The name of the new property is specified using the mandatory configuration parameter `writeProperty`.
The result is a single summary row including information about timings and how many properties were written.
The `write` mode enables directly persisting the results to the database.

For more details on the `write` mode in general, see xref:common-usage/running-algos.adoc#running-algos-write[Write].

[role=query-example, group=nc]
--
[source, cypher, role=noplay]
----
CALL gds.beta.pipeline.nodeClassification.predict.write('myGraph', {
  targetNodeLabels: ['UnknownHouse'],
  modelName: 'nc-pipeline-model',
  writeProperty: 'predictedClass',
  predictedProbabilityProperty: 'predictedProbabilities'
}) YIELD nodePropertiesWritten
----

.Results
[opts="header"]
|===
| nodePropertiesWritten
| 6
|===
--

Since we specified also the `predictedProbabilityProperty` we are writing two properties for each of the 3 `UnknownHouse` nodes.
