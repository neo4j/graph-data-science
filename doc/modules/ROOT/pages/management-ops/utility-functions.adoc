

[[utility-functions]]
= Utility functions
:description: This section provides explanations and examples for each of the utility functions in the Neo4j Graph Data Science library.


[[utility-functions-system]]
== System Functions

[[system-functions-table]]
[opts=header,cols="1m,1"]
|===
| Name        | Description
| gds.version | Return the version of the installed Neo4j Graph Data Science library.
|===

[role=query-example]
--
.Usage:
[source, cypher, role=noplay]
----
RETURN gds.version() AS version
----

.Results
[opts="header"]
|===
| version
| "2.1.7"
|===
--


[[utility-functions-numeric]]
== Numeric Functions

.Numeric Functions
[[Numeric-functions-table]]
[opts=header,cols="1m,1"]
|===
| Name                | Description
| gds.util.NaN        | Returns NaN as a Cypher value.
| gds.util.infinity   | Return infinity as a Cypher value.
| gds.util.isFinite   | Return false if the given argument is ±Infinity, NaN, or null.
| gds.util.isInfinite | Return true if the given argument is ±Infinity, NaN, or null.
|===


=== Syntax

[[Numeric-functions-syntax-table]]
[opts=header,cols="1m,1"]
|===
| Name                                  | Parameter
| gds.util.NaN()                        | -
| gds.util.infinity()                   | -
| gds.util.isFinite(value: NUMBER)      | value to be checked if it is finite.
| gds.util.isInfinite(value: NUMBER)    | value to be checked if it is infinite.
|===


=== Examples

[role=query-example]
--
.Example for gds.util.IsFinite:
[source, cypher, role=noplay]
----
UNWIND [1.0, gds.util.NaN(), gds.util.infinity()] AS value
RETURN gds.util.isFinite(value) AS isFinite
----

.Results
[opts="header"]
|===
| isFinite
| true
| false
| false
|===
--

[role=query-example]
--
.Example for gds.util.isInfinite():
[source, cypher, role=noplay]
----
UNWIND [1.0, gds.util.NaN(), gds.util.infinity()] AS value
RETURN gds.util.isInfinite(value) AS isInfinite
----

.Results
[opts="header"]
|===
| isInfinite
| false
| true
| true
|===
--

A common usage of `gds.util.IsFinite` and `gds.util.IsInfinite` is for filtering streamed results, as for instance seen in the examples of xref::alpha-algorithms/all-pairs-shortest-path.adoc#algorithm-all-pairs-shortest-path-sample[`gds.alpha.allShortestPaths`].


[[utility-functions-node-path]]
== Node id functions

.Node id functions
[[Node-and-Path-functions-table]]
[opts=header,cols="1m,1m"]
|===
| Name             | Description
| gds.util.asNode  | Return the node object for the given node id or null if none exists.
| gds.util.asNodes | Return the node objects for the given node ids or an empty list if none exists.
|===


=== Syntax

[[Node-and-Path-functions-syntax-table]]
[opts=header,cols="1m,1"]
|===
| Name                                | Parameters
| gds.util.asNode(nodeId: NUMBER)     | nodeId of a node in the neo4j-graph
| gds.util.asNodes(nodeIds: List of NUMBER) | list of nodeIds of nodes in the neo4j-graph
|===


=== Examples

Consider the graph created by the following Cypher statement:

.Example graph:
[source, cypher, role=noplay setup-query]
----
CREATE  (nAlice:User {name: 'Alice'})
CREATE  (nBridget:User {name: 'Bridget'})
CREATE  (nCharles:User {name: 'Charles'})
CREATE  (nAlice)-[:LINK]->(nBridget)
CREATE  (nBridget)-[:LINK]->(nCharles)
----

[role=query-example]
--
.Example for gds.util.asNode:
[source, cypher, role=noplay]
----
MATCH (u:User{name: 'Alice'})
WITH id(u) AS nodeId
RETURN gds.util.asNode(nodeId).name AS node
----

.Results
[opts="header"]
|===
| node
| "Alice"
|===
--

[role=query-example]
--
.Example for gds.util.asNodes:
[source, cypher, role=noplay]
----
MATCH (u:User)
WHERE NOT u.name = 'Charles'
WITH collect(id(u)) AS nodeIds
RETURN [x in gds.util.asNodes(nodeIds)| x.name] AS nodes
----

.Results
[opts="header"]
|===
| nodes
| [Alice, Bridget]
|===
--

As many algorithms streaming mode only return the node id, `gds.util.asNode` and `gds.util.asNodes` can be used to retrieve the whole node from the neo4j database.
