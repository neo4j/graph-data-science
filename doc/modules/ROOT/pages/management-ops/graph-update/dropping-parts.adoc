= Dropping parts of the graph
:description: This chapter explains how to drop node properties or relationships of a given type from a projected graph.

Dropping parts of the graph is useful to free up memory or remove previously mutated algorithms results.

== Syntax

.Syntax descriptions of the different drop operations over a graph
[.tabbed-example, caption = ]
====
[.include-with-drop-node-property]
======
[source, cypher, role=noplay]
----
CALL gds.graph.nodeProperties.drop(
    graphName: String,
    nodeProperties: String or List of Strings,
    configuration: Map
)
YIELD
    propertiesRemoved: Integer,
    graphName: String,
    nodeProperties: String or List of String
----

.Parameters
[opts="header",cols="1,3,1,5"]
|===
| Name           | Type                       | Optional | Description
| graphName      | String                     | no       | The name under which the graph is stored in the catalog.
| nodeProperties | String or List of Strings  | no       | The node properties in the graph to drop.
| configuration  | Map                        | yes      | Additional parameters to configure.
|===

.Configuration
[opts="header",cols="1,1,1,7"]
|===
| Name                   | Type                  | Default           | Description
| concurrency            | Integer               | 4                 | The number of concurrent threads. Note, this procedure is always running single-threaded.
|===

.Results
[opts="header",cols="2,3,5"]
|===
| Name              | Type                     | Description
| propertiesRemoved | Integer                  | Number of properties dropped.
| graphName         | String                   | The name of a graph stored in the catalog.
| nodeProperties    | String or List of String | The dropped node properties.
|===
======

[.include-with-drop-relationship-type]
======
[source, cypher, role=noplay]
----
CALL gds.graph.relationships.drop(
    graphName: String,
    relationshipType: String
)
YIELD
  graphName: String,
  relationshipType: String,
  deletedRelationships: Integer,
  deletedProperties: Map
----

.Parameters
[opts="header",cols="1,3,1,5"]
|===
| Name             | Type    | Optional | Description
| graphName        | String  | no       | The name under which the graph is stored in the catalog.
| relationshipType | String  | no       | The relationship type in the graph to drop.
|===

.Results
[opts="header",cols="2,3,5"]
|===
| Name                 | Type       | Description
| graphName            | String     | The name of a graph stored in the catalog.
| relationshipType     | String     | The type of the dropped relationships.
| deletedRelationships | Integer    | Number of dropped relationships from the in-memory graph.
| deletedProperties    | Integer    | Map where the key is the name of the relationship property, and the value is the number of dropped properties under that name.
|===
======
====

== Examples

include::partial$/algorithms/shared/examples-named-native-note.adoc[]

In order to demonstrate the GDS capabilities over node properties, we are going to create a small social network graph in Neo4j and project it into our graph catalog.

.The following Cypher statement will create the example graph in the Neo4j database:
[source, cypher, role=noplay setup-query]
----
CREATE
  (florentin:Person { name: 'Florentin', age: 16 }),
  (adam:Person { name: 'Adam', age: 18 }),
  (veselin:Person { name: 'Veselin', age: 20 }),
  (hobbit:Book { name: 'The Hobbit', numberOfPages: 310 }),
  (florentin)-[:KNOWS { since: 2010 }]->(adam),
  (florentin)-[:KNOWS { since: 2018 }]->(veselin),
  (adam)-[:READ]->(hobbit)
----

.Project the small social network graph:
[source, cypher, role=noplay graph-project-query]
----
MATCH (n:Person)-[r:KNOWS|READ]->(m:Person|Book)
RETURN gds.graph.project('socialGraph', n, m,
  {
    sourceNodeLabels: labels(n),
    targetNodeLabels: labels(m),
    sourceNodeProperties: n { .age },
    targetNodeProperties: CASE WHEN m:Person THEN m { .age } ELSE {} END,
    relationshipType: type(r)
  }
)
----

.Compute the Degree Centrality in our social graph:
[source, cypher, role=noplay graph-project-query]
----
CALL gds.degree.mutate('socialGraph', {mutateProperty: 'score'})
----

[[catalog-graph-remove-node-properties-example]]
=== Dropping node properties


[role=query-example]
--
.Drop the `score` property from all projected nodes in the `socialGraph`:
[source, cypher, role=noplay]
----
CALL gds.graph.nodeProperties.drop('socialGraph', ['score'])
YIELD propertiesRemoved
----

.Results
[opts="header"]
|===
| propertiesRemoved
| 4
|===
--

[NOTE]
--
The above example requires all given properties to be present on _at least one_ projected node label.
--

[[catalog-graph-delete-rel-type]]
=== Drop relationships of a given type

We can drop all relationships of a given type from a named graph in the catalog.
This is useful to free up main memory or to drop accidentally added relationship types.

[NOTE]
====
Deleting relationships of a given type is only possible if it is not the last relationship type present in the graph.
If we still want to drop these relationships we need to xref:management-ops/graph-drop.adoc[drop the graph] instead.
====

[role=query-example]
--
.Drop all relationships of type `SIMILAR` from a named graph:
[source, cypher, role=noplay]
----
CALL gds.graph.relationships.drop(
  'socialGraph',                    // <1>
  'READ'                            // <2>
)
YIELD
  graphName, relationshipType, deletedRelationships, deletedProperties
----
<1> The name of the projected graph.
<2> The relationship type we want to delete from the projected graph.

.Results
[opts="header"]
|===
| graphName     | relationshipType | deletedRelationships | deletedProperties
| "socialGraph" | "READ"        | 1                   | {}
|===
--
