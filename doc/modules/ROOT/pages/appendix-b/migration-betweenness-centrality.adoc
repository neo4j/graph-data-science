[[migration-betweenness-centrality]]
= Betweenness Centrality

In Graph Algorithms v3.5, Betweenness Centrality was surfaced in two different procedures:

* `algo.betweenness` and `algo.betweenness.stream`
* `algo.betweenness.sampled` and `algo.betweenness.sampled.stream`

These two have been merged into a single procedure `gds.betweenness` with all four execution modes supported.
The sampling is controlled via configuration parameter rather than explicit procedures.
Setting the sampling size to the node count will produce exact results.

.Changes in Configuration
[opts=header]
|===
|Graph Algorithms v3.5 |Graph Data Science v1.3
| `stats`              | -
| `strategy`           | -
| `probability`        | -
| -                    | `samplingSize`
| -                    | `samplingSeed`
| `maxDepth`           | -
| `direction`          | -
| `concurrency`        | `concurrency`
| `readConcurrency`    | `readConcurrency` footnote:bc-read[Only when using anonymous graph]
| `writeConcurrency`   | `writeConcurrency` footnote:bc-write[Only for `write` mode]
| `writeProperty`      | `writeProperty` footnote:bc-write[]
| `write`              | -
| `graph`              | -
|===

.Changes in YIELD fields
[opts=header]
|===
|Graph Algorithms v3.5 |Graph Data Science v1.3
| `centrality`         | `score` footnote:bc-stream-yield[Only for `stream` mode]
| `nodes`              | -
| `minCentrality`      | `minimumScore`
| `maxCentrality`      | `maximumScore`
| `sumCentrality`      | `scoreSum`
| `loadMillis`         | `createMillis`
| `evalMillis`         | `computeMillis`
| `writeMillis`        | `writeMillis`
| -                    | `postProcessingMillis`
| -                    | `configuration` footnote:bc-gds-config[The configuration used to run the algorithm]
| -                    | `nodePropertiesWritten` footnote:bc-write-yield[Only for `write` mode]
| `write`              | -
| `writeProperty` footnote:bc-config[Inlined into `configuration`] | -
|===

.Betweenness Centrality Stream Mode
[opts=header,cols="1a,1a"]
|===
|Graph Algorithms v3.5 |Graph Data Science v1.3
2+|Minimalistic stream over named graph:
|
[source, cypher, role=noplay]
----
CALL algo.betweenness.stream(null, null, {graph: 'myGraph'})
YIELD nodeId, centrality
----
|
[source, cypher, role=noplay]
----
CALL gds.betweenness.stream('myGraph')
YIELD nodeId, score
----

2+|Minimalistic stream over named graph, sampled:
|
[source, cypher, role=noplay]
----
CALL algo.betweenness.sampled.stream(null, null, {graph: 'myGraph', probability: 0.5})
YIELD nodeId, centrality
----
|
[source, cypher, role=noplay]
----
CALL gds.betweenness.stream('myGraph', {samplingSize: 1000}) // assume 2000 nodes
YIELD nodeId, score
----

2+| Minimalistic streaming over anonymous graph:
|
[source, cypher, role=noplay]
----
CALL algo.betweenness.stream(
  'MyLabel',
  'MY_RELATIONSHIP_TYPE'
)
----
|
[source, cypher, role=noplay]
----
CALL gds.betweenness.stream({
  nodeProjection: 'MyLabel',
  relationshipProjection: 'MY_RELATIONSHIP_TYPE'
})
----

2+| Streaming over anonymous graph with `UNDRECTED` relationship orientation:
|
[source, cypher, role=noplay]
----
CALL algo.betweenness.stream(
  'MyLabel',
  'MY_RELATIONSHIP_TYPE',
  { undirected: true }
)
----
|
[source, cypher, role=noplay]
----
CALL gds.betweenness.stream({
  nodeProjection: 'MyLabel',
  relationshipProjection: {
    MY_RELATIONSHIP_TYPE: {
      orientation: 'UNDIRECTED'
    }
  }
})
----
|===

.Betweenness Centrality Write Mode
[opts=header,cols="1a,1a"]
|===
|Graph Algorithms v3.5 |Graph Data Science v1.3
2+| Running `write` mode on named graph:
|
[source, cypher, role=noplay]
----
CALL algo.betweenness(
  null,
  null,
  {
    graph: 'myGraph',
    writeProperty: 'myWriteProperty',
    write: true
  }
)
YIELD
  nodes,
  minCentrality,
  maxCentrality,
  sumCentrality,
  loadMillis,
  evalMillis,
  writeMillis
----
|
[source, cypher, role=noplay]
----
CALL gds.betweenness.write(
  'myGraph',
  {
    writeProperty: 'myWriteProperty'
  }
)
YIELD
  nodePropertiesWritten,
  minimumScore,
  maximumScore,
  scoreSum,
  createMillis,
  computeMillis,
  writeMillis,
  configuration
----
|===
