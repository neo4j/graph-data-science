[[algorithms-louvain]]
= Louvain
:description: This section describes the Louvain algorithm in the Neo4j Graph Data Science library.
:entity: node
:result: community ID
:algorithm: Louvain


:directed:
:homogeneous:
:weighted:
include::partial$/algorithms/shared/algorithm-traits.adoc[]


[[algorithms-louvain-intro]]
== Introduction
The Louvain method is an algorithm to detect communities in large networks.
It maximizes a modularity score for each community, where the modularity quantifies the quality of an assignment of nodes to communities.
This means evaluating how much more densely connected the nodes within a community are, compared to how connected they would be in a random network.

The Louvain algorithm is a hierarchical clustering algorithm, that recursively merges communities into a single node and executes the modularity clustering on the condensed graphs.

For more information on this algorithm, see:

* https://arxiv.org/pdf/1410.1237.pdf[Lu, Hao, Mahantesh Halappanavar, and Ananth Kalyanaraman "Parallel heuristics for scalable community detection."^]
* https://en.wikipedia.org/wiki/Louvain_modularity

[NOTE]
====
Running this algorithm requires sufficient memory availability.
Before running this algorithm, we recommend that you read xref:common-usage/memory-estimation.adoc[Memory Estimation].
====


[[algorithms-louvain-syntax]]
== Syntax

include::partial$/algorithms/shared/syntax-intro-named-graph.adoc[]

.Louvain syntax per mode
[.tabbed-example, caption = ]
====

[.include-with-stream]
======
.Run Louvain in stream mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.louvain.stream(
  graphName: String,
  configuration: Map
)
YIELD
  nodeId: Integer,
  communityId: Integer,
  intermediateCommunityIds: List of Integer
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-stream-stats-configuration-entries.adoc[]
include::partial$/algorithms/louvain/specific-configuration-stream.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                      | Type      | Description
| nodeId                    | Integer   | Node ID.
| communityId               | Integer   | The community ID of the final level.
| intermediateCommunityIds  | List of Integer | Community IDs for each level. `Null` if `includeIntermediateCommunities` is set to false.
|===
======


[.include-with-stats]
======
.Run Louvain in stats mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.louvain.stats(
  graphName: String,
  configuration: Map
)
YIELD
  preProcessingMillis: Integer,
  computeMillis: Integer,
  postProcessingMillis: Integer,
  communityCount: Integer,
  ranLevels: Integer,
  modularity: Float,
  modularities: List of Float,
  communityDistribution: Map,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-stream-stats-configuration-entries.adoc[]
include::partial$/algorithms/louvain/specific-configuration.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                  | Type      | Description
| preProcessingMillis   | Integer   | Milliseconds for preprocessing the data.
| computeMillis         | Integer   | Milliseconds for running the algorithm.
| postProcessingMillis  | Integer   | Milliseconds for computing percentiles and community count.
| communityCount        | Integer   | The number of communities found.
| ranLevels             | Integer   | The number of supersteps the algorithm actually ran.
| modularity            | Float     | The final modularity score.
| modularities          | List of Float | The modularity scores for each level.
| communityDistribution | Map       | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of community size for the last level.
| configuration         | Map       | The configuration used for running the algorithm.
|===
======

[.include-with-mutate]
======
.Run Louvain in mutate mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.louvain.mutate(
  graphName: String,
  configuration: Map
)
YIELD
  preProcessingMillis: Integer,
  computeMillis: Integer,
  mutateMillis: Integer,
  postProcessingMillis: Integer,
  communityCount: Integer,
  ranLevels: Integer,
  modularity: Float,
  modularities: List of Float,
  nodePropertiesWritten: Integer,
  communityDistribution: Map,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-mutate-configuration-entries.adoc[]
include::partial$/algorithms/louvain/specific-configuration.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                   | Type      | Description
| preProcessingMillis    | Integer   | Milliseconds for preprocessing the data.
| computeMillis          | Integer   | Milliseconds for running the algorithm.
| mutateMillis           | Integer   | Milliseconds for adding properties to the projected graph.
| postProcessingMillis   | Integer   | Milliseconds for computing percentiles and community count.
| communityCount         | Integer   | The number of communities found.
| ranLevels              | Integer   | The number of supersteps the algorithm actually ran.
| modularity             | Float     | The final modularity score.
| modularities           | List of Float | The modularity scores for each level.
| nodePropertiesWritten  | Integer   | Number of properties added to the projected graph.
| communityDistribution  | Map       | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of community size for the last level.
| configuration          | Map       | The configuration used for running the algorithm.
|===
======


[.include-with-write]
======
.Run Louvain in write mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.louvain.write(
  graphName: String,
  configuration: Map
)
YIELD
  preProcessingMillis: Integer,
  computeMillis: Integer,
  writeMillis: Integer,
  postProcessingMillis: Integer,
  nodePropertiesWritten: Integer,
  communityCount: Integer,
  ranLevels: Integer,
  modularity: Float,
  modularities: List of Float,
  communityDistribution: Map,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-write-configuration-entries.adoc[]
include::partial$/algorithms/louvain/specific-configuration-write.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                  | Type      | Description
| preProcessingMillis   | Integer   | Milliseconds for preprocessing the data.
| computeMillis         | Integer   | Milliseconds for running the algorithm.
| writeMillis           | Integer   | Milliseconds for writing result data back.
| postProcessingMillis  | Integer   | Milliseconds for computing percentiles and community count.
| nodePropertiesWritten | Integer   | The number of node properties written.
| communityCount        | Integer   | The number of communities found.
| ranLevels             | Integer   | The number of supersteps the algorithm actually ran.
| modularity            | Float     | The final modularity score.
| modularities          | List of Float | The modularity scores for each level.
| communityDistribution | Map       | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of community size for the last level.
| configuration         | Map       | The configuration used for running the algorithm.
|===
======

====


[[algorithms-louvain-examples]]
== Examples

:algorithm-name: Louvain community detection
:graph-description: social network
:image-file: two-user-communities.svg
include::partial$/algorithms/shared/examples-intro.adoc[]

.The following Cypher statement will create the example graph in the Neo4j database:
[source, cypher, role=noplay setup-query]
----
CREATE
  (nAlice:User {name: 'Alice', seed: 42}),
  (nBridget:User {name: 'Bridget', seed: 42}),
  (nCharles:User {name: 'Charles', seed: 42}),
  (nDoug:User {name: 'Doug'}),
  (nMark:User {name: 'Mark'}),
  (nMichael:User {name: 'Michael'}),

  (nAlice)-[:LINK {weight: 1}]->(nBridget),
  (nAlice)-[:LINK {weight: 1}]->(nCharles),
  (nCharles)-[:LINK {weight: 1}]->(nBridget),

  (nAlice)-[:LINK {weight: 5}]->(nDoug),

  (nMark)-[:LINK {weight: 1}]->(nDoug),
  (nMark)-[:LINK {weight: 1}]->(nMichael),
  (nMichael)-[:LINK {weight: 1}]->(nMark);
----

This graph has two clusters of _Users_, that are closely connected.
Between those clusters there is one single edge.
The relationships that connect the nodes in each component have a property `weight` which determines the strength of the relationship.

We can now project the graph and store it in the graph catalog.
We load the `LINK` relationships with orientation set to `UNDIRECTED` as this works best with the Louvain algorithm.

include::partial$/algorithms/shared/examples-named-native-note.adoc[]

.The following statement will project the graph and store it in the graph catalog.
[source, cypher, role=noplay graph-project-query]
----
CALL gds.graph.project(
    'myGraph',
    'User',
    {
        LINK: {
            orientation: 'UNDIRECTED'
        }
    },
    {
        nodeProperties: 'seed',
        relationshipProperties: 'weight'
    }
)
----

In the following examples we will demonstrate using the Louvain algorithm on this graph.

[[algorithms-louvain-examples-memory-estimation]]
=== Memory Estimation

:mode: write
include::partial$/algorithms/shared/examples-estimate-intro.adoc[]

[role=query-example]
--
.The following will estimate the memory requirements for running the algorithm:
[source, cypher, role=noplay]
----
CALL gds.louvain.write.estimate('myGraph', { writeProperty: 'community' })
YIELD nodeCount, relationshipCount, bytesMin, bytesMax, requiredMemory
----

.Results
[opts="header",cols="1,1,1,1,1"]
|===
| nodeCount | relationshipCount | bytesMin | bytesMax | requiredMemory
| 6         | 14                | 5329     | 563208   | "[5329 Bytes \... 550 KiB]"
|===
--


[[algorithms-louvain-examples-stream]]
=== Stream

include::partial$/algorithms/shared/examples-stream-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm and stream results:
[source, cypher, role=noplay]
----
CALL gds.louvain.stream('myGraph')
YIELD nodeId, communityId, intermediateCommunityIds
RETURN gds.util.asNode(nodeId).name AS name, communityId
ORDER BY name ASC
----

.Results
[opts="header"]
|===
| name      | communityId
| "Alice"   | 2
| "Bridget" | 2
| "Charles" | 2
| "Doug"    | 5
| "Mark"    | 5
| "Michael" | 5
|===
--

We use default values for the procedure configuration parameter.
Levels and `innerIterations` are set to 10 and the tolerance value is 0.0001.


[[algorithms-louvain-examples-stats]]
=== Stats

:stats-syntax: algorithms-louvain-syntax
include::partial$/algorithms/shared/examples-stats-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm and returns the result in form of statistical and measurement values
[source, cypher, role=noplay]
----
CALL gds.louvain.stats('myGraph')
YIELD communityCount
----

.Results
[opts="header",cols="1"]
|===
| communityCount
| 2
|===
--


[[algorithms-louvain-examples-mutate]]
=== Mutate

include::partial$/algorithms/shared/examples-mutate-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm and store the results in `myGraph`:
[source, cypher, role=noplay]
----
CALL gds.louvain.mutate('myGraph', { mutateProperty: 'communityId' })
YIELD communityCount, modularity, modularities
----

.Results
[opts="header"]
|===
| communityCount | modularity         | modularities
| 2              | 0.3571428571428571 | [0.3571428571428571]
|1 row
|===
--

In `mutate` mode, only a single row is returned by the procedure.
The result contains meta information, like the number of identified communities and the modularity values.
In contrast to the `write` mode the result is written to the GDS in-memory graph instead of the Neo4j database.


[[algorithms-louvain-examples-write]]
=== Write

include::partial$/algorithms/shared/examples-write-intro.adoc[]

[role=query-example]
--
.The following run the algorithm, and write back results:
[source, cypher, role=noplay]
----
CALL gds.louvain.write('myGraph', { writeProperty: 'community' })
YIELD communityCount, modularity, modularities
----

.Results
[opts="header"]
|===
| communityCount | modularity         | modularities
| 2              | 0.3571428571428571 | [0.3571428571428571]
|1 row
|===
--

When writing back the results, only a single row is returned by the procedure.
The result contains meta information, like the number of identified communities and the modularity values.


[[algorithms-louvain-examples-stream-weighted]]
=== Weighted

The Louvain algorithm can also run on weighted graphs, taking the given relationship weights into concern when calculating the modularity.

[role=query-example]
--
.The following will run the algorithm on a weighted graph and stream results:
[source, cypher, role=noplay]
----
CALL gds.louvain.stream('myGraph', { relationshipWeightProperty: 'weight' })
YIELD nodeId, communityId, intermediateCommunityIds
RETURN gds.util.asNode(nodeId).name AS name, communityId
ORDER BY name ASC
----

.Results
[opts="header"]
|===
| name      | communityId
| "Alice"   | 3
| "Bridget" | 2
| "Charles" | 2
| "Doug"    | 3
| "Mark"    | 5
| "Michael" | 5
|===
--

Using the weighted relationships, we see that `Alice` and `Doug` have formed their own community, as their link is much stronger than all the others.


[[algorithms-louvain-examples-stream-seeded]]
=== Seeded

The Louvain algorithm can be run incrementally, by providing a seed property.
With the seed property an initial community mapping can be supplied for a subset of the loaded nodes.
The algorithm will try to keep the seeded community IDs.

[role=query-example]
--
.The following will run the algorithm and stream results:
[source, cypher, role=noplay]
----
CALL gds.louvain.stream('myGraph', { seedProperty: 'seed' })
YIELD nodeId, communityId, intermediateCommunityIds
RETURN gds.util.asNode(nodeId).name AS name, communityId
ORDER BY name ASC
----

.Results
[opts="header"]
|===
| name      | communityId
| "Alice"   | 42
| "Bridget" | 42
| "Charles" | 42
| "Doug"    | 47
| "Mark"    | 47
| "Michael" | 47
|===
--

Using the seeded graph, we see that the community around `Alice` keeps its initial community ID of `42`.
The other community is assigned a new community ID, which is guaranteed to be larger than the largest seeded community ID.
Note that the `consecutiveIds` configuration option cannot be used in combination with seeding in order to retain the seeding values.


=== Using intermediate communities

As described before, Louvain is a hierarchical clustering algorithm.
That means that after every clustering step all nodes that belong to the same cluster are reduced to a single node.
Relationships between nodes of the same cluster become self-relationships, relationships to nodes of other clusters connect to the clusters representative.
This condensed graph is then used to run the next level of clustering.
The process is repeated until the clusters are stable.

In order to demonstrate this iterative behavior, we need to construct a more complex graph.

image::louvain-multilevel-graph.svg[align="center"]

[source, cypher, role=noplay setup-query]
----
CREATE (a:Node {name: 'a'})
CREATE (b:Node {name: 'b'})
CREATE (c:Node {name: 'c'})
CREATE (d:Node {name: 'd'})
CREATE (e:Node {name: 'e'})
CREATE (f:Node {name: 'f'})
CREATE (g:Node {name: 'g'})
CREATE (h:Node {name: 'h'})
CREATE (i:Node {name: 'i'})
CREATE (j:Node {name: 'j'})
CREATE (k:Node {name: 'k'})
CREATE (l:Node {name: 'l'})
CREATE (m:Node {name: 'm'})
CREATE (n:Node {name: 'n'})
CREATE (x:Node {name: 'x'})

CREATE (a)-[:TYPE]->(b)
CREATE (a)-[:TYPE]->(d)
CREATE (a)-[:TYPE]->(f)
CREATE (b)-[:TYPE]->(d)
CREATE (b)-[:TYPE]->(x)
CREATE (b)-[:TYPE]->(g)
CREATE (b)-[:TYPE]->(e)
CREATE (c)-[:TYPE]->(x)
CREATE (c)-[:TYPE]->(f)
CREATE (d)-[:TYPE]->(k)
CREATE (e)-[:TYPE]->(x)
CREATE (e)-[:TYPE]->(f)
CREATE (e)-[:TYPE]->(h)
CREATE (f)-[:TYPE]->(g)
CREATE (g)-[:TYPE]->(h)
CREATE (h)-[:TYPE]->(i)
CREATE (h)-[:TYPE]->(j)
CREATE (i)-[:TYPE]->(k)
CREATE (j)-[:TYPE]->(k)
CREATE (j)-[:TYPE]->(m)
CREATE (j)-[:TYPE]->(n)
CREATE (k)-[:TYPE]->(m)
CREATE (k)-[:TYPE]->(l)
CREATE (l)-[:TYPE]->(n)
CREATE (m)-[:TYPE]->(n);
----

.The following statement will project the graph and store it in the graph catalog.
[source, cypher, role=noplay graph-project-query]
----
CALL gds.graph.project(
    'myGraph2',
    'Node',
    {
        TYPE: {
            orientation: 'undirected',
            aggregation: 'NONE'
        }
    }
)
----


[[algorithms-louvain-examples-stream-intermediate]]
==== Stream intermediate communities

[role=query-example]
--
.The following run the algorithm and stream results including the intermediate communities:
[source, cypher, role=noplay]
----
CALL gds.louvain.stream('myGraph2', { includeIntermediateCommunities: true })
YIELD nodeId, communityId, intermediateCommunityIds
RETURN gds.util.asNode(nodeId).name AS name, communityId, intermediateCommunityIds
ORDER BY name ASC
----

.Results
[opts="header"]
|===
| name  | communityId | intermediateCommunityIds
| "a"   | 14          | [3, 14]
| "b"   | 14          | [3, 14]
| "c"   | 14          | [14, 14]
| "d"   | 14          | [3, 14]
| "e"   | 14          | [14, 14]
| "f"   | 14          | [14, 14]
| "g"   | 7           | [7, 7]
| "h"   | 7           | [7, 7]
| "i"   | 7           | [7, 7]
| "j"   | 12          | [12, 12]
| "k"   | 12          | [12, 12]
| "l"   | 12          | [12, 12]
| "m"   | 12          | [12, 12]
| "n"   | 12          | [12, 12]
| "x"   | 14          | [14, 14]
|===
--

In this example graph, after the first iteration we see 4 clusters, which in the second iteration are reduced to three.


[[algorithms-louvain-examples-mutate-intermediate]]
==== Mutate intermediate communities

[role=query-example, group=mutateIntermediateCommunities]
--
.The following run the algorithm and mutate the in-memory graph:
[source, cypher, role=noplay]
----
CALL gds.louvain.mutate('myGraph2', {
  mutateProperty: 'intermediateCommunities',
  includeIntermediateCommunities: true
})
YIELD communityCount, modularity, modularities
----

.Results
[opts="header"]
|===
| communityCount | modularity         | modularities
| 3              | 0.3816             | [0.37599999999999995, 0.3816]
|===
--

[role=query-example, group=mutateIntermediateCommunities]
--
.The following stream the mutated property from the in-memory graph:
[source, cypher, role=noplay]
----
CALL gds.graph.nodeProperty.stream('myGraph2', 'intermediateCommunities')
YIELD nodeId, propertyValue
RETURN
  gds.util.asNode(nodeId).name AS name,
  toIntegerList(propertyValue) AS intermediateCommunities
ORDER BY name ASC
----

.Results
[opts="header"]
|===
| name  | intermediateCommunities
| "a"   | [3, 14]
| "b"   | [3, 14]
| "c"   | [14, 14]
| "d"   | [3, 14]
| "e"   | [14, 14]
| "f"   | [14, 14]
| "g"   | [7, 7]
| "h"   | [7, 7]
| "i"   | [7, 7]
| "j"   | [12, 12]
| "k"   | [12, 12]
| "l"   | [12, 12]
| "m"   | [12, 12]
| "n"   | [12, 12]
| "x"   | [14, 14]
|===
--


[[algorithms-louvain-examples-write-intermediate]]
==== Write intermediate communities

[role=query-example, group=writeIntermediateCommunities]
--
.The following run the algorithm and write to the Neo4j database:
[source, cypher, role=noplay]
----
CALL gds.louvain.write('myGraph2', {
  writeProperty: 'intermediateCommunities',
  includeIntermediateCommunities: true
})
YIELD communityCount, modularity, modularities
----

.Results
[opts="header"]
|===
| communityCount | modularity         | modularities
| 3              | 0.3816             | [0.37599999999999995, 0.3816]
|===
--

[role=query-example, group=writeIntermediateCommunities]
--
.The following stream the written property from the Neo4j database:
[source, cypher, role=noplay]
----
MATCH (n:Node) RETURN n.name AS name, toIntegerList(n.intermediateCommunities) AS intermediateCommunities
ORDER BY name ASC
----

.Results
[opts="header"]
|===
| name  | intermediateCommunities
| "a"   | [3, 14]
| "b"   | [3, 14]
| "c"   | [14, 14]
| "d"   | [3, 14]
| "e"   | [14, 14]
| "f"   | [14, 14]
| "g"   | [7, 7]
| "h"   | [7, 7]
| "i"   | [7, 7]
| "j"   | [12, 12]
| "k"   | [12, 12]
| "l"   | [12, 12]
| "m"   | [12, 12]
| "n"   | [12, 12]
| "x"   | [14, 14]
|===
--
