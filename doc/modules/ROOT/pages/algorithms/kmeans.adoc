:page-aliases: algorithms/alpha/kmeans.adoc
[[algorithms-k-means]]
[.beta]
= K-Means Clustering
:description: This section describes the K-Means algorithm in the Neo4j Graph Data Science library.
:entity: node
:result: cluster
:algorithm: K-Means

include::partial$/operations-reference/beta-note.adoc[]

:directed:
:undirected:
:homogeneous:
:weighted:
include::partial$/algorithms/shared/algorithm-traits.adoc[]


[[algorithms-k-means-intro]]
== Introduction

K-Means clustering is an unsupervised learning algorithm that is used to solve clustering problems.
It follows a simple procedure of classifying a given data set into a number of clusters, defined by the parameter `k`.
The Neo4j GDS Library conducts clustering based on node properties, with a float array node property being passed as input via the `nodeProperty` parameter.
Nodes in the graph are then positioned as points in a `d`-dimensional space (where `d` is the size of length of the array property).

The algorithm then begins by selecting `k` initial cluster centroids, which are `d`-dimensional arrays (see xref:algorithms-kmeans-sampling[section below] for more details).
The centroids act as representatives for a cluster.

Then, all nodes in the graph calculate their Euclidean distance from each of the cluster centroids and are assigned to the cluster of minimum distance from them.
 After these assignments, each cluster takes the mean of all nodes (as points) assigned to it to form its new representative centroid (as a `d`-dimensional array).

The process repeats with the new centroids until results stabilize, i.e., only a few nodes change clusters per iteration or the number of maximum iterations is reached.

Note that the K-Means implementation ignores relationships as it is only focused on node properties.

For more information on this algorithm, see:

* https://en.wikipedia.org/wiki/K-means_clustering

[[algorithms-kmeans-sampling]]
== Initial Centroid Sampling

The algorithm starts by picking `k` centroids by  randomly sampling from the set of available nodes.
There are two different sampling strategies.

Uniform::

With uniform sampling, each node has the same probability to be picked as one of the `k`  initial centroids.
This is the default sampler for K-Means denoted with the `uniform` parameter.

K-Means++::

This sampling strategy adapts the well-known
K-means\++  http://ilpubs.stanford.edu:8090/778/1/2006-13.pdf[initialization algorithm] for K-Means.
The sampling begins by choosing the  first centroid uniformly at random.
Then, the remaining `k-1`  centroids are picked one-by-one based on weighted random sampling.
That is, the probability a node is chosen as the next centroid is proportional to its minimum distance from the already picked centroids.
Nodes with larger distance hence have higher chance to be picked as a centroid.
This sampling strategy tries to spread the initial clusters more evenly so as to obtain a better final clustering. This option can be enabled by choosing `kmeans++` as the initial sampler in the configuration.


It is also possible to explicitly give the list of initial centroids to the algorithm via the `seedCentroids` parameter. In this case, the value of the `initialSampler` parameter is ignored, even if changed in the configuration.

[[algorithm-k-means-considerations]]
== Considerations

In order for K-Means to work properly, the property arrays for all nodes must have the same number of elements. Also, they should contain exclusively numbers and not contain any NaN values.

[[algorithms-k-means-syntax]]
== Syntax


.K-Means syntax per mode
[.tabbed-example, caption = ]
====

[.include-with-stream]
======
.Run K-Means in stream mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.beta.kmeans.stream(
  graphName: String,
  configuration: Map
)
YIELD
  nodeId: Integer,
  communityId: Integer,
  distanceFromCentroid: Float,
  silhouette: Float

----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-stream-stats-configuration-entries.adoc[]
include::partial$/algorithms/kmeans/specific-configuration.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                      | Type      | Description
| nodeId                    | Integer   | Node ID.
| communityId               | Integer   | The community ID.
| distanceFromCentroid      | Float     | Distance of the node from the centroid of its community.
| silhouette      | Float     | Silhouette score  of the node.
|===
======


[.include-with-stats]
======
.Run K-Means in stats mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.beta.kmeans.stats(
  graphName: String,
  configuration: Map
)
YIELD
  preProcessingMillis: Integer,
  computeMillis: Integer,
  postProcessingMillis: Integer,
  communityDistribution: Map,
  centroids: List of List of Float,
  averageDistanceToCentroid: Float,
  averageSilhouette: Float,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-stream-stats-configuration-entries.adoc[]
include::partial$/algorithms/kmeans/specific-configuration.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                  | Type      | Description
| preProcessingMillis   | Integer   | Milliseconds for preprocessing the data.
| computeMillis         | Integer   | Milliseconds for running the algorithm.
| postProcessingMillis  | Integer   | Milliseconds for computing percentiles and community count.
| communityDistribution | Map       | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of community size for the last level.
| centroids              | List of List of Float | List of centroid coordinates. Each item is a list containing the coordinates of one centroid.
| averageDistanceToCentroid | Float| Average distance between node and centroid.
| averageSilhouette | Float| Average silhouette score over all nodes.
| configuration         | Map       | The configuration used for running the algorithm.
|===
======

[.include-with-mutate]
======
.Run K-Means in mutate mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.beta.kmeans.mutate(
  graphName: String,
  configuration: Map
)
YIELD
  preProcessingMillis: Integer,
  computeMillis: Integer,
  mutateMillis: Integer,
  postProcessingMillis: Integer,
  nodePropertiesWritten: Integer,
  communityDistribution: Map,
  centroids: List of List of Float,
  averageDistanceToCentroid: Float,
  averageSilhouette: Float,
  configuration: Map

----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-mutate-configuration-entries.adoc[]
include::partial$/algorithms/kmeans/specific-configuration.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                   | Type      | Description
| preProcessingMillis    | Integer   | Milliseconds for preprocessing the data.
| computeMillis          | Integer   | Milliseconds for running the algorithm.
| mutateMillis           | Integer   | Milliseconds for adding properties to the projected graph.
| postProcessingMillis   | Integer   | Milliseconds for computing percentiles and community count.
| nodePropertiesWritten  | Integer   | Number of properties added to the projected graph.
| communityDistribution  | Map       | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of community size for the last level.
| centroids              | List of List of Float | List of centroid coordinates. Each item is a list containing the coordinates of one centroid.
| averageDistanceToCentroid | Float | Average distance between node and centroid.
| averageSilhouette | Float| Average silhouette score over all nodes.
| configuration          | Map       | The configuration used for running the algorithm.
|===
======


[.include-with-write]
======
.Run K-Means in write mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.beta.kmeans.write(
  graphName: String,
  configuration: Map
)
YIELD
  preProcessingMillis: Integer,
  computeMillis: Integer,
  writeMillis: Integer,
  postProcessingMillis: Integer,
  nodePropertiesWritten: Integer,
  communityDistribution: Map,
  centroids: List of List of Float,
  averageDistanceToCentroid: Float,
  averageSilhouette: Float,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-write-configuration-entries.adoc[]
include::partial$/algorithms/kmeans/specific-configuration.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                   | Type      | Description
| preProcessingMillis    | Integer   | Milliseconds for preprocessing the data.
| computeMillis          | Integer   | Milliseconds for running the algorithm.
| writeMillis            | Integer   | Milliseconds for adding properties to the Neo4j database.
| postProcessingMillis   | Integer   | Milliseconds for computing percentiles and community count.
| nodePropertiesWritten  | Integer   | Number of properties added to the projected graph.
| communityDistribution  | Map       | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of community size for the last level.
| centroids              | List of List of Float | List of centroid coordinates. Each item is a list containing the coordinates of one centroid.
| averageDistanceToCentroid | Float | Average distance between node and centroid.
| averageSilhouette | Float| Average silhouette score over all nodes.
| configuration          | Map       | The configuration used for running the algorithm.
|===
======

====


[[algorithms-k-means-examples]]
== Examples

:algorithm-name: K-Means
:graph-description: cities
:image-file: cities.svg
include::partial$/algorithms/shared/examples-intro.adoc[]

.The following Cypher statement will create the example graph in the Neo4j database:
[source,cypher,role=noplay setup-query]
----
CREATE
  (:City {name: 'Surbiton', coordinates: [51.39148, -0.29825]}),
  (:City {name: 'Liverpool', coordinates: [53.41058, -2.97794]}),
  (:City {name: 'Kingston upon Thames', coordinates: [51.41259, -0.2974]}),
  (:City {name: 'Sliven', coordinates: [42.68583, 26.32917]}),
  (:City {name: 'Solna', coordinates: [59.36004, 18.00086]}),
  (:City {name: 'Örkelljunga', coordinates: [56.28338, 13.27773]}),
  (:City {name: 'Malmö', coordinates: [55.60587, 13.00073]}),
  (:City {name: 'Xánthi', coordinates: [41.13488, 24.888]});
----

This graph is composed of various _City_ nodes, in three global locations - United Kingdom, Sweden and the Balkan region in Europe.

We can now project the graph and store it in the graph catalog.
We load the `City` node label with `coordinates` node property.

include::partial$/algorithms/shared/examples-named-native-note.adoc[]

.The following statement will project the graph and store it in the graph catalog.
[source,cypher,role=noplay graph-project-query]
----
CALL gds.graph.project(
    'cities',
    {
      City: {
        properties: 'coordinates'
      }
    },
    '*'
)
----

In the following examples we will demonstrate using the K-Means algorithm on this graph to find communities of cities that are close to each other geographically.


[[algorithms-k-means-examples-memory-estimation]]
=== Memory Estimation

:mode: write
include::partial$/algorithms/shared/examples-estimate-intro.adoc[]

[role=query-example]
--
.The following will estimate the memory requirements for running the algorithm:
[source, cypher, role=noplay]
----
CALL gds.beta.kmeans.write.estimate('cities', {
  writeProperty: 'kmeans',
  nodeProperty: 'coordinates'
})
YIELD nodeCount, bytesMin, bytesMax, requiredMemory
----

.Results
[opts="header",cols="1,1,1,1"]
|===
| nodeCount | bytesMin | bytesMax | requiredMemory
| 8         | 33272    | 54264   | "[32 KiB \... 52 KiB]"
|===
--


[[algorithms-k-means-examples-stream]]
=== Stream

include::partial$/algorithms/shared/examples-stream-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm and stream results:
[source,cypher,role=noplay]
----
CALL gds.beta.kmeans.stream('cities', {
  nodeProperty: 'coordinates',
  k: 3,
  randomSeed: 42
})
YIELD nodeId, communityId
RETURN gds.util.asNode(nodeId).name AS name, communityId
ORDER BY communityId, name ASC
----

.Results
[opts="header"]
|===
| name                  | communityId
|"Kingston upon Thames" | 0
|"Liverpool"            | 0
|"Surbiton"             | 0
|"Sliven"               | 1
|"Xánthi"               | 1
|"Malmö"                | 2
|"Solna"                | 2
|"Örkelljunga"          | 2
|===
--

In the example above we can see that the cities are geographically clustered together.


[[algorithms-k-means-examples-stats]]
=== Stats

:stats-syntax: algorithms-k-means-syntax
include::partial$/algorithms/shared/examples-stats-intro.adoc[]

[role=query-example, no-result=true]
--
.The following will run the algorithm and returns the result in form of statistical and measurement values
[source,cypher,role=noplay]
----
CALL gds.beta.kmeans.stats('cities', {
  nodeProperty: 'coordinates',
  k: 3,
  randomSeed: 42
})
YIELD communityDistribution
----

.Results
[opts="header",cols="1"]
|===
| communityDistribution
| { "p99": 3, "min": 2, "max": 3, "mean": 2.6666666666666665, "p90": 3, "p50": 3, "p999": 3, "p95": 3, "p75": 3 }
|===
--


[[algorithms-leiden-examples-mutate]]
=== Mutate

include::partial$/algorithms/shared/examples-mutate-intro.adoc[]

[role=query-example, no-result=true]
--
.The following will run the algorithm and store the results in `cities` graph:
[source,cypher,role=noplay]
----
CALL gds.beta.kmeans.mutate('cities', {
  nodeProperty: 'coordinates',
  k: 3,
  randomSeed: 42,
  mutateProperty: 'kmeans'
})
YIELD communityDistribution
----

.Results
[opts="header"]
|===
| communityDistribution
| { "p99": 3, "min": 2, "max": 3, "mean": 2.6666666666666665, "p90": 3, "p50": 3, "p999": 3, "p95": 3, "p75": 3 }
|===
--

In `mutate` mode, only a single row is returned by the procedure.
The result is written to the GDS in-memory graph instead of the Neo4j database.


[[algorithms-leiden-examples-write]]
=== Write

include::partial$/algorithms/shared/examples-write-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm and write the results back to Neo4j:
[source,cypher,role=noplay]
----
CALL gds.beta.kmeans.write('cities', {
  nodeProperty: 'coordinates',
  k: 3,
  randomSeed: 42,
  writeProperty: 'kmeans'
})
YIELD nodePropertiesWritten
----

.Results
[opts="header"]
|===
| nodePropertiesWritten
| 8
|===
--

In `write` mode, only a single row is returned by the procedure.
The result is written to the Neo4j database instead of the GDS in-memory graph.

[[algorithms-leiden-examples-centroids]]
=== Seeding initial centroids

We now see the effect that seeding centroids has on K-Means.
We run K-Means with initial seeds the coordinates of New York, Amsterdam, and Rome.
[role=query-example]
--
.The following will run the algorithm and stream results:
[source,cypher,role=noplay]
----
CALL gds.beta.kmeans.stream('cities', {
  nodeProperty: 'coordinates',
  k: 3,
  seedCentroids: [[40.712776,-74.005974], [52.370216,4.895168],[41.902782,12.496365]]
})
YIELD nodeId, communityId
RETURN gds.util.asNode(nodeId).name AS name, communityId
ORDER BY communityId, name ASC
----

.Results
[opts="header"]
|===
| name                  | communityId
|"Kingston upon Thames" | 1
|"Liverpool"            | 1
|"Malmö"                | 1
|"Solna"                | 1
|"Surbiton"             | 1
|"Örkelljunga"          | 1
|"Sliven"               | 2
|"Xánthi"               | 2

|===
--
Notice that in this case the cities have been geographically clustered into two clusters: one contains cities in Northern Europe whereas the other contains in Southern Europe.
On the other hand, the cluster with New York as the initial centroid was not the closest to any city at the first phase.
