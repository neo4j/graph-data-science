[[algorithms-leiden]]
[.beta]
= Leiden
:description: This section describes the Leiden algorithm in the Neo4j Graph Data Science library.
:entity: node
:result: community ID
:algorithm: Leiden

include::partial$/operations-reference/beta-note.adoc[]

:undirected:
:homogeneous:
:weighted:
include::partial$/algorithms/shared/algorithm-traits.adoc[]


[[algorithms-leiden-intro]]
== Introduction

The Leiden algorithm is an algorithm for detecting communities in large networks.
The algorithm separates nodes into disjoint communities so as to maximize a modularity score for each community.
Modularity quantifies the quality of an assignment of nodes to communities, that is how densely connected nodes in a community are, compared to how connected they would be in a random network.

The Leiden algorithm is a hierarchical clustering algorithm, that recursively merges communities into single nodes by greedily optimizing the modularity and the process repeats in the condensed graph.
It modifies the xref:algorithms/louvain.adoc[Louvain] algorithm to address some of its shortcomings, namely the case where some of the communities found by Louvain are not well-connected.
This is achieved by periodically randomly breaking down communities into smaller well-connected ones.

For more information on this algorithm, see:

* https://arxiv.org/pdf/1810.08473.pdf[V.A. Traag, L. Waltman and N.J. van Eck "From Louvain to Leiden: guaranteeing well-connected communities"^]

[NOTE]
====
Running this algorithm requires sufficient memory availability.
Before running this algorithm, we recommend that you read xref:common-usage/memory-estimation.adoc[Memory Estimation].
====

[[algorithms-leiden-syntax]]
== Syntax

include::partial$/algorithms/shared/syntax-intro-named-graph.adoc[]

.Leiden syntax per mode
[.tabbed-example,caption = ]
====

[.include-with-stream]
======
.Run Leiden in stream mode on a named graph.
[source,cypher,role=noplay]
----
CALL gds.beta.leiden.stream(
  graphName: String,
  configuration: Map
)
YIELD
  nodeId: Integer,
  communityId: Integer,
  intermediateCommunityIds: List of Integer
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-stream-stats-configuration-entries.adoc[]
include::partial$/algorithms/leiden/specific-configuration.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                      | Type      | Description
| nodeId                    | Integer   | Node ID.
| communityId               | Integer   | The community ID of the final level.
| intermediateCommunityIds  | List of Integer | Community IDs for each level. `Null` if `includeIntermediateCommunities` is set to false.
|===
======

[.include-with-stats]
======
.Run Leiden in stats mode on a named graph.
[source,cypher,role=noplay]
----
CALL gds.beta.leiden.stats(
  graphName: String,
  configuration: Map
)
YIELD
  preProcessingMillis: Integer,
  computeMillis: Integer,
  postProcessingMillis: Integer,
  communityCount: Integer,
  ranLevels: Integer,
  modularity: Float,
  modularities: List of Float,
  nodeCount: Integer,
  didConverge: Boolean,
  communityDistribution: Map,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-stream-stats-configuration-entries.adoc[]
include::partial$/algorithms/leiden/specific-configuration.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                  | Type      | Description
| preProcessingMillis   | Integer   | Milliseconds for preprocessing the data.
| computeMillis         | Integer   | Milliseconds for running the algorithm.
| postProcessingMillis  | Integer   | Milliseconds for computing percentiles and community count.
| communityCount        | Integer   | The number of communities found.
| ranLevels             | Integer   | The number of levels the algorithm actually ran.
| modularity            | Float     | The final modularity score.
| modularities          | List of Float | The modularity scores for each level.
| nodeCount             | Integer   | The number of nodes in the graph.
| didConverge           | Boolean   | Indicates if the algorithm converged.
| communityDistribution | Map       | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of community size for the last level.
| configuration         | Map       | The configuration used for running the algorithm.
|===
======

[.include-with-mutate]
======
.Run Leiden in mutate mode on a named graph.
[source,cypher,role=noplay]
----
CALL gds.beta.leiden.mutate(
  graphName: String,
  configuration: Map
)
YIELD
  preProcessingMillis: Integer,
  computeMillis: Integer,
  mutateMillis: Integer,
  postProcessingMillis: Integer,
  communityCount: Integer,
  ranLevels: Integer,
  modularity: Float,
  modularities: List of Float,
  nodeCount: Integer,
  didConverge: Integer,
  nodePropertiesWritten: Integer,
  communityDistribution: Map,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-mutate-configuration-entries.adoc[]
include::partial$/algorithms/leiden/specific-configuration.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                   | Type      | Description
| preProcessingMillis    | Integer   | Milliseconds for preprocessing the data.
| computeMillis          | Integer   | Milliseconds for running the algorithm.
| mutateMillis           | Integer   | Milliseconds for adding properties to the projected graph.
| postProcessingMillis   | Integer   | Milliseconds for computing percentiles and community count.
| communityCount         | Integer   | The number of communities found.
| ranLevels              | Integer   | The number of levels the algorithm actually ran.
| modularity            | Float     | The final modularity score.
| modularities          | List of Float | The modularity scores for each level.
| nodeCount              | Integer   | Number of nodes in the graph.
| didConverge            | Boolean   | Indicates if the algorithm converged.
| nodePropertiesWritten  | Integer   | Number of properties added to the projected graph.
| communityDistribution  | Map       | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of community size for the last level.
| configuration          | Map       | The configuration used for running the algorithm.
|===
======


[.include-with-write]
======
.Run Leiden in write mode on a named graph.
[source,cypher,role=noplay]
----
CALL gds.beta.leiden.write(
  graphName: String,
  configuration: Map
)
YIELD
  preProcessingMillis: Integer,
  computeMillis: Integer,
  writeMillis: Integer,
  postProcessingMillis: Integer,
  communityCount: Integer,
  ranLevels: Integer,
  modularity: Float,
  modularities: List of Float,
  nodeCount: Integer,
  didConverge: Integer,
  nodePropertiesWritten: Integer,
  communityDistribution: Map,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-write-configuration-entries.adoc[]
include::partial$/algorithms/leiden/specific-configuration.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                   | Type          | Description
| preProcessingMillis    | Integer       | Milliseconds for preprocessing the data.
| computeMillis          | Integer       | Milliseconds for running the algorithm.
| writeMillis            | Integer       | Milliseconds for adding properties to the projected graph.
| postProcessingMillis   | Integer       | Milliseconds for computing percentiles and community count.
| communityCount         | Integer       | The number of communities found.
| ranLevels              | Integer       | The number of levels the algorithm actually ran.
| modularity             | Float         | The final modularity score.
| modularities           | List of Float | The modularity scores for each level.
| nodeCount              | Integer       | Number of nodes in the graph.
| didConverge            | Boolean       | Indicates if the algorithm converged.
| nodePropertiesWritten  | Integer       | Number of properties added to the Neo4j database.
| communityDistribution  | Map           | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of community size for the last level.
| configuration          | Map           | The configuration used for running the algorithm.
|===
======
====

[[algorithms-leiden-examples]]
== Examples

:algorithm-name: Leiden community detection
:graph-description: social network
:image-file: two-user-communities.svg
include::partial$/algorithms/shared/examples-intro.adoc[]

.The following Cypher statement will create the example graph in the Neo4j database:
[source,cypher,role=noplay setup-query]
----
CREATE
  (nAlice:User {name: 'Alice', seed: 42}),
  (nBridget:User {name: 'Bridget', seed: 42}),
  (nCharles:User {name: 'Charles', seed: 42}),
  (nDoug:User {name: 'Doug'}),
  (nMark:User {name: 'Mark'}),
  (nMichael:User {name: 'Michael'}),

  (nAlice)-[:LINK {weight: 1}]->(nBridget),
  (nAlice)-[:LINK {weight: 1}]->(nCharles),
  (nCharles)-[:LINK {weight: 1}]->(nBridget),

  (nAlice)-[:LINK {weight: 5}]->(nDoug),

  (nMark)-[:LINK {weight: 1}]->(nDoug),
  (nMark)-[:LINK {weight: 1}]->(nMichael),
  (nMichael)-[:LINK {weight: 1}]->(nMark);
----

This graph has two clusters of _Users_, that are closely connected.
These clusters are connected by a single edge.
The relationship property `weight`  determines the strength of each respective relationship between nodes.

We can now project the graph and store it in the graph catalog.
We load the `LINK` relationships with orientation set to `UNDIRECTED` as this works best with the Leiden algorithm.

include::partial$/algorithms/shared/examples-named-native-note.adoc[]

.The following statement will project the graph and store it in the graph catalog.
[source,cypher,role=noplay graph-project-query]
----
CALL gds.graph.project(
    'myGraph',
    'User',
    {
        LINK: {
            orientation: 'UNDIRECTED'
        }
    },
    {
        nodeProperties: 'seed',
        relationshipProperties: 'weight'
    }
)
----

In the following examples we will demonstrate using the Leiden algorithm on this graph.


[[algorithms-leiden-examples-memory-estimation]]
=== Memory Estimation

:mode: write
include::partial$/algorithms/shared/examples-estimate-intro.adoc[]

[role=query-example]
--
.The following will estimate the memory requirements for running the algorithm in write mode:
[source, cypher, role=noplay]
----
CALL gds.beta.leiden.write.estimate('myGraph', {writeProperty: 'communityId', randomSeed: 19})
YIELD nodeCount, relationshipCount, bytesMin, bytesMax, requiredMemory
----

.Results
[opts="header"]
|===
| nodeCount | relationshipCount | bytesMin | bytesMax | requiredMemory
| 6         | 14                | 561240   | 561352      | "[548 KiB \... 548 KiB]"
|===
--


[[algorithms-leiden-examples-stream]]
=== Stream

include::partial$/algorithms/shared/examples-stream-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm and stream results:
[source,cypher,role=noplay]
----
CALL gds.beta.leiden.stream('myGraph', { randomSeed: 19 })
YIELD nodeId, communityId
RETURN gds.util.asNode(nodeId).name AS name, communityId
ORDER BY name ASC
----

.Results
[opts="header"]
|===
| name      | communityId
| "Alice"   | 2
| "Bridget" | 2
| "Charles" | 2
| "Doug"    | 5
| "Mark"    | 5
| "Michael" | 5
|===
--

We use default values for the procedure configuration parameter.
The `maxLevels` is set to 10, and the `gamma`, `theta` parameters are set to 1.0 and 0.01 respectively.

[[algorithms-leiden-examples-stats]]
=== Stats

:stats-syntax: algorithms-leiden-syntax
include::partial$/algorithms/shared/examples-stats-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm and returns the result in form of statistical and measurement values
[source,cypher,role=noplay]
----
CALL gds.beta.leiden.stats('myGraph', { randomSeed: 19 })
YIELD communityCount
----

.Results
[opts="header",cols="1"]
|===
| communityCount
| 2
|===
--

[[algorithms-leiden-examples-mutate]]
=== Mutate

include::partial$/algorithms/shared/examples-mutate-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm and store the results in `myGraph`:
[source,cypher,role=noplay]
----
CALL gds.beta.leiden.mutate('myGraph', { mutateProperty: 'communityId', randomSeed: 19 })
YIELD communityCount
----

.Results
[opts="header"]
|===
| communityCount
| 2
|===
--

In `mutate` mode, only a single row is returned by the procedure.
The result contains meta information, like the number of identified communities.
The result is written to the GDS in-memory graph instead of the Neo4j database.


[[algorithms-leiden-examples-write]]
=== Write

include::partial$/algorithms/shared/examples-write-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm and store the results in the Neo4j database:
[source,cypher,role=noplay]
----
CALL gds.beta.leiden.write('myGraph', { writeProperty: 'communityId', randomSeed: 19 })
YIELD communityCount, nodePropertiesWritten
----

.Results
[opts="header"]
|===
| communityCount | nodePropertiesWritten
| 2              | 6
|===
--

In `write` mode, only a single row is returned by the procedure.
The result contains meta information, like the number of identified communities.
The result is written to the Neo4j database instead of the GDS in-memory graph.


[[algorithms-leiden-examples-stream-weighted]]
=== Weighted

The Leiden algorithm can also run on weighted graphs, taking the given relationship weights into concern when calculating the modularity.

[role=query-example]
--
.The following will run the algorithm on a weighted graph and stream results:
[source,cypher,role=noplay]
----
CALL gds.beta.leiden.stream('myGraph', { relationshipWeightProperty: 'weight', randomSeed: 19 })
YIELD nodeId, communityId
RETURN gds.util.asNode(nodeId).name AS name, communityId
ORDER BY name ASC
----

.Results
[opts="header"]
|===
| name      | communityId
| "Alice"   | 3
| "Bridget" | 2
| "Charles" | 2
| "Doug"    | 3
| "Mark"    | 5
| "Michael" | 5
|===
--

Using the weighted relationships, we see that `Alice` and `Doug` have formed their own community, as their link is much stronger than all the others.

=== Using intermediate communities

As described before, Leiden is a hierarchical clustering algorithm.
That means that after every clustering step all nodes that belong to the same cluster are reduced to a single node.
Relationships between nodes of the same cluster become self-relationships, relationships to nodes of other clusters connect to the clusters representative.
This condensed graph is then used to run the next level of clustering.
The process is repeated until the clusters are stable.

In order to demonstrate this iterative behavior, we need to construct a more complex graph.

[source, cypher, role=noplay setup-query]
----
CREATE (a:Node {name: 'a'})
CREATE (b:Node {name: 'b'})
CREATE (c:Node {name: 'c'})
CREATE (d:Node {name: 'd'})
CREATE (e:Node {name: 'e'})
CREATE (f:Node {name: 'f'})
CREATE (g:Node {name: 'g'})
CREATE (h:Node {name: 'h'})
CREATE (i:Node {name: 'i'})
CREATE (j:Node {name: 'j'})
CREATE (k:Node {name: 'k'})
CREATE (l:Node {name: 'l'})
CREATE (m:Node {name: 'm'})
CREATE (n:Node {name: 'n'})
CREATE (x:Node {name: 'x'})

CREATE (a)-[:TYPE]->(b)
CREATE (a)-[:TYPE]->(d)
CREATE (a)-[:TYPE]->(f)
CREATE (b)-[:TYPE]->(d)
CREATE (b)-[:TYPE]->(x)
CREATE (b)-[:TYPE]->(g)
CREATE (b)-[:TYPE]->(e)
CREATE (c)-[:TYPE]->(x)
CREATE (c)-[:TYPE]->(f)
CREATE (d)-[:TYPE]->(k)
CREATE (e)-[:TYPE]->(x)
CREATE (e)-[:TYPE]->(f)
CREATE (e)-[:TYPE]->(h)
CREATE (f)-[:TYPE]->(g)
CREATE (g)-[:TYPE]->(h)
CREATE (h)-[:TYPE]->(i)
CREATE (h)-[:TYPE]->(j)
CREATE (i)-[:TYPE]->(k)
CREATE (j)-[:TYPE]->(k)
CREATE (j)-[:TYPE]->(m)
CREATE (j)-[:TYPE]->(n)
CREATE (k)-[:TYPE]->(m)
CREATE (k)-[:TYPE]->(l)
CREATE (l)-[:TYPE]->(n)
CREATE (m)-[:TYPE]->(n);
----

.The following statement will project the graph and store it in the graph catalog.
[source, cypher, role=noplay graph-project-query]
----
CALL gds.graph.project(
    'myGraph2',
    'Node',
    {
        TYPE: {
            orientation: 'undirected',
            aggregation: 'NONE'
        }
    }
)
----


[[algorithms-leiden-examples-stream-intermediate]]
==== Stream intermediate communities

[role=query-example]
--
.The following will run the algorithm and stream results including intermediate communities:
[source,cypher,role=noplay]
----
CALL gds.beta.leiden.stream('myGraph2', {
  randomSeed: 19,
  includeIntermediateCommunities: true,
  concurrency: 1
})
YIELD nodeId, communityId, intermediateCommunityIds
RETURN gds.util.asNode(nodeId).name AS name, communityId, intermediateCommunityIds
ORDER BY name ASC
----

.Results
[opts="header"]
|===
| name  | communityId | intermediateCommunityIds
| "a"   | 3           | [3, 3]
| "b"   | 3           | [3, 3]
| "c"   | 3           | [14, 3]
| "d"   | 3           | [3, 3]
| "e"   | 3           | [14, 3]
| "f"   | 3           | [14, 3]
| "g"   | 2           | [8, 2]
| "h"   | 2           | [8, 2]
| "i"   | 2           | [8, 2]
| "j"   | 0           | [12, 0]
| "k"   | 0           | [12, 0]
| "l"   | 0           | [12, 0]
| "m"   | 0           | [12, 0]
| "n"   | 0           | [12, 0]
| "x"   | 3           | [14, 3]
|===
--

[[algorithms-leiden-examples-stream-seeded]]
=== Seeded

It is possible to run the  Louvain algorithm incrementally, by providing a seed property.
If specified, the seed property provides an initial community mapping for a subset of the loaded nodes.
The algorithm will try to keep the seeded community IDs.

[role=query-example]
--
.The following will run the algorithm and stream results for a seeded graph:
[source, cypher, role=noplay]
----
CALL gds.beta.leiden.stream('myGraph', { seedProperty: 'seed' })
YIELD nodeId, communityId, intermediateCommunityIds
RETURN gds.util.asNode(nodeId).name AS name, communityId, intermediateCommunityIds
ORDER BY name ASC
----

.Results
[opts="header"]
|===
| name      | communityId | intermediateCommunityIds
| "Alice"   | 42          | null
| "Bridget" | 42          | null
| "Charles" | 42          | null
| "Doug"    | 45          | null
| "Mark"    | 45          | null
| "Michael" | 45          | null
|===
--

As can be seen, using the seeded graph, node `Alice` keeps its initial community ID of `42`.
The other community has been assigned a new community ID which is guaranteed to be larger than the largest seeded community ID.
Note that the `consecutiveIds` configuration option cannot be used in combination with seeding in order to retain the seeding values


[[algorithms-leiden-examples-mutate-intermediate]]
==== Mutate intermediate communities

[role=query-example, group=mutateIntermediateCommunities]
--
.The following will run the algorithm and mutate the in-memory-graph using the intermediate communities:
[source,cypher,role=noplay]
----
CALL gds.beta.leiden.mutate('myGraph2', {
  mutateProperty: 'intermediateCommunities',
  randomSeed: 19,
  includeIntermediateCommunities: true,
  concurrency: 1
})
YIELD communityCount, modularity, modularities
----

.Results
[opts="header"]
|===
| communityCount  | modularity | modularities
| 3               | 0.3816     | [0.37599999999999995, 0.3816]
|===
--

[role=query-example, group=mutateIntermediateCommunities]
--
.The following stream the mutated property from the in-memory graph:
[source, cypher, role=noplay]
----
CALL gds.graph.nodeProperty.stream('myGraph2', 'intermediateCommunities')
YIELD nodeId, propertyValue
RETURN
  gds.util.asNode(nodeId).name AS name,
  toIntegerList(propertyValue) AS intermediateCommunities
ORDER BY name ASC
----

.Results
[opts="header"]
|===
| name  | intermediateCommunities
| "a"   | [3, 3]
| "b"   | [3, 3]
| "c"   | [14, 3]
| "d"   | [3, 3]
| "e"   | [14, 3]
| "f"   | [14, 3]
| "g"   | [8, 2]
| "h"   | [8, 2]
| "i"   | [8, 2]
| "j"   | [12, 0]
| "k"   | [12, 0]
| "l"   | [12, 0]
| "m"   | [12, 0]
| "n"   | [12, 0]
| "x"   | [14, 3]
|===
--


[[algorithms-leiden-examples-write-intermediate]]
==== Write intermediate communities

[role=query-example, group=writeIntermediateCommunities]
--
.The following will run the algorithm and write the intermediate communities to the Neo4j database:
[source,cypher,role=noplay]
----
CALL gds.beta.leiden.write('myGraph2', {
  writeProperty: 'intermediateCommunities',
  randomSeed: 19,
  includeIntermediateCommunities: true,
  concurrency: 1
})
YIELD communityCount, modularity, modularities
----

.Results
[opts="header"]
|===
| communityCount  | modularity | modularities
| 3               | 0.3816     | [0.37599999999999995, 0.3816]
|===
--

[role=query-example, group=writeIntermediateCommunities]
--
.The following stream the written property from the Neo4j database:
[source, cypher, role=noplay]
----
MATCH (n:Node) RETURN n.name AS name, toIntegerList(n.intermediateCommunities) AS intermediateCommunities
ORDER BY name ASC
----

.Results
[opts="header"]
|===
| name  | intermediateCommunities
| "a"   | [3, 3]
| "b"   | [3, 3]
| "c"   | [14, 3]
| "d"   | [3, 3]
| "e"   | [14, 3]
| "f"   | [14, 3]
| "g"   | [8, 2]
| "h"   | [8, 2]
| "i"   | [8, 2]
| "j"   | [12, 0]
| "k"   | [12, 0]
| "l"   | [12, 0]
| "m"   | [12, 0]
| "n"   | [12, 0]
| "x"   | [14, 3]
|===
--
