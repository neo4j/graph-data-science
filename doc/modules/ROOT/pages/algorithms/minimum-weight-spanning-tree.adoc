:page-aliases: alpha-algorithms/minimum-weight-spanning-tree.adoc
[[algorithms-minimum-weight-spanning-tree]]
[.beta]
= Minimum Weight Spanning Tree
:description: This section describes the Minimum Weight Spanning Tree algorithm in the Neo4j Graph Data Science library.
:entity: relationship
:result: weight
:algorithm: Prim
:sequential: true

include::partial$/operations-reference/beta-note.adoc[]

:no-directed:
:undirected:
:homogeneous:
:weighted:
include::partial$/algorithms/shared/algorithm-traits.adoc[]

== Introduction

The Minimum Weight Spanning Tree (MST) starts from a given node, finds all its reachable nodes and returns the set of relationships that connect these nodes together having the minimum possible weight.
Prim's algorithm is one of the simplest and best-known minimum spanning tree algorithms.
It operates similarly to xref:algorithms/dijkstra-single-source.adoc[Dijkstra's shortest path algorithm], but instead of minimizing the total length of a path ending at each relationship, it minimizes the length of each relationship individually.
This allows the algorithm to work on graphs with negative weights.

For more information on this algorithm, see:

* https://en.wikipedia.org/wiki/Minimum_spanning_tree[Minimum Spanning Tree]


[[algorithms-minimum-weight-spanning-tree-usecase]]
=== Use cases

* Minimum spanning trees were used to analyze airline and sea connections of Papua New Guinea, and minimize the travel cost for exploring the country.
For example, they were used to help design low-cost tours that visit many destinations across the country. See  https://junctiv.com/wp-content/uploads/2020/07/2010-V12-1_Fitina_et_al_spanning_trees_for_travel_planning.pdf["An Application of Minimum Spanning Trees to Travel Planning"].
* Minimum spanning trees have been used to analyze and visualize correlations in a network of currencies, based on the correlation between currency returns.
  This is described in https://www.nbs.sk/_img/Documents/_PUBLIK_NBS_FSR/Biatec/Rok2013/07-2013/05_biatec13-7_resovsky_EN.pdf["Minimum Spanning Tree Application in the Currency Market"].
* Minimum spanning trees have also proven to be a useful tool for tracing  transmission of infections  in outbreaks. See https://www.ncbi.nlm.nih.gov/pmc/articles/PMC516344/[Use of the Minimum Spanning Tree Model for Molecular Epidemiological Investigation of a Nosocomial Outbreak of Hepatitis C Virus Infection].


[[algorithms-minimum-weight-spanning-tree-limitations]]
=== Considerations

The MST algorithm provides meaningful results only when run on a graph where relationships have different weights.
If the graph has no weights (or all relationships have the same weight), then any spanning tree is also a minimum spanning tree.
 The algorithm implementation is executed using a single thread. Altering the concurrency configuration has no effect.

[[algorithms-minimum-weight-spanning-tree-syntax]]
== Syntax

include::partial$/algorithms/shared/syntax-intro-named-graph.adoc[]

.Spanning Tree syntax per mode
[.tabbed-example, caption = ]
====

[.include-with-stream]
======
.Run the algorithm in stream mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.beta.spanningTree.stream(
  graphName: String,
  configuration: Map
)
YIELD
      nodeId: Integer,
      parentId: Integer,
      weight: Float
----
include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name                                                                             | Type    | Default   | Optional  | Description
include::partial$/algorithms/common-configuration/common-stream-stats-configuration-entries.adoc[]
include::partial$/algorithms/spanning-tree/specific-configuration.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                | Type    | Description
| nodeId  | Integer | a node in the discovered spanning tree
| parentId  | Integer | the parent of nodeId in the spanning tree or nodeId if it is equal to the source node.
| weight  | Float | The weight of the relationship from parentId to nodeId.

|===
======

[.include-with-stats]
======
.Run the algorithm in stats mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.beta.spanningTree.stats(
  graphName: String,
  configuration: Map
)
YIELD
      effectiveNodeCount: Integer,
      totalWeight: Float,
      preProcessingMillis: Integer,
      computeMillis: Integer,
      configuration: Map
----
include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name                                                                             | Type    | Default   | Optional  | Description
include::partial$/algorithms/common-configuration/common-stream-stats-configuration-entries.adoc[]
include::partial$/algorithms/spanning-tree/specific-configuration.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                | Type    | Description
| effectiveNodeCount  | Integer | The number of visited nodes.
| totalWeight         | Float   | The sum of the weights of the relationships in the spanning tree.
| preProcessingMillis | Integer | Milliseconds for preprocessing the data.
| computeMillis       | Integer | Milliseconds for running the algorithm.
| configuration         | Map       | The configuration used for running the algorithm.
|===
======


[.include-with-write]
======
.Run the Spanning Tree algorithm in write mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.beta.spanningTree.write(
  graphName: String,
  configuration: Map
)
YIELD
      effectiveNodeCount: Integer,
      totalWeight: Float,
      relationshipsWritten: Integer,
      preProcessingMillis: Integer,
      computeMillis: Integer,
      writeMillis: Integer,
      configuration: Map
----
include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name                                                                             | Type    | Default   | Optional  | Description
include::partial$/algorithms/common-configuration/common-write-configuration-entries.adoc[]
include::partial$/algorithms/spanning-tree/specific-configuration.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                | Type    | Description
| effectiveNodeCount  | Integer | The number of visited nodes.
| totalWeight         | Float   | The sum of the weights of the relationships in the spanning tree.
| relationshipsWritten |Integer | The number of relationships written to the graph.
| preProcessingMillis | Integer | Milliseconds for preprocessing the data.
| computeMillis       | Integer | Milliseconds for running the algorithm.
| writeMillis         | Integer | Milliseconds for writing result data back.
| configuration         | Map       | The configuration used for running the algorithm.
|===
======

[.include-with-mutate]
======
.Run the Spanning Tree algorithm in write mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.beta.spanningTree.mutate(
  graphName: String,
  configuration: Map
)
YIELD
      effectiveNodeCount: Integer,
      totalWeight: Float,
      relationshipsWritten: Integer,
      preProcessingMillis: Integer,
      computeMillis: Integer,
      mutateMillis: Integer,
      configuration: Map
----
include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name                                                                             | Type    | Default   | Optional  | Description
include::partial$/algorithms/common-configuration/common-mutate-configuration-entries.adoc[]
include::partial$/algorithms/spanning-tree/specific-configuration.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                | Type    | Description
| effectiveNodeCount  | Integer | The number of visited nodes.
| totalWeight         | Float   | The sum of the weights of the relationships in the spanning tree.
| relationshipsWritten |Integer | The number of relationships added to the in-memory graph.
| preProcessingMillis | Integer | Milliseconds for preprocessing the data.
| computeMillis       | Integer | Milliseconds for running the algorithm.
| mutateMillis         | Integer | Milliseconds for writing result data back.
| configuration         | Map       | The configuration used for running the algorithm.
|===
======
====
[[algorithms-minimum-weight-spanning-tree-sample]]
== Examples

:algorithm-name: {algorithm}
:graph-description: road network
:image-file: spanning-tree-graph.svg
include::partial$/algorithms/shared/examples-intro.adoc[]


.The following will create the sample graph depicted in the figure:
[source, cypher, role=noplay setup-query]
----
CREATE (a:Place {id: 'A'}),
       (b:Place {id: 'B'}),
       (c:Place {id: 'C'}),
       (d:Place {id: 'D'}),
       (e:Place {id: 'E'}),
       (f:Place {id: 'F'}),
       (g:Place {id: 'G'}),
       (d)-[:LINK {cost:4}]->(b),
       (d)-[:LINK {cost:6}]->(e),
       (b)-[:LINK {cost:1}]->(a),
       (b)-[:LINK {cost:3}]->(c),
       (a)-[:LINK {cost:2}]->(c),
       (c)-[:LINK {cost:5}]->(e),
       (f)-[:LINK {cost:1}]->(g);
----

.The following will project and store a named graph:
[source, cypher, role=noplay graph-project-query]
----
CALL gds.graph.project(
  'graph',
  'Place',
  {
    LINK: {
      properties: 'cost',
      orientation: 'UNDIRECTED'
    }
  }
)
----
[[algorithms-spanning-tree-examples-memory-estimation]]
=== Memory Estimation

:mode: stats
include::partial$/algorithms/shared/examples-estimate-intro.adoc[]

[role=query-example]
--
.The following will estimate the memory requirements for running the algorithm in stats mode:
[source, cypher, role=noplay]
----
MATCH (n:Place {id: 'D'})
CALL gds.beta.spanningTree.stats.estimate('graph', {sourceNode: id(n),relationshipWeightProperty:'cost'})
YIELD nodeCount, relationshipCount, bytesMin, bytesMax, requiredMemory
RETURN nodeCount, relationshipCount, bytesMin, bytesMax, requiredMemory
----

.Results
[opts="header"]
|===
| nodeCount | relationshipCount | bytesMin | bytesMax | requiredMemory
| 7         | 14               | 616   | 616      | "616 Bytes"
|===
--
[[algorithms-spanning-tree-examples-sream]]
=== Stream
:!stream-details:
include::partial$/algorithms/shared/examples-stream-intro.adoc[]

[role=query-example]
--
.The following will run the Minimum Weight Spanning Tree algorithm in stream mode and return results for each valid node.
[source,cypher,role=noplay]
----
MATCH (n:Place{id: 'D'})
CALL gds.beta.spanningTree.stream('graph', {
  sourceNode: id(n),
  relationshipWeightProperty: 'cost'
})
YIELD nodeId,parentId, weight
RETURN gds.util.asNode(nodeId).id AS node, gds.util.asNode(parentId).id AS parent,weight
ORDER BY node
----

.Results
[opts="header"]
|===
| node  |parent | weight
| "A"  | "B" | 1
| "B"  | "D" | 4
| "C"  | "A" | 2
| "D"  | "D" | 0
| "E"  | "C" | 5

|===
--

[[algorithms-spanning-tree-examples-stats]]
=== Stats
:stats-syntax: algorithms-minimum-weight-spanning-tree-syntax
include::partial$/algorithms/shared/examples-stats-intro.adoc[]

[role=query-example]
--
.The following will run the Minimum Weight Spanning Tree algorithm and return its statistics.
[source,cypher,role=noplay]
----
MATCH (n:Place{id: 'D'})
CALL gds.beta.spanningTree.stats('graph', {
  sourceNode: id(n),
  relationshipWeightProperty: 'cost'
})
YIELD effectiveNodeCount, totalWeight
RETURN effectiveNodeCount, totalWeight
----

.Results
[opts="header"]
|===
| effectiveNodeCount | totalWeight
| 5  | 12
|===
--


[[algorithms-spanning-tree-examples-write]]
=== Write


include::partial$/algorithms/shared/examples-write-intro.adoc[]

[role=query-example, no-result=true,group=spanning-tree-write]
--
.The following will run the Minimum Weight Spanning Tree algorithm and write the result back to the graph.
[source, cypher, role=noplay]
----
MATCH (n:Place {id: 'D'})
CALL gds.beta.spanningTree.write('graph', {
  sourceNode: id(n),
  relationshipWeightProperty: 'cost',
  writeProperty: 'writeCost',
  writeRelationshipType: 'MINST'
})
YIELD preProcessingMillis, computeMillis, writeMillis, effectiveNodeCount
RETURN preProcessingMillis, computeMillis, writeMillis, effectiveNodeCount;
----
--

[role=query-example, group=spanning-tree-write]
--
.To find the relationships included in the minimum spanning tree, we can run the following query:
[source, cypher, role=noplay]
----
MATCH path = (n:Place {id: 'D'})-[:MINST*]-()
WITH relationships(path) AS rels
UNWIND rels AS rel
WITH DISTINCT rel AS rel
RETURN startNode(rel).id AS Source, endNode(rel).id AS Destination, rel.writeCost AS Cost
----
.Results
[opts="header",cols="1,1,1"]
|===
| Source | Destination | Cost
| "D"      | "B"           | 4
| "B"      | "A"           | 1
| "A"      | "C"           | 2
| "C"      | "E"           | 5
|===
--


The minimum spanning tree excludes the relationship with cost 6 from D to E, and the one with cost 3 from B to C.
Nodes F and G are not included because they're unreachable from D.

[NOTE]
====
The relationships written back to the graph are always directed, even if the input graph is undirected.
====

[[algorithms-spanning-tree-examples-mutate]]
=== Mutate

include::partial$/algorithms/shared/examples-mutate-intro.adoc[]

[role=query-example]
--
.The following will run the Minimum Weight Spanning Tree algorithm and mutate the in-memory graph.
[source, cypher, role=noplay]
----
MATCH (n:Place {id: 'D'})
CALL gds.beta.spanningTree.mutate('graph', {
  sourceNode: id(n),
  relationshipWeightProperty: 'cost',
  mutateProperty: 'writeCost',
  mutateRelationshipType: 'MINST'
})
YIELD relationshipsWritten
RETURN relationshipsWritten
----
.Results
[opts="header"]
|===
| relationshipsWritten
| 4
|===
--
[NOTE]
====
The relationships added back to the graph are always directed, even if the input graph is undirected.
====


[[algorithms-spanning-tree-examples-max]]
=== Maximum spanning Tree

The maximum weighted tree spanning algorithm is similar to the minimum one, except that it returns a spanning tree of all nodes in the component where the total weight of the relationships is maximized.

[role=query-example]
--
.The following will run the Maximum Weight Spanning tree algorithm and return its statistics.
[source, cypher, role=noplay]
----
MATCH (n:Place{id: 'D'})
CALL gds.beta.spanningTree.stats('graph', {
  sourceNode: id(n),
  relationshipWeightProperty: 'cost',
  objective: 'maximum'
})
YIELD totalWeight
RETURN totalWeight
----
.Results
[opts="header"]
|===
| totalWeight
| 17
|===
--

As can be seen, the maximum weighted spanning tree returns a different tree having a larger sum of relationship weights.
