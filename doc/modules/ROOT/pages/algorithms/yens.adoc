[[algorithms-yens]]
= Yen's algorithm Shortest Path
:description: This section describes the Yen's Shortest Path algorithm in the Neo4j Graph Data Science library.
:entity: source-target-pair
:result: shortest path
:algorithm: Yen's
:source-target: true
:procedure-name: pass:q[gds.shortestPath.yens]

:directed:
:undirected:
:homogeneous:
:weighted:
include::partial$/algorithms/shared/algorithm-traits.adoc[]


[[algorithms-yens-intro]]
== Introduction

Yen's Shortest Path algorithm computes a number of shortest paths between two nodes.
The algorithm is often referred to as Yen's k-Shortest Path algorithm, where k is the number of shortest paths to compute.
The algorithm supports weighted graphs with positive relationship weights.
It also respects parallel relationships between the same two nodes when computing multiple shortest paths.

For `k = 1`, the algorithm behaves exactly like xref:algorithms/dijkstra-source-target.adoc[Dijkstra's shortest path algorithm] and returns the shortest path.
For `k = 2`, the algorithm returns the shortest path and the second shortest path between the same source and target node.
Generally, for `k = n`, the algorithm computes at most `n` paths which are discovered in the order of their total cost.

The GDS implementation is based on the https://www.ams.org/journals/qam/1970-27-04/S0033-569X-1970-0253822-7/[original description].
For the actual path computation, Yen's algorithm uses xref:algorithms/dijkstra-source-target.adoc[Dijkstra's shortest path algorithm].
The algorithm makes sure that an already discovered shortest path will not be traversed again.

The algorithm implementation is executed using a single thread.
Altering the concurrency configuration has no effect.

[[algorithms-yens-syntax]]
== Syntax

include::partial$/algorithms/shortest-path/path-syntax.adoc[]


[[algorithms-yens-examples]]
== Examples

:algorithm-name: {algorithm}
:graph-description: transport network
:image-file: dijkstra.svg
include::partial$/algorithms/shared/examples-intro.adoc[]

.The following Cypher statement will create the example graph in the Neo4j database:
[source, cypher, role=noplay setup-query]
----
CREATE (a:Location {name: 'A'}),
       (b:Location {name: 'B'}),
       (c:Location {name: 'C'}),
       (d:Location {name: 'D'}),
       (e:Location {name: 'E'}),
       (f:Location {name: 'F'}),
       (a)-[:ROAD {cost: 50}]->(b),
       (a)-[:ROAD {cost: 50}]->(c),
       (a)-[:ROAD {cost: 100}]->(d),
       (b)-[:ROAD {cost: 40}]->(d),
       (c)-[:ROAD {cost: 40}]->(d),
       (c)-[:ROAD {cost: 80}]->(e),
       (d)-[:ROAD {cost: 30}]->(e),
       (d)-[:ROAD {cost: 80}]->(f),
       (e)-[:ROAD {cost: 40}]->(f);
----

This graph builds a transportation network with roads between locations.
Like in the real world, the roads in the graph have different lengths.
These lengths are represented by the `cost` relationship property.

include::partial$/algorithms/shared/examples-named-native-note.adoc[]

.The following statement will project a graph using a native projection and store it in the graph catalog under the name 'myGraph'.
[source, cypher, role=noplay graph-project-query]
----
CALL gds.graph.project(
    'myGraph',
    'Location',
    'ROAD',
    {
        relationshipProperties: 'cost'
    }
)
----

In the following example we will demonstrate the use of the {algorithm} Shortest Path algorithm using this graph.


[[algorithms-yens-examples-memory-estimation]]
=== Memory Estimation

:mode: write
include::partial$/algorithms/shared/examples-estimate-intro.adoc[]

[role=query-example]
--
.The following will estimate the memory requirements for running the algorithm in write mode:
[source, cypher, role=noplay]
----
MATCH (source:Location {name: 'A'}), (target:Location {name: 'F'})
CALL gds.shortestPath.yens.write.estimate('myGraph', {
    sourceNode: source,
    targetNode: target,
    k: 3,
    relationshipWeightProperty: 'cost',
    writeRelationshipType: 'PATH'
})
YIELD nodeCount, relationshipCount, bytesMin, bytesMax, requiredMemory
RETURN nodeCount, relationshipCount, bytesMin, bytesMax, requiredMemory
----

.Results
[opts="header"]
|===
| nodeCount | relationshipCount | bytesMin | bytesMax | requiredMemory
| 6         | 9                 | 4736      | 4736      | "4736 Bytes"
|===
--


[[algorithms-yens-examples-stream]]
=== Stream

:!stream-details:
include::partial$/algorithms/shared/examples-stream-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm and stream results:
[source, cypher, role=noplay]
----
MATCH (source:Location {name: 'A'}), (target:Location {name: 'F'})
CALL gds.shortestPath.yens.stream('myGraph', {
    sourceNode: source,
    targetNode: target,
    k: 3,
    relationshipWeightProperty: 'cost'
})
YIELD index, sourceNode, targetNode, totalCost, nodeIds, costs, path
RETURN
    index,
    gds.util.asNode(sourceNode).name AS sourceNodeName,
    gds.util.asNode(targetNode).name AS targetNodeName,
    totalCost,
    [nodeId IN nodeIds | gds.util.asNode(nodeId).name] AS nodeNames,
    costs,
    nodes(path) as path
ORDER BY index
----

.Results
[opts="header"]
|===
| index | sourceNodeName | targetNodeName | totalCost | nodeNames         | costs                           | path
| 0     | "A"            | "F"            | 160.0     | [A, B, D, E, F]   | [0.0, 50.0, 90.0, 120.0, 160.0] | [Node[0], Node[1], Node[3], Node[4], Node[5]]
| 1     | "A"            | "F"            | 160.0     | [A, C, D, E, F]   | [0.0, 50.0, 90.0, 120.0, 160.0] | [Node[0], Node[2], Node[3], Node[4], Node[5]]
| 2     | "A"            | "F"            | 170.0     | [A, B, D, F]      | [0.0, 50.0, 90.0, 170.0]        | [Node[0], Node[1], Node[3], Node[5]]
|===
--

The result shows the three shortest paths between node `A` and node `F`.
The first two paths have the same total cost, however the first one traversed from `A` to `D` via the `B` node, while the second traversed via the `C` node.
The third path has a higher total cost as it goes directly from `D` to `F` using the relationship with a cost of `80`, whereas the detour via `E` for the first two paths costs `70`.
This can be verified in the xref:algorithms/yens.adoc#algorithms-yens-examples[example graph].
Cypher Path objects can be returned by the `path` return field.
The Path objects contain the node objects and virtual relationships which have a `cost` property.


[[algorithms-yens-examples-mutate]]
=== Mutate

include::partial$/algorithms/shortest-path/path-examples-mutate-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `mutate` mode:
[source, cypher, role=noplay]
----
MATCH (source:Location {name: 'A'}), (target:Location {name: 'F'})
CALL gds.shortestPath.yens.mutate('myGraph', {
    sourceNode: source,
    targetNode: target,
    k: 3,
    relationshipWeightProperty: 'cost',
    mutateRelationshipType: 'PATH'
})
YIELD relationshipsWritten
RETURN relationshipsWritten
----

.Results
[opts="header"]
|===
| relationshipsWritten
| 3
|===
--

After executing the above query, the projected graph will be updated with a new relationship of type `PATH`.
The new relationship will store a single property `totalCost`.

[NOTE]
====
The relationships produced are always directed, even if the input graph is undirected.
====


[[algorithms-yens-examples-write]]
=== Write

include::partial$/algorithms/shortest-path/path-examples-write-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `write` mode:
[source, cypher, role=noplay]
----
MATCH (source:Location {name: 'A'}), (target:Location {name: 'F'})
CALL gds.shortestPath.yens.write('myGraph', {
    sourceNode: source,
    targetNode: target,
    k: 3,
    relationshipWeightProperty: 'cost',
    writeRelationshipType: 'PATH',
    writeNodeIds: true,
    writeCosts: true
})
YIELD relationshipsWritten
RETURN relationshipsWritten
----

.Results
[opts="header"]
|===
| relationshipsWritten
| 3
|===
--

The above query will write a single relationship of type `PATH` back to Neo4j.
The relationship stores three properties describing the path: `totalCost`, `nodeIds` and `costs`.

[NOTE]
====
The relationships written are always directed, even if the input graph is undirected.
====
