[[algorithms-strongly-connected-components]]
[.alpha]
= Strongly Connected Components
:description: This section describes the Strongly Connected Components algorithm in the Neo4j Graph Data Science library.
:result: component
:entity: node

The Strongly Connected Components (SCC) algorithm finds maximal sets of connected nodes in a directed graph.
A set is considered a strongly connected component if there is a directed path between each pair of nodes within the set.
It is often used early in a graph analysis process to help us get an idea of how our graph is structured.


include::partial$/operations-reference/alpha-note.adoc[]


:directed:
:undirected:
include::partial$/algorithms/shared/algorithm-traits.adoc[]


[[algorithms-strongly-connected-components-context]]
== History and explanation

// tag::explanation[]
SCC is one of the earliest graph algorithms, and the first linear-time algorithm was described by Tarjan in 1972.
Decomposing a directed graph into its strongly connected components is a classic application of the depth-first search algorithm.
// end::explanation[]


[[algorithms-strongly-connected-components-usecase]]
== Use-cases - when to use the Strongly Connected Components algorithm

// tag::use-case[]
* In the analysis of powerful transnational corporations, SCC can be used to find the set of firms in which every member owns directly and/or indirectly owns shares in every other member.
  Although it has benefits, such as reducing transaction costs and increasing trust, this type of structure can weaken market competition.
  Read more in http://journals.plos.org/plosone/article/file?id=10.1371/journal.pone.0025995&type=printable["The Network of Global Corporate Control"^].
* SCC can be used to compute the connectivity of different network configurations when measuring routing performance in multihop wireless networks.
  Read more in https://dl.acm.org/citation.cfm?id=513803["Routing performance in the presence of unidirectional links in multihop wireless networks"^]
* Strongly Connected Components algorithms can be used as a first step in many graph algorithms that work only on strongly connected graph.
  In social networks, a group of people are generally strongly connected (For example, students of a class or any other common place).
  Many people in these groups generally like some common pages, or play common games.
  The SCC algorithms can be used to find such groups, and suggest the commonly liked pages or games to the people in the group who have not yet liked those pages or games.
// end::use-case[]


[[algorithms-strongly-connected-components-syntax]]
== Syntax
Decomposition syntax per mode
[.tabbed-example, caption = ]
====
[.include-with-stream]
======
.The following will run the algorithm and stream results:
[source, cypher, role=noplay]
----
CALL gds.scc.stream(graphName: String, configuration: Map)
YIELD  nodeId,
       componentId
----
include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-stream-stats-configuration-entries.adoc[]
|===

.Results
[opts="header"]
|===
| Name        | Type    | Description
| nodeId      | Integer | Node ID.
| componentId | Integer | Component ID.
|===
======
[.include-with-stats]
======
.The following will run the algorithm in stats mode
[source, cypher, role=noplay]
----
CALL gds.scc.stats(
  graphName: string,
  configuration: map
)
YIELD
  componentCount: Integer,
  preProcessingMillis: Integer,
  computeMillis: Integer,
  postProcessingMillis: Integer,
  componentDistribution: Map,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-stream-stats-configuration-entries.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                  | Type    | Description
| componentCount        | Integer | The number of computed strongly connected components.
| preProcessingMillis   | Integer | Milliseconds for preprocessing the data.
| computeMillis         | Integer | Milliseconds for running the algorithm.
| postProcessingMillis  | Integer | Milliseconds for computing component count and distribution statistics.
| componentDistribution | Map     | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of component sizes.
| configuration         | Map     | The configuration used for running the algorithm.
|===
======
[.include-with-mutate]
======
.The following will run the algorithm and mutate the in-memory graph:
[source, cypher, role=noplay]
----
CALL gds.scc.mutate(
  graphName: string,
  configuration: map
)
YIELD
  componentCount: Integer,
  nodePropertiesWritten: Integer,
  preProcessingMillis: Integer,
  computeMillis: Integer,
  mutateMillis: Integer,
  postProcessingMillis: Integer,
  componentDistribution: Map,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-mutate-configuration-entries.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                 | Type    | Description
| componentCount                | Integer | The number of computed strongly connected components.
| nodePropertiesWritten         | Integer | The number of node properties written.
| preProcessingMillis           | Integer | Milliseconds for preprocessing the data.
| computeMillis                 | Integer | Milliseconds for running the algorithm.
| mutateMillis                   | Integer | Milliseconds for mutating the in-memory graph.
| postProcessingMillis          | Integer | Milliseconds for computing component count and distribution statistics.
| componentDistribution         | Map     | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of component sizes.
| configuration                 | Map     | The configuration used for running the algorithm.

|===
======
[.include-with-write]
======
.The following will run the algorithm and write back results:
[source, cypher, role=noplay]
----
CALL gds.scc.write(
  graphName: string,
  configuration: map
)
YIELD
  componentCount: Integer,
  nodePropertiesWritten: Integer,
  preProcessingMillis: Integer,
  computeMillis: Integer,
  writeMillis: Integer,
  postProcessingMillis: Integer,
  componentDistribution: Map,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-write-configuration-entries.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                 | Type    | Description
| componentCount                | Integer | The number of computed strongly connected components.
| nodePropertiesWritten         | Integer | The number of node properties written.
| preProcessingMillis           | Integer | Milliseconds for preprocessing the data.
| computeMillis                 | Integer | Milliseconds for running the algorithm.
| writeMillis                   | Integer | Milliseconds for writing result back to Neo4j.
| postProcessingMillis          | Integer | Milliseconds for computing component count and distribution statistics.
| componentDistribution         | Map     | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of component sizes.
| configuration                 | Map     | The configuration used for running the algorithm.

|===
======

====

[[algorithms-strongly-connected-components-example]]
== Strongly Connected Components algorithm examples

image::strongly_connected_components.png[]


.The following will create a sample graph:
[source, cypher, role=noplay setup-query]
----
CREATE (nAlice:User {name:'Alice'})
CREATE (nBridget:User {name:'Bridget'})
CREATE (nCharles:User {name:'Charles'})
CREATE (nDoug:User {name:'Doug'})
CREATE (nMark:User {name:'Mark'})
CREATE (nMichael:User {name:'Michael'})

CREATE (nAlice)-[:FOLLOW]->(nBridget)
CREATE (nAlice)-[:FOLLOW]->(nCharles)
CREATE (nMark)-[:FOLLOW]->(nDoug)
CREATE (nMark)-[:FOLLOW]->(nMichael)
CREATE (nBridget)-[:FOLLOW]->(nMichael)
CREATE (nDoug)-[:FOLLOW]->(nMark)
CREATE (nMichael)-[:FOLLOW]->(nAlice)
CREATE (nAlice)-[:FOLLOW]->(nMichael)
CREATE (nBridget)-[:FOLLOW]->(nAlice)
CREATE (nMichael)-[:FOLLOW]->(nBridget);
----

.The following will project and store a named graph:
[source, cypher, role=noplay graph-project-query]
----
CALL gds.graph.project('graph', 'User', 'FOLLOW')
----

[[algorithms-scc-mem-example]]

=== Memory Estimation

:mode: write
include::partial$/algorithms/shared/examples-estimate-intro.adoc[]

[role=query-example]
--
.The following will estimate the memory requirements for running the algorithm:
[source, cypher, role=noplay]
----
CALL gds.scc.write.estimate('graph', { writeProperty: 'componentId' })
YIELD nodeCount, relationshipCount, bytesMin, bytesMax, requiredMemory
----

.Results
[opts="header"]
|===
| nodeCount | relationshipCount | bytesMin | bytesMax | requiredMemory
| 6        | 10                | 33332       | 33332       | "32 KiB"
|===
--


[[algorithms-scc-stream-example]]
=== Stream
include::partial$/algorithms/shared/examples-stream-intro.adoc[]


[role=query-example]
--
.The following will run the algorithm and stream back results:
[source, cypher, role=noplay]
----
CALL gds.scc.stream('graph', {})
YIELD nodeId, componentId
RETURN gds.util.asNode(nodeId).name AS Name, componentId AS Component
ORDER BY Component DESC
----

.Results
[opts="header",cols="1m,1m"]
|===
| Name      | Component
| "Doug"    | 3
| "Mark"    | 3
| "Charles" | 2
| "Alice"   | 0
| "Bridget" | 0
| "Michael" | 0
|===
--
// tag::stream-sample-graph-explanation[]
We have 3 strongly connected components in our sample graph.

The first, and biggest, component has members Alice, Bridget, and Michael, while the second component has Doug and Mark.
Charles ends up in his own component because there isn't an outgoing relationship from that node to any of the others.

[[algorithms-scc-stats-example]]
=== Stats
:stats-syntax: algorithms-strongly-connected-components-syntax
include::partial$/algorithms/shared/examples-stats-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm and returns the result in form of statistical and measurement values
[source,cypher,role=noplay]
----
CALL gds.scc.stats('graph')
YIELD componentCount
----

.Results
[opts="header",cols="1"]
|===
| componentCount
| 3
|===
--

[[algorithms-scc-mutate-example]]
=== Mutate

include::partial$/algorithms/shared/examples-mutate-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm and store the results in `graph`:
[source,cypher,role=noplay]
----
CALL gds.scc.mutate('graph', { mutateProperty: 'componentId'})
YIELD componentCount
----

.Results
[opts="header"]
|===
| componentCount
| 3
|===
--

[[algorithms-scc-write-example]]
=== Write

include::partial$/algorithms/shared/examples-write-intro.adoc[]

[role=query-example, group=writeAndMax]
--
.The following will run the algorithm and write back results:
[source, cypher, role=noplay]
----
CALL gds.scc.write('graph', {
  writeProperty: 'componentId'
})
YIELD componentCount, componentDistribution
RETURN componentCount,componentDistribution.max as maxSetSize, componentDistribution.min as minSetSize
----

.Results
[opts="header",cols="1m,1m,1m"]
|===
| componentCount | maxSetSize | minSetSize
| 3        | 3          | 1
|===
--

// end::stream-sample-graph-explanation[]

[role=query-example, group=writeAndMax]

--
.The following will find the largest partition:
[source, cypher, role=noplay]
----
MATCH (u:User)
RETURN u.componentId AS Component, count(*) AS ComponentSize
ORDER BY ComponentSize DESC
LIMIT 1
----

.Results
[opts="header",cols="1m,1m"]
|===
| Component | ComponentSize
| 0         | 3
|===
--

ifndef::env-docs[]
== References

// tag::references[]

* https://pdfs.semanticscholar.org/61db/6892a92d1d5bdc83e52cc18041613cf895fa.pdf

* http://code.activestate.com/recipes/578507-strongly-connected-components-of-a-directed-graph/

* http://www.sandia.gov/~srajama/publications/BFS_and_Coloring.pdf

// end::references[]
endif::env-docs[]
