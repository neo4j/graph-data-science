[[algorithms-closeness-centrality]]
[.beta]
= Closeness Centrality
:description: This section describes the Closeness Centrality algorithm in the Neo4j Graph Data Science library.
:entity: node
:result: centrality
:algorithm: Closeness Centrality

include::partial$/operations-reference/beta-note.adoc[]

:directed:
:homogeneous:
include::partial$/algorithms/shared/algorithm-traits.adoc[]


[[algorithms-closeness-centrality-intro]]
== Introduction

// tag::introduction[]
Closeness centrality is a way of detecting nodes that are able to spread information very efficiently through a graph.

The closeness centrality of a node measures its average farness (inverse distance) to all other nodes.
Nodes with a high closeness score have the shortest distances to all other nodes.
// end::introduction[]

// tag::explanation[]
For each node _u_, the Closeness Centrality algorithm calculates the sum of its distances to all other nodes, based on calculating the shortest paths between all pairs of nodes.
The resulting sum is then inverted to determine the closeness centrality score for that node.
// end::explanation[]

// tag::formula[]
The *raw closeness centrality* of a node _u_ is calculated using the following formula:

`raw closeness centrality(_u_) = 1 / sum(distance from _u_ to all other nodes)`

It is more common to normalize this score so that it represents the average length of the shortest paths rather than their sum.
This adjustment allow comparisons of the closeness centrality of nodes of graphs of different sizes

The formula for *normalized closeness centrality* of node _u_ is as follows:

`normalized closeness centrality(_u_) = (number of nodes - 1) / sum(distance from _u_ to all other nodes)`
// end::formula[]

Wasserman and Faust have proposed an improved formula for dealing with unconnected graphs.
Assuming that _n_  is the number of nodes reachable from _u_ (counting also itself),
their corrected formula for a given node _u_ is given as follows:

`Wasserman-Faust normalized closeness centrality(_u_) = (_n_-1)^2/ ((number of nodes - 1) * sum(distance from _u_ to all other nodes))`

Note that in the case of a directed graph, closeness centrality is defined alternatively.
That is, rather than considering distances from _u_ to every other node, we instead sum and average the distance from every other node to _u_.


[[algorithms-closeness-centrality-considerations]]
=== Use-cases - when to use the Closeness Centrality algorithm

// tag::use-case[]

* Closeness centrality is used to research organizational networks, where individuals with high closeness centrality are in a favourable position to control and acquire vital information and resources within the organization.
One such study is http://www.orgnet.com/MappingTerroristNetworks.pdf["Mapping Networks of Terrorist Cells"^] by Valdis E. Krebs.

* Closeness centrality can be interpreted as an estimated time of arrival of information flowing through telecommunications or package delivery networks where information flows through shortest paths to a predefined target.
It can also be used in networks where information spreads through all shortest paths simultaneously, such as infection spreading through a social network.
Find more details in http://www.analytictech.com/borgatti/papers/centflow.pdf["Centrality and network flow"] by Stephen P. Borgatti.

* Closeness centrality has been used to estimate the importance of words in a document, based on a graph-based keyphrase extraction process.
This process is described by Florian Boudin in https://www.aclweb.org/anthology/I/I13/I13-1102.pdf["A Comparison of Centrality Measures for Graph-Based Keyphrase Extraction"].

// end::use-case[]


[[algorithms-closeness-centrality-limitations]]
=== Constraints - when not to use the Closeness Centrality algorithm

// tag::constraint[]
* Academically, closeness centrality works best on connected graphs.
If we use the original formula on an unconnected graph, we can end up with an infinite distance between two nodes in separate connected components.
This means that we'll end up with an infinite closeness centrality score when we sum up all the distances from that node.
+
In practice, a variation on the original formula is used so that we don't run into these issues.
// end::constraint[]


[[algorithms-closeness-centrality-syntax]]
== Syntax

include::partial$/algorithms/shared/syntax-intro-named-graph.adoc[]

.Closeness Centrality syntax per mode
[.tabbed-example, caption = ]
====

[.include-with-stream]
======
.Run Closeness Centrality in stream mode on a named graph.
[source,cypher,role=noplay]
----
CALL gds.beta.closeness.stream(
  graphName: String,
  configuration: Map
)
YIELD
  nodeId: Integer,
  score: Float
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-stream-stats-configuration-entries.adoc[]
include::partial$/algorithms/beta/closeness/specific-configuration.adoc[]
|===

.Results
[opts="header"]
|===
| Name      | Type     | Description
| nodeId    | Integer  | Node ID.
| score     | Float    | Closeness centrality score.
|===
======


[.include-with-stats]
======
.Run Closeness Centrality in stats mode on a named graph.
[source,cypher,role=noplay]
----
CALL gds.beta.closeness.stats(
  graphName: String,
  configuration: Map
)
YIELD
  centralityDistribution: Map,
  computeMillis: Integer,
  postProcessingMillis: Integer,
  preProcessingMillis: Integer,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-stream-stats-configuration-entries.adoc[]
include::partial$/algorithms/beta/closeness/specific-configuration.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                   | Type      | Description
| centralityDistribution | Map       | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of centrality values.
| preProcessingMillis    | Integer   | Milliseconds for preprocessing the graph.
| computeMillis          | Integer   | Milliseconds for running the algorithm.
| postProcessingMillis   | Integer   | Milliseconds for computing the statistics.
| configuration          | Map       | Configuration used for running the algorithm.
|===
======


[.include-with-mutate]
======
.Run Betweenness Centrality in mutate mode on a named graph.
[source,cypher,role=noplay]
----
CALL gds.beta.closeness.mutate(
  graphName: String,
  configuration: Map
)
YIELD
  nodePropertiesWritten: Integer,
  preProcessingMillis: Integer,
  computeMillis: Integer,
  postProcessingMillis: Integer,
  mutateMillis: Integer,
  mutateProperty: String,
  centralityDistribution: Map,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-mutate-configuration-entries.adoc[]
include::partial$/algorithms/beta/closeness/specific-configuration.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                   | Type         | Description
| nodePropertiesWritten  | Integer      | Number of properties added to the in-memory graph.
| preProcessingMillis    | Integer      | Milliseconds for preprocessing the graph.
| computeMillis          | Integer      | Milliseconds for running the algorithm.
| postProcessingMillis   | Integer      | Milliseconds for computing the statistics.
| mutateMillis           | Integer      | Milliseconds for mutating the GDS graph.
| mutateProperty         | String       | The node property updated in the GDS graph.
| centralityDistribution | Map          | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of centrality values.
| configuration          | Map          | Configuration used for running the algorithm.
|===
======


[.include-with-write]
======
.Run Closeness Centrality in write mode on a named graph.
[source,cypher,role=noplay]
----
CALL gds.beta.closeness.write(
  graphName: String,
  configuration: Map
)
YIELD
  nodePropertiesWritten: Integer,
  preProcessingMillis: Integer,
  computeMillis: Integer,
  postProcessingMillis: Integer,
  writeMillis: Integer,
  writeProperty: String,
  centralityDistribution: Map,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-write-configuration-entries.adoc[]
include::partial$/algorithms/beta/closeness/specific-configuration.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                   | Type         | Description
| nodePropertiesWritten  | Integer      | Number of properties written to Neo4j.
| preProcessingMillis    | Integer      | Milliseconds for preprocessing the graph.
| computeMillis          | Integer      | Milliseconds for running the algorithm.
| postProcessingMillis   | Integer      | Milliseconds for computing the statistics.
| writeMillis            | Integer      | Milliseconds for mutating the GDS graph.
| writeProperty          | String       | The node property updated in the GDS graph.
| centralityDistribution | Map          | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of centrality values.
| configuration          | Map          | Configuration used for running the algorithm.
|===
======

====


[[algorithms-closeness-centrality-examples]]
== Examples

:algorithm-name: {algorithm}
:graph-description: sample
:image-file: closeness-centrality-graph.png
include::partial$/algorithms/shared/examples-intro.adoc[]


.The following Cypher statement will create the example graph in the Neo4j database:
[source, cypher, role=noplay setup-query]
----
CREATE (a:Node {id:"A"}),
       (b:Node {id:"B"}),
       (c:Node {id:"C"}),
       (d:Node {id:"D"}),
       (e:Node {id:"E"}),
       (a)-[:LINK]->(b),
       (b)-[:LINK]->(a),
       (b)-[:LINK]->(c),
       (c)-[:LINK]->(b),
       (c)-[:LINK]->(d),
       (d)-[:LINK]->(c),
       (d)-[:LINK]->(e),
       (e)-[:LINK]->(d);
----

With the graph in Neo4j we can now project it into the graph catalog to prepare it for algorithm execution.
We do this using a native projection targeting the `Node` nodes and the `LINK` relationships.

include::partial$/algorithms/shared/examples-named-native-note.adoc[]

.The following statement will create a graph using a native projection and store it in the graph catalog under the name 'myGraph'.
[source, cypher, role=noplay graph-project-query]
----
CALL gds.graph.project('myGraph', 'Node', 'LINK')
----

In the following examples we will demonstrate using the Closeness Centrality algorithm on this graph.


[[algorithms-closeness-centrality-examples-stream]]
=== Stream

:stream-details: For example, we can order the results to find the nodes with the highest closeness centrality.
include::partial$/algorithms/shared/examples-stream-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `stream` mode:
[source, cypher, role=noplay]
----
CALL gds.beta.closeness.stream('myGraph')
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).id AS id, score
ORDER BY score DESC
----

// tag::stream-sample-graph-result[]
.Results
[opts="header",cols="1,1"]
|===
| id    | score
| "C"   | 0.6666666666666666
| "B"   | 0.5714285714285714
| "D"   | 0.5714285714285714
| "A"   | 0.4
| "E"   | 0.4
|===
--
// end::stream-sample-graph-result[]

// tag::stream-sample-graph-explanation[]
C is the best connected node in this graph, although B and D aren't far behind.
A and E don't have close ties to many other nodes, so their scores are lower.
Any node that has a direct connection to all other nodes would score 1.
// end::stream-sample-graph-explanation[]


[[algorithms-closeness-centrality-examples-stats]]
=== Stats

:stats-syntax: algorithms-closeness-centrality-syntax
include::partial$/algorithms/shared/examples-stats-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `stats` mode:
[source, cypher, role=noplay]
----
CALL gds.beta.closeness.stats('myGraph')
YIELD centralityDistribution
RETURN centralityDistribution.min AS minimumScore, centralityDistribution.mean AS meanScore
----

.Results
[opts="header"]
|===
| minimumScore      | meanScore
| 0.399999618530273 | 0.521904373168945
|===
--


[[algorithms-closeness-centrality-examples-mutate]]
=== Mutate

include::partial$/algorithms/shared/examples-mutate-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `mutate` mode:
[source, cypher, role=noplay]
----
CALL gds.beta.closeness.mutate('myGraph', { mutateProperty: 'centrality' })
YIELD centralityDistribution, nodePropertiesWritten
RETURN centralityDistribution.min AS minimumScore, centralityDistribution.mean AS meanScore, nodePropertiesWritten
----

.Results
[opts="header"]
|===
| minimumScore      | meanScore         | nodePropertiesWritten
| 0.399999618530273 | 0.521904373168945 | 5
|===
--

[[algorithms-closeness-centrality-examples-write]]
=== Write

include::partial$/algorithms/shared/examples-write-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `write` mode:
[source, cypher, role=noplay]
----
CALL gds.beta.closeness.write('myGraph', { writeProperty: 'centrality' })
YIELD centralityDistribution, nodePropertiesWritten
RETURN centralityDistribution.min AS minimumScore, centralityDistribution.mean AS meanScore, nodePropertiesWritten
----

.Results
[opts="header"]
|===
| minimumScore      | meanScore         | nodePropertiesWritten
| 0.399999618530273 | 0.521904373168945 | 5
|===
--
