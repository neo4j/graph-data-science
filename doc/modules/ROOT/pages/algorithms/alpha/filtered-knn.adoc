[[algorithms-filtered-knn]]
[.alpha]
= Filtered K-Nearest Neighbors
:description: This section describes the Filtered K-Nearest Neighbors (Filtered KNN) algorithm in the Neo4j Graph Data Science library.

:entity: relationship
:result: similarity score
:algorithm: Filtered K-Nearest Neighbors
:knnSpecificConfigurationTableTitle: KNN specific configuration

include::partial$/algorithms/alpha/alpha-note.adoc[]


[[algorithms-filtered-knn-intro]]
== Introduction

The Filtered K-Nearest Neighbors algorithm extends our popular xref::algorithms/knn.adoc[K-Nearest Neighbors] algorithm with filtering on source nodes, target nodes or both.

=== Types of Filtering

We are in a world of source nodes, target nodes and the relationship between them that hold a _similarity score_ or _distance_.

Just like for the K-Nearest Neighbors algorithm, output with filtering are new relationships between nodes and their k-nearest neighbors. Similarity scores are expressed via relationship properties.

Filtered K-Nearest Neighbors in addition give you control over nodes on either end of the relationships, saving you from having to filter a big result set on your own, and enabling better control over output volumes.

==== Source node filtering

For some use cases you will want to restrict the set of nodes that can act as source nodes; or the type of node that can act as source node. This is _source node filtering_. You want the best scoring relationships that originate from these particular nodes or this particular type of node.

A source node filter can be in either of these forms:

* A set of nodes
* A label
* A set of nodes _and_ a label

==== Target node filtering

Just like for source nodes, you sometimes want to restrict the set of nodes or type of node that can act as target node, i.e. _target node filtering_. The best scoring relationships for a given source node where the target node is from a set, or of a type.

Just like for the source node filter, a target nodes filter can be in either of these forms:

* A set of nodes
* A label
* A set of nodes _and_ a label

==== Seeding for target node filtering

A further use case for target node filtering is that you absolutely want to produce _k_ results. You want to fill a fixed size bucket with relationships, you hope that there are enough high scoring relationships found by the K-Nearest Neighbors algorithm, but as an insurance policy we can seed your result set with arbitrary relationships to "guarantee" a full bucket of _k_ results.

Just like the K-Nearest Neighbors algorithm is not guaranteed to find _k_ results, the Filtered K-Nearest Neighbors algorithm is not strictly guaranteed to find _k_ results either. But you will increase your odds massively if you employ seeing. In fact, with seeding, the _only_ time you would _not_ get _k_ results is when there are not _k_ target nodes in your graph.

Now, the _quality_ of the arbitrary padding results is unknown. How does that square with the `similarityCutoff` parameter? Here we have chosen semantics where seeding overrides similarity cutoff, and you risk getting results where the similarity score is below the cutoff - but guaranteeing that at least there are _k_ of them.

Seeding is a boolean property you switch on or off (default).

[NOTE]
====
You can mix and match source node filtering, target node filtering and seeding to achieve your goals.
====

[[algorithms-filtered-knn-filter-seeding-configuration]]
==== Configuring filters and seeding

You should consult xref::algorithms/knn.adoc#algorithms-knn-node-properties-configuration[K-Nearest Neighbors configuration] for the standard configuration options.

The source node filter to use is specified with the `sourceNodeFilter` configuration parameter. It is not mandatory.

This parameter accepts one of:

.sourceNodeFilter syntax
[cols="1,2a"]
|===
| a single node id
| `sourceNodeFilter: 42`
| a list of node ids
| `sourceNodeFilter: [23, 42, 87]`
| a single node
| `MATCH (person:Person) WITH person ORDER BY person.age DESC LIMIT 1` ... `sourceNodeFilter: n`
| a list of nodes
| `MATCH (person:Person) WHERE person.age > 35 collect(person) AS people` ... `sourceNodeFilter: people`
| a single label
| `sourceNodeFilter: 'Person'`
|===

The target node filter to use are specified with the `targetNodeFilter` configuration parameter. It is not mandatory.

This parameter accepts one of:

.targetNodeFilter syntax
[cols="1,2a"]
|===
| a single node id
| `targetNodeFilter: 117`
| a list of node ids
| `targetNodeFilter: [256, 512]`
| a single node
| `MATCH (person:Person) WITH person ORDER BY person.age ASC LIMIT 1` ... `targetNodeFilter: n`
| a list of nodes
| `MATCH (person:Person) WHERE person.age < 35 collect(person) AS people` ... `targetNodeFilter: people`
| a single label
| `targetNodeFilter: 'Person'`
|===

Seeding can be enabled with the `seedTargetNodes` configuration parameter. It defaults to `false`.

[[algorithms-filtered-knn-syntax]]
== Syntax

include::partial$/algorithms/shared/syntax-intro-named-graph.adoc[]

.Filtered K-Nearest Neighbors syntax per mode
[.tabbed-example, caption = ]
====

[.include-with-stream]
======
.Run Filtered K-Nearest Neighbors in stream mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.alpha.knn.filtered.stream(
  graphName: String,
  configuration: Map
) YIELD
  node1: Integer,
  node2: Integer,
  similarity: Float
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.General configuration for algorithm execution on a named graph.
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-stream-stats-configuration-entries.adoc[]
|===

include::partial$/algorithms/knn/knn-specific-algo-config.adoc[]

include::partial$/algorithms/alpha/filtered-knn/filtered-knn-specific-algo-config.adoc[]

.Results
[opts="header",cols="1m,1,6"]
|===
| Name       | Type    | Description
| node1      | Integer | Node ID of the first node.
| node2      | Integer | Node ID of the second node.
| similarity | Float   | Similarity score for the two nodes.
|===
======

[.include-with-stats]
======
.Run K-Nearest Neighbors in stats mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.alpha.knn.filtered.stats(
  graphName: String,
  configuration: Map
)
YIELD
  preProcessingMillis: Integer,
  computeMillis: Integer,
  postProcessingMillis: Integer,
  nodesCompared: Integer,
  ranIterations: Integer,
  didConverge: Boolean,
  nodePairsConsidered: Integer,
  similarityPairs: Integer,
  similarityDistribution: Map,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.General configuration for algorithm execution on a named graph.
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-stream-stats-configuration-entries.adoc[]
|===

include::partial$/algorithms/knn/knn-specific-algo-config.adoc[]
include::partial$/algorithms/alpha/filtered-knn/filtered-knn-specific-algo-config.adoc[]


.Results
[opts="header",cols="1,1,6"]
|===
| Name                          | Type      | Description
| ranIterations                 | Integer   | Number of iterations run.
| didConverge                   | Boolean   | Indicates if the algorithm converged.
| nodePairsConsidered           | Integer   | The number of similarity computations.
| preProcessingMillis           | Integer   | Milliseconds for preprocessing the data.
| computeMillis                 | Integer   | Milliseconds for running the algorithm.
| postProcessingMillis          | Integer   | Milliseconds for computing similarity value distribution statistics.
| nodesCompared                 | Integer   | The number of nodes for which similarity was computed.
| similarityPairs               | Integer   | The number of similarities in the result.
| similarityDistribution        | Map       | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of the computed similarity results.
| configuration                 | Map       | The configuration used for running the algorithm.
|===
======

[.include-with-mutate]
======
.Run K-Nearest Neighbors in mutate mode on a graph stored in the catalog.
[source, cypher, role=noplay]
----
CALL gds.alpha.knn.filtered.mutate(
  graphName: String,
  configuration: Map
)
YIELD
  preProcessingMillis: Integer,
  computeMillis: Integer,
  mutateMillis: Integer,
  postProcessingMillis: Integer,
  relationshipsWritten: Integer,
  nodesCompared: Integer,
  ranIterations: Integer,
  didConverge: Boolean,
  nodePairsConsidered: Integer,
  similarityDistribution: Map,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.General configuration for algorithm execution on a named graph.
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-mutate-configuration-entries.adoc[]
|===

include::partial$/algorithms/knn/knn-specific-algo-config.adoc[]
include::partial$/algorithms/alpha/filtered-knn/filtered-knn-specific-algo-config.adoc[]

.Results
[opts="header",cols="1,1,6"]
|===
| Name                   | Type    | Description
| ranIterations          | Integer | Number of iterations run.
| didConverge            | Boolean | Indicates if the algorithm converged.
| nodePairsConsidered    | Integer | The number of similarity computations.
| preProcessingMillis    | Integer | Milliseconds for preprocessing the data.
| computeMillis          | Integer | Milliseconds for running the algorithm.
| mutateMillis           | Integer | Milliseconds for adding properties to the projected graph.
| postProcessingMillis   | Integer | Milliseconds for computing similarity value distribution statistics.
| nodesCompared          | Integer | The number of nodes for which similarity was computed.
| relationshipsWritten   | Integer | The number of relationships created.
| similarityDistribution | Map     | Map containing min, max, mean, stdDev and p1, p5, p10, p25, p75, p90, p95, p99, p100 percentile values of the computed similarity results.
| configuration          | Map     | The configuration used for running the algorithm.
|===
======

[.include-with-write]
======
.Run K-Nearest Neighbors in write mode on a graph stored in the catalog.
[source, cypher, role=noplay]
----
CALL gds.alpha.knn.filtered.write(
  graphName: String,
  configuration: Map
)
YIELD
  preProcessingMillis: Integer,
  computeMillis: Integer,
  writeMillis: Integer,
  postProcessingMillis: Integer,
  nodesCompared: Integer,
  ranIterations: Integer,
  didConverge: Boolean,
  nodePairsConsidered: Integer,
  relationshipsWritten: Integer,
  similarityDistribution: Map,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.General configuration for algorithm execution on a named graph.
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-write-configuration-entries.adoc[]
|===

include::partial$/algorithms/knn/knn-specific-algo-config.adoc[]
include::partial$/algorithms/alpha/filtered-knn/filtered-knn-specific-algo-config.adoc[]


.Results
[opts="header",cols="1,1,6"]
|===
| Name                   | Type    | Description
| ranIterations          | Integer | Number of iterations run.
| didConverge            | Boolean | Indicates if the algorithm converged.
| nodePairsConsidered    | Integer | The number of similarity computations.
| preProcessingMillis    | Integer | Milliseconds for preprocessing the data.
| computeMillis          | Integer | Milliseconds for running the algorithm.
| writeMillis            | Integer | Milliseconds for writing result data back to Neo4j.
| postProcessingMillis   | Integer | Milliseconds for computing similarity value distribution statistics.
| nodesCompared          | Integer | The number of nodes for which similarity was computed.
| relationshipsWritten   | Integer | The number of relationships created.
| similarityDistribution | Map     | Map containing min, max, mean, stdDev and p1, p5, p10, p25, p75, p90, p95, p99, p100 percentile values of the computed similarity results.
| configuration          | Map     | The configuration used for running the algorithm.
|===
======
====

====

[[algorithms-filtered-knn-examples]]
== Examples

In this section we will show examples of running the Filtered K-Nearest Neighbors algorithm on a concrete graph.

Recall that KNN can run on a graph of only nodes, without any relationships.

Consider the following graph of five disconnected Person nodes, some of whom are Vegan.

image::example-graphs/knn.svg[Visualization of the example graph,align="center"]

[source, cypher, role=noplay setup-query]
----
CREATE (alice:Person:Vegan {name: 'Alice', age: 24, lotteryNumbers: [1, 3], embedding: [1.0, 3.0]})
CREATE (bob:Person:Vegan {name: 'Bob', age: 73, lotteryNumbers: [1, 2, 3], embedding: [2.1, 1.6]})
CREATE (carol:Person {name: 'Carol', age: 24, lotteryNumbers: [3], embedding: [1.5, 3.1]})
CREATE (dave:Person:Vegan {name: 'Dave', age: 48, lotteryNumbers: [2, 4], embedding: [0.6, 0.2]})
CREATE (eve:Person:Vegan {name: 'Eve', age: 67, lotteryNumbers: [1, 5], embedding: [1.8, 2.7]});
----

In the example, we want to use the Filtered K-Nearest Neighbors algorithm to compare people based on either their age or a combination on all provided properties.

include::partial$/algorithms/shared/examples-named-native-note.adoc[]

.The following statement will project the graph and store it in the graph catalog.
[source,cypher,role=noplay graph-project-query]
----
CALL gds.graph.project(
    'myGraph',
    {
        Person: {
            label: 'Person',
            properties: ['age','lotteryNumbers','embedding']
        },
        Vegan: {
            label: 'Vegan',
            properties: ['age']
        }
    },
    '*'
);
----


[[algorithms-filtered-knn-examples-stream]]
=== Filtering source nodes

include::partial$/algorithms/shared/examples-stream-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm, filter on source nodes, and stream results:
[source, cypher, role=noplay]
----
CALL gds.alpha.knn.filtered.stream('myGraph', {
    topK: 1,
    nodeProperties: ['age'],
    sourceNodeFilter: 'Vegan',
    // The following parameters are set to produce a deterministic result
    randomSeed: 1337,
    concurrency: 1,
    sampleRate: 1.0,
    deltaThreshold: 0.0
})
YIELD node1, node2, similarity
RETURN gds.util.asNode(node1).name AS Person1, gds.util.asNode(node2).name AS Person2, similarity
ORDER BY similarity DESCENDING, Person1, Person2
----

.Results
[opts="header"]
|===
| Person1 | Person2 | similarity
| "Alice" | "Carol" | 1.0
| "Bob"   | "Eve"   | 0.14285714285714285
| "Eve"   | "Bob"   | 0.14285714285714285
| "Dave"  | "Eve"   | 0.05
|===
--

We use default values for the procedure configuration parameter for most parameters.
The `randomSeed` and `concurrency` is set to produce the same result on every invocation.
The `topK` parameter is set to 1 to only return the single nearest neighbor for every node.
Notice that because Carol is not Vegan, she is not included in the result set - she was filtered out by the source node filter.

[[algorithms-filtered-knn-examples-stream-2]]
=== Filtering and seeding target nodes

include::partial$/algorithms/shared/examples-stream-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm, seeding the target node set. It will then filter for target nodes and stream results:
[source, cypher, role=noplay]
----
CALL gds.alpha.knn.filtered.stream('myGraph', {
    topK: 1,
    nodeProperties: ['age'],
    targetNodeFilter: 'Vegan',
    seedTargetNodes: true,
    similarityCutoff: 0.3,
    // The following parameters are set to produce a deterministic result
    randomSeed: 1337,
    concurrency: 1,
    sampleRate: 1.0,
    deltaThreshold: 0.0
})
YIELD node1, node2, similarity
RETURN gds.util.asNode(node1).name AS Person1, gds.util.asNode(node2).name AS Person2, similarity
ORDER BY similarity DESCENDING, Person1, Person2
----

.Results
[opts="header"]
|===
| Person1 | Person2 | similarity
| "Carol" | "Alice" | 1.0
| "Bob"   | "Eve"   | 0.14285714285714285
| "Eve"   | "Bob"   | 0.14285714285714285
| "Dave"  | "Eve"   | 0.05
| "Alice" | "Dave"  | 0.04
|===
--

Here we filter for target nodes with label Vegan, and set a similarity cutoff to ensure good quality results. Normally that would mean fewer results.
But we also enable seeding, which is what you do when you want to guarantee that for every node we output _k_ neighbours.
In this case seeding overrides similarity cutoff, and you see in the output that each source node has 1 result, even if they score rather poorly.
We happen to know that Alice scores very highly with Carol on age similarity under normal circumstances. However, because Carol is not Vegan, she is not included in the result set - she was filtered out by the target node filter - and instead Alice is matched with Dave.
