[[algorithms-filtered-node-similarity]]
[.alpha]
= Filtered Node Similarity
:description: This section describes the Filtered Node Similarity algorithm in the Neo4j Graph Data Science library.

:entity: relationship
:result: similarity score
:algorithm: Filtered Node Similarity
:nodeSimilaritySpecificConfigurationTableTitle: Node Similarity specific configuration


include::partial$/operations-reference/alpha-note.adoc[]

:directed:
:undirected:
:homogeneous:
:heterogeneous:
:weighted:
include::partial$/algorithms/shared/algorithm-traits.adoc[]


[[algorithms-filtered-node-similarity-intro]]
== Introduction

The Filtered Node Similarity algorithm is an extension to the xref:algorithms/node-similarity.adoc[Node Similarity] algorithm.
It adds support for filtering on source nodes, target nodes, or both.


[[algorithms-filtered-node-similarity-node-filtering]]
== Node filtering

A node filter reduces the node space for which the algorithm will produce results.
Consider two similarity results: _A_ = `(alice)-[:SIMILAR_TO]->(bob)` and _B_ `(bob)-[:SIMILAR_TO]->(alice)`.
Result A will be produced if the `(alice)` node matches the source node filter and the `(bob)` node matches the target node filter
If the `(alice)` node does not match the target node filter, or the `(bob)` node does not match the source node filter, result *B* will not be produce.


[[algorithms-filtered-node-similarity-filter-configuration]]
== Configuring node filters

For the standard configuration of node similarity, see xref:algorithms/node-similarity.adoc#algorithms-node-similarity-syntax[Node Similarity syntax].

The source node filter is specified with the `sourceNodeFilter` configuration parameter.
The target node filter is specified with the `targetNodeFilter` configuration parameter.
Neither parameter is mandatory.

The node filter parameters accept one of the following:

.Syntax for `sourceNodeFilter` and `targetNodeFilter`
[cols="1,2a"]
|===
| a single node id
| `sourceNodeFilter: 42`
| a list of node ids
| `sourceNodeFilter: [23, 42, 87]`
| a single node
| `MATCH (person:Person) WITH person ORDER BY person.age DESC LIMIT 1` ... `sourceNodeFilter: n`
| a list of nodes
| `MATCH (person:Person) WHERE person.age > 35 collect(person) AS people` ... `sourceNodeFilter: people`
| a single label
| `sourceNodeFilter: 'Person'`
|===

[[algorithms-filtered-node-similarity-syntax]]
== Syntax

include::partial$/algorithms/shared/syntax-intro-named-graph.adoc[]

.Filtered Node Similarity syntax per mode
[.tabbed-example, caption = ]
====

[.include-with-stream]
======
.Run Filtered Node Similarity  in stream mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.alpha.nodeSimilarity.filtered.stream(
  graphName: String,
  configuration: Map
) YIELD
  node1: Integer,
  node2: Integer,
  similarity: Float
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.General configuration for algorithm execution on a named graph.
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-stream-stats-configuration-entries.adoc[]
|===

:nodeSimilaritySpecificConfigurationTableTitle: Node Similarity specific configuration
include::partial$/algorithms/node-similarity/specific-configuration.adoc[]

include::partial$/algorithms/alpha/filtered-node-similarity/filtered-node-similarity-specific-algo-config.adoc[]

.Results
[opts="header",cols="1m,1,6"]
|===
| Name       | Type    | Description
| node1      | Integer | Node ID of the first node.
| node2      | Integer | Node ID of the second node.
| similarity | Float   | Similarity score for the two nodes.
|===
======

[.include-with-stats]
======
.Run Node Similarity in stats mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.alpha.nodeSimilarity.filtered.stats(
  graphName: String,
  configuration: Map
)
YIELD
  preProcessingMillis: Integer,
  computeMillis: Integer,
  postProcessingMillis: Integer,
  nodesCompared: Integer,
  similarityPairs: Integer,
  similarityDistribution: Map,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.General configuration for algorithm execution on a named graph.
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-stream-stats-configuration-entries.adoc[]
|===

include::partial$/algorithms/node-similarity/specific-configuration.adoc[]

include::partial$/algorithms/alpha/filtered-node-similarity/filtered-node-similarity-specific-algo-config.adoc[]

.Results
[opts="header",cols="1,1,6"]
|===
| Name                          | Type      | Description
| preProcessingMillis           | Integer   | Milliseconds for preprocessing the data.
| computeMillis                 | Integer   | Milliseconds for running the algorithm.
| postProcessingMillis          | Integer   | Milliseconds for computing component count and distribution statistics.
| nodesCompared                 | Integer   | The number of nodes for which similarity was computed.
| similarityPairs               | Integer   | The number of similarities in the result.
| similarityDistribution        | Map       | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of the computed similarity results.
| configuration                 | Map       | The configuration used for running the algorithm.
|===
======

[.include-with-mutate]
======
.Run Filtered Node Similarity  in mutate mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.alpha.nodeSimilarity.filtered.mutate(
  graphName: String,
  configuration: Map
)
YIELD
  preProcessingMillis: Integer,
  computeMillis: Integer,
  mutateMillis: Integer,
  postProcessingMillis: Integer,
  relationshipsWritten: Integer,
  nodesCompared: Integer,
  similarityDistribution: Map,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.General configuration for algorithm execution on a named graph.
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-mutate-configuration-entries.adoc[]
|===

:nodeSimilaritySpecificConfigurationTableTitle: Node Similarity specific configuration
include::partial$/algorithms/node-similarity/specific-configuration.adoc[]

include::partial$/algorithms/alpha/filtered-node-similarity/filtered-node-similarity-specific-algo-config.adoc[]

.Results
[opts="header",cols="1m,1,6"]
|===
| Name                   | Type    | Description
| preProcessingMillis    | Integer | Milliseconds for preprocessing the data.
| computeMillis          | Integer | Milliseconds for running the algorithm.
| mutateMillis           | Integer | Milliseconds for adding properties to the projected graph.
| postProcessingMillis   | Integer | Milliseconds for computing percentiles.
| nodesCompared          | Integer | The number of nodes for which similarity was computed.
| relationshipsWritten   | Integer | The number of relationships created.
| similarityDistribution | Map     | Map containing min, max, mean, stdDev and p1, p5, p10, p25, p75, p90, p95, p99, p100 percentile values of the computed similarity results.
| configuration          | Map     | The configuration used for running the algorithm.
|===
======

[.include-with-write]
======
.Run Filtered Node Similarity  in write mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.alpha.nodeSimilarity.filtered.write(
  graphName: String,
  configuration: Map
)
YIELD
 preProcessingMillis: Integer,
  computeMillis: Integer,
  writeMillis: Integer,
  postProcessingMillis: Integer,
  nodesCompared: Integer,
  relationshipsWritten: Integer,
  similarityDistribution: Map,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.General configuration for algorithm execution on a named graph.
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-write-configuration-entries.adoc[]
|===

:nodeSimilaritySpecificConfigurationTableTitle: Node Similarity specific configuration
include::partial$/algorithms/node-similarity/specific-configuration.adoc[]

include::partial$/algorithms/alpha/filtered-node-similarity/filtered-node-similarity-specific-algo-config.adoc[]

.Results
[opts="header",cols="1,1,6"]
|===
| Name                   | Type    | Description
| preProcessingMillis    | Integer | Milliseconds for preprocessing data.
| computeMillis          | Integer | Milliseconds for running the algorithm.
| writeMillis            | Integer | Milliseconds for writing result data back to Neo4j.
| postProcessingMillis   | Integer | Milliseconds for computing percentiles.
| nodesCompared          | Integer | The number of nodes for which similarity was computed.
| relationshipsWritten   | Integer | The number of relationships created.
| similarityDistribution | Map     | Map containing min, max, mean, stdDev and p1, p5, p10, p25, p75, p90, p95, p99, p100 percentile values of the computed similarity results.
| configuration          | Map     | The configuration used for running the algorithm.
|===
======

====

[[algorithms-filtered-node-similarity-examples]]
== Examples

:algorithm-name: {algorithm}
:graph-description: knowledge
:image-file: node-similarity.svg
include::partial$/algorithms/shared/examples-intro.adoc[]

.The following Cypher statement will create the example graph in the Neo4j database:
[source, cypher, role=noplay setup-query]
----
CREATE
  (alice:Person:Singer {name: 'Alice'}),
  (bob:Person:Singer {name: 'Bob'}),
  (carol:Person:Singer {name: 'Carol'}),
  (dave:Person {name: 'Dave'}),
  (eve:Person:Singer {name: 'Eve'}),
  (guitar:Instrument {name: 'Guitar'}),
  (synth:Instrument {name: 'Synthesizer'}),
  (bongos:Instrument {name: 'Bongos'}),
  (trumpet:Instrument {name: 'Trumpet'}),

  (alice)-[:LIKES]->(guitar),
  (alice)-[:LIKES]->(synth),
  (alice)-[:LIKES {strength: 0.5}]->(bongos),
  (bob)-[:LIKES]->(guitar),
  (bob)-[:LIKES]->(synth),
  (carol)-[:LIKES]->(bongos),
  (dave)-[:LIKES]->(guitar),
  (dave)-[:LIKES]->(synth),
  (dave)-[:LIKES]->(bongos);
----

This bipartite graph has two node sets, Person nodes and Instrument nodes.
Some of the Person nodes are also singers.
The two node sets are connected via LIKES relationships.
Each relationship starts at a Person node and ends at an Instrument node.

The Filtered Node Similarity algorithm will only compute similarity for nodes that have a degree of at least 1. Eve hence shall not be included in the results as her degree is zero.

include::partial$/algorithms/shared/examples-named-native-note.adoc[]

.The following statement will project the graph and store it in the graph catalog.
[source, cypher, role=noplay graph-project-query]
----
CALL gds.graph.project(
    'myGraph',
    ['Person', 'Instrument','Singer'],
    {
        LIKES: {
            properties: {
                strength: {
                    property: 'strength',
                    defaultValue: 1.0
                }
            }
        }
    }
);
----

In the following examples we will demonstrate the usage of the Filtered Node Similarity algorithm on this graph. In particular, we will apply the  `sourceNodeFilter`  and `targetNodeFilter`  filters to limit our similarity search to strictly Person nodes that also have the Singer label.

[[algorithms-filtered-node-similarity-examples-memory]]

=== Memory Estimation

:mode: write
include::partial$/algorithms/shared/examples-estimate-intro.adoc[]

[role=query-example]
--
.The following will estimate the memory requirements for running the algorithm:
[source, cypher, role=noplay]
----
CALL gds.alpha.nodeSimilarity.filtered.write.estimate('myGraph', {
    writeRelationshipType: 'SIMILAR',
    writeProperty: 'score',
    sourceNodeFilter:'Singer',
    targetNodeFilter:'Singer'
})
YIELD nodeCount, relationshipCount, bytesMin, bytesMax, requiredMemory
----

.Results
[opts="header",cols="1,1,1,1,1"]
|===
| nodeCount | relationshipCount | bytesMin | bytesMax | requiredMemory
| 9         | 9                 | 2384     | 2600     | "[2384 Bytes \... 2600 Bytes]"
|===
--
[[algorithms-filtered-node-similarity-examples-stream]]
=== Stream

include::partial$/algorithms/shared/examples-stream-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm, and stream the results:
[source, cypher, role=noplay]
----
CALL gds.alpha.nodeSimilarity.filtered.stream('myGraph', {sourceNodeFilter:'Singer' , targetNodeFilter:'Singer' } )
YIELD node1, node2, similarity
RETURN gds.util.asNode(node1).name AS Person1, gds.util.asNode(node2).name AS Person2, similarity
ORDER BY similarity DESCENDING, Person1, Person2
----

.Results
[opts="header"]
|===
| Person1 | Person2 | similarity
| "Alice" | "Bob"   | 0.6666666666666666
| "Bob"   | "Alice" | 0.6666666666666666
| "Alice" | "Carol" | 0.3333333333333333
| "Carol" | "Alice" | 0.3333333333333333
|===
--


[[algorithms-filtered-node-similarity-examples-stats]]
=== Stats

:stats-syntax: algorithms-filtered-node-similarity-syntax
include::partial$/algorithms/shared/examples-stats-intro.adoc[]

[role=query-example]
--
.The following will run the Node Similarity algorithm  with the specified filters sand return the result in form of statistical and measurement values
[source, cypher, role=noplay]
----
CALL gds.alpha.nodeSimilarity.filtered.stats('myGraph', {sourceNodeFilter:'Singer' , targetNodeFilter:'Singer' } )
YIELD nodesCompared, similarityPairs
----
.Results
[opts="header",cols="2"]
|===
| nodesCompared | similarityPairs
| 3             | 4
|===
--

[[algorithms-filtered-node-similarity-examples-mutate]]
=== Mutate

include::partial$/algorithms/shared/examples-mutate-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm, and write back results to the in-memory graph:
[source, cypher, role=noplay]
----
CALL gds.alpha.nodeSimilarity.filtered.mutate('myGraph',{
    mutateRelationshipType: 'SIMILAR',
    mutateProperty: 'score',
    sourceNodeFilter:'Singer',
    targetNodeFilter:'Singer'
})

YIELD nodesCompared, relationshipsWritten
----

.Results
[opts="header"]
|===
| nodesCompared | relationshipsWritten
| 3            | 4
|===
--

As can be seen in the results, the number of created relationships is the same as the number of rows in the streaming example.

[NOTE]
====
The relationships that are produced by the mutation are always directed, even if the input graph is undirected.
If `a -> b` is topK for `a` and symmetrically `b -> a` is topK for `b` (or both `a -> b` and `b -> a` are topN), it appears as though an undirected relationship is produced.
However, they are just two directed relationships that have been independently produced.
====

[[algorithms-node-similarity-examples-write]]
=== Write

The `write` execution mode for each pair of nodes creates a relationship with their similarity score as a property to the Neo4j database.
The type of the new relationship is specified using the mandatory configuration parameter `writeRelationshipType`.
The name of the new property is specified using the mandatory configuration parameter `writeProperty`.
The result is a single summary row, similar to `stats`, but with some additional metrics.

For more details on the `write` mode in general, see xref:common-usage/running-algos.adoc#running-algos-write[Write].

[role=query-example]
--
.The following will run the algorithm, and write back results:
[source,cypher,role=noplay]
----
CALL gds.alpha.nodeSimilarity.filtered.write('myGraph',{
    writeRelationshipType: 'SIMILAR',
    writeProperty: 'score',
    sourceNodeFilter:'Singer',
    targetNodeFilter:'Singer'
})
YIELD nodesCompared, relationshipsWritten
----

.Results
[opts="header"]
|===
| nodesCompared | relationshipsWritten
| 3             | 4
|===
--

As we can see from the results, the number of created relationships is equal to the number of rows in the streaming example.

[NOTE]
====
The relationships that are written are always directed, even if the input graph is undirected.
If `a -> b` is topK for `a` and symmetrically `b -> a` is topK for `b` (or both `a -> b` and `b -> a` are topN), it appears as though an undirected relationship is written.
However, they are just two directed relationships that have been independently written.
====
