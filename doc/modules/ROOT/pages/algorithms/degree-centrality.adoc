[[algorithms-degree-centrality]]
= Degree Centrality
:description: This section describes the Degree Centrality algorithm in the Neo4j Graph Data Science library.
:entity: node
:result: degree centrality
:algorithm: Degree Centrality


:directed:
:undirected:
:homogeneous:
:weighted:
include::partial$/algorithms/shared/algorithm-traits.adoc[]


[[algorithms-degree-centrality-intro]]
== Introduction

The Degree Centrality algorithm can be used to find popular nodes within a graph.
Degree centrality measures the number of incoming or outgoing (or both) relationships from a node, depending on the orientation of a relationship projection.
For more information on relationship orientations, see the xref:graph-project.adoc#relationship-projection-syntax[relationship projection syntax section].
It can be applied to either weighted or unweighted graphs.
In the weighted case the algorithm computes the sum of all positive weights of adjacent relationships of a node, for each node in the graph.
Non-positive weights are ignored.

For more information on this algorithm, see:

* http://leonidzhukov.net/hse/2014/socialnetworks/papers/freeman79-centrality.pdf[Linton C. Freeman: Centrality in Social Networks Conceptual Clarification, 1979.^]


[[algorithms-degree-centrality-usecase]]
== Use-cases

The Degree Centrality algorithm has been shown to be useful in many different applications.
For example:

* Degree centrality is an important component of any attempt to determine the most important people in a social network.
For example, in BrandWatch's https://www.brandwatch.com/blog/react-influential-men-and-women-2017/[most influential men and women on Twitter 2017^] the top 5 people in each category have over 40m followers each, which is a lot higher than the average degree.

* Weighted degree centrality has been used to help separate fraudsters from legitimate users of an online auction.
The weighted centrality for fraudsters is significantly higher because they tend to collude with each other to artificially increase the price of items.
Read more in https://link.springer.com/chapter/10.1007/978-3-319-23461-8_11[Two Step graph-based semi-supervised Learning for Online Auction Fraud Detection^]


[[algorithms-degree-centrality-syntax]]
== Syntax

include::partial$/algorithms/shared/syntax-intro-named-graph.adoc[]

.Degree Centrality syntax per mode
[.tabbed-example, caption = ]
====

[.include-with-stream]
======
.Run Degree Centrality in stream mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.degree.stream(
  graphName: String,
  configuration: Map
) YIELD
  nodeId: Integer,
  score: Float
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-stream-stats-configuration-entries.adoc[]
include::partial$/algorithms/degree-centrality/specific-configuration.adoc[]
|===

.Results
[opts="header"]
|===
| Name   | Type     | Description
| nodeId | Integer  | Node ID.
| score  | Float    | Degree Centrality score.
|===
======


[.include-with-stats]
======
.Run Degree Centrality in stats mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.degree.stats(
  graphName: String,
  configuration: Map
) YIELD
  centralityDistribution: Map,
  preProcessingMillis: Integer,
  computeMillis: Integer,
  postProcessingMillis: Integer,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-stream-stats-configuration-entries.adoc[]
include::partial$/algorithms/degree-centrality/specific-configuration.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                   | Type      | Description
| centralityDistribution | Map       | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of centrality values.
| preProcessingMillis    | Integer   | Milliseconds for preprocessing the graph.
| computeMillis          | Integer   | Milliseconds for running the algorithm.
| postProcessingMillis   | Integer   | Milliseconds for computing the statistics.
| configuration          | Map       | Configuration used for running the algorithm.
|===
======


[.include-with-mutate]
======
.Run Degree Centrality in mutate mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.degree.mutate(
  graphName: String,
  configuration: Map
) YIELD
  centralityDistribution: Map,
  preProcessingMillis: Integer,
  computeMillis: Integer,
  postProcessingMillis: Integer,
  mutateMillis: Integer,
  nodePropertiesWritten: Integer,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-mutate-configuration-entries.adoc[]
include::partial$/algorithms/degree-centrality/specific-configuration.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                   | Type      | Description
| centralityDistribution | Map       | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of centrality values.
| preProcessingMillis    | Integer   | Milliseconds for preprocessing the graph.
| computeMillis          | Integer   | Milliseconds for running the algorithm.
| postProcessingMillis   | Integer   | Milliseconds for computing the statistics.
| mutateMillis           | Integer   | Milliseconds for adding properties to the projected graph.
| nodePropertiesWritten  | Integer   | Number of properties added to the projected graph.
| configuration          | Map       | Configuration used for running the algorithm.
|===
======


[.include-with-write]
======
.Run Degree Centrality in write mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.degree.write(
  graphName: String,
  configuration: Map
) YIELD
  centralityDistribution: Map,
  preProcessingMillis: Integer,
  computeMillis: Integer,
  postProcessingMillis: Integer,
  writeMillis: Integer,
  nodePropertiesWritten: Integer,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-write-configuration-entries.adoc[]
include::partial$/algorithms/degree-centrality/specific-configuration.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                   | Type      | Description
| centralityDistribution | Map       | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of centrality values.
| preProcessingMillis    | Integer   | Milliseconds for preprocessing the graph.
| computeMillis          | Integer   | Milliseconds for running the algorithm.
| postProcessingMillis   | Integer   | Milliseconds for computing the statistics.
| writeMillis            | Integer   | Milliseconds for writing result data back.
| nodePropertiesWritten  | Integer   | Number of properties written to Neo4j.
| configuration          | Map       | The configuration used for running the algorithm.
|===
======

====


[[algorithms-degree-centrality-exsamples]]
== Examples

:algorithm-name: {algorithm}
:graph-description: social network
:image-file: degree-centrality-graph.svg
include::partial$/algorithms/shared/examples-intro.adoc[]

.The following Cypher statement will create the example graph in the Neo4j database:
[source, cypher, role=noplay setup-query]
----
CREATE
  (alice:User {name: 'Alice'}),
  (bridget:User {name: 'Bridget'}),
  (charles:User {name: 'Charles'}),
  (doug:User {name: 'Doug'}),
  (mark:User {name: 'Mark'}),
  (michael:User {name: 'Michael'}),

  (alice)-[:FOLLOWS {score: 1}]->(doug),
  (alice)-[:FOLLOWS {score: -2}]->(bridget),
  (alice)-[:FOLLOWS {score: 5}]->(charles),
  (mark)-[:FOLLOWS {score: 1.5}]->(doug),
  (mark)-[:FOLLOWS {score: 4.5}]->(michael),
  (bridget)-[:FOLLOWS {score: 1.5}]->(doug),
  (charles)-[:FOLLOWS {score: 2}]->(doug),
  (michael)-[:FOLLOWS {score: 1.5}]->(doug)
----

With the graph in Neo4j we can now project it into the graph catalog to prepare it for algorithm execution.
We do this using a native projection targeting the `User` nodes and the `FOLLOWS` relationships.

include::partial$/algorithms/shared/examples-named-native-note.adoc[]

.The following statement will project a graph using a reverse projection and store it in the graph catalog under the name 'myGraph'.
[source, cypher, role=noplay graph-project-query]
----
CALL gds.graph.project(
  'myGraph',
  'User',
  {
    FOLLOWS: {
      orientation: 'REVERSE',
      properties: ['score']
    }
  }
)
----

The graph is projected in a `REVERSE` orientation in order to retrieve people with the most followers in the following examples.
This will be demonstrated using the Degree Centrality algorithm on this graph.


[[algorithms-degree-centrality-examples-memory-estimation]]
=== Memory Estimation

:mode: write
include::partial$/algorithms/shared/examples-estimate-intro.adoc[]

[role=query-example]
--
.The following will estimate the memory requirements for running the algorithm:
[source, cypher, role=noplay]
----
CALL gds.degree.write.estimate('myGraph', { writeProperty: 'degree' })
YIELD nodeCount, relationshipCount, bytesMin, bytesMax, requiredMemory
----

.Results
[opts="header"]
|===
| nodeCount | relationshipCount | bytesMin | bytesMax | requiredMemory
| 6         | 8                 | 32       | 32       | "32 Bytes"
|===
--


[[algorithms-degree-centrality-examples-stream]]
=== Stream

:stream-details: For example, we can order the results to find the nodes with the highest degree centrality.
include::partial$/algorithms/shared/examples-stream-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `stream` mode:
[source, cypher, role=noplay]
----
CALL gds.degree.stream('myGraph')
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name AS name, score AS followers
ORDER BY followers DESC, name DESC
----

.Results
[opts="header",cols="1,1"]
|===
| name      | followers
| "Doug"    | 5.0
| "Michael" | 1.0
| "Charles" | 1.0
| "Bridget" | 1.0
| "Mark"    | 0.0
| "Alice"   | 0.0
|===
--

We can see that Doug is the most popular user in our imaginary social network graph, with 5 followers - all other users follow them, but they don't follow anybody back.
In a real social network, celebrities have very high follower counts but tend to follow only very few people.
We could therefore consider Doug quite the celebrity!


[[algorithms-degree-centrality-examples-stats]]
=== Stats

:stats-syntax: algorithms-degree-centrality-syntax
include::partial$/algorithms/shared/examples-stats-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `stats` mode:
[source, cypher, role=noplay]
----
CALL gds.degree.stats('myGraph')
YIELD centralityDistribution
RETURN centralityDistribution.min AS minimumScore, centralityDistribution.mean AS meanScore
----

.Results
[opts="header"]
|===
| minimumScore | meanScore
| 0.0          | 1.3333358764648438
|===
--

Comparing this to the results we saw in the xref:algorithms/degree-centrality.adoc#algorithms-degree-centrality-examples-stream[stream example], we can find our minimum and mean values from the table.


[[algorithms-degree-centrality-examples-mutate]]
=== Mutate

include::partial$/algorithms/shared/examples-mutate-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `mutate` mode:
[source, cypher, role=noplay]
----
CALL gds.degree.mutate('myGraph', { mutateProperty: 'degree' })
YIELD centralityDistribution, nodePropertiesWritten
RETURN centralityDistribution.min AS minimumScore, centralityDistribution.mean AS meanScore, nodePropertiesWritten
----

.Results
[opts="header"]
|===
| minimumScore | meanScore                  | nodePropertiesWritten
| 0.0          | 1.3333358764648438         | 6
|===
--

The returned result is the same as in the `stats` example.
Additionally, the graph 'myGraph' now has a node property `degree` which stores the degree centrality score for each node.
To find out how to inspect the new schema of the in-memory graph, see xref:graph-list.adoc[Listing graphs in the catalog].


[[algorithms-degree-centrality-examples-write]]
=== Write

include::partial$/algorithms/shared/examples-write-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `write` mode:
[source, cypher, role=noplay]
----
CALL gds.degree.write('myGraph', { writeProperty: 'degree' })
YIELD centralityDistribution, nodePropertiesWritten
RETURN centralityDistribution.min AS minimumScore, centralityDistribution.mean AS meanScore, nodePropertiesWritten
----

.Results
[opts="header"]
|===
| minimumScore | meanScore                  | nodePropertiesWritten
| 0.0          | 1.3333358764648438         | 6
|===
--

The returned result is the same as in the `stats` example.
Additionally, each of the seven nodes now has a new property `degree` in the Neo4j database, containing the degree centrality score for that node.


[[algorithms-degree-centrality-weighted-example]]
== Weighted Degree Centrality example

This example will explain the weighted Degree Centrality algorithm.
This algorithm is a variant of the Degree Centrality algorithm, that measures the sum of positive weights of incoming and outgoing relationships.

[role=query-example]
--
.The following will run the algorithm in `stream` mode, showing which users have the highest weighted degree centrality:
[source, cypher, role=noplay]
----
CALL gds.degree.stream(
   'myGraph',
   { relationshipWeightProperty: 'score' }
)
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name AS name, score AS weightedFollowers
ORDER BY weightedFollowers DESC, name DESC
----

.Results
[opts="header",cols="1,1"]
|===
| name      | weightedFollowers
| "Doug"    | 7.5
| "Charles" | 5.0
| "Michael" | 4.5
| "Mark"    | 0.0
| "Bridget" | 0.0
| "Alice"   | 0.0
|===
--

Doug still remains our most popular user, but there isn't such a big gap to the next person.
Charles and Michael both only have one follower, but those relationships have a high relationship weight.
Note that Bridget also has a weighted score of 0.0, despite having a connection from Alice.
That is because the `score` property value between Bridget and Alice is negative and will be ignored by the algorithm.


[[algorithms-degree-centrality-orientation-example]]
== Setting an orientation

By default, node centrality uses the `NATURAL` orientation to compute degrees.
For some use-cases it makes sense to analyze a different orientation, for example, if we want to find out how many users follow another user.
In order to change the orientation, we can use the `orientation` configuration key.
Supported values are `NATURAL` (default), `REVERSE` and `UNDIRECTED`.

[role=query-example]
--
.The following will run the algorithm in `stream` mode, showing which users have the highest in-degree centrality using the reverse orientation of the relationships:
[source, cypher, role=noplay]
----
CALL gds.degree.stream(
   'myGraph',
   { orientation: 'REVERSE' }
)
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name AS name, score AS followees
ORDER BY followees DESC, name DESC
----

.Results
[opts="header",cols="1,1"]
|===
| name      | followees
| "Alice"   | 3.0
| "Mark"    | 2.0
| "Michael" | 1.0
| "Charles" | 1.0
| "Bridget" | 1.0
| "Doug"    | 0.0
|===
--

The example shows that when looking at the reverse orientation, `Alice` is more central in the network than `Doug`.
