[[algorithms-article-rank]]
= Article Rank
:description: This section describes the Article Rank algorithm in the Neo4j Graph Data Science library.
:entity: node
:result: score
:algorithm: Article Rank
:algorithm-name: Article Rank


:directed:
:undirected:
:weighted:
include::partial$/algorithms/shared/algorithm-traits.adoc[]


[[algorithms-article-rank-introduction]]
== Introduction

ArticleRank is a variant of the xref:algorithms/page-rank.adoc[Page Rank algorithm], which measures the *transitive* influence of nodes.

Page Rank follows the assumption that relationships originating from low-degree nodes have a higher influence than relationships from high-degree nodes.
Article Rank lowers the influence of low-degree nodes by lowering the scores being sent to their neighbors in each iteration.

The Article Rank of a node _v_ at iteration _i_ is defined as:

image::equations/articleRank.svg[]

where,

* _N~in~(v)_ denotes incoming neighbors and _N~out~(v)_ denotes outgoing neighbors of node _v_.
* _d_ is a damping factor in _[0, 1]_.
* _[overline]#N#~out~_ is the average out-degree

For more information, see https://www.emeraldinsight.com/doi/abs/10.1108/00012530911005544[ArticleRank: a PageRankâ€based alternative to numbers of citations for analysing citation networks^].

[[algorithms-article-rank-considerations]]
== Considerations

There are some things to be aware of when using the Article Rank algorithm:

* If there are no relationships from within a group of pages to outside the group, then the group is considered a spider trap.
* Rank sink can occur when a network of pages is forming an infinite cycle.
* Dead-ends occur when pages have no outgoing relationship.

Changing the damping factor can help with all the considerations above.
It can be interpreted as a probability of a web surfer to sometimes jump to a random page and therefore not getting stuck in sinks.


[[algorithms-article-rank-syntax]]
== Syntax

include::partial$/algorithms/shared/syntax-intro-named-graph.adoc[]

.Article Rank syntax per mode
[.tabbed-example, caption = ]
====

[.include-with-stream]
======

.Run Article Rank in stream mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.articleRank.stream(
  graphName: String,
  configuration: Map
)
YIELD
  nodeId: Integer,
  score: Float
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-stream-stats-configuration-entries.adoc[]
include::partial$/algorithms/article-rank/specific-configuration.adoc[]
|===

.Results
[opts="header"]
|===
| Name    | Type    | Description
| nodeId  | Integer | Node ID.
| score   | Float   | Eigenvector score.
|===

======

[.include-with-stats]
======

.Run Article Rank in stats mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.articleRank.stats(
  graphName: String,
  configuration: Map
)
YIELD
  ranIterations: Integer,
  didConverge: Boolean,
  preProcessingMillis: Integer,
  computeMillis: Integer,
  postProcessingMillis: Integer,
  centralityDistribution: Map,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-stream-stats-configuration-entries.adoc[]
include::partial$/algorithms/article-rank/specific-configuration.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                   | Type      | Description
| ranIterations          | Integer   | The number of iterations run.
| didConverge            | Boolean   | Indicates if the algorithm converged.
| preProcessingMillis    | Integer   | Milliseconds for preprocessing the graph.
| computeMillis          | Integer   | Milliseconds for running the algorithm.
| postProcessingMillis   | Integer   | Milliseconds for computing the `centralityDistribution`.
| centralityDistribution | Map       | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of centrality values.
| configuration          | Map       | The configuration used for running the algorithm.
|===

======

[.include-with-mutate]
======

.Run Article Rank in mutate mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.articleRank.mutate(
  graphName: String,
  configuration: Map
)
YIELD
  nodePropertiesWritten: Integer,
  ranIterations: Integer,
  didConverge: Boolean,
  preProcessingMillis: Integer,
  computeMillis: Integer,
  postProcessingMillis: Integer,
  mutateMillis: Integer,
  centralityDistribution: Map,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-mutate-configuration-entries.adoc[]
include::partial$/algorithms/article-rank/specific-configuration.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                   | Type      | Description
| ranIterations          | Integer   | The number of iterations run.
| didConverge            | Boolean   | Indicates if the algorithm converged.
| preProcessingMillis    | Integer   | Milliseconds for preprocessing the graph.
| computeMillis          | Integer   | Milliseconds for running the algorithm.
| postProcessingMillis   | Integer   | Milliseconds for computing the `centralityDistribution`.
| mutateMillis           | Integer   | Milliseconds for adding properties to the projected graph.
| nodePropertiesWritten  | Integer   | The number of properties that were written to the projected graph.
| centralityDistribution | Map       | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of centrality values.
| configuration          | Map       | The configuration used for running the algorithm.
|===

======

[.include-with-write]
======

.Run Article Rank in write mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.articleRank.write(
  graphName: String,
  configuration: Map
)
YIELD
  nodePropertiesWritten: Integer,
  ranIterations: Integer,
  didConverge: Boolean,
  preProcessingMillis: Integer,
  computeMillis: Integer,
  postProcessingMillis: Integer,
  writeMillis: Integer,
  centralityDistribution: Map,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-write-configuration-entries.adoc[]
include::partial$/algorithms/article-rank/specific-configuration.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                   | Type      | Description
| ranIterations          | Integer   | The number of iterations run.
| didConverge            | Boolean   | Indicates if the algorithm converged.
| preProcessingMillis    | Integer   | Milliseconds for preprocessing the graph.
| computeMillis          | Integer   | Milliseconds for running the algorithm.
| postProcessingMillis   | Integer   | Milliseconds for computing the `centralityDistribution`.
| writeMillis            | Integer   | Milliseconds for writing result data back.
| nodePropertiesWritten  | Integer   | The number of properties that were written to Neo4j.
| centralityDistribution | Map       | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of centrality values.
| configuration          | Map       | The configuration used for running the algorithm.
|===

======

====


[[algorithms-article-rank-examples]]
== Examples

:graph-description: web network
:image-file: page-rank-graph.svg
include::partial$/algorithms/shared/examples-intro.adoc[]

.The following Cypher statement will create the example graph in the Neo4j database:
[source, cypher, role=noplay setup-query]
----
CREATE
  (home:Page {name:'Home'}),
  (about:Page {name:'About'}),
  (product:Page {name:'Product'}),
  (links:Page {name:'Links'}),
  (a:Page {name:'Site A'}),
  (b:Page {name:'Site B'}),
  (c:Page {name:'Site C'}),
  (d:Page {name:'Site D'}),

  (home)-[:LINKS {weight: 0.2}]->(about),
  (home)-[:LINKS {weight: 0.2}]->(links),
  (home)-[:LINKS {weight: 0.6}]->(product),
  (about)-[:LINKS {weight: 1.0}]->(home),
  (product)-[:LINKS {weight: 1.0}]->(home),
  (a)-[:LINKS {weight: 1.0}]->(home),
  (b)-[:LINKS {weight: 1.0}]->(home),
  (c)-[:LINKS {weight: 1.0}]->(home),
  (d)-[:LINKS {weight: 1.0}]->(home),
  (links)-[:LINKS {weight: 0.8}]->(home),
  (links)-[:LINKS {weight: 0.05}]->(a),
  (links)-[:LINKS {weight: 0.05}]->(b),
  (links)-[:LINKS {weight: 0.05}]->(c),
  (links)-[:LINKS {weight: 0.05}]->(d);
----

This graph represents eight pages, linking to one another.
Each relationship has a property called `weight`, which describes the importance of the relationship.

include::partial$/algorithms/shared/examples-named-native-note.adoc[]

.The following statement will project a graph using a native projection and store it in the graph catalog under the name 'myGraph'.
[source, cypher, role=noplay graph-project-query]
----
CALL gds.graph.project(
  'myGraph',
  'Page',
  'LINKS',
  {
    relationshipProperties: 'weight'
  }
)
----


[[algorithms-page-rank-examples-memory-estimation]]
=== Memory Estimation

:mode: write
include::partial$/algorithms/shared/examples-estimate-intro.adoc[]

[role=query-example]
--
.The following will estimate the memory requirements for running the algorithm:
[source, cypher, role=noplay]
----
CALL gds.articleRank.write.estimate('myGraph', {
  writeProperty: 'centrality',
  maxIterations: 20
})
YIELD nodeCount, relationshipCount, bytesMin, bytesMax, requiredMemory
----

.Results
[opts="header",cols="1,1,1,1,1"]
|===
| nodeCount | relationshipCount | bytesMin | bytesMax | requiredMemory
| 8         | 14                | 696     | 696     | "696 Bytes"
|===
--


[[algorithms-article-rank-examples-stream]]
=== Stream

:stream-details: For example, we can order the results to find the nodes with the highest Eigenvector score.
include::partial$/algorithms/shared/examples-stream-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `stream` mode:
[source, cypher, role=noplay]
----
CALL gds.articleRank.stream('myGraph')
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name AS name, score
ORDER BY score DESC, name ASC
----

.Results
[opts="header",cols="1,1"]
|===
| name      | score
| "Home"    | 0.5607071761939444
| "About"   | 0.250337073634706
| "Links"   | 0.250337073634706
| "Product" | 0.250337073634706
| "Site A"  | 0.18152391630760797
| "Site B"  | 0.18152391630760797
| "Site C"  | 0.18152391630760797
| "Site D"  | 0.18152391630760797
|===
--

The above query is running the algorithm in `stream` mode as `unweighted`.
Below, one can find an example for xref:algorithms/article-rank.adoc#algorithms-article-rank-examples-weighted[weighted graphs].


[[algorithms-article-rank-examples-stats]]
=== Stats

:stats-details: For example Eigenvector stats returns centrality histogram which can be used to monitor the distribution of centrality scores across all computed nodes.
:stats-syntax: algorithms-article-rank-syntax
include::partial$/algorithms/shared/examples-stats-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm and return statistics about the centrality scores.
[source, cypher, role=noplay]
----
CALL gds.articleRank.stats('myGraph')
YIELD centralityDistribution
RETURN centralityDistribution.max AS max
----

.Results
[opts="header",cols="1"]
|===
| max
| 0.5607099533081055
|===
--


[[algorithms-article-rank-examples-mutate]]
=== Mutate

include::partial$/algorithms/shared/examples-mutate-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `mutate` mode:
[source, cypher, role=noplay]
----
CALL gds.articleRank.mutate('myGraph', {
  mutateProperty: 'centrality'
})
YIELD nodePropertiesWritten, ranIterations
----

.Results
[opts="header",cols="1m,1m"]
|===
| nodePropertiesWritten | ranIterations
| 8                     | 19
|===
--


[[algorithms-article-rank-examples-write]]
=== Write

include::partial$/algorithms/shared/examples-write-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `write` mode:
[source, cypher, role=noplay]
----
CALL gds.articleRank.write('myGraph', {
  writeProperty: 'centrality'
})
YIELD nodePropertiesWritten, ranIterations
----

.Results
[opts="header",cols="1m,1m"]
|===
| nodePropertiesWritten | ranIterations
| 8                     | 19
|===
--


[[algorithms-article-rank-examples-weighted]]
=== Weighted

By default, the algorithm considers the relationships of the graph to be unweighted.
To change this behaviour, we can use the `relationshipWeightProperty` configuration parameter.
If the parameter is set, the associated property value is used as relationship weight.
In the `weighted` case, the previous score of a node sent to its neighbors is multiplied by the normalized relationship weight.
Note, that negative relationship weights are ignored during the computation.

In the following example, we use the `weight` property of the input graph as relationship weight property.

[role=query-example]
--
.The following will run the algorithm in `stream` mode using relationship weights:
[source, cypher, role=noplay]
----
CALL gds.articleRank.stream('myGraph', {
  relationshipWeightProperty: 'weight'
})
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name AS name, score
ORDER BY score DESC, name ASC
----

.Results
[opts="header",cols="1,1"]
|===
| name      | score
| "Home"    | 0.5160810726222141
| "Product" | 0.24570958074084706
| "About"   | 0.1819031935802824
| "Links"   | 0.1819031935802824
| "Site A"  | 0.15281123078335393
| "Site B"  | 0.15281123078335393
| "Site C"  | 0.15281123078335393
| "Site D"  | 0.15281123078335393
|===
--

As in the unweighted example, the "Home" node has the highest score.
In contrast, the "Product" now has the second highest instead of the fourth highest score.

NOTE: We are using `stream` mode to illustrate running the algorithm as `weighted`, however, all the algorithm modes support the `relationshipWeightProperty` configuration parameter.


[[algorithms-article-rank-examples-tolerance]]
=== Tolerance

The `tolerance` configuration parameter denotes the minimum change in scores between iterations.
If all scores change less than the configured tolerance, the iteration is aborted and considered converged.
Note, that setting a higher tolerance leads to earlier convergence, but also to less accurate centrality scores.

[role=query-example]
--
.The following will run the algorithm in `stream` mode using a high `tolerance` value:
[source, cypher, role=noplay]
----
CALL gds.articleRank.stream('myGraph', {
  tolerance: 0.1
})
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name AS name, score
ORDER BY score DESC, name ASC
----

.Results
[opts="header",cols="1,1"]
|===
| name      | score
| "Home"    | 0.4470707070707072
| "About"   | 0.23000212652844235
| "Links"   | 0.23000212652844235
| "Product" | 0.23000212652844235
| "Site A"  | 0.16888888888888892
| "Site B"  | 0.16888888888888892
| "Site C"  | 0.16888888888888892
| "Site D"  | 0.16888888888888892
|===
--

We are using `tolerance: 0.1`, which leads to slightly different results compared to the xref:algorithms/article-rank.adoc#algorithms-article-rank-examples-stream[stream example].
However, the computation converges after four iterations, and we can already observe a trend in the resulting scores.


[[algorithms-article-rank-examples-personalised]]
=== Personalised Article Rank

Personalized Article Rank is a variation of Article Rank which is biased towards a set of `sourceNodes`.
By default, the power iteration starts with the same value for all nodes: `1 / |V|`.
For a given set of source nodes `S`, the initial value of each source node is set to `1 / |S|` and to `0` for all remaining nodes.

The following examples show how to run Eigenvector centrality centered around 'Site A' and 'Site B'.

[role=query-example]
--
.The following will run the algorithm and stream results:
[source, cypher, role=noplay]
----
MATCH (siteA:Page {name: 'Site A'}), (siteB:Page {name: 'Site B'})
CALL gds.articleRank.stream('myGraph', {
  maxIterations: 20,
  sourceNodes: [siteA, siteB]
})
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name AS name, score
ORDER BY score DESC, name ASC
----

.Results
[opts="header",cols="1,1"]
|===
| name      | score
| "Site A"  | 0.15249052775314756
| "Site B"  | 0.15249052775314756
| "Home"    | 0.1105231342997017
| "About"   | 0.019777824032578193
| "Links"   | 0.019777824032578193
| "Product" | 0.019777824032578193
| "Site C"  | 0.002490527753147571
| "Site D"  | 0.002490527753147571
|===
--

Comparing these results to the ones from the xref:algorithms/article-rank.adoc#algorithms-article-rank-examples-stream[stream example] (which is not using `sourceNodes` configuration parameter) shows the 'Site A' and `Site B` nodes we used in the `sourceNodes` list now score second and third instead of fourth and fifth.


[[algorithms-article-rank-examples-scaler]]
=== Scaling centrality scores

To normalize the final scores as part of the algorithm execution, one can use the `scaler` configuration parameter.
A common scaler is the `L1Norm`, which normalizes each score to a value between 0 and 1.
A description of all available scalers can be found in the documentation for the xref:algorithms/scale-properties.adoc[`scaleProperties`] procedure.

[role=query-example]
--
.The following will run the algorithm in `stream` mode and returns normalized results:
[source, cypher, role=noplay]
----
CALL gds.articleRank.stream('myGraph', {
  scaler: "L1Norm"
})
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name AS name, score
ORDER BY score DESC, name ASC
----

.Results
[opts="header",cols="1,1"]
|===
| name      | score
| "Home"    | 0.275151294006312
| "About"   | 0.12284588582564794
| "Links"   | 0.12284588582564794
| "Product" | 0.12284588582564794
| "Site A"  | 0.08907776212918608
| "Site B"  | 0.08907776212918608
| "Site C"  | 0.08907776212918608
| "Site D"  | 0.08907776212918608

|===
--

Comparing the results with the xref:algorithms/article-rank.adoc#algorithms-article-rank-examples-stream[stream example], we can see that the relative order of scores is the same.
