[[algorithms-page-rank]]
= PageRank
:description: This section describes the PageRank algorithm in the Neo4j Graph Data Science library.
:entity: node
:result: score
:algorithm: PageRank


:directed:
:undirected:
:weighted:
include::partial$/algorithms/shared/algorithm-traits.adoc[]


[[algorithms-page-rank-intro]]
== Introduction

The PageRank algorithm measures the importance of each node within the graph, based on the number incoming relationships and the importance of the corresponding source nodes.
The underlying assumption roughly speaking is that a page is only as important as the pages that link to it.

PageRank is introduced in the original Google paper as a function that solves the following equation:

image::equations/page-rank-formula.svg[align="center"]

where,

* we assume that a page _A_ has pages _T~1~_ to _T~n~_ which point to it.
* _d_ is a damping factor which can be set between 0 (inclusive) and 1 (exclusive).
It is usually set to 0.85.
* _C(A)_ is defined as the number of links going out of page _A_.

This equation is used to iteratively update a candidate solution and arrive at an approximate solution to the same equation.

For more information on this algorithm, see:

* http://infolab.stanford.edu/~backrub/google.html[The original google paper^]
* http://delab.csd.auth.gr/~dimitris/courses/ir_spring06/page_rank_computing/01531136.pdf[An Efficient Partition-Based Parallel PageRank Algorithm^]
* https://arxiv.org/pdf/1407.5107.pdf[PageRank beyond the web^] for use cases

[NOTE]
====
Running this algorithm requires sufficient memory availability.
Before running this algorithm, we recommend that you read xref:common-usage/memory-estimation.adoc[Memory Estimation].
====


[[algorithms-page-rank-considerations]]
== Considerations

There are some things to be aware of when using the PageRank algorithm:

* If there are no relationships from within a group of pages to outside the group, then the group is considered a spider trap.
* Rank sink can occur when a network of pages is forming an infinite cycle.
* Dead-ends occur when pages have no outgoing relationship.

Changing the damping factor can help with all the considerations above.
It can be interpreted as a probability of a web surfer to sometimes jump to a random page and therefore not getting stuck in sinks.


[[algorithms-page-rank-syntax]]
== Syntax

include::partial$/algorithms/shared/syntax-intro-named-graph.adoc[]

.PageRank syntax per mode
[.tabbed-example, caption = ]
====

[.include-with-stream]
======

.Run PageRank in stream mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.pageRank.stream(
  graphName: String,
  configuration: Map
)
YIELD
  nodeId: Integer,
  score: Float
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-stream-stats-configuration-entries.adoc[]
include::partial$/algorithms/page-rank/specific-configuration.adoc[]
|===

.Results
[opts="header"]
|===
| Name    | Type    | Description
| nodeId  | Integer | Node ID.
| score   | Float   | PageRank score.
|===

======

[.include-with-stats]
======

.Run PageRank in stats mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.pageRank.stats(
  graphName: String,
  configuration: Map
)
YIELD
  ranIterations: Integer,
  didConverge: Boolean,
  preProcessingMillis: Integer,
  computeMillis: Integer,
  postProcessingMillis: Integer,
  centralityDistribution: Map,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-stream-stats-configuration-entries.adoc[]
include::partial$/algorithms/page-rank/specific-configuration.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                   | Type      | Description
| ranIterations          | Integer   | The number of iterations run.
| didConverge            | Boolean   | Indicates if the algorithm converged.
| preProcessingMillis    | Integer   | Milliseconds for preprocessing the graph.
| computeMillis          | Integer   | Milliseconds for running the algorithm.
| postProcessingMillis   | Integer   | Milliseconds for computing the `centralityDistribution`.
| centralityDistribution | Map       | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of centrality values.
| configuration          | Map       | The configuration used for running the algorithm.
|===

======

[.include-with-mutate]
======

.Run PageRank in mutate mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.pageRank.mutate(
  graphName: String,
  configuration: Map
)
YIELD
  nodePropertiesWritten: Integer,
  ranIterations: Integer,
  didConverge: Boolean,
  preProcessingMillis: Integer,
  computeMillis: Integer,
  postProcessingMillis: Integer,
  mutateMillis: Integer,
  centralityDistribution: Map,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-mutate-configuration-entries.adoc[]
include::partial$/algorithms/page-rank/specific-configuration.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                   | Type      | Description
| ranIterations          | Integer   | The number of iterations run.
| didConverge            | Boolean   | Indicates if the algorithm converged.
| preProcessingMillis    | Integer   | Milliseconds for preprocessing the graph.
| computeMillis          | Integer   | Milliseconds for running the algorithm.
| postProcessingMillis   | Integer   | Milliseconds for computing the `centralityDistribution`.
| mutateMillis           | Integer   | Milliseconds for adding properties to the projected graph.
| nodePropertiesWritten  | Integer   | The number of properties that were written to the projected graph.
| centralityDistribution | Map       | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of centrality values.
| configuration          | Map       | The configuration used for running the algorithm.
|===

======

[.include-with-write]
======

.Run PageRank in write mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.pageRank.write(
  graphName: String,
  configuration: Map
)
YIELD
  nodePropertiesWritten: Integer,
  ranIterations: Integer,
  didConverge: Boolean,
  preProcessingMillis: Integer,
  computeMillis: Integer,
  postProcessingMillis: Integer,
  writeMillis: Integer,
  centralityDistribution: Map,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-write-configuration-entries.adoc[]
include::partial$/algorithms/page-rank/specific-configuration.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                   | Type      | Description
| ranIterations          | Integer   | The number of iterations run.
| didConverge            | Boolean   | Indicates if the algorithm converged.
| preProcessingMillis    | Integer   | Milliseconds for preprocessing the graph.
| computeMillis          | Integer   | Milliseconds for running the algorithm.
| postProcessingMillis   | Integer   | Milliseconds for computing the `centralityDistribution`.
| writeMillis            | Integer   | Milliseconds for writing result data back.
| nodePropertiesWritten  | Integer   | The number of properties that were written to Neo4j.
| centralityDistribution | Map       | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of centrality values.
| configuration          | Map       | The configuration used for running the algorithm.
|===

======

====


[[algorithms-page-rank-examples]]
== Examples

:algorithm-name: {algorithm}
:graph-description: web network
:image-file: page-rank-graph.svg
include::partial$/algorithms/shared/examples-intro.adoc[]

.The following Cypher statement will create the example graph in the Neo4j database:
[source, cypher, role=noplay setup-query]
----
CREATE
  (home:Page {name:'Home'}),
  (about:Page {name:'About'}),
  (product:Page {name:'Product'}),
  (links:Page {name:'Links'}),
  (a:Page {name:'Site A'}),
  (b:Page {name:'Site B'}),
  (c:Page {name:'Site C'}),
  (d:Page {name:'Site D'}),

  (home)-[:LINKS {weight: 0.2}]->(about),
  (home)-[:LINKS {weight: 0.2}]->(links),
  (home)-[:LINKS {weight: 0.6}]->(product),
  (about)-[:LINKS {weight: 1.0}]->(home),
  (product)-[:LINKS {weight: 1.0}]->(home),
  (a)-[:LINKS {weight: 1.0}]->(home),
  (b)-[:LINKS {weight: 1.0}]->(home),
  (c)-[:LINKS {weight: 1.0}]->(home),
  (d)-[:LINKS {weight: 1.0}]->(home),
  (links)-[:LINKS {weight: 0.8}]->(home),
  (links)-[:LINKS {weight: 0.05}]->(a),
  (links)-[:LINKS {weight: 0.05}]->(b),
  (links)-[:LINKS {weight: 0.05}]->(c),
  (links)-[:LINKS {weight: 0.05}]->(d);
----

This graph represents eight pages, linking to one another.
Each relationship has a property called `weight`, which describes the importance of the relationship.

include::partial$/algorithms/shared/examples-named-native-note.adoc[]

.The following statement will project a graph using a native projection and store it in the graph catalog under the name 'myGraph'.
[source, cypher, role=noplay graph-project-query]
----
CALL gds.graph.project(
  'myGraph',
  'Page',
  'LINKS',
  {
    relationshipProperties: 'weight'
  }
)
----


[[algorithms-page-rank-examples-memory-estimation]]
=== Memory Estimation

:mode: write
include::partial$/algorithms/shared/examples-estimate-intro.adoc[]

[role=query-example]
--
.The following will estimate the memory requirements for running the algorithm:
[source, cypher, role=noplay]
----
CALL gds.pageRank.write.estimate('myGraph', {
  writeProperty: 'pageRank',
  maxIterations: 20,
  dampingFactor: 0.85
})
YIELD nodeCount, relationshipCount, bytesMin, bytesMax, requiredMemory
----

.Results
[opts="header",cols="1,1,1,1,1"]
|===
| nodeCount | relationshipCount | bytesMin | bytesMax | requiredMemory
| 8         | 14                | 696     | 696     | "696 Bytes"
|===
--


[[algorithms-page-rank-examples-stream]]
=== Stream

:stream-details: For example, we can order the results to find the nodes with the highest PageRank score.
include::partial$/algorithms/shared/examples-stream-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `stream` mode:
[source, cypher, role=noplay]
----
CALL gds.pageRank.stream('myGraph')
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name AS name, score
ORDER BY score DESC, name ASC
----

.Results
[opts="header",cols="1,1"]
|===
| name      | score
| "Home"    | 3.215681999884452
| "About"   | 1.0542700552146722
| "Links"   | 1.0542700552146722
| "Product" | 1.0542700552146722
| "Site A"  | 0.3278578964488539
| "Site B"  | 0.3278578964488539
| "Site C"  | 0.3278578964488539
| "Site D"  | 0.3278578964488539
|===
--

The above query is running the algorithm in `stream` mode as `unweighted` and the returned scores are not normalized.
Below, one can find an example for xref:algorithms/page-rank.adoc#algorithms-page-rank-examples-weighted[weighted graphs].
Another xref:algorithms/page-rank.adoc#algorithms-page-rank-examples-scaler[example] shows the application of a scaler to normalize the final scores.

NOTE: While we are using the `stream` mode to illustrate running the algorithm as `weighted` or `unweighted`, all the algorithm modes support this configuration parameter.

[[algorithms-page-rank-examples-stats]]
=== Stats

:stats-details: For example PageRank stats returns centrality histogram which can be used to monitor the distribution of PageRank score values across all computed nodes.
:stats-syntax: algorithms-page-rank-syntax
include::partial$/algorithms/shared/examples-stats-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm and returns the result in form of statistical and measurement values
[source, cypher, role=noplay]
----
CALL gds.pageRank.stats('myGraph', {
  maxIterations: 20,
  dampingFactor: 0.85
})
YIELD centralityDistribution
RETURN centralityDistribution.max AS max
----

.Results
[opts="header",cols="1"]
|===
| max
| 3.2156810760498047
|===
--

The centrality histogram can be useful for inspecting the computed scores or perform normalizations.

[[algorithms-page-rank-examples-mutate]]
=== Mutate

include::partial$/algorithms/shared/examples-mutate-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `mutate` mode:
[source, cypher, role=noplay]
----
CALL gds.pageRank.mutate('myGraph', {
  maxIterations: 20,
  dampingFactor: 0.85,
  mutateProperty: 'pagerank'
})
YIELD nodePropertiesWritten, ranIterations
----

.Results
[opts="header",cols="1m,1m"]
|===
| nodePropertiesWritten | ranIterations
| 8                     | 20
|===
--


[[algorithms-page-rank-examples-write]]
=== Write

include::partial$/algorithms/shared/examples-write-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `write` mode:
[source, cypher, role=noplay]
----
CALL gds.pageRank.write('myGraph', {
  maxIterations: 20,
  dampingFactor: 0.85,
  writeProperty: 'pagerank'
})
YIELD nodePropertiesWritten, ranIterations
----

.Results
[opts="header",cols="1m,1m"]
|===
| nodePropertiesWritten | ranIterations
| 8                     | 20
|===
--


[[algorithms-page-rank-examples-weighted]]
=== Weighted

By default, the algorithm is considering the relationships of the graph to be `unweighted`, to change this behaviour we can use configuration parameter called `relationshipWeightProperty`.
In the `weighted` case, the previous score of a node send to its neighbors, is multiplied by the relationship weight and then divided by the sum of the weights of its outgoing relationships.
If the value of the relationship property is negative it will be ignored during computation.
Below is an example of running the algorithm using the relationship property.

[role=query-example]
--
.The following will run the algorithm in `stream` mode using relationship weights:
[source, cypher, role=noplay]
----
CALL gds.pageRank.stream('myGraph', {
  maxIterations: 20,
  dampingFactor: 0.85,
  relationshipWeightProperty: 'weight'
})
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name AS name, score
ORDER BY score DESC, name ASC
----

.Results
[opts="header",cols="1,1"]
|===
| name      | score
| "Home"    | 3.53751028396339
| "Product" | 1.9357838291651097
| "About"   | 0.7452612763883698
| "Links"   | 0.7452612763883698
| "Site A"  | 0.18152677135466103
| "Site B"  | 0.18152677135466103
| "Site C"  | 0.18152677135466103
| "Site D"  | 0.18152677135466103
|===
--

NOTE: We are using `stream` mode to illustrate running the algorithm as `weighted` or `unweighted`, all the algorithm modes support this configuration parameter.


[[algorithms-page-rank-examples-tolerance]]
=== Tolerance

The `tolerance` configuration parameter denotes the minimum change in scores between iterations.
If all scores change less than the configured `tolerance` value the result stabilises, and the algorithm returns.

[role=query-example]
--
.The following will run the algorithm in `stream` mode using bigger `tolerance` value:
[source, cypher, role=noplay]
----
CALL gds.pageRank.stream('myGraph', {
  maxIterations: 20,
  dampingFactor: 0.85,
  tolerance: 0.1
})
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name AS name, score
ORDER BY score DESC, name ASC
----

.Results
[opts="header",cols="1,1"]
|===
| name      | score
| "Home"    | 1.5812450669583336
| "About"   | 0.5980194356381945
| "Links"   | 0.5980194356381945
| "Product" | 0.5980194356381945
| "Site A"  | 0.23374955154166668
| "Site B"  | 0.23374955154166668
| "Site C"  | 0.23374955154166668
| "Site D"  | 0.23374955154166668
|===
--

In this example we are using `tolerance: 0.1`, so the results are a bit different compared to the ones from xref:algorithms/page-rank.adoc#algorithms-page-rank-examples-stream[stream example] which is using the default value of `tolerance`.
Note that the nodes 'About', 'Link' and 'Product' now have the same score, while with the default value of `tolerance` the node 'Product' has higher score than the other two.


[[algorithms-page-rank-examples-damping]]
=== Damping Factor

The damping factor configuration parameter accepts values between 0 (inclusive) and 1 (exclusive).
If its value is too high then problems of sinks and spider traps may occur, and the values may oscillate so that the algorithm does not converge.
If it's too low then all scores are pushed towards 1, and the result will not sufficiently reflect the structure of the graph.

[role=query-example]
--
.The following will run the algorithm in `stream` mode using smaller `dampingFactor` value:
[source, cypher, role=noplay]
----
CALL gds.pageRank.stream('myGraph', {
  maxIterations: 20,
  dampingFactor: 0.05
})
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name AS name, score
ORDER BY score DESC, name ASC
----

.Results
[opts="header",cols="1,1"]
|===
| name      | score
| "Home"    | 1.2487309425844906
| "About"   | 0.9708121818724536
| "Links"   | 0.9708121818724536
| "Product" | 0.9708121818724536
| "Site A"  | 0.9597081216238426
| "Site B"  | 0.9597081216238426
| "Site C"  | 0.9597081216238426
| "Site D"  | 0.9597081216238426
|===
--

Compared to the results from the xref:algorithms/page-rank.adoc#algorithms-page-rank-examples-stream[stream example] which is using the default value of `dampingFactor` the score values are closer to each other when using `dampingFactor: 0.05`.
Also, note that the nodes 'About', 'Link' and 'Product' now have the same score, while with the default value of `dampingFactor` the node 'Product' has higher score than the other two.


[[algorithms-page-rank-examples-personalised]]
=== Personalised PageRank

Personalized PageRank is a variation of PageRank which is biased towards a set of `sourceNodes`.
This variant of PageRank is often used as part of https://www.r-bloggers.com/from-random-walks-to-personalized-pagerank/[recommender systems^].

The following examples show how to run PageRank centered around 'Site A'.

[role=query-example]
--
.The following will run the algorithm and stream results:
[source, cypher, role=noplay]
----
MATCH (siteA:Page {name: 'Site A'})
CALL gds.pageRank.stream('myGraph', {
  maxIterations: 20,
  dampingFactor: 0.85,
  sourceNodes: [siteA]
})
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name AS name, score
ORDER BY score DESC, name ASC
----

.Results
[opts="header",cols="1,1"]
|===
| name      | score
| "Home"    | 0.39902290442518784
| "Site A"  | 0.16890325301726694
| "About"   | 0.11220151747374331
| "Links"   | 0.11220151747374331
| "Product" | 0.11220151747374331
| "Site B"  | 0.01890325301726691
| "Site C"  | 0.01890325301726691
| "Site D"  | 0.01890325301726691
|===
--

Comparing these results to the ones from the xref:algorithms/page-rank.adoc#algorithms-page-rank-examples-stream[stream example] (which is not using `sourceNodes` configuration parameter) shows that the 'Site A' node  that we used in the `sourceNodes` list now scores second instead of fourth.


[[algorithms-page-rank-examples-scaler]]
=== Scaling centrality scores

To normalize the final scores as part of the algorithm execution, one can use the `scaler` configuration parameter.
A description of all available scalers can be found in the documentation for the xref:algorithms/scale-properties.adoc[`scaleProperties`] procedure.

[role=query-example]
--
.The following will run the algorithm in `stream` mode and returns normalized results:
[source, cypher, role=noplay]
----
CALL gds.pageRank.stream('myGraph', {
  scaler: "MEAN"
})
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name AS name, score
ORDER BY score DESC, name ASC
----

.Results
[opts="header",cols="1,1"]
|===
| name      | score
| "Home"    | 0.780671346390832
| "About"   | 0.032214422681946
| "Links"   | 0.032214422681946
| "Product" | 0.032214422681946
| "Site A"  | -0.219328653609168
| "Site B"  | -0.219328653609168
| "Site C"  | -0.219328653609168
| "Site D"  | -0.219328653609168
|===
--

Comparing the results with the xref:algorithms/page-rank.adoc#algorithms-page-rank-examples-stream[stream example], we can see that the relative order of scores is the same.
