[[algorithms-dijkstra-single-source]]
= Dijkstra Single-Source Shortest Path
:description: This section describes the Dijkstra Shortest Path algorithm in the Neo4j Graph Data Science library.
:entity: source-target-pair
:result: shortest path
:algorithm: Dijkstra
:source-target: false
:procedure-name: pass:q[gds.allShortestPaths.dijkstra]
:sequential: true


:directed:
:undirected:
:homogeneous:
:weighted:
include::partial$/algorithms/shared/algorithm-traits.adoc[]


[[algorithms-dijkstra-single-source-intro]]
== Introduction

The Dijkstra Shortest Path algorithm computes the shortest path between nodes.
The algorithm supports weighted graphs with positive relationship weights.
The Dijkstra Single-Source algorithm computes the shortest paths between a source node and all nodes reachable from that node.
To compute the shortest path between a source and a target node, xref:algorithms/dijkstra-source-target.adoc[Dijkstra Source-Target] can be used.

The GDS implementation is based on the http://www-m3.ma.tum.de/twiki/pub/MN0506/WebHome/dijkstra.pdf[original description] and uses a binary heap as priority queue.
The implementation is also used for the xref:algorithms/astar.adoc[A*] and xref:algorithms/yens.adoc[Yen's] algorithms, as well as xref:algorithms/betweenness-centrality.adoc[weighted Betweenness Centrality].
The algorithm implementation is executed using a single thread.
You can consider xref:algorithms/delta-single-source.adoc[Delta-Stepping] for an efficient parallel shortest path algorithm instead.

[[algorithms-dijkstra-single-source-syntax]]
== Syntax

include::partial$/algorithms/shortest-path/path-syntax.adoc[]


[[algorithms-dijkstra-single-source-examples]]
== Examples

:algorithm-name: {algorithm}
:graph-description: transport network
:image-file: dijkstra.svg
include::partial$/algorithms/shared/examples-intro.adoc[]

.The following Cypher statement will create the example graph in the Neo4j database:
[source, cypher, role=noplay setup-query]
----
CREATE (a:Location {name: 'A'}),
       (b:Location {name: 'B'}),
       (c:Location {name: 'C'}),
       (d:Location {name: 'D'}),
       (e:Location {name: 'E'}),
       (f:Location {name: 'F'}),
       (a)-[:ROAD {cost: 50}]->(b),
       (a)-[:ROAD {cost: 50}]->(c),
       (a)-[:ROAD {cost: 100}]->(d),
       (b)-[:ROAD {cost: 40}]->(d),
       (c)-[:ROAD {cost: 40}]->(d),
       (c)-[:ROAD {cost: 80}]->(e),
       (d)-[:ROAD {cost: 30}]->(e),
       (d)-[:ROAD {cost: 80}]->(f),
       (e)-[:ROAD {cost: 40}]->(f);
----

This graph builds a transportation network with roads between locations.
Like in the real world, the roads in the graph have different lengths.
These lengths are represented by the `cost` relationship property.

include::partial$/algorithms/shared/examples-named-native-note.adoc[]

.The following statement will project a graph using a native projection and store it in the graph catalog under the name 'myGraph'.
[source, cypher, role=noplay graph-project-query]
----
CALL gds.graph.project(
    'myGraph',
    'Location',
    'ROAD',
    {
        relationshipProperties: 'cost'
    }
)
----

In the following example we will demonstrate the use of the {algorithm} Shortest Path algorithm using this graph.


[[algorithms-dijkstra-single-source-examples-memory-estimation]]
=== Memory Estimation

:mode: write
include::partial$/algorithms/shared/examples-estimate-intro.adoc[]

[role=query-example]
--
.The following will estimate the memory requirements for running the algorithm in write mode:
[source, cypher, role=noplay]
----
MATCH (source:Location {name: 'A'})
CALL gds.allShortestPaths.dijkstra.write.estimate('myGraph', {
    sourceNode: source,
    relationshipWeightProperty: 'cost',
    writeRelationshipType: 'PATH'
})
YIELD nodeCount, relationshipCount, bytesMin, bytesMax, requiredMemory
RETURN nodeCount, relationshipCount, bytesMin, bytesMax, requiredMemory
----

.Results
[opts="header"]
|===
| nodeCount | relationshipCount | bytesMin | bytesMax | requiredMemory
| 6         | 9                 | 736      | 736      | "736 Bytes"
|===
--


[[algorithms-dijkstra-single-source-examples-stream]]
=== Stream

:!stream-details:
include::partial$/algorithms/shared/examples-stream-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm and stream results:
[source, cypher, role=noplay]
----
MATCH (source:Location {name: 'A'})
CALL gds.allShortestPaths.dijkstra.stream('myGraph', {
    sourceNode: source,
    relationshipWeightProperty: 'cost'
})
YIELD index, sourceNode, targetNode, totalCost, nodeIds, costs, path
RETURN
    index,
    gds.util.asNode(sourceNode).name AS sourceNodeName,
    gds.util.asNode(targetNode).name AS targetNodeName,
    totalCost,
    [nodeId IN nodeIds | gds.util.asNode(nodeId).name] AS nodeNames,
    costs,
    nodes(path) as path
ORDER BY index
----

.Results
[opts="header"]
|===
| index | sourceNodeName | targetNodeName | totalCost | nodeNames       | costs                             | path
| 0     | "A"            | "A"            | 0.0       | [A]             | [0.0]                             | [Node[0]]
| 1     | "A"            | "B"            | 50.0      | [A, B]          | [0.0, 50.0]                       | [Node[0], Node[1]]
| 2     | "A"            | "C"            | 50.0      | [A, C]          | [0.0, 50.0]                       | [Node[0], Node[2]]
| 3     | "A"            | "D"            | 90.0      | [A, B, D]       | [0.0, 50.0, 90.0]                 | [Node[0], Node[1], Node[3]]
| 4     | "A"            | "E"            | 120.0     | [A, B, D, E]    | [0.0, 50.0, 90.0, 120.0]          | [Node[0], Node[1], Node[3], Node[4]]
| 5     | "A"            | "F"            | 160.0     | [A, B, D, E, F] | [0.0, 50.0, 90.0, 120.0, 160.0]   | [Node[0], Node[1], Node[3], Node[4], Node[5]]
|===
--

The result shows the total cost of the shortest path between node `A` and all other reachable nodes in the graph.
It also shows ordered lists of node ids that were traversed to find the shortest paths as well as the accumulated costs of the visited nodes.
This can be verified in the xref:algorithms/dijkstra-single-source.adoc#algorithms-dijkstra-single-source-examples[example graph].
Cypher Path objects can be returned by the `path` return field.
The Path objects contain the node objects and virtual relationships which have a `cost` property.

[[algorithms-dijkstra-single-source-examples-mutate]]
=== Mutate

include::partial$/algorithms/shortest-path/path-examples-mutate-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `mutate` mode:
[source, cypher, role=noplay]
----
MATCH (source:Location {name: 'A'})
CALL gds.allShortestPaths.dijkstra.mutate('myGraph', {
    sourceNode: source,
    relationshipWeightProperty: 'cost',
    mutateRelationshipType: 'PATH'
})
YIELD relationshipsWritten
RETURN relationshipsWritten
----

.Results
[opts="header"]
|===
| relationshipsWritten
| 6
|===
--

After executing the above query, the in-memory graph will be updated with new relationships of type `PATH`.
The new relationships will store a single property `totalCost`.

[NOTE]
====
The relationships produced are always directed, even if the input graph is undirected.
====


[[algorithms-dijkstra-single-source-examples-write]]
=== Write

include::partial$/algorithms/shortest-path/path-examples-write-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `write` mode:
[source, cypher, role=noplay]
----
MATCH (source:Location {name: 'A'})
CALL gds.allShortestPaths.dijkstra.write('myGraph', {
    sourceNode: source,
    relationshipWeightProperty: 'cost',
    writeRelationshipType: 'PATH',
    writeNodeIds: true,
    writeCosts: true
})
YIELD relationshipsWritten
RETURN relationshipsWritten
----

.Results
[opts="header"]
|===
| relationshipsWritten
| 6
|===
--

The above query will write 6 relationships of type `PATH` back to Neo4j.
The relationships store three properties describing the path: `totalCost`, `nodeIds` and `costs`.

[NOTE]
====
The relationships written are always directed, even if the input graph is undirected.
====
