[[algorithms-wcc]]
= Weakly Connected Components
:description: This section describes the Weakly Connected Components (WCC) algorithm in the Neo4j Graph Data Science library.
:entity: node
:result: component ID
:algorithm: Weakly Connected Components


:directed:
:undirected:
:homogeneous:
:weighted:
include::partial$/algorithms/shared/algorithm-traits.adoc[]


[[algorithms-wcc-intro]]
== Introduction

The Weakly Connected Components (WCC) algorithm finds sets of connected nodes in directed and undirected graphs.
Two nodes are connected, if there exists a path between them.
The set of all nodes that are connected with each other form a component.
In contrast to Strongly Connected Components (SCC), the direction of relationships on the path between two nodes is not considered.
For example, in a directed graph `(a)->(b)`, `a` and `b` will be in the same component, even if there is no directed relationship `(b)->(a)`.

WCC is often used early in an analysis to understand the structure of a graph.
Using WCC to understand the graph structure enables running other algorithms independently on an identified cluster.

The implementation of the algorithm is based on the following papers:

* https://dl.acm.org/doi/pdf/10.1145/103418.103458[Wait-free Parallel Algorithms for the Union-Find Problem^]
* https://ieeexplore.ieee.org/document/8425156[Optimizing Parallel Graph Connectivity Computation via Subgraph Sampling^]


[[algorithms-wcc-syntax]]
== Syntax

include::partial$/algorithms/shared/syntax-intro-named-graph.adoc[]


.WCC syntax per mode
[.tabbed-example, caption = ]
====

[.include-with-stream]
======
.Run WCC in stream mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.wcc.stream(
  graphName: String,
  configuration: Map
)
YIELD
  nodeId: Integer,
  componentId: Integer
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-stream-stats-configuration-entries.adoc[]
include::partial$/algorithms/wcc/specific-configuration.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name        | Type    | Description
| nodeId      | Integer | Node ID.
| componentId | Integer | Component ID.
|===
======


[.include-with-stats]
======
.Run WCC in stats mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.wcc.stats(
  graphName: String,
  configuration: Map
)
YIELD
  componentCount: Integer,
  preProcessingMillis: Integer,
  computeMillis: Integer,
  postProcessingMillis: Integer,
  componentDistribution: Map,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-stream-stats-configuration-entries.adoc[]
include::partial$/algorithms/wcc/specific-configuration.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                  | Type    | Description
| componentCount        | Integer | The number of computed components.
| preProcessingMillis   | Integer | Milliseconds for preprocessing the data.
| computeMillis         | Integer | Milliseconds for running the algorithm.
| postProcessingMillis  | Integer | Milliseconds for computing component count and distribution statistics.
| componentDistribution | Map     | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of component sizes.
| configuration         | Map     | The configuration used for running the algorithm.
|===
======


[.include-with-mutate]
======
.Run WCC in mutate mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.wcc.mutate(
  graphName: String,
  configuration: Map
)
YIELD
  componentCount: Integer,
  nodePropertiesWritten: Integer,
  preProcessingMillis: Integer,
  computeMillis: Integer,
  mutateMillis: Integer,
  postProcessingMillis: Integer,
  componentDistribution: Map,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-mutate-configuration-entries.adoc[]
include::partial$/algorithms/wcc/specific-configuration.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                          | Type    | Description
| componentCount                | Integer | The number of computed components.
| nodePropertiesWritten         | Integer | The number of node properties written.
| preProcessingMillis           | Integer | Milliseconds for preprocessing the data.
| computeMillis                 | Integer | Milliseconds for running the algorithm.
| mutateMillis                  | Integer | Milliseconds for adding properties to the projected graph.
| postProcessingMillis          | Integer | Milliseconds for computing component count and distribution statistics.
| componentDistribution         | Map     | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of component sizes.
| configuration                 | Map     | The configuration used for running the algorithm.
|===
======


[.include-with-write]
======
.Run WCC in write mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.wcc.write(
  graphName: String,
  configuration: Map
)
YIELD
  componentCount: Integer,
  nodePropertiesWritten: Integer,
  preProcessingMillis: Integer,
  computeMillis: Integer,
  writeMillis: Integer,
  postProcessingMillis: Integer,
  componentDistribution: Map,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name          | Type   | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-write-configuration-entries.adoc[]
include::partial$/algorithms/wcc/specific-configuration.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                          | Type    | Description
| componentCount                | Integer | The number of computed components.
| nodePropertiesWritten         | Integer | The number of node properties written.
| preProcessingMillis           | Integer | Milliseconds for preprocessing the data.
| computeMillis                 | Integer | Milliseconds for running the algorithm.
| writeMillis                   | Integer | Milliseconds for writing result back to Neo4j.
| postProcessingMillis          | Integer | Milliseconds for computing component count and distribution statistics.
| componentDistribution         | Map     | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of component sizes.
| configuration                 | Map     | The configuration used for running the algorithm.
|===
======
====


[[algorithms-wcc-examples]]
== Examples

:algorithm-name: {algorithm}
:graph-description: user network
:image-file: wcc-graph.svg
include::partial$/algorithms/shared/examples-intro.adoc[]

.The following Cypher statement will create the example graph in the Neo4j database:
[source, cypher, role=noplay setup-query]
----
CREATE
  (nAlice:User {name: 'Alice'}),
  (nBridget:User {name: 'Bridget'}),
  (nCharles:User {name: 'Charles'}),
  (nDoug:User {name: 'Doug'}),
  (nMark:User {name: 'Mark'}),
  (nMichael:User {name: 'Michael'}),

  (nAlice)-[:LINK {weight: 0.5}]->(nBridget),
  (nAlice)-[:LINK {weight: 4}]->(nCharles),
  (nMark)-[:LINK {weight: 1.1}]->(nDoug),
  (nMark)-[:LINK {weight: 2}]->(nMichael);
----

This graph has two connected components, each with three nodes.
The relationships that connect the nodes in each component have a property `weight` which determines the strength of the relationship.

include::partial$/algorithms/shared/examples-named-native-note.adoc[]

.The following statement will project a graph using a native projection and store it in the graph catalog under the name 'myGraph'.
[source, cypher, role=noplay graph-project-query]
----
CALL gds.graph.project(
  'myGraph',
  'User',
  'LINK',
  {
    relationshipProperties: 'weight'
  }
)
----

In the following examples we will demonstrate using the Weakly Connected Components algorithm on this graph.

[[algorithms-wcc-examples-memory-estimation]]
=== Memory Estimation

:mode: write
include::partial$/algorithms/shared/examples-estimate-intro.adoc[]

[role=query-example]
--
.The following will estimate the memory requirements for running the algorithm in write mode:
[source, cypher, role=noplay]
----
CALL gds.wcc.write.estimate('myGraph', { writeProperty: 'component' })
YIELD nodeCount, relationshipCount, bytesMin, bytesMax, requiredMemory
----

.Results
[opts="header"]
|===
| nodeCount | relationshipCount | bytesMin | bytesMax | requiredMemory
| 6         | 4                 | 112      | 112      | "112 Bytes"
|===
--


[[algorithms-wcc-examples-stream]]
=== Stream

:stream-details: For example, we can order the results to see the nodes that belong to the same component displayed next to each other.
include::partial$/algorithms/shared/examples-stream-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm and stream results:
[source, cypher, role=noplay]
----
CALL gds.wcc.stream('myGraph')
YIELD nodeId, componentId
RETURN gds.util.asNode(nodeId).name AS name, componentId
ORDER BY componentId, name
----

.Results
[opts="header"]
|===
| name      | componentId
| "Alice"   | 0
| "Bridget" | 0
| "Charles" | 0
| "Doug"    | 3
| "Mark"    | 3
| "Michael" | 3
|===
--

The result shows that the algorithm identifies two components.
This can be verified in the xref:algorithms/wcc.adoc#algorithms-wcc-examples[example graph].

The default behaviour of the algorithm is to run `unweighted`, e.g. without using `relationship` weights.
The `weighted` option will be demonstrated in xref:algorithms/wcc.adoc#algorithms-wcc-examples-weighted[Weighted]

[[algorithms-wcc-examples-stats]]
=== Stats

:stats-syntax: algorithms-wcc-syntax
include::partial$/algorithms/shared/examples-stats-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `stats` mode:
[source, cypher, role=noplay]
----
CALL gds.wcc.stats('myGraph')
YIELD componentCount
----

.Results
[opts="header"]
|===
| componentCount
| 2
|===
--

The result shows that `myGraph` has two components and this can be verified by looking at the xref:algorithms/wcc.adoc#algorithms-wcc-examples[example graph].


[[algorithms-wcc-examples-mutate]]
=== Mutate

include::partial$/algorithms/shared/examples-mutate-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `mutate` mode:
[source, cypher, role=noplay]
----
CALL gds.wcc.mutate('myGraph', { mutateProperty: 'componentId' })
YIELD nodePropertiesWritten, componentCount;
----

.Results
[opts="header"]
|===
| nodePropertiesWritten | componentCount
| 6                     | 2
|===
--

[[algorithms-wcc-examples-write]]
=== Write

include::partial$/algorithms/shared/examples-write-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `write` mode:
[source, cypher, role=noplay]
----
CALL gds.wcc.write('myGraph', { writeProperty: 'componentId' })
YIELD nodePropertiesWritten, componentCount;
----

.Results
[opts="header"]
|===
| nodePropertiesWritten | componentCount
| 6                     | 2
|===
--

As we can see from the results, the nodes connected to one another are calculated by the algorithm as belonging to the same connected component.


[[algorithms-wcc-examples-weighted]]
=== Weighted

By configuring the algorithm to use a weight we can increase granularity in the way the algorithm calculates component assignment.
We do this by specifying the property key with the `relationshipWeightProperty` configuration parameter.
Additionally, we can specify a threshold for the weight value.
Then, only weights greater than the threshold value will be considered by the algorithm.
We do this by specifying the threshold value with the `threshold` configuration parameter.

If a relationship does not have the specified weight property, the algorithm falls back to using a default value of zero.

[role=query-example]
--
.The following will run the algorithm and stream results:
[source, cypher, role=noplay]
----
CALL gds.wcc.stream('myGraph', {
  relationshipWeightProperty: 'weight',
  threshold: 1.0
}) YIELD nodeId, componentId
RETURN gds.util.asNode(nodeId).name AS Name, componentId AS ComponentId
ORDER BY ComponentId, Name
----

.Results
[opts="header",cols="1m,1m"]
|===
| Name      | ComponentId
| "Alice"   | 0
| "Charles" | 0
| "Bridget" | 1
| "Doug"    | 3
| "Mark"    | 3
| "Michael" | 3
|===
--

As we can see from the results, the node named 'Bridget' is now in its own component, due to its relationship weight being less than the configured threshold and thus ignored.

NOTE: We are using stream mode to illustrate running the algorithm as weighted or unweighted, all the other  algorithm modes also support this configuration parameter.

[[algorithms-wcc-examples-seeding]]
=== Seeded components

It is possible to define preliminary component IDs for nodes using the `seedProperty` configuration parameter.
This is helpful if we want to retain components from a previous run and it is known that no components have been split by removing relationships.
The property value needs to be a number.

The algorithm first checks if there is a seeded component ID assigned to the node.
If there is one, that component ID is used.
Otherwise, a new unique component ID is assigned to the node.

Once every node belongs to a component, the algorithm merges components of connected nodes.
When components are merged, the resulting component is always the one with the lower component ID.
Note that the `consecutiveIds` configuration option cannot be used in combination with seeding in order to retain the seeding values.

[NOTE]
====
The algorithm assumes that nodes with the same seed value do in fact belong to the same component.
If any two nodes in different components have the same seed, behavior is undefined.
It is then recommended running WCC without seeds.
====

[[algorithms-wcc-examples-seeding-steps]]
To demonstrate this in practice, we will go through a few steps:

. We will run the algorithm and write the results to Neo4j.
. Then we will add another node to our graph, this node will not have the property computed in Step 1.
. We will project a new graph that has the result from Step 1 as `nodeProperty`
. And then we will run the algorithm again, this time in `stream` mode, and we will use the `seedProperty` configuration parameter.

We will use the weighted variant of WCC.

_Step 1_
[role=query-example, group=seeding]
--
.The following will run the algorithm in `write` mode:
[source, cypher, role=noplay]
----
CALL gds.wcc.write('myGraph', {
  writeProperty: 'componentId',
  relationshipWeightProperty: 'weight',
  threshold: 1.0
})
YIELD nodePropertiesWritten, componentCount;
----

.Results
[opts="header"]
|===
| nodePropertiesWritten | componentCount
| 6                     | 3
|===
--

_Step 2_

After the algorithm has finished writing to Neo4j we want to create a new node in the database.

[role=query-example, no-result=true, group=seeding]
--
.The following will create a new node in the Neo4j graph, with no component ID:
[source, cypher, role=noplay]
----
MATCH (b:User {name: 'Bridget'})
CREATE (b)-[:LINK {weight: 2.0}]->(new:User {name: 'Mats'})
----
--

_Step 3_

Note, that we cannot use our already projected graph as it does not contain the component id.
We will therefore project a second graph that contains the previously computed component id.

[role=query-example, no-result=true, group=seeding]
--
.The following will project a new graph containing the previously computed component id:
[source, cypher, role=noplay]
----
CALL gds.graph.project(
  'myGraph-seeded',
  'User',
  'LINK',
  {
    nodeProperties: 'componentId',
    relationshipProperties: 'weight'
  }
)
----
--

_Step 4_


[role=query-example, group=seeding]
--
.The following will run the algorithm in `stream` mode using `seedProperty`:
[source, cypher, role=noplay]
----
CALL gds.wcc.stream('myGraph-seeded', {
  seedProperty: 'componentId',
  relationshipWeightProperty: 'weight',
  threshold: 1.0
}) YIELD nodeId, componentId
RETURN gds.util.asNode(nodeId).name AS name, componentId
ORDER BY componentId, name
----

.Results
[opts="header"]
|===
| name      | componentId
| "Alice"   | 0
| "Charles" | 0
| "Bridget" | 1
| "Mats"    | 1
| "Doug"    | 3
| "Mark"    | 3
| "Michael" | 3
|===
--

The result shows that despite not having the `seedProperty` when it was projected, the node 'Mats' has been assigned to the same component as the node 'Bridget'.
This is correct because these two nodes are connected.

[[algorithms-wcc-examples-seed-write]]
=== Writing Seeded components

In the xref:algorithms/wcc.adoc#algorithms-wcc-examples-seeding[previous section] we demonstrated the `seedProperty` usage in `stream` mode.
It is also available in the other modes of the algorithm.
Below is an example on how to use `seedProperty` in `write` mode.
Note that the example below relies on xref:algorithms/wcc.adoc#algorithms-wcc-examples-seeding-steps[Steps 1 - 3] from the previous section.

[role=query-example, group=seeding]
--
.The following will run the algorithm in `write` mode using `seedProperty`:
[source, cypher, role=noplay]
----
CALL gds.wcc.write('myGraph-seeded', {
  seedProperty: 'componentId',
  writeProperty: 'componentId',
  relationshipWeightProperty: 'weight',
  threshold: 1.0
})
YIELD nodePropertiesWritten, componentCount;
----

.Results
[opts="header"]
|===
| nodePropertiesWritten | componentCount
| 1                     | 3
|===
--

[NOTE]
====
If the `seedProperty` configuration parameter has the same value as `writeProperty`, the algorithm only writes properties for nodes where the component ID has changed.
If they differ, the algorithm writes properties for all nodes.
====


[[algorithms-wcc-inverse-index]]
=== Graph Sampling optimization

The WCC implementation provides two compute strategies:

* The unsampled strategy as described in https://dl.acm.org/doi/pdf/10.1145/103418.103458[Wait-free Parallel Algorithms for the Union-Find Problem^].
* The sampled strategy as described in https://ieeexplore.ieee.org/document/8425156[Optimizing Parallel Graph Connectivity Computation via Subgraph Sampling^]

While both strategies provide very good performance, the sampled strategy is usually the faster one.
The decision, which strategy to use, depends on the input graph.
If the relationships of the graph are ...

* ... undirected, the algorithm picks the sampled strategy.
* ... directed, the algorithm picks the unsampled strategy.
* ... directed and _inverse indexed_, the algorithm picks the sampled strategy.

The direction of a relationship is defined by the `orientation` which can be set during a graph projection.
While `NATURAL` and `REVERSE` orientation result in a directed graph, the `UNDIRECTED` orientation leads to undirected relationships.
In order to create a directed graph with inverse indexed relationships, one can use the `indexInverse` parameter as part of the relationship projection.
An inverse index allows the algorithm to traverse the relationships of a node according to the opposite orientation.
If the graph is projected using a `NATURAL` orientation, the inverse index represents the `REVERSE` orientation and vice versa.


.The following statement will project the above example graph using a native projection with inverse index  and store it in the graph catalog under the name `myIndexedGraph`.
[source, cypher, role=noplay graph-project-query]
----
CALL gds.graph.project(
  'myIndexedGraph',
  'User',
  {LINK: {orientation: 'NATURAL', indexInverse: true }}
)
----

The following query is identical to the stream example in the xref:algorithms/wcc.adoc#algorithms-wcc-examples-seeding[previous section].
This time, we execute WCC on `myIndexedGraph` which will allow the algorithm to use the sampled strategy.

[role=query-example]
--
.The following will run the algorithm with sampled strategy and stream results:
[source, cypher, role=noplay]
----
CALL gds.wcc.stream('myIndexedGraph', {concurrency: 1})
YIELD nodeId, componentId
RETURN gds.util.asNode(nodeId).name AS name, componentId
ORDER BY componentId, name
----

.Results
[opts="header"]
|===
| name      | componentId
| "Alice"   | 0
| "Bridget" | 0
| "Charles" | 0
| "Doug"    | 3
| "Mark"    | 3
| "Michael" | 3
|===
--

[NOTE]
====
Because of the randomness in the Graph sampling optimization we are using `concurrency: 1` in the example.
Running it with higher concurrency may yield different componentIds but the actual components should stay the same.
For our case here it would be equally plausible to get the inverse solution, f.i. when our community `0` nodes are mapped to community `3` instead, and vice versa.
====
