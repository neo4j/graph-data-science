[[algorithms-modularity-optimization]]
[.beta]
= Modularity Optimization
:description: This section describes the Modularity Optimization algorithm in the Neo4j Graph Data Science library.


include::partial$/operations-reference/beta-note.adoc[]


[[algorithms-modularity-optimization-intro]]
== Introduction

The Modularity Optimization algorithm tries to detect communities in the graph based on their _modularity_.
_Modularity_ is a measure of the structure of a graph, measuring the density of connections within a module or community.
Graphs with a high modularity score will have many connections within a community but only few pointing outwards to other communities.
The algorithm will explore for every node if its modularity score might increase if it changes its community to one of its neighboring nodes.

For more information on this algorithm, see:

* https://arxiv.org/pdf/cond-mat/0308217.pdf[MEJ Newman, M Girvan "Finding and evaluating community structure in networks"^]
* https://++en.wikipedia.org/wiki/Modularity_(networks)++

[NOTE]
====
Running this algorithm requires sufficient memory availability.
Before running this algorithm, we recommend that you read xref:common-usage/memory-estimation.adoc[Memory Estimation].
====

[[algorithms-modularity-optimization-syntax]]
== Syntax

.Modularity Optimization syntax per mode
[.tabbed-example, caption = ]
====

[.include-with-stream]
======
.Run Modularity Optimization in stream mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.beta.modularityOptimization.stream(graphName: String, configuration: Map)
YIELD
  nodeId: Integer,
  communityId: Integer
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

include::partial$/algorithms/common-configuration/common-configuration.adoc[]

.Algorithm specific configuration
[opts="header",cols="1,1,1,1,4"]
|===
| Name                                                                             | Type    | Default                | Optional | Description
| xref:common-usage/running-algos.adoc#common-configuration-max-iterations[maxIterations]                            | Integer | 10                     | yes      | The maximum number of iterations to run.
| xref:common-usage/running-algos.adoc#common-configuration-tolerance[tolerance]                                     | Float   | 0.0001                 | yes      | Minimum change in modularity between iterations. If the modularity changes less than the tolerance value, the result is considered stable and the algorithm returns.
| seedProperty                                                                     | String  | n/a                    | yes      | Used to define initial set of labels (must be a non-negative number).
| consecutiveIds                                                                   | Boolean | false                  | yes      | Flag to decide whether component identifiers are mapped into a consecutive id space (requires additional memory).
| xref:common-usage/running-algos.adoc#common-configuration-relationship-weight-property[relationshipWeightProperty] | String  | null                   | yes      | Name of the relationship property to use as weights. If unspecified, the algorithm runs unweighted.
|===

.Results
[opts="header"]
|===
| Name        | Type    | Description
| nodeId      | Integer | Node ID
| communityId | Integer | Community ID
|===
======


[.include-with-mutate]
======
.Run Modularity Optimization in mutate mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.beta.modularityOptimization.mutate(graphName: String, configuration: Map})
YIELD
  preProcessingMillis: Integer,
  computeMillis: Integer,
  postProcessingMillis: Integer,
  mutateMillis: Integer,
  communityCount: Integer,
  communityDistribution: Map,
  modularity: Float,
  ranIterations: Integer,
  didConverge: Boolean,
  nodes: Integer,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

The configuration for the `mutate` mode is similar to the `write` mode.
Instead of specifying a `writeProperty`, we need to specify a `mutateProperty`.
Also, specifying `writeConcurrency` is not possible in `mutate` mode.

.Results
[opts="header",cols="1,1,6"]
|===
| Name                  | Type      | Description
| preProcessingMillis   | Integer   | Milliseconds for preprocessing the data.
| computeMillis         | Integer   | Milliseconds for running the algorithm.
| mutateMillis          | Integer   | Milliseconds for adding properties to the projected graph.
| postProcessingMillis  | Integer   | Milliseconds for computing percentiles and community count.
| nodes                 | Integer   | The number of nodes considered.
| didConverge           | Boolean   | True if the algorithm did converge to a stable modularity score within the provided number of maximum iterations.
| ranIterations         | Integer   | The number of iterations run.
| modularity            | Float     | The final modularity score.
| communityCount        | Integer   | The number of communities found.
| communityDistribution | Map       | The containing min, max, mean as well as 50, 75, 90, 95, 99 and 999 percentile of community size.
| configuration         | Map       | The configuration used for running the algorithm.
|===
======


[.include-with-write]
======
.Run Modularity Optimization in write mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.beta.modularityOptimization.write(graphName: String, configuration: Map})
YIELD
  preProcessingMillis: Integer,
  computeMillis: Integer,
  postProcessingMillis: Integer,
  writeMillis: Integer,
  communityCount: Integer,
  communityDistribution: Map,
  modularity: Float,
  ranIterations: Integer,
  didConverge: Boolean,
  nodes: Integer,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

include::partial$/algorithms/common-configuration/common-configuration.adoc[]

.Algorithm specific configuration
[opts="header",cols="1,1,1m,1,4"]
|===
| Name                                                                             | Type    | Default     | Optional | Description
| xref:common-usage/running-algos.adoc#common-configuration-seed-property[seedProperty]                              | String  | n/a         | yes      | Used to set the initial community for a node. The property value needs to be a number.
| xref:common-usage/running-algos.adoc#common-configuration-write-property[writeProperty]                            | String  | n/a         | yes      | The property name written back the ID of the partition particular node belongs to.
| xref:common-usage/running-algos.adoc#common-configuration-max-iterations[maxIterations]                            | Integer | 10          | yes      | The maximum number of iterations that the modularity optimization will run for each level.
| xref:common-usage/running-algos.adoc#common-configuration-tolerance[tolerance]                                     | Float   | 0.0001      | yes      | Minimum change in modularity between iterations. If the modularity changes less than the tolerance value, the result is considered stable and the algorithm returns.
| consecutiveIds                                                                   | Boolean | false       | yes      | Flag to decide whether component identifiers are mapped into a consecutive id space (requires additional memory).
| xref:common-usage/running-algos.adoc#common-configuration-relationship-weight-property[relationshipWeightProperty] | String  | null        | yes      | Name of the relationship property to use as weights. If unspecified, the algorithm runs unweighted.
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                  | Type      | Description
| preProcessingMillis   | Integer   | Milliseconds for preprocessing the data.
| computeMillis         | Integer   | Milliseconds for running the algorithm.
| writeMillis           | Integer   | Milliseconds for writing result data back.
| postProcessingMillis  | Integer   | Milliseconds for computing percentiles and community count.
| nodes                 | Integer   | The number of nodes considered.
| didConverge           | Boolean   | True if the algorithm did converge to a stable modularity score within the provided number of maximum iterations.
| ranIterations         | Integer   | The number of iterations run.
| modularity            | Float     | The final modularity score.
| communityCount        | Integer   | The number of communities found.
| communityDistribution | Map       | The containing min, max, mean as well as 50, 75, 90, 95, 99 and 999 percentile of community size.
| configuration         | Map       | The configuration used for running the algorithm.
|===
======
====


[[algorithms-modularity-optimization-examples]]
== Examples

Consider the graph created by the following Cypher statement:

[source, cypher, role=noplay setup-query]
----
CREATE
  (a:Person {name:'Alice'})
, (b:Person {name:'Bridget'})
, (c:Person {name:'Charles'})
, (d:Person {name:'Doug'})
, (e:Person {name:'Elton'})
, (f:Person {name:'Frank'})
, (a)-[:KNOWS {weight: 0.01}]->(b)
, (a)-[:KNOWS {weight: 5.0}]->(e)
, (a)-[:KNOWS {weight: 5.0}]->(f)
, (b)-[:KNOWS {weight: 5.0}]->(c)
, (b)-[:KNOWS {weight: 5.0}]->(d)
, (c)-[:KNOWS {weight: 0.01}]->(e)
, (f)-[:KNOWS {weight: 0.01}]->(d)
----

This graph consists of two center nodes "Alice" and "Bridget" each of which have two more neighbors.
Additionally, each neighbor of "Alice" is connected to one of the neighbors of "Bridget".
Looking at the weights of the relationships, it can be seen that the connections from the two center nodes to their neighbors are very strong, while connections between those groups are weak.
Therefore the Modularity Optimization algorithm should detect two communities: "Alice" and "Bob" together with their neighbors respectively.


include::partial$/algorithms/shared/examples-named-native-note.adoc[]

.The following statement will project the graph and store it in the graph catalog.
[source, cypher, role=noplay graph-project-query]
----
CALL gds.graph.project(
    'myGraph',
    'Person',
    {
        KNOWS: {
            orientation: 'UNDIRECTED',
            properties: ['weight']
        }
    })
----

The following example demonstrates using the Modularity Algorithm on this weighted graph.

[role=query-example]
--
.Running the Modularity Optimization algorithm in stream mode:
[source, cypher, role=noplay]
----
CALL gds.beta.modularityOptimization.stream('myGraph', { relationshipWeightProperty: 'weight' })
YIELD nodeId, communityId
RETURN gds.util.asNode(nodeId).name AS name, communityId
ORDER BY name
----

.Results
[opts="header"]
|===
| name      | communityId
| "Alice"   | 4
| "Bridget" | 1
| "Charles" | 1
| "Doug"    | 1
| "Elton"   | 4
| "Frank"   | 4
|===
--

It is also possible to write the assigned community ids back to the database using the `write` mode.

[role=query-example]
--
.Running the Modularity Optimization algorithm in write mode:
[source, cypher, role=noplay]
----
CALL gds.beta.modularityOptimization.write('myGraph', { relationshipWeightProperty: 'weight', writeProperty: 'community' })
YIELD nodes, communityCount, ranIterations, didConverge
----

.Results
[opts="header"]
|===
| nodes | communityCount | ranIterations | didConverge
| 6     | 2              | 2             | true
|===
--

When using `write` mode the procedure will return information about the algorithm execution.
In this example we return the number of processed nodes, the number of communities assigned to the nodes in the graph, the number of iterations and information whether the algorithm converged.

Running the algorithm without specifying the `relationshipWeightProperty` will default all relationship weights to 1.0.

To instead mutate the in-memory graph with the assigned community ids, the `mutate` mode is used.

[role=query-example]
--
.Running the Modularity Optimization algorithm in mutate mode:
[source, cypher, role=noplay]
----
CALL gds.beta.modularityOptimization.mutate('myGraph', { relationshipWeightProperty: 'weight', mutateProperty: 'community' })
YIELD nodes, communityCount, ranIterations, didConverge
----

.Results
[opts="header"]
|===
| nodes | communityCount | ranIterations | didConverge
| 6     | 2              | 2             | true
|===
--

When using `mutate` mode the procedure will return information about the algorithm execution as in `write` mode.
