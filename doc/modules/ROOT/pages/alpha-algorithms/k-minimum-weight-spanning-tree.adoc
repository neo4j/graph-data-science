[[alpha-algorithms-k-spanning-tree]]
[.alpha]
= Minimum Weight k-Spanning Tree
:description: This section describes the Minimum Weight k-Spanning Tree algorithm in the Neo4j Graph Data Science library.
:entity: node
:result: spanning tree
:algorithm: k-Spanning Tree heuristic

include::partial$/operations-reference/alpha-note.adoc[]


== Introduction

Sometimes, we might require a spanning tree(a tree where its nodes are connected with each via a single path) that does not necessarily span all nodes in the graph.
The K-Spanning tree heuristic algorithm returns a tree with `k` nodes and `k âˆ’ 1` relationships.
Our heuristic processes the result found by Prim's algorithm for the  xref:algorithms/minimum-weight-spanning-tree.adoc[Minimum Weight Spanning Tree] problem.
Like Prim, it starts from a given source node, finds a spanning tree for all nodes and then removes nodes using heuristics to produce a tree with 'k' nodes.
Note that the source node will not be necessarily included in the final output as the heuristic tries to find a globally good tree.

[[algorithms-k-spanning]]
== Considerations
The Minimum weight k-Spanning Tree is NP-Hard. The algorithm in the Neo4j GDS Library is therefore not guaranteed to find the optimal answer, but should hopefully return a good approximation in practice.

Like Prim algorithm, the algorithm focuses only on the component of the source node. If that component has fewer than `k` nodes, it will not look into other components, but will instead return the component.

[[algorithms-minimum-k-weight-spanning-tree-syntax]]
== Syntax

include::partial$/algorithms/shared/syntax-intro-named-graph.adoc[]

.K Spanning Tree syntax per mode
[.tabbed-example, caption = ]
====

[.include-with-write]
======
.The following will run the k-spanning tree algorithms and write back results:
[source, cypher, role=noplay]
----
CALL gds.alpha.kSpanningTree.write(
  graphName: String,
  configuration: Map
)
YIELD effectiveNodeCount: Integer,
      preProcessingMillis: Integer,
      computeMillis: Integer,
      postProcessingMillis: Integer,
      writeMillis: Integer,
      configuration: Map
----
include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name                                                                             | Type    | Default   | Optional  | Description
include::partial$/algorithms/common-configuration/common-write-configuration-entries.adoc[]
include::partial$/algorithms/k-spanning-tree/specific-configuration.adoc[]

|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                | Type    | Description
| effectiveNodeCount  | Integer | The number of visited nodes.
| preProcessingMillis | Integer | Milliseconds for preprocessing the data.
| computeMillis       | Integer | Milliseconds for running the algorithm.
| postProcessingMillis | Integer | Milliseconds for postprocessing results of the algorithm.
| writeMillis         | Integer | Milliseconds for writing result data back.
| configuration         | Map       | The configuration used for running the algorithm.

|===
======
====

[[algorithms-minimum-weight-spanning-tree-sample]]
== Minimum Weight k-Spanning Tree algorithm examples

:algorithm-name: {algorithm}
:graph-description: road network
:image-file: spanning-tree-graph.svg
include::partial$/algorithms/shared/examples-intro.adoc[]

.The following will create the sample graph depicted in the figure:
[source, cypher, role=noplay setup-query]
----
CREATE (a:Place {id: 'A'}),
       (b:Place {id: 'B'}),
       (c:Place {id: 'C'}),
       (d:Place {id: 'D'}),
       (e:Place {id: 'E'}),
       (f:Place {id: 'F'}),
       (g:Place {id: 'G'}),
       (d)-[:LINK {cost:4}]->(b),
       (d)-[:LINK {cost:6}]->(e),
       (b)-[:LINK {cost:1}]->(a),
       (b)-[:LINK {cost:3}]->(c),
       (a)-[:LINK {cost:2}]->(c),
       (c)-[:LINK {cost:5}]->(e),
       (f)-[:LINK {cost:1}]->(g);
----

.The following will project and store a named graph:
[source, cypher, role=noplay graph-project-query]
----
CALL gds.graph.project(
  'graph',
  'Place',
  {
    LINK: {
      properties: 'cost',
      orientation: 'UNDIRECTED'
    }
  }
)
----

[[algorithms-minimum-weight-spanning-tree-k]]
== K-Spanning tree examples

=== Minimum K-Spanning Tree example

In our sample graph we have 7 nodes.
By setting the `k=3`, we define that we want to find a 3-minimum spanning tree that covers 3 nodes and has 2 relationships.

.The following will run the k-minimum spanning tree algorithm and write back results:
[role=query-example, no-result=true, group=write-example]
--
[source, cypher, role=noplay]
----
MATCH (n:Place{id: 'A'})
CALL gds.alpha.kSpanningTree.write('graph', {
  k: 3,
  sourceNode: id(n),
  relationshipWeightProperty: 'cost',
  writeProperty:'kmin'
})
YIELD preProcessingMillis, computeMillis, writeMillis, effectiveNodeCount
RETURN preProcessingMillis,computeMillis,writeMillis, effectiveNodeCount;
----
--

[role=query-example, group=write-example]
--
.The following will find the nodes that belong to our k-spanning tree result:
[source, cypher, role=noplay]
----
MATCH (n)
WITH n.kmin AS p, count(n) AS c
WHERE c = 3
MATCH (n)
WHERE n.kmin = p
RETURN n.id As Place, p as Partition

----

.Results
[opts="header",cols="1,1"]
|===
| Place | Partition
| "A"     | 0
| "B"     | 0
| "C"     | 0
|===
--
Nodes A, B, and C form the discovered 3-minimum spanning tree of our graph.

=== Maximum K-Spanning Tree example

[role=query-example,no-result=true, group=max-example]
--
.The following will run the k-maximum spanning tree algorithm and write back results:
[source, cypher, role=noplay]
----
MATCH (n:Place{id: 'D'})
CALL gds.alpha.kSpanningTree.write('graph', {
  k: 3,
  sourceNode: id(n),
  relationshipWeightProperty: 'cost',
  writeProperty:'kmax',
  objective: 'maximum'
})
YIELD preProcessingMillis, computeMillis, writeMillis, effectiveNodeCount
RETURN preProcessingMillis,computeMillis,writeMillis, effectiveNodeCount;
----
--

[role=query-example, group=max-example]
--
.Find nodes that belong to our k-spanning tree result:
[source, cypher, role=noplay]
----
MATCH (n)
WITH n.kmax AS p, count(n) AS c
WHERE c = 3
MATCH (n)
WHERE n.kmax = p
RETURN n.id As Place, p as Partition
----
.Results
[opts="header",cols="1,1"]
|===
| Place | Partition
| "C"     | 3
| "D"     | 3
| "E"     | 3
|===
--
Nodes C, D, and E form a 3-maximum spanning tree of our graph.
