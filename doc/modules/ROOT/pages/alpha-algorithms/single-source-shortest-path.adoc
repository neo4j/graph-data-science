[[alpha-algorithms-single-source-shortest-path]]
[.alpha]
= Single Source Shortest Path
:description: This section describes the Single Source Shortest Path algorithm in the Neo4j Graph Data Science library.
:algorithm: Single Source Shortest Path

The Single Source Shortest Path (SSSP) algorithm calculates the shortest (weighted) path from a node to all other nodes in the graph.

include::partial$/algorithms/alpha/alpha-note.adoc[]


[[algorithms-single-source-shortest-path-context]]
== History and explanation

SSSP came into prominence at the same time as the shortest path algorithm and Dijkstra's algorithm can act as an implementation for both problems.

We implement a delta-stepping algorithm that has been https://arxiv.org/pdf/1604.02113v1.pdf[shown to outperform Dijkstra's^].


[[algorithms-single-source-shortest-path-usecase]]
== Use-cases - when to use the Single Source Shortest Path algorithm

* https://en.wikipedia.org/wiki/Open_Shortest_Path_First[Open Shortest Path First^] is a routing protocol for IP networks.
It uses Dijkstra's algorithm to help detect changes in topology, such as link failures, and https://routing-bits.com/2009/08/06/ospf-convergence/[come up with a new routing structure in seconds^].


[[algorithms-single-source-shortest-path-limitations]]
== Constraints - when not to use the Single Source Shortest Path algorithm

Delta stepping does not support negative weights.
The algorithm assumes that adding a relationship to a path can never make a path shorter - an invariant that would be violated with negative weights.


[[algorithms-single-source-shortest-path-syntax]]
== Syntax

.The following will run the algorithm and write back results:
[source, cypher, role=noplay]
----
CALL gds.alpha.shortestPath.deltaStepping.write(configuration: Map)
YIELD nodeCount, loadDuration, evalDuration, writeDuration
----

.Configuration
[opts="header",cols="1,1,1,1,4"]
|===
| Name                                                                             | Type    | Default  | Optional | Description
| startNode                                                                        | Node    | null     | no       | The start node
| xref::common-usage/running-algos.adoc#common-configuration-relationship-weight-property[relationshipWeightProperty] | String  | null     | yes      | Name of the relationship property to use as weights. If unspecified, the algorithm runs unweighted.
| delta                                                                            | Float   | null     | yes      | The grade of concurrency to use.
| writeProperty                                                                    | String  | 'sssp'   | yes      | The property name written back to the node sequence of the node in the path. The property contains the cost it takes to get from the start node to the specific node.
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name          | Type    | Description
| nodeCount     | Integer | The number of nodes considered
| loadDuration  | Integer | Milliseconds for loading data
| evalDuration  | Integer | Milliseconds for running the algorithm
| writeDuration | Integer | Milliseconds for writing result data back
|===


.The following will run the algorithm and stream results:
[source, cypher, role=noplay]
----
CALL gds.alpha.shortestPath.deltaStepping.stream(configuration: Map)
YIELD nodeId, distance
----

.Parameters
[opts="header",cols="1,1,1,1,4"]
|===
| Name                                                                             | Type   | Default  | Optional | Description
| startNode                                                                        | Node   | null     | no       | The start node
| delta                                                                            | Float  | null     | no       | The grade of concurrency to use.
| xref::common-usage/running-algos.adoc#common-configuration-relationship-weight-property[relationshipWeightProperty] | String | null     | yes      | Name of the relationship property to use as weights. If unspecified, the algorithm runs unweighted.
|===

.Results
[opts="header"]
|===
| Name      | Type    | Description
| nodeId    | Integer | Node ID
| distance  | Integer | The cost it takes to get from the start node to the specific node.
|===


[[algorithms-single-source-shortest-path-sample]]
== Single Source Shortest Path algorithm sample

:algorithm-name: {algorithm}
:graph-description: transport network
:image-file: dijkstra.svg
include::partial$/algorithms/shared/examples-intro.adoc[]

.The following will create a sample graph:
[source, cypher, role=noplay setup-query]
----
CREATE (a:Loc {name: "A"}),
       (b:Loc {name: "B"}),
       (c:Loc {name: "C"}),
       (d:Loc {name: "D"}),
       (e:Loc {name: "E"}),
       (f:Loc {name: "F"}),
       (a)-[:ROAD {cost: 50}]->(b),
       (a)-[:ROAD {cost: 50}]->(c),
       (a)-[:ROAD {cost: 100}]->(d),
       (b)-[:ROAD {cost: 40}]->(d),
       (c)-[:ROAD {cost: 40}]->(d),
       (c)-[:ROAD {cost: 80}]->(e),
       (d)-[:ROAD {cost: 30}]->(e),
       (d)-[:ROAD {cost: 80}]->(f),
       (e)-[:ROAD {cost: 40}]->(f);
----


[[algorithms-single-source-shortest-path-delta]]
=== Delta stepping algorithm

[role=query-example]
--
.The following will run the algorithm and stream results:
[source, cypher, role=noplay]
----
MATCH (n:Loc {name: 'A'})
CALL gds.alpha.shortestPath.deltaStepping.stream({
  nodeProjection: 'Loc',
  relationshipProjection: {
    ROAD: {
      type: 'ROAD',
      properties: 'cost'
    }
  },
  startNode: n,
  relationshipWeightProperty: 'cost',
  delta: 3.0
})
YIELD nodeId, distance
RETURN gds.util.asNode(nodeId).name AS Name, distance AS Cost
----

.Results
[opts="header",cols="1,1"]
|===
| Name | Cost
| "A"    | 0.0
| "B"    | 50.0
| "C"    | 50.0
| "D"    | 90.0
| "E"    | 120.0
| "F"    | 160.0
|===
--

The above table shows the cost of going from A to each of the other nodes, including itself at a cost of 0.

[role=query-example]
--
.The following will run the algorithm and write back results:
[source, cypher, role=noplay]
----
MATCH (n:Loc {name: 'A'})
CALL gds.alpha.shortestPath.deltaStepping.write({
  nodeProjection: 'Loc',
  relationshipProjection: {
    ROAD: {
      type: 'ROAD',
      properties: 'cost'
    }
  },
  startNode: n,
  relationshipWeightProperty: 'cost',
  delta: 3.0,
  writeProperty: 'sssp'
})
YIELD nodeCount
RETURN nodeCount
----

.Results
[opts="header",cols="1"]
|===
| nodeCount
| 6
|===
--

[[algorithms-single-source-shortest-path-cp]]
==== Cypher projection

include::partial$/algorithms/alpha/alpha-cypher-explanation.adoc[]

[role=query-example]
--
[source, cypher, role=noplay]
----
MATCH (start:Loc {name: 'A'})
CALL gds.alpha.shortestPath.deltaStepping.write({
  nodeQuery:'MATCH(n:Loc) WHERE not n.name = "C" RETURN id(n) AS id',
  relationshipQuery:'MATCH(n:Loc)-[r:ROAD]->(m:Loc) WHERE not (n.name = "C" OR m.name = "C")  RETURN id(n) AS source, id(m) AS target, r.cost AS weight',
  startNode: start,
  relationshipWeightProperty: 'weight',
  delta: 3.0,
  writeProperty: 'sssp'
})
YIELD nodeCount
RETURN nodeCount
----

.Results
[opts="header",cols="1"]
|===
| nodeCount
| 5
|===
--
