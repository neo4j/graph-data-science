apply plugin: 'java-library'
apply plugin: 'com.github.johnrengelman.shadow'

description = 'Neo4j Graph Data Science :: Packaging'
group = 'com.neo4j.gds'

dependencies {
    implementation project(':proc')
}

task licenseFile {
    outputs.file(distributionLicenseFile)
}

task checkLicensesFile {
    dependsOn generateLicensesFiles

    doLast {
        def licenceHeader = rootProject.file("$publicDir/etc/licenses/headers/LICENSES-header.txt").text
        def generatedLicenseFile = file("$buildDir/reports/license/LICENSES.txt").text
        if (generatedLicenseFile.equals(licenceHeader)) {
            throw new GradleException("LICENSES.txt file is empty!")
        }
    }
}

shadowJar {
    dependsOn checkLicensesFile

    relocate('com', 'org.neo4j.gds.shaded.com') {
        exclude 'com/neo4j/**'
    }
    relocate('org', 'org.neo4j.gds.shaded.org') {
        exclude 'org/neo4j/**'
    }
    relocate('de', 'org.neo4j.gds.shaded.de')
    relocate('io', 'org.neo4j.gds.shaded.io')
    relocate('pabeles', 'org.neo4j.gds.shaded.pabeles')

    if (project.hasProperty("gdsDropVersion")) {
        version project.property("gdsDropVersion")
    }

    mergeServiceFiles()
    archiveBaseName.set("neo4j-graph-data-science")
    archiveClassifier.set("")
    exclude 'META-INF/versions/**/*'
    exclude 'module-info.class'
    from licenseFile
    from file("$buildDir/reports/license")
}

task shadowCopy(type: Copy) {
    from(shadowJar)
    into distributionDir
}

task versionsJson {
    ext.outFile = file("$distributionDir/versions.json")
    def bucketUrl = "https://s3-eu-west-1.amazonaws.com/com.neo4j.graphalgorithms.dist/graph-data-science"
    def versionsJsonUrl = new URL("https://s3-eu-west-1.amazonaws.com/com.neo4j.graphalgorithms.dist/graph-data-science/versions.json")
    outputs.file(outFile)
    outputs.upToDateWhen { false }
    doLast {
        def versionsJson = new groovy.json.JsonSlurper().parse(versionsJsonUrl)
        def indexedVersionsJson = versionsJson.collectEntries {
            [(it.neo4j): it]
        }

        def neo4jVersions
        if (project.hasProperty('neo4jVersions')) {
            neo4jVersions = project.property('neo4jVersions').split(',')
        } else {
            neo4jVersions = neos.findAll { !it.value.contains('drop') }.collect {it.value}
        }

        def gdsVersion = project.hasProperty('gdsVersion') ? project.property('gdsVersion') : project.version
        def shouldUpdateVersion = gdsVersion.toString().matches("^(\\d+\\.){2}\\d+\$")
        if (shouldUpdateVersion) {
            shadowJar.version = gdsVersion
            neo4jVersions.flatten().forEach { version ->
                indexedVersionsJson[version] = [
                        'version': gdsVersion,
                        'neo4jVersion'  : version,
                        'downloadUrl'    : "$bucketUrl/${shadowJar.archiveName}",
                        'config': [
                                '+:dbms.security.procedures.unrestricted': [
                                        "gds.*"
                                ]
                        ],
                        'neo4j'  : version,
                        'jar'    : "$bucketUrl/${shadowJar.archiveName}",
                ]
            }

            // Restore shadowJar version
            shadowJar.version = project.version
        }
        outFile.text = groovy.json.JsonOutput.prettyPrint(groovy.json.JsonOutput.toJson(indexedVersionsJson.values()))
    }
}

task distZip(type: Zip) {
    description 'Create a ZIP archive of the main distribution JAR.'
    archiveBaseName = shadowJar.archiveBaseName
    archiveClassifier = shadowJar.archiveClassifier
    from shadowJar
    destinationDirectory.set(file(distributionDir))
}

ci.finalizedBy shadowCopy, versionsJson, distZip
cipr.finalizedBy shadowCopy, versionsJson, distZip
