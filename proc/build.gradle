description = 'Neo4j Graph Data Science :: Procedures'

apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
    annotationProcessor project(':annotations')
    annotationProcessor group: 'org.immutables',           name: 'builder',            version: ver.'immutables'
    annotationProcessor group: 'org.immutables',           name: 'value',              version: ver.'immutables'

    compile project(':algo')

    compileOnly group: 'org.immutables', name: 'value-annotations', version: ver.'immutables'
    compileOnly group: 'org.jetbrains',  name: 'annotations',       version: ver.'jetbrains-annotations'
    compileOnly group: 'org.neo4j',      name: 'neo4j',             version: ver.'neo4j'

    testCompile project(':test-utils')
    testCompileOnly group: 'org.immutables', name: 'value-annotations', version: ver.'immutables'
    testCompileOnly group: 'org.jetbrains',  name: 'annotations',       version: ver.'jetbrains-annotations'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from(javadoc.destinationDir)
}

publishing {
    publications {
        algo(MavenPublication) {
            project.shadow.component(it)
        }
    }
}

shadowJar {
    baseName = "neo4j-graph-data-science-product"
    classifier = "standalone"
    minimize()
    exclude 'META-INF/versions/**/*'
    from licenseFile
    from generateLicensesFiles
}

task shadowCopy(type: Copy) {
    from(shadowJar)
    into distributionDir
}

task distZip(type: Zip) {
    description 'Create a ZIP archive of the main distribution JAR.'
    baseName = shadowJar.baseName
    classifier = shadowJar.classifier
    from shadowJar
    destinationDir file(distributionDir)
}
