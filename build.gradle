plugins {
    id 'com.github.hierynomus.license'              version '0.16.1'                apply false
    id 'com.github.johnrengelman.shadow'            version '7.0.0'                 apply false
    id 'de.undercouch.download'                     version '4.1.1'                 apply false
    id 'ch.kk7.spawn'                               version '1.0.20180924200750'    apply false
    id 'com.vanniktech.dependency.graph.generator'  version '0.5.0'
    id 'de.thetaphi.forbiddenapis'                  version '3.2'                   apply false
    id 'com.github.spotbugs'                        version '4.7.1'                 apply false
    id 'nebula.info-dependencies'                   version '9.3.0'                 apply false
    id 'com.google.protobuf'                        version '0.8.16'                apply false
}

description = 'Neo4j Graph Data Science :: OpenGDS'

group = 'org.neo4j.gds'

ext {
    distributionDir = "$buildDir/distributions"
    cliDistributionDir = "$buildDir/distributions/estimation-cli"
    distributionLicenseFile = file('LICENSE.txt')
    publicDir = "${project.rootDir}"

    checkstyleReportsDir = "${buildDir}/reports/checkstyle/"

    spotbugsReportsDir = "${buildDir}/reports/spotbugs/"
    SpotBugsTask = com.github.spotbugs.snom.SpotBugsTask

    compatibilityModules = [
            'kernel-adapter': [
                    project(':neo4j-kernel-adapter-4.1'),
                    project(':neo4j-kernel-adapter-4.2'),
                    project(':neo4j-kernel-adapter-4.3'),
            ],
            'storage-engine-adapter': [
                    project(':storage-engine-adapter-unsupported'),
                    project(':storage-engine-adapter-4.3.3'),
                    project(':storage-engine-adapter-4.3.4plus'),
            ]
    ]
}

apply from: 'gradle/version.gradle'

allprojects {
    apply plugin: 'base'

    group = 'org.neo4j.gds'
    version = gdsVersion

    task ci {
        dependsOn tasks.check
    }

    task cipr {
        dependsOn tasks.check
    }

    task checkstyle {
        group = "verification"
        description = "Run all checkstyle related tasks"
    }

    task spotbugs {
        group = "verification"
        description = "Run all spotbugs related tasks"
    }
}

apply from: 'gradle/licensing.gradle'
apply from: 'gradle/dependencies.gradle'
apply from: 'gradle/defaults-java.gradle'
apply from: 'gradle/forbidden-apis.gradle'
apply from: 'gradle/checkstyle.gradle'
apply from: 'gradle/spotbugs.gradle'
apply from: 'gradle/pregel-bootstrap.gradle'
