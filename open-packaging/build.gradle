apply plugin: 'java-library'
apply plugin: 'com.gradleup.shadow'

description = 'Neo4j Graph Data Science :: Open Packaging'
group = 'org.neo4j.gds'

apply from: '../gradle/licensing.gradle'

dependencies {
    implementation project(':proc')
    implementation project(':proc-sysinfo')
    implementation project(':opengds-extension')
    implementation project(':open-model-catalog')
    implementation project(':open-write-services')

    testImplementation project(':opengds-extension')

    testImplementation project(':proc-test')
}

tasks.register("licenseFile") {
    outputs.file(file("$publicDir/LICENSE.txt"))
}

tasks.register("checkLicensesFile") {
    dependsOn generateLicensesFiles

    doLast {
        def licenceHeader = rootProject.file("$publicDir/etc/licenses/headers/LICENSES-header.txt").text
        def generatedLicenseFile = file("${layout.buildDirectory.get()}/reports/license/LICENSES.txt").text
        if (generatedLicenseFile.equals(licenceHeader)) {
            throw new GradleException("LICENSES.txt file is empty!")
        }
    }
}

shadowJar {
    dependsOn checkLicensesFile

    relocate('com.', 'org.neo4j.gds.shaded.com.') {
        exclude 'com/neo4j/**'
        exclude 'com/codahale/**'
    }
    relocate('org.', 'org.neo4j.gds.shaded.org.') {
        exclude 'org/neo4j/**'
    }

    relocate('android.annotation.', 'com.neo4j.gds.shaded.android.annotation.')
    relocate('de.siegmar.', 'com.neo4j.gds.shaded.de.siegmar.')
    relocate('io.grpc.', 'com.neo4j.gds.shaded.io.grpc.')
    relocate('io.jsonwebtoken.', 'com.neo4j.gds.shaded.io.jsonwebtoken.')
    relocate('io.netty.', 'com.neo4j.gds.shaded.io.netty.')
    relocate('io.perfmark.', 'com.neo4j.gds.shaded.io.perfmark.')
    relocate('javassist', 'com.neo4j.gds.shaded.javassist')
    relocate('javax.annotation.', 'com.neo4j.gds.shaded.javax.annotation.')
    relocate('net.bytebuddy', 'com.neo4j.gds.shaded.net.bytebuddy')
    relocate('pabeles', 'com.neo4j.gds.shaded.pabeles')
    relocate('stormpot', 'com.neo4j.gds.shaded.stormpot')


    mergeServiceFiles()
    archiveBaseName.set("open-gds")
    archiveClassifier.set("")
    exclude 'META-INF/versions/**/*'
    exclude 'module-info.class'
    exclude 'LICENSE'
    exclude 'LICENSE-*'
    exclude 'META-INF/LICENSE*'
    exclude 'META-INF/NOTICE*'
    exclude 'THIRD-PARTY'
    exclude '*.html'
    // Exclude proto files
    exclude 'config/'
    exclude '*.proto'
    exclude 'google/'
    exclude 'ml/'

    from licenseFile
    from generateLicensesFiles
}

tasks.register("shadowCopy", Copy) {
    from(shadowJar)
    into distributionDir
}

ci.finalizedBy shadowCopy
cipr.finalizedBy shadowCopy
